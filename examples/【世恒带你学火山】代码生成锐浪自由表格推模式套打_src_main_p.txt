
###### 以下为一个火山软件开发平台系统类库程序的内容

<火山程序 类型 = "通常" 版本 = 1 />

包 火山.世恒带你学火山

类 启动类 <公开 基础类 = 窗口程序类>
{
    变量 主窗口对象 <类型 = 我的主窗口 标题 = "我的主窗口">

    方法 启动方法 <公开 类型 = 整数>
    {
        主窗口对象.创建主窗口 ()
        返回 (1)
    }
}

# ===

类 我的主窗口 <基础类 = 窗口 注释 = "样例主窗口" @视窗.布局 = "client_size = \"937, 756\"" 标题 = "【世恒带你学火山】-代码生成锐浪自由表格推模式套打">
{
    变量 查询显示器_显示 <类型 = 查询显示器 隐藏值属性 = "0" @视窗.布局 = "id = 101\r\npos = \"13, 423, 912, 320\"">
    变量 报表_工资表 <类型 = 报表>
    变量 明细网格_工资表 <类型 = 明细网格>
    变量 部件框_临时存放 <类型 = 部件框>
    变量 内容格_工资表 <类型 = 内容格>
    变量 内容行_工资表 <类型 = 内容行>
    变量 自由表格_工资表 <类型 = 自由表格>
    变量 报表设计器_显示 <类型 = 报表设计器>
    变量 图片框_显示报表设计器 <类型 = 图片框 隐藏值属性 = "0" @视窗.布局 = "id = 103\r\npos = \"13, 7, 912, 352\"">
    变量 ""
    变量 按钮_生成报表 <类型 = 按钮 隐藏值属性 = "0" @视窗.布局 = "id = 104\r\npos = \"125, 367, 232, 40\"" 标题 = "生成报表"
            字体 = ", 14, 0, 0, 0, 0, 0">
    变量 按钮_打印预览 <类型 = 按钮 折叠2 隐藏值属性 = "0" @视窗.布局 = "id = 105\r\npos = \"541, 367, 232, 40\"" 标题 = "打印预览"
            字体 = ", 14, 0, 0, 0, 0, 0">
    变量 ""
    变量 ""

    方法 按钮_被单击 <接收事件 类型 = 整数 注释 = "当按钮被单击后发送此事件">
    参数 来源对象 <类型 = 按钮 注释 = "提供事件产生的具体来源对象">
    参数 标记值 <类型 = 整数 注释 = "用户调用\"挂接事件\"命令时所提供的\"标记值\"参数值,非此方式挂接事件则本参数值固定为0.">
    {
        如果 (来源对象 == 按钮_生成报表)
        {
            // ★★★在进行以下用代码生成报表之前,最好是学会手工设计,熟悉了报表的各组成部分以及相关名称概念之后再进行★★★
            报表_工资表.初始化 ()  // 这个“初始化”必须写并且写在最前面
            明细网格_工资表 = 报表_工资表.插入明细网格 ()  // 插入“明细网格”并且返回到“明细网格_工资表”变量
            明细网格_工资表.获取边框.获取画笔.宽度 = 0  // 设置边框宽度为0,也就是不显示"明细网格_工资表"的边框
            明细网格_工资表.显示列线 = 假  // 设置不显示"明细网格_工资表"里的列线
            明细网格_工资表.显示行线 = 假  // 设置不显示"明细网格_工资表"里的行线
            明细网格_工资表.增加列 ("", "", "", 16)  // 给"明细网格_工资表"里增加一列,这样才有了放置其他元素的区域
            明细网格_工资表.获取标题行.高度 = 0  // 给"明细网格_工资表"的“标题行”的高度设置为0,也就是为了不显示
            明细网格_工资表.获取标题行.可见性 = 假  // 给"明细网格_工资表"的“标题行”的可见性设置为假,也是保证不显示
            内容行_工资表 = 明细网格_工资表.获取内容行  // 获取"明细网格_工资表"里的“内容行”
            内容行_工资表.高度 = 4  // 设置"明细网格_工资表"里的“内容行”高度为4厘米
            内容格_工资表 = 内容行_工资表.获取内容格集合.按序号获取项目 (1)  // 获取“内容行”里的第1格,序号从1开始
            内容格_工资表.自由格 = 真  // 设置“内容行”里的第1格为“自由格”,这样才可以给里面任意放其他元素
            部件框_临时存放 = 内容格_工资表.获取部件框集合.增加 (部件框类型枚举.自由表格)  // 给这个内容格里面插入“自由表格”
            自由表格_工资表 = 部件框_临时存放.获取为自由表格  // 取出插入的“自由表格”并且关联到“自由表格_工资表”变量
            自由表格_工资表.上 = 0.5  // 设置"自由表格_工资表"的上边距为0.5厘米
            自由表格_工资表.居中方式 = 居中方式枚举.水平  // 设置"自由表格_工资表"的“居中方式”为水平居中
            // 下面这行设置"自由表格_工资表"的“边框样式”,上下左右可以自由组合,显示什么或者不显示什么
            自由表格_工资表.获取边框.边框模式 = 边框模式枚举.画上 + 边框模式枚举.画下 + 边框模式枚举.画左 + 边框模式枚举.画右
            自由表格_工资表.列数 = 6  // 设置"自由表格_工资表"的“列数”为6列
            自由表格_工资表.行数 = 4  // 设置"自由表格_工资表"的“行数”为4行
            计次循环 (6)  // 循环给6列都设置宽度为2厘米
            {
                自由表格_工资表.获取指定列 (取循环索引 () + 1).宽度 = 2  // 列号从1开始

            }
            自由表格_工资表.获取指定行 (1).高度 = 1  // 指定第1行的高度为1厘米

            变量 局部_行 <类型 = 整数>
            变量 局部_列 <类型 = 整数>
            // 以下为循环对4行6列自由表格里的所有单元格设置文字对齐方式为水平和垂直都是居中
            计次循环 (4)
            {
                局部_行 = 取循环索引 () + 1  // 行号从1开始
                计次循环 (6)
                {
                    局部_列 = 取循环索引 () + 1  // 列号从1开始
                    自由表格_工资表.获取指定单元格 (局部_行, 局部_列).获取文本格式.文字对齐方式 = 文字对齐方式枚举.中中

                }
            }
            自由表格_工资表.获取指定单元格 (1, 1).合并列数 = 6  // 对第1行的6个列进行合并
            自由表格_工资表.获取指定单元格 (1, 1).文本 = "工资条"  // 给第1行合并后的单元格显示文字“工资条”
            自由表格_工资表.获取指定单元格 (1, 1).获取字体.字号 = 18  // 给第1行合并后的单元格设置字体大小18
            自由表格_工资表.获取指定单元格 (1, 1).获取字体.名称 = "华文行楷"  // 给第1行合并后的单元格设置字体名称
            自由表格_工资表.获取指定单元格 (2, 1).合并行数 = 2  // 把第2行和第3行的第1列进行合并
            自由表格_工资表.获取指定单元格 (2, 1).文本 = "姓名"  // 给合并后的单元格显示文字“姓名”
            自由表格_工资表.获取指定单元格 (2, 2).合并列数 = 5  // 把第2行的第2列到第6列进行合并
            自由表格_工资表.获取指定单元格 (2, 2).文本 = "工资项目"  // 给合并后的单元格显示文字“工资项目”
            变量 局部_字段名 <类型 = "文本型 []" 值 = {"姓名","基本工资","全勤奖金","交通补助","伙食补贴","其他"}>
            计次循环 (6)  // 下面是循环增加数据字段名,这个很重要,后续填充数据要按照这个名字找对应,用了数组+循环方式
            {
                明细网格_工资表.获取数据集.增加字段 (局部_字段名 [取循环索引 ()], 字段类型枚举.字符型)

            }

            计次循环 (6)  // 下面是循环给第3行各列设置固定文本和给第4行各列设置后期填数据用的标识文本,格式如:[#姓名#]
            {
                自由表格_工资表.获取指定单元格 (3, 取循环索引 () + 1).文本 = 局部_字段名 [取循环索引 ()]
                自由表格_工资表.获取指定单元格 (4, 取循环索引 () + 1).文本 = "[#" + 局部_字段名 [取循环索引 ()] + "#]"

            }

            如果 (报表设计器_显示.是否为空 () == 真)  // 下面用来在窗口中某个组件里创建一个显示报表设计器的区域,我们用个图片框
            {
                报表设计器_显示.创建组件 (图片框_显示报表设计器, 0, 0, 图片框_显示报表设计器.宽度, 图片框_显示报表设计器.高度, 12345)

            }
            报表_工资表.保存到文件 (取运行目录 () + "工资条.grf")  // 可以把设计好的报表保存到文件中,类似手工设计时报表保存到文件
            报表设计器_显示.关联报表 = 报表_工资表  // 关联到报表设计器,就可以看到用代码创建的报表是什么样子了
            变量 局部_员工数组 <类型 = "文本型 []"
                    值 = {"狐突","狐偃","狐毛","介子推","里克","邳郑","宋襄公","秦穆公","楚庄王","石碏","石厚","州吁","郑突","郑忽","老子","孔子","孙武","左丘明","伍子胥","范蠡","西施","勾践","阖闾","夫差","文种","专诸","要离","庆忌","吴王僚","伊尹","烛之武","公子光","魏舒","晏婴","庆父","乐毅","吴起","孙膑","庞涓","廉颇","赵牧","赵奢","赵括","项燕","田单","韩非","荀子","庄子","墨子","惠子","孟子","燕丹","荆轲","高渐离","樊於期","孟尝君","春申君","信陵君","平原君","邹忌","白起","商鞅","李悝","蔺相如","屈原","魏斯","乐羊","西门豹","孔伋","杨朱","聂政","申不害","尸佼","赵武灵王","匡章","淳于髡","张仪","苏秦","田辟疆","田忌","鬼谷子","甘德","石申","李冰","扁鹊","范雎","蔡泽","郭隗","唐蔑","宋玉","触龙","毛遂","鲁仲连","公孙龙","嬴政","吕不韦","李斯","尉缭","王翦","王贲","李信","蒙骜","蒙武","蒙恬","嬴扶苏","嬴胡亥","赵高","章邯","司马欣","董翳","李由","甘罗","嫪毐","阳泉君","嬴子婴","嬴成矫","徐福","卢生","陈胜","吴广","项梁","项羽","张良","韩信","萧何","刘邦","陈平","周勃","夏侯婴","曹参","范增","项伯","项庄","樊哙","英布","彭越","熊心","龙且","郦食其","钟离昧","周殷","灌婴","周章","魏咎","赵歇","田儋","田臧","李归","邓说","伍徐","张贺","庄贾","朱鸡石","余樊君","王离","陈余","张耳","宋义","涉间","苏角","李左车","田荣","殷通","刘邦","萧何","曹参","韩信","张良","英布","周勃","周亚夫","吕雉","吕禄","吕产","陈平","灌婴","灌夫","郅都","宁成","张汤","东方朔","刘彻","刘启","刘安","卫青","卫子夫","霍去病","霍光","刘贺","田蚡","窦婴","苏建","苏武","司马相如","卓文君","司马谈","司马迁","扬雄","李广","李陵","王莽","赵飞燕","赵合德","刘秀","马援","邓禹","吴汉","盖延","赵破奴","张骞","刘细君","王昭君","梁冀","陈蕃","窦武","何进","卢植","皇甫嵩","朱隽","班超","班固","班昭","董卓","蔡邕","袁绍","袁术","郑玄","张衡","贾谊","晁错","郭解","剧孟","曹操","刘备","孙权","关羽","张飞","诸葛亮","赵云","黄忠","马超","魏延","法正","庞统","周瑜","司马懿","徐庶","曹丕","曹植","曹彰","郭嘉","荀彧","荀攸","贾诩","程昱","满宠","蒋济","韩浩","陈群","张辽","徐晃","许褚","典韦","张郃","于禁","乐进","李典","曹仁","文聘","张燕","曹洪","曹休","曹真","夏侯敦","夏侯渊","夏侯霸","夏侯威","姜维","鲁肃","吕蒙","陆逊","张昭","张纮","虞翻","丁奉","徐盛","陈武","潘璋","甘宁","太史慈","蒋钦","周泰","董袭","骆统","廖化","马谡","王平","蒋琬","刘禅","孙亮","凌统","费祎","诸葛瑾","公孙渊","邓艾","钟会","诸葛恪","司马师","司马昭","孙皓","曹髦","司马炎","司马衷","贾充","贾南风","王祥","羊祜","王浑","王浚","陆机","陆云","周处","刘琨","谢逖","陶侃","孙秀","刘渊","石勒","石虎","王弥","苟晞","潘岳","石崇","左思","王览","王导","嵇康","阮籍","阮咸","刘伶","王戎","杜预","荀勖","何曾","张华","司马攸","司马玮","司马亮","司马伦","司马冏","司马颖","司马颙","司马乂","司马越","段匹磾","张寔","刘曜","刘聪","司马睿","王羲之","王献之","桓温","谢安","谢玄","谢道韫","#123","321","666","233"}>
            报表_工资表.加载数据准备 ()  // 这就是锐浪报表的“推模式”,用代码给表格中设置数据
            置随机数种子 ()
            计次循环 (取数组成员数 (局部_员工数组))  // 构造很多条数据到报表中,也就是这个模板复制(追加)了很多次
            {
                报表_工资表.获取明细网格.获取数据集.追加 ()  // 相当于复制了一份设计好的报表模板出来
                // 以下就是给之前报表模板中设置的类似“[#姓名#]”、“[#基本工资#]”的文本标记位置设置具体数据
                报表_工资表.获取明细网格.获取数据集.按名称取字段 ("姓名").字符值 = 局部_员工数组 [取循环索引 ()]
                报表_工资表.获取明细网格.获取数据集.按名称取字段 ("基本工资").字符值 = 到文本 (多项选择 (取随机数 (0, 4), 2000, 3000, 4000, 5000, 6000))
                报表_工资表.获取明细网格.获取数据集.按名称取字段 ("全勤奖金").字符值 = 到文本 (多项选择 (取随机数 (0, 2), 200, 0, 400))
                报表_工资表.获取明细网格.获取数据集.按名称取字段 ("交通补助").字符值 = 到文本 (多项选择 (取随机数 (0, 1), 200, 300))
                报表_工资表.获取明细网格.获取数据集.按名称取字段 ("伙食补贴").字符值 = 到文本 (多项选择 (取随机数 (0, 3), 200, 300, 400, 500))
                报表_工资表.获取明细网格.获取数据集.按名称取字段 ("其他").字符值 = 到文本 (取随机数 (100, 800))
                报表_工资表.获取明细网格.获取数据集.提交 ()  // 完成了模板的复制和数据的设置并准备显示


            }
            查询显示器_显示.初始化 ()  // 这个“初始化”必须先写上,然后再“关联报表”
            查询显示器_显示.关联报表 = 报表_工资表  // 这个“查询显示器_显示”和报表建立关联
            查询显示器_显示.启动 ()  // 启动查询显示器,也就是让显示报表,就可以在界面上看到报表样子

        }
        否则 (来源对象 == 按钮_打印预览)
        {
            报表_工资表.打印预览 (真)  // 这样还可以直接显示出这个报表的打印预览


        }
        返回 (0)

    }

    方法 我的主窗口_创建完毕 <接收事件 类型 = 整数 注释 = "当本组件及其中所有子组件均被创建完毕后发送此事件.">
    参数 来源对象 <类型 = 我的主窗口 注释 = "提供事件产生的具体来源对象">
    参数 标记值 <类型 = 整数 注释 = "用户调用\"挂接事件\"命令时所提供的\"标记值\"参数值,非此方式挂接事件则本参数值固定为0.">
    {
        标题 = "【世恒带你学火山】- 代码生成锐浪自由表格推模式套打"
        返回 (0)
    }
}
