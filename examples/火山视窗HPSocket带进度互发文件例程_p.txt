
###### 以下为一个火山软件开发平台系统类库程序的内容

<火山程序 类型 = "通常" 版本 = 1 />

包 火山.程序

类 启动类 <公开 基础类 = 窗口程序类>
{
    变量 主窗口对象 <类型 = 我的主窗口>

    方法 启动方法 <公开 类型 = 整数>
    {
        主窗口对象.创建主窗口 ()
        返回 (1)
    }
}

类 我的主窗口 <基础类 = 窗口 标题 = "TCP客户端" @视窗.布局 = "client_size = \"563, 361\"">
{
    变量 按钮_发送文件 <类型 = 按钮 折叠2 隐藏值属性 = "0" @视窗.布局 = "id = 101\r\npos = \"413, 288, 112, 32\"" 横向对齐方式 = 居中
            纵向对齐方式 = 居中 "类型" = 通常 标题 = "发送文件">
    变量 编辑框_接收数据 <类型 = 编辑框 折叠2 隐藏值属性 = "0" @视窗.布局 = "id = 102\r\npos = \"269, 33, 264, 130\"" 是否允许多行 = 真
            滚动条 = 纵向滚动条 输入方式 = 只读方式>
    变量 标签5 <类型 = 标签 折叠2 隐藏值属性 = "0" @视窗.布局 = "id = 103\r\npos = \"269, 9, 136, 24\"" 横向对齐方式 = 左边
            纵向对齐方式 = 居中 标题 = "已接收数据文本：">
    变量 编辑框_要发送的数据 <类型 = 编辑框 折叠2 隐藏值属性 = "0" @视窗.布局 = "id = 104\r\npos = \"269, 195, 264, 80\"" 是否允许多行 = 真>
    变量 按钮_发送文本 <类型 = 按钮 折叠2 隐藏值属性 = "0" @视窗.布局 = "id = 105\r\npos = \"277, 288, 112, 32\"" 横向对齐方式 = 居中
            纵向对齐方式 = 居中 "类型" = 通常 标题 = "发送内容">
    变量 标签6 <类型 = 标签 折叠2 隐藏值属性 = "0" @视窗.布局 = "id = 106\r\npos = \"269, 171, 152, 24\"" 横向对齐方式 = 左边
            纵向对齐方式 = 居中 标题 = "待发送文本数据：">
    变量 分组框1 <类型 = 分组框 折叠2 隐藏值属性 = "0" @视窗.布局 = "id = 107\r\npos = \"13, 16, 232, 280\"" 标题 = "连接服务器">
    变量 编辑框_端口 <类型 = 编辑框 隐藏值属性 = "0" @视窗.布局 = "id = 108\r\nparent_id = 107\r\npos = \"56, 88, 160, 32\""
            输入方式 = 输入短整数 内容 = "999">
    变量 按钮_连接 <类型 = 按钮 折叠2 隐藏值属性 = "0" @视窗.布局 = "id = 109\r\nparent_id = 107\r\npos = \"64, 136, 112, 40\""
            标题 = "连接服务器">
    变量 标签1 <类型 = 标签 折叠2 隐藏值属性 = "0" @视窗.布局 = "id = 110\r\nparent_id = 107\r\npos = \"12, 43, 40, 32\""
            标题 = "地址：">
    变量 编辑框_IP地址 <类型 = 编辑框 隐藏值属性 = "0" @视窗.布局 = "id = 111\r\nparent_id = 107\r\npos = \"56, 45, 160, 32\""
            内容 = "192.168.2.193">
    变量 标签2 <类型 = 标签 折叠2 隐藏值属性 = "0" @视窗.布局 = "id = 112\r\nparent_id = 107\r\npos = \"12, 88, 40, 32\""
            标题 = "端口：">
    变量 进度条1 <类型 = 进度条 折叠2 隐藏值属性 = "0" @视窗.布局 = "id = 113\r\npos = \"6, 332, 552, 22\"" 方向 = 横向 显示方式 = 连续>
    变量 ""
    变量 客户端1 <类型 = HP_TCP客户端>

    方法 我的主窗口_创建完毕 <接收事件 类型 = 整数 注释 = "当本组件及其中所有子组件均被创建完毕后发送此事件.">
    参数 来源对象 <类型 = 我的主窗口 注释 = "提供事件产生的具体来源对象">
    参数 标记值 <类型 = 整数 注释 = "用户调用\"挂接事件\"命令时所提供的\"标记值\"参数值,非此方式挂接事件则本参数值固定为0.">
    {
        创建目录 (取运行目录 () + "文件接收")
        返回 (0)
    }

    方法 按钮_被单击 <接收事件 类型 = 整数 注释 = "当按钮被单击后发送此事件">
    参数 来源对象 <类型 = 按钮 注释 = "提供事件产生的具体来源对象">
    参数 标记值 <类型 = 整数 注释 = "用户调用\"挂接事件\"命令时所提供的\"标记值\"参数值,非此方式挂接事件则本参数值固定为0.">
    {
        如果 (来源对象 == 按钮_连接)
        {
            如果 (客户端1.启动 (编辑框_IP地址.内容, (短整数)文本到整数 (编辑框_端口.内容)) == 假)
            {
                信息框 ("连接失败", 信息框按钮.错误图标, "提示")
            }
        }
        否则 (来源对象 == 按钮_发送文件)
        {
            如果 (客户端1.是否已启动)
            {
                变量 浏览文件 <类型 = 通用对话框 "类型" = 打开文件>
                如果真 (浏览文件.打开 (本对象))
                {
                    如果真 (浏览文件.文件名 != "")
                    {
                        发送文件 = 真
                        累积数据长度 = 0
                        要发送的文件字节 = 生成要发送的文件 (浏览文件.文件名)
                        客户端1.发送数据 (要发送的文件字节)
                    }
                }
            }
            否则
            {
                信息框 ("需要先连接服务器")
            }
        }
        否则 (来源对象 == 按钮_发送文本)
        {
            发送文件 = 假
            如果 (客户端1.是否已启动)
            {
                如果 (文本是否为空 (编辑框_要发送的数据.内容))
                {
                    信息框 ("发送的内容不能为空！")
                }
                否则
                {
                    调试输出 ("正在给服务器发送数据")
                    客户端1.发送数据 (生成要发送的文本数据 (编辑框_要发送的数据.内容))
                }
            }
            否则
            {
                信息框 ("需要先连接服务器")
            }
        }
        返回 (0)
    }

    方法 HP_TCP客户端_连接完成 <接收事件 类型 = 整数 注释 = "与服务端成功建立连接时本事件将被触发." 返回值注释 = "可选返回值:"
            返回值注释 = "  0 = 本次操作成功, 继续执行." 返回值注释 = "  1 = 忽略本次操作, 继续执行."
            返回值注释 = "  2 = 本次操作错误, 如果为同步连接将终止启动通信客户端,为异步则会关闭连接." 折叠>
    参数 来源对象 <类型 = HP_TCP客户端 注释 = "提供事件产生的具体来源对象">
    参数 标记值 <类型 = 整数 注释 = "用户调用\"挂接事件\"命令时所提供的\"标记值\"参数值,非此方式挂接事件则本参数值固定为0.">
    参数 连接ID <类型 = 变整数>
    {
        调试输出 ("连接完毕")
        返回 (0)
    }

    变量 发送文件 <类型 = 逻辑型>
    变量 要发送的文件字节 <类型 = 字节集类>
    变量 累积数据长度 <类型 = 整数>

    方法 HP_TCP客户端_发送完毕 <接收事件 类型 = 整数 注释 = "当客户端向当前连接发送数据成功后,将触发本事件.">
    参数 来源对象 <类型 = HP_TCP客户端 注释 = "提供事件产生的具体来源对象">
    参数 标记值 <类型 = 整数 注释 = "用户调用\"挂接事件\"命令时所提供的\"标记值\"参数值,非此方式挂接事件则本参数值固定为0.">
    参数 连接ID <类型 = 变整数 注释 = "接收本次发送所对应的连接ID.">
    参数 数据 <类型 = 字节集类 注释 = "本次所发送的数据.">
    参数 数据长度 <类型 = 整数 注释 = "所发送数据的长度.">
    {
        如果 (发送文件)
        {
            累积数据长度 = 累积数据长度 + 数据长度
            进度条1.位置 = 100 * 累积数据长度 / 要发送的文件字节.取字节集长度 ()
            编辑框_接收数据.加入文本行 ("正在发送：" + 到文本 (四舍五入 (100.00 * (小数)累积数据长度 / (小数)要发送的文件字节.取字节集长度 (), 2)))
            调试输出 (要发送的文件字节.取字节集长度 (), 累积数据长度)
            如果 (累积数据长度 == 要发送的文件字节.取字节集长度 ())
            {
                进度条1.位置 = 100
            }
        }
        返回 (0)
    }

    变量 数据总字节 <类型 = 字节集类>

    方法 HP_TCP客户端_数据进入 <接收事件 类型 = 整数 注释 = "当客户端成功接收数据后将触发本事件." 返回值注释 = "可选返回值:"
            返回值注释 = "  0 = 本次操作成功, 继续执行." 返回值注释 = "  1 = 忽略本次操作."
            返回值注释 = "  2 = 本次操作错误, 服务器主动与当前连接ID断开连接并触发关闭事件." 折叠>
    参数 来源对象 <类型 = HP_TCP客户端 注释 = "提供事件产生的具体来源对象">
    参数 标记值 <类型 = 整数 注释 = "用户调用\"挂接事件\"命令时所提供的\"标记值\"参数值,非此方式挂接事件则本参数值固定为0.">
    参数 连接ID <类型 = 变整数 注释 = "接收到的数据由该连接ID发过来.">
    参数 已接收数据 <类型 = 字节集类 注释 = "所接收到的数据.">
    参数 已接收长度 <类型 = 整数 注释 = "已接收数据长度.">
    {
        数据总字节.插入字节集 (数据总字节.取字节集长度 (), 已接收数据)
        变量 标记头 <类型 = 整数>
        标记头 = 取字节集数据 (数据总字节, , 整数)
        如果 (标记头 == 发送类型_文本)
        {
            // 文件头类型 + 要发送的数据长度 +  实际文本内容
            变量 文本数据长度 <类型 = 整数 编辑时信息 = "34B48, 0, 0, 0">
            文本数据长度 = 取字节集数据 (数据总字节, 4, 整数)
            变量 字节内容 <类型 = 字节集类>
            字节内容 = 取字节集右边 (数据总字节, 数据总字节.取字节集长度 () - 8)
            如果 (文本数据长度 == 字节内容.取字节集长度 ())
            {
                编辑框_接收数据.加入文本行 ("数据内容 -- >  " + 字节集到文本 (字节内容))
                数据总字节.清空字节集 ()
            }
        }
        否则 (标记头 == 发送类型_文件)
        {
            // 文件头  要发送的数据长度(后面3个长度)  实际文件尺寸   文件名    实际文件
            变量 实际接收长度 <类型 = 整数>
            实际接收长度 = 取字节集数据 (数据总字节, 4, 整数)

            进度条1.位置 = 100 * 数据总字节.取字节集长度 () / 实际接收长度
            编辑框_接收数据.加入文本行 ("正在接收：" + 到文本 (四舍五入 (100.00 * (小数)数据总字节.取字节集长度 () / (小数)实际接收长度, 2)))
            // 设置接收进度

            如果 (实际接收长度 == 数据总字节.取字节集长度 () - 8 || 实际接收长度 == 数据总字节.取字节集长度 () - 12)
            {
                进度条1.位置 = 100
                // -8是因为发送的小文件没有带hp自己的头,-12是因为hp带自己的头了。
                变量 文件名 <类型 = 文本型>
                变量 实际文件长度 <类型 = 整数>
                变量 结果数组 <类型 = 文本数组类>
                分割文本 (取字节集数据 (数据总字节, 12, 文本型), "\n", 结果数组)
                文件名 = 结果数组.取成员 (0)
                实际文件长度 = 取字节集数据 (数据总字节, 8, 整数)
                调试输出 (" --> 接收完毕：" + 文件名)
                如果 (文件是否存在 (取运行目录 () + "文件接收\\" + 文件名))
                {
                    写到文件 (取运行目录 () + "文件接收\\" + 到文本 (取时间戳 (取现行时间 ())) + 文件名, 取字节集右边 (数据总字节, 实际文件长度))
                }
                否则
                {
                    写到文件 (取运行目录 () + "文件接收\\" + 文件名, 取字节集右边 (数据总字节, 实际文件长度))
                }
                浏览选中文件 (取运行目录 () + "文件接收\\" + 文件名)
                数据总字节.清空字节集 ()
            }
        }
        返回 (0)
    }

    方法 生成要发送的文件 <类型 = 字节集类>
    参数 文件路径 <类型 = 文本型>
    {
        // 文件头  要发送的数据长度(后面3个长度)  实际文件尺寸   文件名    实际文件
        变量 要发送的字节集 <类型 = 字节集类>
        要发送的字节集.插入字节集 (要发送的字节集.取字节集长度 (), 到字节集 (发送类型_文件))

        变量 要发送的文件 <类型 = 字节集类>
        要发送的文件 = 读入文件 (文件路径)
        变量 文件长度 <类型 = 整数>
        文件长度 = 取字节集长度 (要发送的文件)

        变量 文件名 <类型 = 字节集类>
        文件名 = 到字节集 (取文件名无路径部分 (文件路径) + "\n")

        要发送的字节集.插入字节集 (要发送的字节集.取字节集长度 (), 到字节集 (文件长度 + 文件名.取字节集长度 () + 4))
        要发送的字节集.插入字节集 (要发送的字节集.取字节集长度 (), 到字节集 (文件长度))
        要发送的字节集.插入字节集 (要发送的字节集.取字节集长度 (), 文件名)
        要发送的字节集.插入字节集 (要发送的字节集.取字节集长度 (), 要发送的文件)

        返回 (要发送的字节集)

    }

    常量 发送类型_文本 <类型 = 整数 值 = 1111>
    常量 发送类型_文件 <类型 = 整数 值 = 2222>

    方法 生成要发送的文本数据 <类型 = 字节集类 注释 = "火山无名菜鸟驿站：http://www.vowm.cn" 折叠>
    参数 待发送数据 <类型 = 文本型>
    {
        // 文件头类型 + 要发送的数据长度 +  实际文本内容
        变量 文本字节集 <类型 = 字节集类>
        文本字节集 = 到字节集 (待发送数据)
        变量 要发的数据 <类型 = 字节集类>
        要发的数据.插入字节集 (要发的数据.取字节集长度 (), 到字节集 (发送类型_文本))
        要发的数据.插入字节集 (要发的数据.取字节集长度 (), 到字节集 (取字节集长度 (文本字节集)))
        要发的数据.插入字节集 (要发的数据.取字节集长度 (), 文本字节集)
        返回 (要发的数据)
    }

    方法 我的主窗口_将被销毁 <接收事件 类型 = 整数 注释 = "当本组件被销毁前发送此事件">
    参数 来源对象 <类型 = 我的主窗口 注释 = "提供事件产生的具体来源对象">
    参数 标记值 <类型 = 整数 注释 = "用户调用\"挂接事件\"命令时所提供的\"标记值\"参数值,非此方式挂接事件则本参数值固定为0.">
    {
        客户端1.停止 ()
        返回 (0)
    }

    方法 HP_TCP客户端_通信发生错误 <接收事件 类型 = 整数 注释 = "当客户端主动或被动与服务器断开后将触发本事件.">
    参数 来源对象 <类型 = HP_TCP客户端 注释 = "提供事件产生的具体来源对象">
    参数 标记值 <类型 = 整数 注释 = "用户调用\"挂接事件\"命令时所提供的\"标记值\"参数值,非此方式挂接事件则本参数值固定为0.">
    参数 连接ID <类型 = 变整数 注释 = "触发该事件的连接ID.">
    参数 关闭原因 <类型 = HP操作类型 注释 = "返回导致连接被关闭的操作.">
    参数 错误码 <类型 = HP错误码 注释 = "如果客户端为非正常退出,此参数为错误代码.">
    {
        调试输出 ("通讯错误")
        返回 (0)
    }
}

###### 以下为一个火山软件开发平台系统类库程序的内容

<火山程序 类型 = "通常" 版本 = 1 />

包 火山.无名.菜鸟驿站 <注释 = "火山无名菜鸟驿站：http://www.vowm.cn">

类 启动类 <公开 基础类 = 窗口程序类>
{
    变量 主窗口对象 <类型 = TCP服务器窗口>

    方法 启动方法 <公开 类型 = 整数 注释 = "火山无名菜鸟驿站：http://www.vowm.cn">
    {
        主窗口对象.创建主窗口 ()
        返回 (1)
    }
}

类 TCP服务器窗口 <基础类 = 窗口 注释 = "" 注释 = "火山无名菜鸟驿站：http://www.vowm.cn" @视窗.布局 = "client_size = \"516, 329\""
        标题 = "TCP服务器">
{
    变量 按钮_发送文件 <类型 = 按钮 折叠2 隐藏值属性 = "0" @视窗.布局 = "id = 101\r\npos = \"381, 288, 112, 32\"" 横向对齐方式 = 居中
            纵向对齐方式 = 居中 "类型" = 通常 标题 = "发送文件">
    变量 列表框_客户列表 <类型 = 列表框 隐藏值属性 = "0" @视窗.布局 = "id = 102\r\npos = \"5, 30, 208, 290\"">
    变量 标签1 <类型 = 标签 折叠2 隐藏值属性 = "0" @视窗.布局 = "id = 103\r\npos = \"5, 6, 112, 24\"" 横向对齐方式 = 居中 纵向对齐方式 = 居中
            标题 = "已连接客户端">
    变量 编辑框_接收数据 <类型 = 编辑框 折叠2 隐藏值属性 = "0" @视窗.布局 = "id = 104\r\npos = \"237, 33, 264, 130\"" 是否允许多行 = 真
            滚动条 = 纵向滚动条 输入方式 = 只读方式>
    变量 标签2 <类型 = 标签 折叠2 隐藏值属性 = "0" @视窗.布局 = "id = 105\r\npos = \"237, 9, 136, 24\"" 横向对齐方式 = 左边
            纵向对齐方式 = 居中 标题 = "已接收数据文本：">
    变量 编辑框_要发送的数据 <类型 = 编辑框 折叠2 隐藏值属性 = "0" @视窗.布局 = "id = 106\r\npos = \"237, 195, 264, 80\"" 是否允许多行 = 真>
    变量 按钮_发送文本 <类型 = 按钮 折叠2 隐藏值属性 = "0" @视窗.布局 = "id = 107\r\npos = \"245, 288, 112, 32\"" 横向对齐方式 = 居中
            纵向对齐方式 = 居中 "类型" = 通常 标题 = "发送内容">
    变量 标签3 <类型 = 标签 折叠2 隐藏值属性 = "0" @视窗.布局 = "id = 108\r\npos = \"237, 171, 152, 24\"" 横向对齐方式 = 左边
            纵向对齐方式 = 居中 标题 = "待发送文本数据：">
    变量 服务器1 <类型 = HP_TCP服务器>

    方法 TCP服务器窗口_创建完毕 <接收事件 类型 = 整数 注释 = "火山无名菜鸟驿站：http://www.vowm.cn">
    参数 来源对象 <类型 = TCP服务器窗口>
    参数 标记值 <类型 = 整数>
    {
        创建目录 (取运行目录 () + "文件接收")

        如果 (服务器1.启动 (HPSocket类.取主机IP地址 ().取成员 (0), 999))  // 默认使用本机地址,监听端口999
        {
            调试输出 ("服务器正在启动")
        }
        否则
        {
            调试输出 ("TCP服务器启动错误：" + 到文本 (HPSocket类.取上次错误码 ()))
        }
        返回 (0)
    }

    方法 TCP服务器窗口_将被销毁 <接收事件 类型 = 整数 折叠>
    参数 来源对象 <类型 = TCP服务器窗口>
    参数 标记值 <类型 = 整数>
    {
        服务器1.停止 ()
        返回 (0)
    }

    方法 HP_TCP服务器_客户进入 <接收事件 类型 = 整数 注释 = "火山无名菜鸟驿站：http://www.vowm.cn" 折叠>
    参数 来源对象 <类型 = HP_TCP服务器>
    参数 标记值 <类型 = 整数>
    参数 客户ID <类型 = 变整数>
    {
        调试输出 ("有客户进入 ID->>>>>" + 到文本 (客户ID))
        变量 客户IP <类型 = 文本型>
        来源对象.取客户远程地址 (客户ID, 客户IP)
        调试输出 (客户IP)
        列表框_客户列表.加入项目 ("客户端" + 到文本 (客户ID) + " - " + 客户IP + 到文本 (来源对象.取客户远程端口 (客户ID)), 客户ID)
        返回 (0)
    }

    方法 HP_TCP服务器_客户离开 <接收事件 类型 = 整数 折叠>
    参数 来源对象 <类型 = HP_TCP服务器>
    参数 标记值 <类型 = 整数>
    参数 客户ID <类型 = 变整数>
    参数 关闭原因 <类型 = HP操作类型>
    参数 错误码 <类型 = HP错误码>
    {
        调试输出 ("客户断开 ID->>>>>" + 到文本 (客户ID))
        列表框_客户列表.删除项目 (客户ID找列表索引 (客户ID))
        返回 (0)
    }

    方法 客户ID找列表索引 <类型 = 整数 注释 = "火山无名菜鸟驿站：http://www.vowm.cn" 折叠>
    参数 ID <类型 = 变整数>
    {
        变量 i <类型 = 整数>
        循环 (, 列表框_客户列表.取项目数 (), i)
        {
            如果 (列表框_客户列表.取项目数值 (i) == ID)
            {
                返回 (i)
            }
        }
        返回 (-1)
    }

    变量 数据总字节 <类型 = 字节集类>

    常量 发送类型_文本 <类型 = 整数 值 = 1111>
    常量 发送类型_文件 <类型 = 整数 值 = 2222>

    方法 HP_TCP服务器_数据进入 <接收事件 类型 = 整数>
    参数 来源对象 <类型 = HP_TCP服务器>
    参数 标记值 <类型 = 整数>
    参数 客户ID <类型 = 变整数>
    参数 已接收数据 <类型 = 字节集类>
    参数 已接收长度 <类型 = 整数>
    {
        数据总字节.插入字节集 (数据总字节.取字节集长度 (), 已接收数据)
        变量 标记头 <类型 = 整数>
        标记头 = 取字节集数据 (数据总字节, , 整数)
        如果 (标记头 == 发送类型_文本)
        {
            // 文件头类型 + 要发送的数据长度 +  实际文本内容
            变量 文本数据长度 <类型 = 整数>
            文本数据长度 = 取字节集数据 (数据总字节, 4, 整数)
            变量 字节内容 <类型 = 字节集类>
            字节内容 = 取字节集右边 (数据总字节, 数据总字节.取字节集长度 () - 8)
            如果 (文本数据长度 == 字节内容.取字节集长度 ())
            {
                输出到编辑框 ("数据内容 -- >  " + 字节集到文本 (字节内容))
                数据总字节.清空字节集 ()
            }
        }
        否则 (标记头 == 发送类型_文件)
        {
            // 文件头  要发送的数据长度(后面3个长度)  实际文件尺寸   文件名    实际文件
            变量 实际发送长度 <类型 = 整数>
            实际发送长度 = 取字节集数据 (数据总字节, 4, 整数)
            如果 (实际发送长度 == 数据总字节.取字节集长度 () - 8 || 实际发送长度 == 数据总字节.取字节集长度 () - 12)
            {
                // -8是因为发送的小文件没有带hp自己的头,-12是因为hp带自己的头了。
                变量 文件名 <类型 = 文本型>
                变量 实际文件长度 <类型 = 整数>
                变量 结果数组 <类型 = 文本数组类>
                分割文本 (取字节集数据 (数据总字节, 12, 文本型), "\n", 结果数组)
                文件名 = 结果数组.取成员 (0)
                实际文件长度 = 取字节集数据 (数据总字节, 8, 整数)
                输出到编辑框 (" --> 接收完毕：" + 文件名)
                如果 (文件是否存在 (取运行目录 () + "文件接收\\" + 文件名))
                {
                    写到文件 (取运行目录 () + "文件接收\\" + 到文本 (取时间戳 (取现行时间 ())) + 文件名, 取字节集右边 (数据总字节, 实际文件长度))
                }
                否则
                {
                    写到文件 (取运行目录 () + "文件接收\\" + 文件名, 取字节集右边 (数据总字节, 实际文件长度))
                }
                浏览选中文件 (取运行目录 () + "文件接收\\" + 文件名)
                数据总字节.清空字节集 ()
            }
        }
        返回 (0)
    }

    方法 输出到编辑框 <注释 = "火山无名菜鸟驿站：http://www.vowm.cn" 折叠>
    参数 内容 <类型 = 文本型>
    {
        编辑框_接收数据.加入文本行 (内容)
    }

    方法 按钮_被单击 <接收事件 类型 = 整数 注释 = "火山无名菜鸟驿站：http://www.vowm.cn">
    参数 来源对象 <类型 = 按钮>
    参数 标记值 <类型 = 整数>
    {
        如果 (列表框_客户列表.现行选中项 < 0)
        {
            信息框 ("请选择一个客户")
            返回 (0)
        }
        如果 (来源对象 == 按钮_发送文本)
        {
            如果 (文本是否为空 (编辑框_要发送的数据.内容))
            {
                信息框 ("发送的内容不能为空！")
            }
            否则
            {
                调试输出 ("正在给客户" + 列表框_客户列表.取项目文本 (列表框_客户列表.现行选中项) + "发送数据")
                服务器1.发送数据 (列表框_客户列表.取项目数值 (列表框_客户列表.现行选中项), 生成要发送的文本数据 (编辑框_要发送的数据.内容))

            }
        }
        否则 (来源对象 == 按钮_发送文件)
        {
            变量 浏览文件 <类型 = 通用对话框 "类型" = 打开文件>
            如果真 (浏览文件.打开 (本对象))
            {
                如果真 (浏览文件.文件名 != "")
                {
                    服务器1.发送数据 (列表框_客户列表.取项目数值 (列表框_客户列表.现行选中项), 生成要发送的文件 (浏览文件.文件名))

                }
            }
        }
        返回 (0)
    }

    方法 生成要发送的文件 <类型 = 字节集类 折叠>
    参数 文件路径 <类型 = 文本型>
    {
        // 文件头  要发送的数据长度(后面3个长度)  实际文件尺寸   文件名    实际文件
        变量 要发送的字节集 <类型 = 字节集类>
        要发送的字节集.插入字节集 (要发送的字节集.取字节集长度 (), 到字节集 (发送类型_文件))
        变量 要发送的文件 <类型 = 字节集类>
        要发送的文件 = 读入文件 (文件路径)
        变量 文件长度 <类型 = 整数>
        文件长度 = 取字节集长度 (要发送的文件)
        变量 文件名 <类型 = 字节集类>
        文件名 = 文本到字节集 (取文件名无路径部分 (文件路径) + "\n")
        要发送的字节集.插入字节集 (要发送的字节集.取字节集长度 (), 到字节集 (文件长度 + 文件名.取字节集长度 () + 4))
        要发送的字节集.插入字节集 (要发送的字节集.取字节集长度 (), 到字节集 (文件长度))
        要发送的字节集.插入字节集 (要发送的字节集.取字节集长度 (), 文件名)
        要发送的字节集.插入字节集 (要发送的字节集.取字节集长度 (), 要发送的文件)
        调试输出 (要发送的字节集.取字节集长度 (), 文件名.取字节集长度 (), 文件长度)
        返回 (要发送的字节集)
    }

    方法 生成要发送的文本数据 <类型 = 字节集类 注释 = "火山无名菜鸟驿站：http://www.vowm.cn" 折叠>
    参数 待发送数据 <类型 = 文本型>
    {
        // 文件头类型 + 要发送的数据长度 +  实际文本内容
        变量 文本字节集 <类型 = 字节集类>
        文本字节集 = 到字节集 (待发送数据)
        变量 要发的数据 <类型 = 字节集类>
        要发的数据.插入字节集 (要发的数据.取字节集长度 (), 到字节集 (发送类型_文本))
        要发的数据.插入字节集 (要发的数据.取字节集长度 (), 到字节集 (取字节集长度 (文本字节集)))
        要发的数据.插入字节集 (要发的数据.取字节集长度 (), 文本字节集)
        返回 (要发的数据)
    }

    方法 HP_TCP服务器_启动完毕 <接收事件 类型 = 整数 折叠>
    参数 来源对象 <类型 = HP_TCP服务器>
    参数 标记值 <类型 = 整数>
    {
        调试输出 ("服务器已启动完毕")
        返回 (0)
    }

    方法 HP_TCP服务器_服务器被关闭 <接收事件 类型 = 整数 注释 = "火山无名菜鸟驿站：http://www.vowm.cn">
    参数 来源对象 <类型 = HP_TCP服务器>
    参数 标记值 <类型 = 整数>
    {
        调试输出 ("服务器已被关闭")
        返回 (0)
    }
}
