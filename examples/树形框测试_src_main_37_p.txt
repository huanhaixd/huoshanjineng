
###### 以下为一个火山软件开发平台系统类库程序的内容

<火山程序 类型 = "通常" 版本 = 1 />

包 火山.程序

类 启动类 <公开 基础类 = 窗口程序类>
{
    变量 主窗口对象 <类型 = 我的主窗口>

    方法 启动方法 <公开 类型 = 整数>
    {
        主窗口对象.创建主窗口 ()
        返回 (1)
    }
}

# ===

类 我的主窗口 <基础类 = 窗口 注释 = "样例主窗口" @视窗.布局 = "client_size = \"484, 532\"" 标题 = "我的主窗口">
{
    变量 按钮2 <类型 = 按钮 折叠2 隐藏值属性 = "0" @视窗.布局 = "id = 101\r\npos = \"289, 363, 83, 26\"" 标题 = "取同级成员">
    变量 按钮4 <类型 = 按钮 折叠2 隐藏值属性 = "0" @视窗.布局 = "id = 102\r\npos = \"391, 363, 83, 26\"" 标题 = "取所有子成员">
    变量 按钮3 <类型 = 按钮 折叠2 隐藏值属性 = "0" @视窗.布局 = "id = 103\r\npos = \"289, 407, 83, 26\"" 标题 = "取下级成员">
    变量 按钮6 <类型 = 按钮 折叠2 隐藏值属性 = "0" @视窗.布局 = "id = 104\r\npos = \"391, 407, 83, 26\"" 标题 = "取所在层">
    变量 分组框1 <类型 = 分组框 折叠2 隐藏值属性 = "0" @视窗.布局 = "id = 105\r\npos = \"234, 147, 230, 147\"" 标题 = "分组框1">
    变量 按钮5 <类型 = 按钮 折叠2 隐藏值属性 = "0" @视窗.布局 = "id = 106\r\npos = \"350, 77, 83, 26\"" 标题 = "插入子成员">
    变量 按钮1 <类型 = 按钮 折叠2 隐藏值属性 = "0" @视窗.布局 = "id = 107\r\npos = \"259, 77, 83, 26\"" 标题 = "插入数据">
    变量 树形框 <类型 = 增强树形框 折叠2 隐藏值属性 = "0" @视窗.布局 = "id = 108\r\npos = \"23, 26, 166, 448\"" 标题 = "增强树形框1"
            显示加减框 = 真 显示连线 = 真 显示根部线 = 真>

    方法 按钮_被单击 <接收事件 类型 = 整数 注释 = "当按钮被单击后发送此事件">
    参数 来源对象 <类型 = 按钮 注释 = "提供事件产生的具体来源对象">
    参数 标记值 <类型 = 整数 注释 = "用户调用\"挂接事件\"命令时所提供的\"标记值\"参数值,非此方式挂接事件则本参数值固定为0.">
    {
        如果 (来源对象 == 按钮6)
        {
            取所在层级 (树形框.现行选中项)

        }
        变量 新成员名 <类型 = 文本型>
        如果 (来源对象 == 按钮5)
        {
            新成员名 = 树形框.取项目文本 (树形框.现行选中项) + 到文本 (取下级成员 (树形框.现行选中项).取成员数 ())
            树形框.加入项目 (树形框.现行选中项, 新成员名, , , , , , )

        }
        如果 (来源对象 == 按钮4)
        {
            枚举所有子成员 (树形框.现行选中项)

        }
        如果 (来源对象 == 按钮3)
        {
            取下级成员 (树形框.现行选中项)

        }
        如果 (来源对象 == 按钮2)
        {
            取同级成员 (树形框.现行选中项)

        }
        如果 (来源对象 == 按钮1)
        {
            计次循环 (5)
            {
                变量 FF <类型 = 整数>
                变量 句柄 <类型 = 变整数>
                变量 顶级名称 <类型 = 文本型>
                FF = FF + 1
                顶级名称 = "顶级" + 到文本 (取循环索引 ())
                句柄 = 树形框.加入项目 (, 顶级名称, , , , , , )
                计次循环 (3)
                {
                    树形框.加入项目 (句柄, 顶级名称 + "成员" + 到文本 (取循环索引 ()), , , , , , )

                }

            }


        }

        返回 (0)
    }

    方法 取所在层级 <类型 = 整数 注释 = "判断在第几层">
    参数 句柄 <类型 = 变整数>
    {
        变量 层级 <类型 = 整数>
        层级 = 0
        调试输出 ("cengji", 树形框.取父项目 (句柄))

        判断循环 (树形框.取父项目 (句柄) != -1)
        {
            层级 = 层级 + 1
            句柄 = 树形框.取父项目 (句柄)

        }

        调试输出 ("层级", 层级)

        返回 (层级)
    }

    方法 取同级成员 <类型 = 长整数数组类>
    参数 句柄 <类型 = 变整数>
    {
        变量 成员合集 <类型 = 长整数数组类>
        变量 临时句柄 <类型 = 变整数>
        成员合集.加入成员 (句柄)  // 先加入自身
        临时句柄 = 句柄
        判断循环 (树形框.取前兄弟项目 (临时句柄) != 0)  // 枚举前面成员
        {
            临时句柄 = 树形框.取前兄弟项目 (临时句柄)
            成员合集.加入成员 (临时句柄)
            调试输出 (22, 临时句柄, 树形框.取项目文本 (临时句柄))


        }

        临时句柄 = 句柄
        判断循环 (树形框.取后兄弟项目 (临时句柄) != 0)  // 枚举后面成员
        {
            临时句柄 = 树形框.取后兄弟项目 (临时句柄)
            成员合集.加入成员 (临时句柄)
            调试输出 (33, 临时句柄, 树形框.取项目文本 (临时句柄))

        }

        调试输出 (成员合集.取成员数 ())
        返回 (成员合集)


    }

    方法 取下级成员 <类型 = 长整数数组类 注释 = "树形框命令,枚举项目子成员句柄,返回句柄数组,本命令只枚举第一层子成员" 折叠>
    参数 句柄 <类型 = 变整数>
    {
        变量 成员合集 <类型 = 长整数数组类>
        变量 子项句柄 <类型 = 变整数>
        子项句柄 = 树形框.取首子项目 (句柄)
        成员合集.加入成员 (子项句柄)
        判断循环 (树形框.取后兄弟项目 (子项句柄) != 0)  // 枚举的时候是从第二个成员开始
        {
            成员合集.加入成员 (树形框.取后兄弟项目 (子项句柄))
            子项句柄 = 树形框.取后兄弟项目 (子项句柄)
            // 调试输出 (树形框1.取项目文本 (子项句柄), "子项文本2")
            调试输出 ("子级", 树形框.取项目文本 (子项句柄))

        }

        调试输出 (成员合集.取成员数 ())

        返回 (成员合集)

    }

    方法 枚举所有子成员 <类型 = 长整数数组类 注释 = "树形框命令,枚举项目所有子成员句柄" 折叠>
    参数 句柄 <类型 = 变整数>
    {
        变量 返回数组 <类型 = 长整数数组类>
        变量 所有成员 <类型 = 长整数数组类>
        变量 子级数组 <类型 = 长整数数组类 注释 = "当前层有子成员的数组">
        变量 FF <类型 = 整数>
        返回数组 = 取下级成员 (句柄)
        判断循环 (返回数组.取成员数 () > 0)  // 判断是否有下级成员
        {
            // 调试输出 (树形框.取项目文本 (返回数组.取成员 (0)), 树形框.是否有子项目 (返回数组.取成员 (0)))
            调试输出 ("枚举下级", 树形框.取项目文本 (返回数组.取成员 (0)))

            如果 (树形框.是否有子项目 (返回数组.取成员 (0)))
            {
                子级数组.加入成员 (返回数组.取成员 (0))

            }

            所有成员.加入成员 (返回数组.取成员 (0))
            返回数组.删除成员 (0, )

            如果真 (返回数组.取成员数 () == 0)  // 当前层项目已经枚举完毕
            {

                计次循环 (子级数组.取成员数 ())  // 将获取到有子成员的项目成员全部添加到子级成员里面
                {
                    数组合并_长整数 (返回数组, 取下级成员 (子级数组.取成员 (取循环索引 ())))

                }

                子级数组.删除所有成员 ()

            }

        }

        调试输出 (所有成员.取成员数 ())

        返回 (所有成员)
    }

    方法 数组合并_长整数 <类型 = 长整数数组类 注释 = "私有命令,只针对长整数数组" 折叠>
    参数 被添加数组 <类型 = 长整数数组类>
    参数 添加数组 <类型 = 长整数数组类>
    {
        计次循环 (添加数组.取成员数 ())
        {
            被添加数组.加入成员 (添加数组.取成员 (取循环索引 ()))

        }

        返回 (被添加数组)

    }
}
