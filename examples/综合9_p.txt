
###### 以下为一个火山软件开发平台系统类库程序的内容

<火山程序 类型 = "通常" 版本 = 1 />

包 火山.ADO数据库

类 ADO连接池类 <公开 注释 = "ADO数据库连接池">
{
    变量 连接池 <类型 = ADO数据库数组类>
    变量 成_最大连接数 <类型 = 整数>
    变量 成_连接文本 <类型 = 文本型>
    变量 成_用户名 <类型 = 文本型>
    变量 成_密码 <类型 = 文本型>

    方法 初始化 <公开 类型 = 整数 注释 = "初始化连接" 返回值注释 = "返回连接数量">
    参数 连接文本 <类型 = 文本型
            注释 = "提供包含连接信息的一个文本,具体信息可参阅文档\"https://docs.microsoft.com/zh-cn/sql/ado/reference/ado-api/connectionstring-property-ado?view=sql-server-ver15\"."
            注释 = "如果本参数为空,将使用\"连接文本\"属性作为该参数的值,如果本参数提供非空内容,将会优先于\"连接文本\"属性." @默认值 = "">
    参数 用户名 <类型 = 文本型 注释 = "提供连接该数据库所需的用户名(如果连接文本中包含,此参数将会覆盖连接文本中的对应用户名信息)." @默认值 = "">
    参数 密码 <类型 = 文本型 注释 = "提供连接该数据库所需的密码(如果连接文本中包含,此参数将会覆盖连接文本中的对应密码信息)." @默认值 = "">
    参数 最大连接数 <类型 = 整数 注释 = "连接池中所能创建的最大连接数量." @默认值 = 50>
    参数 初始连接数 <类型 = 整数 注释 = "初始化时所欲创建的可用连接数." @默认值 = 10>
    {
        成_最大连接数 = 最大连接数
        成_连接文本 = 连接文本
        成_用户名 = 用户名
        成_密码 = 密码
        计次循环 (初始连接数)
        {
            变量 数据库 <类型 = 数据库类>
            如果 (数据库.连接 (连接文本, 用户名, 密码, 真, 假))
            {
                连接池.加入成员 (数据库)
            }
        }
        返回 (连接池.取成员数 ())
    }

    方法 初始化MySQL <公开 类型 = 整数 注释 = "初始化mysql." 注释 = "注意:使用前确保系统中已安装对应的数据库驱动程序." 返回值注释 = "返回连接数量"
            编辑时信息 = "9B4AB, 0, 0, 0">
    参数 地址 <类型 = 文本型 注释 = "提供数据库连接地址." 编辑时信息 = "9B4AB, 0, 0, 0">
    参数 数据库名 <类型 = 文本型 注释 = "提供所欲打开的数据库名称." 编辑时信息 = "9B4AB, 0, 0, 0">
    参数 用户名 <类型 = 文本型 注释 = "提供连接该数据库所需的用户名." 编辑时信息 = "9B4AB, 0, 0, 0">
    参数 密码 <类型 = 文本型 注释 = "提供连接该数据库所需的密码." 编辑时信息 = "9B4AB, 0, 0, 0">
    参数 端口号 <类型 = 整数 注释 = "通过该数据库的连接端口." 编辑时信息 = "9B4AB, 0, 0, 0" @默认值 = 3306>
    参数 驱动程序 <类型 = 文本型
            注释 = "提供连接Mysql所需的驱动程序名称,例如\"MySQL ODBC 8.0 Unicode Driver\",使用前请确保系统中已安装该驱动,否则无法进行连接."
            注释 = "  驱动下载地址:https://dev.mysql.com/downloads/connector/odbc/" 编辑时信息 = "9B4AB, 0, 0, 0"
            @默认值 = "MySQL ODBC 8.0 Unicode Driver">
    参数 最大连接数 <类型 = 整数 注释 = "连接池中所能创建的最大连接数量." @默认值 = 50>
    参数 初始连接数 <类型 = 整数 注释 = "初始化时所欲创建的可用连接数." @默认值 = 10>
    {
        返回 (初始化 ("Provider=MSDASQL; DRIVER=" + 驱动程序 + "; SERVER=" + 地址 + "; PORT=" + 到文本 (端口号) + "; DATABASE=" + 数据库名 + ";", 用户名, 密码, 最大连接数, 初始连接数))
    }

    方法 初始化Oracle <公开 类型 = 整数 注释 = "开始连接到指定的远程Oracle数据库." 注释 = "注意:使用前确保系统中已安装对应的数据库驱动程序." 返回值注释 = "返回连接数量"
            折叠>
    参数 地址 <类型 = 文本型 注释 = "提供数据库连接地址.">
    参数 用户名 <类型 = 文本型 注释 = "提供连接该数据库所需的用户名.">
    参数 密码 <类型 = 文本型 注释 = "提供连接该数据库所需的密码.">
    参数 驱动程序 <类型 = 文本型
            注释 = "提供连接Oracle所需的驱动程序名称,例如\"Oracle in OraClient11g_home1\",使用前请确保系统中已安装该驱动,否则无法进行连接."
            @默认值 = "Oracle in OraClient11g_home1">
    参数 最大连接数 <类型 = 整数 注释 = "连接池中所能创建的最大连接数量." @默认值 = 50>
    参数 初始连接数 <类型 = 整数 注释 = "初始化时所欲创建的可用连接数." @默认值 = 10>
    {
        返回 (初始化 ("DRIVER=" + 驱动程序 + "; SERVER=" + 地址, 用户名, 密码, 最大连接数, 初始连接数))
    }

    方法 初始化Access <公开 类型 = 整数 注释 = "连接到指定Access数据库." 返回值注释 = "返回连接数量" 折叠>
    参数 文件路径 <类型 = 文本型 注释 = "提供数据库文件完整路径.">
    参数 密码 <类型 = 文本型 注释 = "提供连接该数据库所需的用户密码." @默认值 = "">
    参数 是否新版 <类型 = 逻辑型 注释 = "本参数指定Access版本,为假则使用2003以下连接方式,为真则使用2007版本连接方式." @默认值 = 假>
    参数 最大连接数 <类型 = 整数 注释 = "连接池中所能创建的最大连接数量." @默认值 = 50>
    参数 初始连接数 <类型 = 整数 注释 = "初始化时所欲创建的可用连接数." @默认值 = 10>
    {
        如果 (是否新版)
        {
            返回 (初始化 ("Provider=Microsoft.ACE.OLEDB.12.0; Data Source=" + 文件路径 + ";Jet OLEDB:Database Password=" + 密码, , , 最大连接数, 初始连接数))
        }
        否则
        {
            返回 (初始化 ("Provider=Microsoft.Jet.OLEDB.4.0; Data Source=" + 文件路径 + ";Jet OLEDB:Database Password=" + 密码, , , 最大连接数, 初始连接数))
        }
    }

    方法 初始化Mssql <公开 类型 = 整数 注释 = "开始连接到指定的远程Mssql数据库." 返回值注释 = "返回连接数量" 折叠>
    参数 地址 <类型 = 文本型 注释 = "提供数据库连接地址.">
    参数 数据库名 <类型 = 文本型 注释 = "提供所欲打开的数据库名称.">
    参数 用户名 <类型 = 文本型 注释 = "提供连接该数据库所需的用户名.">
    参数 密码 <类型 = 文本型 注释 = "提供连接该数据库所需的密码.">
    参数 最大连接数 <类型 = 整数 注释 = "连接池中所能创建的最大连接数量." @默认值 = 50>
    参数 初始连接数 <类型 = 整数 注释 = "初始化时所欲创建的可用连接数." @默认值 = 10>
    {
        返回 (初始化 ("Provider=SqlOleDB; SERVER=" + 地址 + "; DATABASE=" + 数据库名 + ";", 用户名, 密码, 最大连接数, 初始连接数))
    }

    方法 初始化Postgresql <公开 类型 = 整数 注释 = "开始连接到指定的远程PostgreSQL数据库." 返回值注释 = "返回连接数量" 折叠>
    参数 地址 <类型 = 文本型 注释 = "提供数据库连接地址.">
    参数 数据库名 <类型 = 文本型 注释 = "提供所欲打开的数据库名称.">
    参数 用户名 <类型 = 文本型 注释 = "提供连接该数据库所需的用户名.">
    参数 密码 <类型 = 文本型 注释 = "提供连接该数据库所需的密码.">
    参数 端口号 <类型 = 整数 注释 = "通过该数据库的连接端口." @默认值 = 5432>
    参数 驱动程序 <类型 = 文本型
            注释 = "指定是否使用ODBC驱动方式连接(例如:PostgreSQL UNICODE),提供空文本将会使用OLEDB方式连接,如果使用驱动方式连接,务必保证系统中已安装该驱动." @默认值 = "">
    参数 最大连接数 <类型 = 整数 注释 = "连接池中所能创建的最大连接数量." @默认值 = 50>
    参数 初始连接数 <类型 = 整数 注释 = "初始化时所欲创建的可用连接数." @默认值 = 10>
    {
        如果 (文本是否为空 (驱动程序))
        {
            返回 (初始化 ("Provider=PostgreSQL OLE DB; Data Source=" + 地址 + "; location=" + 数据库名 + ";", 用户名, 密码, 最大连接数, 初始连接数))
        }
        否则
        {
            返回 (初始化 ("Provider=MSDASQL; DRIVER=" + 驱动程序 + "; SERVER=" + 地址 + "; PORT=" + 到文本 (端口号) + "; DATABASE=" + 数据库名 + ";", 用户名, 密码, 最大连接数, 初始连接数))
        }
    }

    变量 已用索引 <类型 = 整数数组类>
    变量 已用连接 <类型 = 对象数组类>

    方法 释放连接 <公开>
    参数 连接对象 <类型 = 数据库类>
    {
        已用连接.枚举循环 ()
        {
            如果 ((数据库类)已用连接.取枚举值 () == 连接对象)
            {
                已用连接.删除成员 (对象数组类.取枚举索引 ())
                已用索引.删除成员 (已用索引.取成员数 () - 1)
            }
        }
    }

    方法 释放所有连接 <公开 折叠>
    {
        已用连接.删除所有成员 ()
        已用索引.删除所有成员 ()
    }

    方法 取空闲连接数 <公开 类型 = 整数 折叠>
    {
        返回 (连接池.取成员数 () - 已用索引.取成员数 ())
    }

    方法 取连接 <公开 类型 = 逻辑型>
    参数 结果连接 <类型 = 数据库类>
    参数 超时 <类型 = 整数 注释 = "单位秒" @默认值 = 10>
    {
        如果 (已用索引.取成员数 () == 0)
        {
            如果 (连接池.取成员数 () > 0)
            {
                结果连接 = 连接池.取成员 (0)
                已用连接.加入成员 (结果连接)
                已用索引.加入成员 (0)
                返回 (真)
            }
            返回 (假)
        }
        否则
        {
            变量 空闲索引 <类型 = 整数>
            空闲索引 = 取空闲索引 ()
            如果 (空闲索引 != -1)
            {
                结果连接 = 连接池.取成员 (空闲索引)
                已用连接.加入成员 (结果连接)
                已用索引.加入成员 (空闲索引)
                返回 (真)
            }
            否则
            {
                变量 开始时间 <类型 = 整数>
                开始时间 = 取时间戳 (取现行时间 ())
                判断循环 (真)
                {
                    如果 (成_最大连接数 != 连接池.取成员数 ())
                    {
                        变量 数据库 <类型 = 数据库类>
                        判断循环 (真)
                        {
                            如果真 (数据库.连接 (成_连接文本, 成_用户名, 成_密码, 真, 假))
                            {
                                已用索引.加入成员 (连接池.加入成员 (数据库))
                                已用连接.加入成员 (数据库)
                                结果连接 = 数据库
                                返回 (真)
                            }
                            如果真 (取时间戳 (取现行时间 ()) - 开始时间 >= 10)
                            {
                                返回 (假)
                            }
                        }
                    }
                    否则
                    {
                        判断循环 (取空闲索引 () == -1)
                        {
                            如果真 (取时间戳 (取现行时间 ()) - 开始时间 >= 10)
                            {
                                返回 (假)
                            }
                            变量 空闲 <类型 = 整数>
                            空闲 = 取空闲索引 ()
                            如果 (空闲 != -1)
                            {
                                结果连接 = 连接池.取成员 (空闲索引)
                                已用索引.加入成员 (空闲索引)
                                已用连接.加入成员 (结果连接)
                                返回 (真)
                            }
                        }
                        变量 空闲 <类型 = 整数>
                        空闲 = 取空闲索引 ()
                        如果 (空闲 != -1)
                        {
                            结果连接 = 连接池.取成员 (空闲索引)
                            已用索引.加入成员 (空闲索引)
                            已用连接.加入成员 (结果连接)
                            返回 (真)

                        }

                    }
                    如果真 (取时间戳 (取现行时间 ()) - 开始时间 >= 10)
                    {
                        返回 (假)
                    }

                }
                返回 (假)

            }
            返回 (假)
        }
    }

    方法 取空闲索引 <类型 = 整数 注释 = "已用索引中，存在连接池索引,失败返回-1">
    {
        连接池.枚举循环 ()
        {
            变量 索引 <类型 = 整数>
            索引 = ADO数据库数组类.取枚举索引 ()
            如果 (索引是否存在 (索引) == 假)
            {
                返回 (索引)
            }
        }
        返回 (-1)
    }

    方法 索引是否存在 <类型 = 逻辑型>
    参数 连接池索引 <类型 = 整数>
    {
        已用索引.枚举循环 ()
        {
            如果 (已用索引.取枚举值 () == 连接池索引)
            {
                返回 (真)
            }
        }
        返回 (假)
    }
}

类 ADO数据库数组类 <基础类 = 对象数组模板类 @模板实现类 = "数据库类">
{

    #
}

【分隔符】


###### 以下为一个火山软件开发平台系统类库程序的内容

<火山程序 类型 = "通常" 版本 = 1 />

包 火山.程序 <注释 = "无名模块：http://m.vowm.cn">

类 启动类 <公开 基础类 = 窗口程序类>
{
    变量 主窗口对象 <类型 = 我的主窗口>

    方法 启动方法 <公开 类型 = 整数>
    {
        主窗口对象.创建主窗口 ()
        返回 (1)
    }
}

# ===

类 我的主窗口 <基础类 = 窗口 注释 = "无名模块：http://m.vowm.cn" 标题 = "ADO连接池类" @视窗.布局 = "client_size = \"500, 300\"">
{
    变量 连接池 <类型 = ADO连接池类>
    变量 数据库 <类型 = 数据库类>

    方法 我的主窗口_创建完毕 <接收事件 类型 = 整数>
    参数 来源对象 <类型 = 我的主窗口>
    参数 标记值 <类型 = 整数>
    {
        调试输出 (连接池.初始化MySQL ("127.0.0.1", "huoshan", "huoshan", "huoshan", ))
        如果 (连接池.取连接 (数据库))
        {
            信息框 ("取连接成功")
            变量 记录集 <类型 = 记录集类>
            记录集 = 数据库.执行SQL ("SELECT * FROM user")
            调试输出 (记录集.记录数量)
        }
        否则
        {
            信息框 ("失败。。。")
        }
        返回 (0)
    }
}

【分隔符】


###### 以下为一个火山软件开发平台系统类库程序的内容

<火山程序 类型 = "通常" 版本 = 1 />

包 火山.程序 <"">

类 启动类 <公开 基础类 = 窗口程序类>
{
    变量 主窗口对象 <公开 类型 = 我的主窗口>

    方法 启动方法 <公开 类型 = 整数>
    {
        主窗口对象.创建主窗口 ()
        返回 (1)
    }
}

类 我的主窗口 <基础类 = 窗口 注释 = "样例主窗口" @视窗.布局 = "client_size = \"388, 301\"" 标题 = "鼠标放大镜" "">
{
    变量 画板_预览 <类型 = 画板 隐藏值属性 = "0" @视窗.布局 = "id = 101\r\npos = \"109, 14, 260, 260\"" 组件边框 = 单线边框式
            绘画单位 = 像素点 画笔类型 = 直线 画出方式 = 复制笔 刷子类型 = 颜色刷子 自动重画 = 真>
    变量 按钮_放大 <类型 = 按钮 隐藏值属性 = "0" @视窗.布局 = "id = 102\r\npos = \"21, 14, 80, 32\"" 标题 = "放大">
    变量 时钟1 <类型 = 时钟 隐藏值属性 = "0" @视窗.布局 = "id = 103\r\npos = \"45, 94, 26, 26\"">

    方法 我的主窗口_创建完毕 <接收事件 类型 = 整数 注释 = "当本组件及其中所有子组件均被创建完毕后发送此事件.">
    参数 来源对象 <类型 = 我的主窗口 注释 = "提供事件产生的具体来源对象">
    参数 标记值 <类型 = 整数 注释 = "用户调用\"挂接事件\"命令时所提供的\"标记值\"参数值,非此方式挂接事件则本参数值固定为0.">
    {

        返回 (0)
    }

    变量 成_是否可捕获 <类型 = 逻辑型 值 = 假>

    方法 按钮_被单击 <接收事件 类型 = 整数 注释 = "当按钮被单击后发送此事件">
    参数 来源对象 <类型 = 按钮 注释 = "提供事件产生的具体来源对象">
    参数 标记值 <类型 = 整数 注释 = "用户调用\"挂接事件\"命令时所提供的\"标记值\"参数值,非此方式挂接事件则本参数值固定为0.">
    {
        如果 (来源对象 == 按钮_放大)
        {
            处理事件 ()
            // 捕获鼠标 (按钮_放大.取窗口句柄 ())
            时钟1.时钟周期 = 10
            成_是否可捕获 = 真
        }
        返回 (0)
    }

    方法 捕获鼠标 <公开 静态 类型 = 变整数 @嵌入式方法 = "">
    参数 窗口句柄 <类型 = 变整数>
    {
        @ SetCapture((HWND)@<窗口句柄>)
    }

    方法 释放鼠标 <公开 静态 类型 = 逻辑型 @嵌入式方法 = "">
    {
        @ ReleaseCapture()
    }

    方法 时钟_周期事件 <接收事件 类型 = 整数 注释 = "每当经过了指定的时钟周期,将发送本事件.">
    参数 来源对象 <类型 = 时钟 注释 = "提供事件产生的具体来源对象">
    参数 标记值 <类型 = 整数 注释 = "用户调用\"挂接事件\"命令时所提供的\"标记值\"参数值,非此方式挂接事件则本参数值固定为0.">
    {
        如果 (来源对象 == 时钟1)
        {
            变量 鼠标水平位置 <类型 = 整数>
            变量 鼠标垂直位置 <类型 = 整数>
            鼠标水平位置 = 取鼠标水平位置 ()
            鼠标垂直位置 = 取鼠标垂直位置 ()
            变量 矩形区域 <类型 = 矩形类>
            矩形区域.左边 = 选择 (鼠标水平位置 < 30, 0, 鼠标水平位置 - 30)
            矩形区域.顶边 = 选择 (鼠标垂直位置 < 30, 0, 鼠标垂直位置 - 30)
            矩形区域.右边 = 选择 (鼠标水平位置 > 取屏幕宽度 () - 30, 取屏幕宽度 (), 鼠标水平位置 + 30)
            矩形区域.底边 = 选择 (鼠标垂直位置 > 取屏幕高度 () - 30, 取屏幕高度 (), 鼠标垂直位置 + 30)
            变量 快照图片 <类型 = 位图对象类>
            快照 (快照图片)
            变量 图像处理 <类型 = 图像处理类>
            图像处理.创建自位图对象 (快照图片)
            图像处理.裁剪2 (矩形区域)
            画板_预览.画图片 (图像处理.到位图对象 (), 0, 0, 画板_预览.宽度, 画板_预览.高度, 图像复制方法.拷贝)
            画板_预览.画笔颜色 = 颜色类.红色
            画板_预览.画直线 (0, 画板_预览.高度 / 2, 画板_预览.宽度, 画板_预览.高度 / 2)
            画板_预览.画直线 (画板_预览.宽度 / 2, 0, 画板_预览.宽度 / 2, 画板_预览.高度)
        }
        返回 (0)
    }
}

【分隔符】


###### 以下为一个火山软件开发平台系统类库程序的内容

<火山程序 类型 = "通常" 版本 = 1 />

包 火山.计算器 <注释 = "原作者：https://bbs.voldp.com/thread-290-1-1.html" 注释 = "修改作者：创世魂" 注释 = "时间：2023年12月30日">

类 函数数组类 <公开 基础类 = 对象数组模板类 @模板实现类 = "函数计算类">

# --

类 函数计算类 <公开 "//@模板实现类" = "函数计算数据类">
{
    方法 取函数名 <公开 类型 = 文本型 @虚拟方法 = 可覆盖>
    {
        返回 ("")
    }

    方法 取参数个数 <公开 类型 = 整数 @虚拟方法 = 可覆盖>
    {
        返回 (0)
    }

    方法 运行方法 <公开 类型 = 小数 @虚拟方法 = 可覆盖>
    参数 参数 <类型 = 小数数组类>
    {
        返回 (0)
    }
}

# --

类 派 <公开 基础类 = 函数计算类 注释 = "取π值函数例子" "">
{
    方法 取函数名 <公开 类型 = 文本型 @虚拟方法 = 可覆盖>
    {
        返回 ("pai")
    }

    方法 取参数个数 <公开 类型 = 整数 @虚拟方法 = 可覆盖>
    {
        返回 (0)
    }

    方法 运行方法 <公开 类型 = 小数 @虚拟方法 = 可覆盖>
    参数 参数 <类型 = 小数数组类>
    {
        返回 (3.1415926)
    }
}

类 幂 <公开 基础类 = 函数计算类 注释 = "幂运算函数例子" "">
{
    方法 取函数名 <公开 类型 = 文本型 @虚拟方法 = 可覆盖>
    {
        返回 ("pow")
    }

    方法 取参数个数 <公开 类型 = 整数 @虚拟方法 = 可覆盖>
    {
        返回 (2)
    }

    方法 运行方法 <公开 类型 = 小数 @虚拟方法 = 可覆盖>
    参数 参数 <类型 = 小数数组类>
    {
        返回 (求次方 (参数.取成员 (0), 参数.取成员 (1)))
    }
}

# --

类 计算器 <公开 "">
{
    变量 函数数组 <类型 = 函数数组类>
    变量 私_表达式 <类型 = 文本型 值 = "">
    变量 字 <类型 = 文本型 值 = "">
    变量 位置 <类型 = 整数 值 = 0>
    变量 语法错误 <类型 = 逻辑型 值 = 假>
    变量 运行错误 <类型 = 文本型 值 = "">

    方法 添加函数 <公开 类型 = 逻辑型>
    参数 例程 <类型 = 函数计算类>
    {
        如果 (查找函数 (例程.取函数名 ()) == -1)
        {
            调试输出 ("加入函数成功")
            函数数组.加入成员 (例程)
            返回 (真)
        }
        返回 (假)
    }

    方法 查找函数 <类型 = 整数>
    参数 函数名 <类型 = 文本型>
    {
        计次循环 (函数数组.取成员数 ())
        {
            如果 (函数数组.取成员 (取循环索引 ()).取函数名 () == 函数名)
            {
                返回 (取循环索引 ())
            }
        }
        返回 (-1)
    }

    方法 下一个字 <公开 类型 = 文本型 折叠>
    {
        变量 结果 <类型 = 文本型>
        如果 (位置 >= 取文本长度 (私_表达式))
        {
            返回 ("")
        }
        结果 = 取文本中间 (私_表达式, 位置, 1)
        // 结果 = 取区域子文本 (私_表达式, 位置, 位置 + 1)
        位置 = 位置 + 1
        返回 (结果)
    }

    方法 为串子集 <公开 静态 类型 = 逻辑型 折叠>
    参数 串 <类型 = 文本型>
    参数 目标 <类型 = 文本型>
    {
        计次循环 (取文本长度 (串))
        {
            // 调试输出 (取字符 (目标, 0), 取字符 (串, 取循环索引 ()))
            如果 (取字符 (目标, 0) == 取字符 (串, 取循环索引 ()))
            {
                返回 (真)
            }
        }
        返回 (假)
    }

    方法 为整数 <公开 静态 类型 = 逻辑型 折叠>
    参数 目标 <类型 = 文本型>
    {
        如果 (文本_对比 (目标, ""))
        {
            返回 (假)
        }
        返回 (为串子集 ("1234567890", 目标))
    }

    方法 为特殊符号 <公开 静态 类型 = 逻辑型 折叠>
    参数 目标 <类型 = 文本型>
    {
        如果 (文本_对比 (目标, ""))
        {
            返回 (真)
        }
        返回 (为串子集 ("~`!@#$%^&*()_+=-{}[]|\\;:\'\"<>,.?/ \f\b\t\r\n", 目标))
    }

    方法 为标识符 <公开 类型 = 逻辑型 折叠>
    参数 目标 <类型 = 文本型>
    {
        如果 (文本_对比 (目标, ""))
        {
            返回 (假)

        }
        如果 (为整数 (目标) == 假 && 为特殊符号 (目标) == 假)
        {
            返回 (真)

        }
        返回 (假)
    }

    方法 函数 <类型 = 小数>
    {
        变量 结果 <类型 = 小数 值 = 0>
        变量 函数名 <类型 = 文本型 值 = "">
        变量 参数列表 <类型 = 小数数组类>
        变量 函数位置 <类型 = 整数 值 = -1>
        函数名 = 函数名 + 字
        字 = 下一个字 ()
        判断循环 (为特殊符号 (字) == 假)
        {
            函数名 = 函数名 + 字
            字 = 下一个字 ()

        }
        如果 (文本_对比 (字, "(") == 假)
        {
            语法错误 = 真

        }
        字 = 下一个字 ()
        判断循环 (文本_对比 (字, " "))
        {
            字 = 下一个字 ()

        }
        如果 (为整数 (字) || 文本_对比 (字, "(") || 文本_对比 (字, "-") || 为标识符 (字))
        {
            位置 = 位置 - 1
            参数列表.加入成员 (表达式 ())
            判断循环 (文本_对比 (字, ","))
            {
                参数列表.加入成员 (表达式 ())

            }

        }
        如果 (文本_对比 (字, ")") == 假)
        {
            语法错误 = 真

        }
        字 = 下一个字 ()
        函数位置 = 查找函数 (函数名)
        如果 (函数位置 != -1)
        {
            调试输出 (函数数组.取成员 (函数位置).取函数名 (), 函数数组.取成员 (函数位置).运行方法 (参数列表))

            如果 (函数数组.取成员 (函数位置).取参数个数 () != 参数列表.取成员数 ())
            {
                语法错误 = 真
            }
            否则
            {
                结果 = 函数数组.取成员 (函数位置).运行方法 (参数列表)

            }
        }
        否则
        {
            语法错误 = 真

        }
        返回 (结果)

    }

    方法 因子 <类型 = 小数 "">
    {
        变量 结果 <类型 = 小数 值 = 0>
        变量 临时结果 <类型 = 文本型 值 = "">
        字 = 下一个字 ()
        判断循环 (文本_对比 (字, " ") && 文本_对比 (字, " "))
        {
            字 = 下一个字 ()

        }
        如果 (文本_对比 (字, "("))
        {
            结果 = 表达式 ()
            如果 (文本_对比 (字, ")") == 假)
            {
                语法错误 = 真

            }
            字 = 下一个字 ()
        }
        否则 (为整数 (字))
        {
            判断循环 (为整数 (字))
            {
                临时结果 = 临时结果 + 字
                字 = 下一个字 ()

            }
            如果 (文本_对比 (字, "."))
            {
                临时结果 = 临时结果 + 字
                字 = 下一个字 ()
                判断循环 (为整数 (字))
                {
                    临时结果 = 临时结果 + 字
                    字 = 下一个字 ()

                }

            }
        }
        否则 (文本_对比 (字, "-"))
        {
            结果 = -因子 ()
        }
        否则 (为标识符 (字))
        {
            结果 = 函数 ()
        }
        否则
        {
            语法错误 = 真

        }
        判断循环 (文本_对比 (字, " ") && 文本_对比 (字, " "))
        {
            字 = 下一个字 ()

        }
        如果 (文本_对比 (临时结果, "") == 假)
        {
            结果 = 文本到小数 (临时结果)
        }
        返回 (结果)

    }

    方法 项 <类型 = 小数>
    {
        变量 结果 <类型 = 小数 值 = 0>
        变量 临时数 <类型 = 小数>
        结果 = 因子 ()

        判断循环 (文本_对比 (字, "*") || 文本_对比 (字, "/"))
        {
            如果 (文本_对比 (字, "*"))
            {
                结果 = 结果 * 因子 ()
            }
            否则
            {
                临时数 = 因子 ()
                如果 (临时数 == 0 && 结果 == 0)
                {
                    运行错误 = "结果未定义"
                }
                否则 (临时数 == 0)
                {
                    运行错误 = "除数不能为零"
                }
                否则
                {
                    结果 = 结果 / 临时数

                }

            }

        }
        返回 (结果)

    }

    方法 表达式 <类型 = 小数>
    {
        变量 结果 <类型 = 小数 值 = 0>
        结果 = 项 ()

        判断循环 (文本_对比 (字, "+") || 文本_对比 (字, "-"))
        {
            如果 (文本_对比 (字, "+"))
            {
                结果 = 结果 + 项 ()
            }
            否则
            {
                结果 = 结果 - 项 ()
            }

        }
        返回 (结果)

    }

    方法 初始化 <折叠>
    参数 公式 <类型 = 文本型>
    {
        字 = ""
        位置 = 0
        语法错误 = 假
        运行错误 = ""
        私_表达式 = 公式
    }

    方法 计算 <公开 类型 = 文本型>
    参数 公式 <类型 = 文本型>
    {
        变量 临时 <类型 = 小数>
        初始化 (公式)
        临时 = 表达式 ()

        如果 (字 != "" && 文本_对比 (字, "") == 假)
        {
            语法错误 = 真
        }
        如果 (语法错误)
        {
            返回 ("语法错误")

        }
        如果 (文本_对比 (运行错误, "") == 假)
        {
            返回 (运行错误)
        }
        返回 (到文本 (临时))
    }

    方法 文本_对比 <公开 静态 类型 = 逻辑型>
    参数 参1 <类型 = 文本型>
    参数 参2 <类型 = 文本型>
    {
        返回 (参1 == 参2)


    }
}

#
