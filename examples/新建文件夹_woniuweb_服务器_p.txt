
###### 以下为一个火山软件开发平台系统类库程序的内容

<火山程序 类型 = "通常" 版本 = 1 />

包 蜗牛服务器.服务器

类 网站编码 <公开 折叠 @常量类 = 整数>
{
    常量 GBK <公开 值 = 0>
    常量 UTF8 <公开 值 = 1>

    #
}

类 视图
{
    变量 停用视图 <公开 类型 = 逻辑型 注释 = "停用当前视图...这个功能先不做....">
    变量 匹配类 <公开 类型 = w_文本匹配类 注释 = "用于匹配请求文件">
    变量 订阅路径 <公开 类型 = 文本型>
    变量 视图函数 <公开 类型 = 变整数>
    变量 用户参数 <公开 类型 = 变整数 注释 = "用户订阅时提供的参数">
    变量 用户文本参数 <公开 类型 = 文本型 注释 = "用户订阅时提供的参数">
}

类 错误视图
{
    变量 停用视图 <公开 类型 = 逻辑型 注释 = "停用当前视图...这个功能先不做....">
    变量 视图函数 <公开 类型 = 变整数>
    变量 用户参数 <公开 类型 = 变整数 注释 = "用户订阅时提供的参数">
    变量 用户文本参数 <公开 类型 = 文本型 注释 = "用户订阅时提供的参数">

    #
}

类 网站 <公开>
{
    变量 域名 <公开 类型 = 文本型 注释 = "127.0.0.1">
    变量 模糊匹配 <公开 类型 = 逻辑型>
    变量 视图数组 <公开 类型 = 变整数数组类 注释 = "指针 一个网站可以有很多个视图函数">
    变量 m_404视图 <公开 类型 = 变整数数组类 注释 = "指针 错误视图">
    变量 网站编码 <公开 类型 = 网站编码 注释 = "0=GBK        1=UTF-8">
    变量 模板 <公开 类型 = 模板引擎>
    变量 模板目录 <公开 类型 = 文本型>
    变量 静态文件目录 <公开 类型 = 文本型>
    变量 静态文件缓存时间 <公开 类型 = 整数 注释 = "单位:秒,如果为0,则使用304缓存">
    变量 服务器名称 <公开 类型 = 文本型>
    变量 GZIP压缩 <公开 类型 = 逻辑型>
    变量 Session名称 <公开 类型 = 文本型>
    变量 Session缓存 <公开 类型 = w_内存缓存对象>
    变量 Session缓存时间 <公开 类型 = 整数>

    方法 类_清理 <折叠>
    {
        变量 i <类型 = 整数>
        循环 (0, 视图数组.取成员数 (), i)
        {
            销毁对象指针 (视图数组.取成员 (i))
        }
    }

    方法 匹配域名 <公开 类型 = 逻辑型 折叠>
    参数 要匹配的域名 <类型 = 文本型>
    {
        如果 (模糊匹配)
        {
            返回 (w_文本_模糊匹配 (要匹配的域名, 域名))
        }
        返回 (要匹配的域名 == 域名)
    }

    方法 调用视图函数 <公开 类型 = 逻辑型>
    参数 请求文件 <类型 = 文本型>
    参数 请求信息指针 <类型 = 变整数>
    参数 请求参数 <类型 = 哈希表>
    {
        变量 i <类型 = 整数>
        变量 视图对象 <类型 = 变整数>
        循环 (0, 视图数组.取成员数 (), i)
        {
            视图对象 = 视图数组.取成员 (i)
            如果 (读指针处对象 (视图对象, 视图).匹配类.匹配 (请求文件, 请求参数, 真, 真))  // 视图匹配成功!
            {
                变量 回调函数 <类型 = 变整数>
                变量 返回值 <类型 = 变整数>
                变量 标签 <类型 = 文本数组类>
                回调函数 = 读指针处对象 (视图对象, 视图).视图函数
                读指针处对象 (请求信息指针, 请求信息).用户文本参数 = 读指针处对象 (视图对象, 视图).用户文本参数
                读指针处对象 (请求信息指针, 请求信息).用户数值参数 = 读指针处对象 (视图对象, 视图).用户参数
                读指针处对象 (请求信息指针, 请求信息).订阅路径 = 读指针处对象 (视图对象, 视图).订阅路径

                返回值 = w_call_0 (回调函数)
                如果 (返回值 == 1)  // 用户不要...
                {
                    到循环尾
                }
                返回 (真)
            }
        }
        返回 (假)
    }

    方法 调用404函数 <公开 类型 = 逻辑型>
    参数 请求文件 <类型 = 文本型>
    参数 请求信息指针 <类型 = 变整数>
    参数 请求参数 <类型 = 哈希表>
    {
        变量 i <类型 = 整数>
        变量 回调函数 <类型 = 变整数>
        变量 返回值 <类型 = 变整数>
        变量 对象 <类型 = 变整数>
        循环 (0, m_404视图.取成员数 (), i)
        {
            对象 = m_404视图.取成员 (i)
            回调函数 = 读指针处对象 (对象, 错误视图).视图函数
            读指针处对象 (请求信息指针, 请求信息).用户文本参数 = 读指针处对象 (对象, 错误视图).用户文本参数
            读指针处对象 (请求信息指针, 请求信息).订阅路径 = ""
            读指针处对象 (请求信息指针, 请求信息).用户数值参数 = 读指针处对象 (对象, 错误视图).用户参数

            返回值 = w_call_0 (回调函数)
            如果 (返回值 == 1)  // 用户不要...
            {
                到循环尾
            }
            返回 (真)
        }
        返回 (假)
    }
}

类 服务器 <公开>
{
    变量 m_服务器 <公开 类型 = HP_HTTP服务器>
    变量 m_网站列表 <类型 = 变整数数组类 注释 = "指针">
    变量 m_服务器锁 <类型 = w_读写锁 注释 = "保护 \"m_网站列表\"">
    变量 m_POST上传大小限制 <公开 类型 = 整数>

    方法 类_初始化

    方法 类_清理 <折叠>
    {
        变量 i <类型 = 整数>
        变量 对象指针 <类型 = 变整数>
        循环 (0, m_网站列表.取成员数 (), i)
        {
            对象指针 = m_网站列表.取成员 (i)
            销毁对象指针 (对象指针)
        }

    }

    方法 域名_创建 <公开 类型 = 变整数 返回值注释 = "失败返回 0 成功返回 域名对象指针...">
    参数 域名 <类型 = 文本型 注释 = "支持 * 比如:\"*.baidu.com\"" @默认值 = "*">
    参数 网站编码 <类型 = 网站编码 @默认值 = 网站编码.UTF8>
    参数 模板目录 <类型 = 文本型>
    参数 静态文件目录 <类型 = 文本型>
    参数 静态文件缓存时间 <类型 = 整数 注释 = "如果为0,则使用304缓存!,如果不为0,则缓存指定秒数" @默认值 = 0>
    参数 压缩HTML <类型 = 逻辑型>
    参数 服务器名称 <类型 = 文本型 @默认值 = "woniu">
    参数 GZIP压缩 <类型 = 逻辑型>
    参数 Session名称 <类型 = 文本型>
    参数 Session缓存时间 <类型 = 整数 注释 = "单位:秒">
    {
        变量 对象指针 <类型 = 变整数>
        变量 i <类型 = 整数>
        如果 (域名 == "")
        {
            返回 (0)
        }
        如果 (静态文件目录 == 取运行目录 () || 静态文件目录 + "\\" == 取运行目录 ())
        {
            w_信息框 ("静态文件目录,不能为程序的根目录,有可能会被别人偷走东西哦...", "创建域名>报错", 16)
            返回 (0)
        }
        m_服务器锁.写 ()
        循环 (0, m_网站列表.取成员数 (), i)
        {
            对象指针 = m_网站列表.取成员 (i)
            如果 (读指针处对象 (对象指针, 网站).域名 == 域名)
            {
                m_服务器锁.结束写 ()
                返回 (对象指针)  // 已经创建过了...
            }
        }
        对象指针 = 创建对象指针 (网站)
        如果 (对象指针 == -1)  // 创建失败...
        {
            m_服务器锁.结束写 ()
            返回 (0)
        }
        变量 缓存时间 <类型 = 整数>
        缓存时间 = 选择 (Session缓存时间 <= 0, 30 * 60, Session缓存时间)

        读指针处对象 (对象指针, 网站).域名 = 域名
        读指针处对象 (对象指针, 网站).模糊匹配 = 寻找字符 (域名, '*') != -1
        读指针处对象 (对象指针, 网站).网站编码 = 网站编码
        读指针处对象 (对象指针, 网站).模板.设置 (模板目录, 压缩HTML, 压缩HTML, 0)
        读指针处对象 (对象指针, 网站).模板目录 = 模板目录
        读指针处对象 (对象指针, 网站).静态文件目录 = 静态文件目录
        读指针处对象 (对象指针, 网站).静态文件缓存时间 = 静态文件缓存时间
        读指针处对象 (对象指针, 网站).服务器名称 = 服务器名称
        读指针处对象 (对象指针, 网站).GZIP压缩 = GZIP压缩
        读指针处对象 (对象指针, 网站).Session名称 = 选择 (文本是否为空 (Session名称), "woniuweb", Session名称)

        读指针处对象 (对象指针, 网站).Session缓存.初始化 (缓存时间)
        读指针处对象 (对象指针, 网站).Session缓存时间 = 缓存时间


        m_网站列表.加入成员 (对象指针)
        m_服务器锁.结束写 ()
        返回 (对象指针)

    }

    方法 域名_取对象指针 <公开 类型 = 变整数 返回值注释 = "没有这个域名返回 0  成功返回对象指针" 折叠>
    参数 域名 <类型 = 文本型 注释 = "不支持通配符" "">
    {
        变量 i <类型 = 整数>
        变量 对象指针 <类型 = 变整数>
        m_服务器锁.读 ()
        循环 (0, m_网站列表.取成员数 (), i)
        {
            对象指针 = m_网站列表.取成员 (i)
            如果 (读指针处对象 (对象指针, 网站).域名 == 域名)
            {
                m_服务器锁.结束读 ()
                返回 (对象指针)
            }
        }
        m_服务器锁.结束读 ()
        返回 (0)
    }

    方法 域名_取数量 <公开 类型 = 整数 折叠>
    {
        变量 返回值 <类型 = 整数>
        m_服务器锁.读 ()
        返回值 = m_网站列表.取成员数 ()
        m_服务器锁.结束读 ()
        返回 (返回值)
    }

    方法 域名_取域名 <公开 类型 = 文本型 折叠>
    参数 索引 <类型 = 整数>
    {
        变量 域名 <类型 = 文本型>
        m_服务器锁.读 ()
        如果 (m_网站列表.索引是否有效 (索引) == 假)
        {
            m_服务器锁.结束读 ()
            返回 ("")
        }
        域名 = 读指针处对象 (m_网站列表.取成员 (索引), 网站).域名
        m_服务器锁.结束读 ()
        返回 (域名)

    }

    方法 域名_删除 <公开 类型 = 逻辑型 折叠>
    参数 域名对象 <类型 = 变整数 注释 = "创建域名 的返回值" "">
    {
        变量 i <类型 = 整数>
        m_服务器锁.写 ()
        循环 (0, m_网站列表.取成员数 (), i)
        {
            如果 (m_网站列表.取成员 (i) == 域名对象)
            {
                m_网站列表.删除成员 (i)
                销毁对象指针 (域名对象)
                m_服务器锁.结束写 ()
                返回 (真)  // 删除成功
            }
        }
        m_服务器锁.结束写 ()
        返回 (假)  // 域名,不是我管理的...或者就没有这个域名..

    }

    方法 域名_注册模板函数 <公开 类型 = 逻辑型 折叠>
    参数 网站对象 <类型 = 变整数 注释 = "创建域名() 的返回值!">
    参数 注册函数名 <类型 = 文本型>
    参数 函数地址 <类型 = 变整数>
    {
        变量 返回值 <类型 = 逻辑型>
        如果 (网站对象 == 0)
        {
            返回 (假)
        }
        m_服务器锁.写 ()
        返回值 = 读指针处对象 (网站对象, 网站).模板.注册模板函数 (注册函数名, 函数地址)
        m_服务器锁.结束写 ()
        返回 (返回值)

    }

    方法 域名_取消模板函数 <公开 类型 = 逻辑型 折叠>
    参数 网站对象 <类型 = 变整数 注释 = "创建域名() 的返回值!">
    参数 注册函数名 <类型 = 文本型>
    {
        变量 返回值 <类型 = 逻辑型>
        如果 (网站对象 == 0)
        {
            返回 (假)
        }
        m_服务器锁.写 ()
        返回值 = 读指针处对象 (网站对象, 网站).模板.取消模板函数 (注册函数名)
        m_服务器锁.结束写 ()
        返回 (返回值)

    }

    方法 视图_删除 <公开 类型 = 逻辑型>
    参数 网站对象 <类型 = 变整数 注释 = "创建域名() 的返回值!">
    参数 订阅路径 <类型 = 文本型>
    {
        变量 i <类型 = 整数>
        变量 视图对象 <类型 = 变整数>
        如果 (网站对象 == 0)
        {
            返回 (假)
        }
        m_服务器锁.写 ()
        循环 (0, 读指针处对象 (网站对象, 网站).视图数组.取成员数 (), i)
        {
            视图对象 = 读指针处对象 (网站对象, 网站).视图数组.取成员 (i)
            如果 (读指针处对象 (视图对象, 视图).订阅路径 == 订阅路径)
            {
                销毁对象指针 (视图对象)
                读指针处对象 (网站对象, 网站).视图数组.删除成员 (i)
                m_服务器锁.结束写 ()
                返回 (真)
            }
        }
        m_服务器锁.结束写 ()
        返回 (假)
    }

    方法 视图_订阅404 <公开 类型 = 逻辑型 折叠>
    参数 网站对象 <类型 = 变整数 注释 = "创建域名() 的返回值!">
    参数 视图函数 <类型 = 变整数>
    参数 用户参数 <类型 = 变整数>
    参数 用户文本参数 <类型 = 文本型 注释 = "....">
    {
        如果 (网站对象 == 0 || 视图函数 == 0)
        {
            返回 (假)
        }
        变量 对象指针 <类型 = 变整数>
        对象指针 = 创建对象指针 (错误视图)
        如果 (对象指针 == 0)
        {
            返回 (假)
        }
        读指针处对象 (对象指针, 错误视图).用户文本参数 = 用户文本参数
        读指针处对象 (对象指针, 错误视图).视图函数 = 视图函数
        读指针处对象 (对象指针, 错误视图).停用视图 = 假
        读指针处对象 (对象指针, 错误视图).用户参数 = 用户参数
        m_服务器锁.写 ()
        读指针处对象 (网站对象, 网站).m_404视图.加入成员 (对象指针)
        m_服务器锁.结束写 ()
        返回 (真)
    }

    方法 视图_订阅 <公开 类型 = 逻辑型>
    参数 网站对象 <类型 = 变整数 注释 = "创建域名() 的返回值!">
    参数 视图函数 <类型 = 变整数>
    参数 订阅路径 <类型 = 文本型 注释 = "/{栏目}/{编号}.html">
    参数 用户参数 <类型 = 变整数>
    参数 用户文本参数 <类型 = 文本型 注释 = "....">
    {
        如果 (网站对象 == 0 || 视图函数 == 0 || 文本是否为空 (订阅路径))
        {
            返回 (假)
        }
        变量 对象指针 <类型 = 变整数>
        对象指针 = 创建对象指针 (视图)
        如果 (对象指针 == 0)
        {
            返回 (假)
        }
        如果 (读指针处对象 (对象指针, 视图).匹配类.创建 (订阅路径) == 假)
        {
            销毁对象指针 (对象指针)
            返回 (假)
        }
        读指针处对象 (对象指针, 视图).用户文本参数 = 用户文本参数
        读指针处对象 (对象指针, 视图).视图函数 = 视图函数
        读指针处对象 (对象指针, 视图).停用视图 = 假
        读指针处对象 (对象指针, 视图).订阅路径 = 订阅路径
        读指针处对象 (对象指针, 视图).用户参数 = 用户参数

        m_服务器锁.写 ()
        读指针处对象 (网站对象, 网站).视图数组.加入成员 (对象指针)
        m_服务器锁.结束写 ()

        返回 (真)



    }

    方法 服务器_停止 <公开 类型 = 逻辑型 折叠>
    {
        返回 (m_服务器.停止 ())
    }

    方法 服务器_热更新模板 <公开 折叠>
    {
        变量 i <类型 = 整数>
        变量 对象指针 <类型 = 变整数>
        m_服务器锁.读 ()
        循环 (0, m_网站列表.取成员数 (), i)
        {
            对象指针 = m_网站列表.取成员 (i)
            读指针处对象 (对象指针, 网站).模板.热更新 ()
        }
        m_服务器锁.结束读 ()

    }

    方法 服务器_取连接数 <公开 类型 = 整数 注释 = "还没封装..." 折叠>
    {
        返回 (m_服务器.客户端连接数)
    }

    方法 服务器_启动 <公开 类型 = 逻辑型 注释 = "如果没有任何网站,或者端口被占用,将会返回假..">
    参数 地址 <类型 = 文本型 注释 = "提供所欲监听的地址." @默认值 = "0.0.0.0">
    参数 端口号 <类型 = 短整数 注释 = "提供所欲监听的端口号.">
    参数 工作线程数 <类型 = 整数 注释 = "默认不设置">
    参数 POST上传大小限制 <类型 = 整数 注释 = "单位:M">
    {
        如果 (m_网站列表.取成员数 () == 0)
        {
            返回 (假)
        }
        如果 (工作线程数 > 0)
        {
            m_服务器.工作线程数 = 工作线程数
        }
        m_POST上传大小限制 = 选择 (POST上传大小限制 <= 0, 5 * 1024 * 1024, POST上传大小限制 * 1024 * 1024)
        m_服务器.发送事件同步策略 = HP发送事件同步策略.同步接收事件
        m_服务器.数据发送策略 = HP数据发送策略.打包模式

        返回 (m_服务器.启动 (地址, 端口号))
    }

    方法 _收到请求 <类型 = 整数>
    参数 来源对象 <类型 = HP_HTTP服务器>
    参数 连接ID <类型 = 变整数>
    {
        变量 Host <类型 = 文本型>
        变量 请求文件 <类型 = 文本型>
        变量 ""
        变量 网站对象 <类型 = 变整数 注释 = "这个不能销毁哦..">
        变量 指针_请求信息 <类型 = 变整数>
        变量 指针_响应信息 <类型 = 变整数>

        Host = 来源对象.取主机 (连接ID)
        如果 (寻找字符 (Host, ':') != -1)
        {
            Host = 取文本左边 (Host, 寻找字符 (Host, ':'))
        }

        请求文件 = 来源对象.取请求URL域值 (连接ID, HPURL字段.路径)

        网站对象 = __匹配域名 (Host, m_网站列表)  // 查找网站指针

        指针_响应信息 = 响应信息_创建 (连接ID, 网站对象)
        指针_请求信息 = 请求信息_创建 (连接ID, 本对象.取对象自身指针 (), 网站对象, 来源对象)
        如果 (指针_请求信息 == 0 || 指针_响应信息 == 0)
        {
            响应信息_销毁 ()
            请求信息_销毁 ()
            返回 (1)
        }

        如果 (网站对象 == 0)
        {
            读指针处对象 (指针_响应信息, 响应信息).响应数据 = 文本到UTF8 (单个模板.取错误页面 ("蜗牛服务器", "当前域名未创建哦..."))
            读指针处对象 (指针_响应信息, 响应信息).状态码 = 200
        }
        否则 (读指针处对象 (网站对象, 网站).调用视图函数 (请求文件, 指针_请求信息, 读指针处对象 (指针_请求信息, 请求信息).请求参数))
        {
            // 有视图处理了请求...
        }
        否则 (__发送静态文件 (网站对象, 指针_请求信息, 指针_响应信息, 请求文件) == 假)  // 不是静态文件...
        {
            读指针处对象 (指针_响应信息, 响应信息).状态码 = 404
            如果 (读指针处对象 (网站对象, 网站).调用404函数 (请求文件, 指针_请求信息, 读指针处对象 (指针_请求信息, 请求信息).请求参数))
            {
                // 用户自己处理了404页面
            }
            否则  // 系统内置404
            {
                读指针处对象 (指针_响应信息, 响应信息).响应数据 = 文本到UTF8 (单个模板.取错误页面 ("404", "法海不懂爱,页面出不来!"))
            }

        }

        __回复请求 (网站对象, 来源对象)

        响应信息_销毁 ()
        请求信息_销毁 ()
        如果 (来源对象.是否为保持连接 (连接ID) == 假)  // 不是长连接!!!....
        {
            来源对象.释放连接 (连接ID)  // 过三秒之后,断开连接..
        }
        返回 (0)
    }

    方法 __发送静态文件 <静态 类型 = 逻辑型>
    参数 网站指针 <类型 = 变整数>
    参数 请求指针 <类型 = 变整数>
    参数 响应指针 <类型 = 变整数>
    参数 请求文件 <类型 = 文本型 注释 = "不带查询参数的哦..">
    {
        变量 静态目录 <类型 = 文本型>
        静态目录 = 读指针处对象 (网站指针, 网站).静态文件目录
        如果 (文本是否为空 (静态目录))  // 没有设置静态文件目录!
        {
            返回 (假)
        }
        如果 (请求文件 == "/")  // 首页..不是请求文件!
        {
            返回 (假)
        }
        变量 文件 <类型 = 文本型>
        变量 后缀名 <类型 = 文本型>
        变量 文件时间 <类型 = 小数>
        文件 = 请求文件
        替换字符 (文件, '/', '\\', 0)
        后缀名 = w_文件_取后缀名 (文件, 真)
        文件 = 静态目录 + 文件
        如果 (文本是否为空 (后缀名))  // 没有文件后缀...
        {
            返回 (假)
        }
        文件时间 = 取文件时间 (文件)
        如果 (为最小时间 (文件时间))  // 静态文件不存在!
        {
            返回 (假)
        }
        如果 (读指针处对象 (网站指针, 网站).静态文件缓存时间 == 0)  // 使用304...
        {
            变量 文件标记 <类型 = 文本型>
            加入字符 (文件标记, '"')
            加入文本 (文件标记, 到文本 (文件时间))  // 等同于: 文件标记 = "+文件时间+"
            加入字符 (文件标记, '"')
            如果 (请求信息_取请求头值 (请求指针, "If-None-Match") == 文件标记)  // 这个文件,发过给用户,直接说,不需要更新!
            {
                读指针处对象 (响应指针, 响应信息).文档类型 = 后缀名
                读指针处对象 (响应指针, 响应信息).状态码 = 304
                返回 (真)
            }
            响应信息_置响应头 (响应指针, "ETag", 文件标记)  // 设置文件标记...
        }
        否则
        {
            响应信息_置响应头 (响应指针, "Cache-Control", "max-age=" + 到文本 (读指针处对象 (网站指针, 网站).静态文件缓存时间))

        }


        读指针处对象 (响应指针, 响应信息).文档类型 = 后缀名
        读指针处对象 (响应指针, 响应信息).响应数据 = 读入文件 (文件)
        读指针处对象 (响应指针, 响应信息).状态码 = 200
        返回 (真)
    }

    方法 __回复请求 <静态 折叠>
    参数 网站指针 <类型 = 变整数 注释 = "可以为0">
    参数 来源对象 <类型 = HP_HTTP服务器>
    {
        变量 响应头数组 <类型 = HP请求头数组类>
        变量 指针_响应信息 <类型 = 变整数>
        变量 状态码 <类型 = 短整数>
        变量 连接ID <类型 = 变整数>
        变量 响应数据 <类型 = 字节集类>
        变量 文档类型 <类型 = 文本型>
        变量 描述文本 <类型 = 文本型>
        指针_响应信息 = 响应信息_取指针 ()
        如果 (指针_响应信息 == 0)  // 不属于我管...
        {
            返回
        }
        状态码 = 读指针处对象 (指针_响应信息, 响应信息).状态码
        连接ID = 读指针处对象 (指针_响应信息, 响应信息).连接ID
        响应数据 = 读指针处对象 (指针_响应信息, 响应信息).响应数据
        文档类型 = 读指针处对象 (指针_响应信息, 响应信息).文档类型
        描述文本 = 全局类_取描述文本 (状态码)

        如果 (响应信息_响应头是否存在 (指针_响应信息, "Date") == 假)
        {
            响应信息_置响应头 (指针_响应信息, "Date", 全局类_取协议头时间文本 (取现行时间 ()))
        }

        如果 (响应信息_响应头是否存在 (指针_响应信息, "Server") == 假)
        {
            如果 (网站指针 != 0)
            {
                响应信息_置响应头 (指针_响应信息, "Server", 读指针处对象 (网站指针, 网站).服务器名称)
            }
            否则
            {
                响应信息_置响应头 (指针_响应信息, "Server", "woniu")  // 没有找到网站...
            }
        }

        如果 (响应信息_响应头是否存在 (指针_响应信息, "Connection") == 假)
        {
            响应信息_置响应头 (指针_响应信息, "Connection", "keep-alive")
        }

        如果 (响应信息_响应头是否存在 (指针_响应信息, "Content-Type") == 假)
        {
            响应信息_置响应头 (指针_响应信息, "Content-Type", 全局类_取后缀类型 (文档类型))
        }

        如果 (网站指针 != 0)
        {
            如果 (读指针处对象 (网站指针, 网站).GZIP压缩)
            {
                响应数据 = HPSocket类.压缩数据_GZip (响应数据)
                响应信息_置响应头 (指针_响应信息, "Content-Encoding", "gzip")
            }
        }

        响应头数组 = 读指针处对象 (指针_响应信息, 响应信息).响应头

        变量 i <类型 = 整数>
        变量 set_Cookie <类型 = HP请求头类>
        变量 Cookie <类型 = Cookie>
        变量 Cookie数量 <类型 = 整数>
        set_Cookie.名称 = "Set-Cookie"
        Cookie数量 = 读指针处对象 (指针_响应信息, 响应信息).Cookie.取成员数 ()


        循环 (0, Cookie数量, i)  // 组合COOKIE...
        {
            变量 过期时间 <类型 = 小数>
            Cookie = 读指针处对象 (指针_响应信息, 响应信息).Cookie.取成员 (i)
            过期时间 = 增减时间 (取现行时间 (), 时间字段类型.秒, Cookie.过期时间)

            清空文本 (set_Cookie.值)

            加入文本 (set_Cookie.值, Cookie.名称)
            加入字符 (set_Cookie.值, '=')
            加入文本 (set_Cookie.值, Cookie.值)
            加入文本 (set_Cookie.值, "; path=")
            加入文本 (set_Cookie.值, Cookie.目录)
            加入文本 (set_Cookie.值, "; expires=")
            加入文本 (set_Cookie.值, 全局类_取协议头时间文本 (过期时间))

            如果 (文本是否为空 (Cookie.域名) == 假)
            {
                加入文本 (set_Cookie.值, "; domain=")
                加入文本 (set_Cookie.值, Cookie.域名)
            }


            响应头数组.加入成员 (set_Cookie)
        }


        来源对象.回复请求 (连接ID, 状态码, 描述文本, 响应头数组, 响应数据)
    }

    方法 __匹配域名 <静态 类型 = 变整数 返回值注释 = "成功返回网站指针,失败返回0" 折叠>
    参数 Host <类型 = 文本型>
    参数 网站列表 <类型 = 变整数数组类>
    {
        变量 网站数量 <类型 = 整数>
        变量 i <类型 = 整数>
        变量 网站指针 <类型 = 变整数>
        网站数量 = 网站列表.取成员数 ()
        循环 (0, 网站数量, i)
        {
            网站指针 = 网站列表.取成员 (i)
            如果 (读指针处对象 (网站指针, 网站).匹配域名 (Host))
            {
                返回 (网站指针)
            }

        }
        返回 (0)
    }

    方法 HP_HTTP服务器_开始握手 <接收事件 类型 = 整数 注释 = "  当客户端通过事件\"客户进入\"后,将会立即触发本事件,只有通过本事件,才会进行握手动作."
            返回值注释 = "可选返回值:" 返回值注释 = "  0 = 本次操作成功, 客户端将与服务器进行握手." 返回值注释 = "  1 = 忽略本次操作, 客户端可与服务器建立正式连接."
            返回值注释 = "  2 = 本次操作错误, 握手动作失败, 主动断开与客户的连接." 折叠>
    参数 来源对象 <类型 = HP_HTTP服务器 注释 = "提供事件产生的具体来源对象">
    参数 标记值 <类型 = 整数 注释 = "用户调用\"挂接事件\"命令时所提供的\"标记值\"参数值,非此方式挂接事件则本参数值固定为0.">
    参数 连接ID <类型 = 变整数>
    {
        来源对象.置连接附加数据 (连接ID, 0)
        返回 (0)
    }

    方法 HP_HTTP服务器_请求头解析完毕 <接收事件 类型 = 整数 注释 = " 当服务器解析请求头完毕后,将通过本事件通知用户."
            注释 = "  请注意:本事件只是通知用户请求头解析完毕,不提供所解析的请" 注释 = "求头数据,如果想要获取请求头数据,请添加事件\"请求头进入\",此事"
            注释 = "件下将会提供请求头名称和请求头值." 返回值注释 = "返回-1则认定为解析错误并关闭该连接." 折叠 折叠2>
    参数 来源对象 <类型 = HP_HTTP服务器 注释 = "提供事件产生的具体来源对象">
    参数 标记值 <类型 = 整数 注释 = "用户调用\"挂接事件\"命令时所提供的\"标记值\"参数值,非此方式挂接事件则本参数值固定为0.">
    参数 连接ID <类型 = 变整数>
    {
        返回 (0)
    }

    方法 HP_HTTP服务器_请求数据进入 <接收事件 类型 = 整数 注释 = "  当接收到HTTP BODY报文时本事件将被触发." 注释 = "  本事件返回值拥有具体意义,请您参考如下返回值:"
            注释 = "    0  - 本次操作成功,默认返回值." 注释 = "    -1 - 本次操作失败,主动与客户断开连接."
            注释 = "  请注意:如果本事件返回-1,将会触发事件\"解析错误\",您可以忽略本次\"解析" 注释 = "错误\"事件." 返回值注释 = "返回-1则认定为解析错误并关闭该连接." 折叠 折叠2>
    参数 来源对象 <类型 = HP_HTTP服务器 注释 = "提供事件产生的具体来源对象">
    参数 标记值 <类型 = 整数 注释 = "用户调用\"挂接事件\"命令时所提供的\"标记值\"参数值,非此方式挂接事件则本参数值固定为0.">
    参数 连接ID <类型 = 变整数>
    参数 数据 <类型 = 字节集类 注释 = "所收到的请求体数据.">
    参数 数据长度 <类型 = 整数 注释 = "所收到的请求体数据长度.">
    {
        变量 数据指针 <类型 = 变整数>
        来源对象.取连接附加数据 (连接ID, 取变量地址 (数据指针))
        如果 (数据指针 == 0)
        {
            数据指针 = 创建对象指针 (字节集类)  // 创建组包对象
            如果 (数据指针 == 0)  // 创建指针失败!
            {
                返回 (-1)
            }
            如果 (来源对象.置连接附加数据 (连接ID, 数据指针) == 假)  // 附加失败!
            {
                销毁对象指针 (数据指针)
                返回 (-1)
            }
        }

        如果 (读指针处对象 (数据指针, 字节集类).取字节集长度 () > m_POST上传大小限制)  // 你上传太大了..
        {
            销毁对象指针 (数据指针)
            来源对象.置连接附加数据 (连接ID, 0)
            返回 (-1)
        }
        读指针处对象 (数据指针, 字节集类).添加字节集 (数据)
        返回 (0)
    }

    方法 HP_HTTP服务器_消息解析完毕 <接收事件 类型 = 整数 注释 = "  客户请求解析完毕." 注释 = "  本事件返回值拥有具体意义,请您参考如下返回值:"
            注释 = "    0  - 本次操作成功,默认返回值." 注释 = "    -1 - 本次操作失败,主动与客户断开连接."
            注释 = "  请注意:如果本事件返回-1,将会触发事件\"解析错误\",您可以忽略本次\"解析" 注释 = "错误\"事件." 返回值注释 = "返回-1则认定为解析错误并关闭该连接." 折叠2>
    参数 来源对象 <类型 = HP_HTTP服务器 注释 = "提供事件产生的具体来源对象">
    参数 标记值 <类型 = 整数 注释 = "用户调用\"挂接事件\"命令时所提供的\"标记值\"参数值,非此方式挂接事件则本参数值固定为0.">
    参数 连接ID <类型 = 变整数>
    {
        变量 返回值 <类型 = 整数>
        如果 (来源对象.是否升级协议 (连接ID))
        {
            返回 (0)
        }
        m_服务器锁.读 ()
        返回值 = _收到请求 (来源对象, 连接ID)
        m_服务器锁.结束读 ()

        变量 数据指针 <类型 = 变整数>
        来源对象.取连接附加数据 (连接ID, 取变量地址 (数据指针))
        如果 (数据指针 != 0)  // 有POST包...
        {
            销毁对象指针 (数据指针)  // 直接释放
            来源对象.置连接附加数据 (连接ID, 0)  // 设置没有POST包...以免断开事件的时候重复回收,导致崩溃!
        }
        返回 (返回值)
    }

    方法 HP_HTTP服务器_客户离开 <接收事件 类型 = 整数 注释 = "当客户端主动或被动与服务器断开后将触发本事件." 折叠>
    参数 来源对象 <类型 = HP_HTTP服务器 注释 = "提供事件产生的具体来源对象">
    参数 标记值 <类型 = 整数 注释 = "用户调用\"挂接事件\"命令时所提供的\"标记值\"参数值,非此方式挂接事件则本参数值固定为0.">
    参数 连接ID <类型 = 变整数 注释 = "触发该事件的连接ID.">
    参数 关闭原因 <类型 = HP操作类型 注释 = "返回导致连接被关闭的操作.">
    参数 错误码 <类型 = HP错误码 注释 = "如果客户端为非正常退出,此参数为错误代码.">
    {
        变量 数据指针 <类型 = 变整数>
        来源对象.取连接附加数据 (连接ID, 取变量地址 (数据指针))
        如果 (数据指针 != 0)
        {
            销毁对象指针 (数据指针)
        }
        返回 (0)
    }
}
