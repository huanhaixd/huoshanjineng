
###### 以下为一个火山软件开发平台系统类库程序的内容

<火山程序 类型 = "通常" 版本 = 1 />

包 火山.程序

类 启动类 <公开 基础类 = 窗口程序类>
{
    变量 主窗口对象 <类型 = 我的主窗口 标题 = "【世恒带你学火山】- 在画板上画出股票K线图">

    方法 启动方法 <公开 类型 = 整数>
    {
        主窗口对象.创建主窗口 ()
        返回 (1)
    }
}

# ===

类 我的主窗口 <基础类 = 窗口 注释 = "样例主窗口" @视窗.布局 = "client_size = \"1024, 768\"" 标题 = "【世恒带你学火山】- 在画板上画出股票K线图">
{
    变量 画板1 <类型 = 画板 折叠2 隐藏值属性 = "0" @视窗.布局 = "id = 101\r\npos = \"2, 0, 1020, 766\"" 自动重画 = 真 画板背景色 = 0x0>
    变量 时钟1 <类型 = 时钟 隐藏值属性 = "0" @视窗.布局 = "id = 102\r\npos = \"141, 95, 72, 48\"">
    变量 成员_JSON总 <类型 = JSON对象类>
    变量 成员_JSON数组 <类型 = JSON数组类>
    变量 成员_昨日收盘价 <类型 = 小数>

    方法 我的主窗口_创建完毕 <接收事件 类型 = 整数 注释 = "当本组件及其中所有子组件均被创建完毕后发送此事件.">
    参数 来源对象 <类型 = 我的主窗口 注释 = "提供事件产生的具体来源对象">
    参数 标记值 <类型 = 整数 注释 = "用户调用\"挂接事件\"命令时所提供的\"标记值\"参数值,非此方式挂接事件则本参数值固定为0.">
    {
        如果真 (文件是否存在 (取当前目录 () + "\\600822-上海物贸.json") == 假)
        {
            结束 ()

        }
        成员_JSON总.创建自文本 (UTF8到文本 (读入文件 (取当前目录 () + "\\600822-上海物贸.json")))
        成员_昨日收盘价 = 文本到小数 (成员_JSON总.取文本值 ("昨日收盘"))
        成员_JSON总.取数组值 ("今日数据", 成员_JSON数组)
        如果真 (成员_JSON数组.成员数 < 48)
        {
            结束 ()
        }
        方法_画框架 ()
        时钟1.时钟周期 = 200

        返回 (0)
    }

    方法 方法_画框架
    {
        画板1.自动重画 = 真
        变量 局部_临时变量 <类型 = 整数>
        变量 局部_计次 <类型 = 整数>
        画板1.画笔粗细 = 1
        画板1.画笔类型 = 画笔类型.点线
        画板1.画笔颜色 = 取颜色值 (146, 0, 0)
        循环 (画板1.高度 / 2, 0, 局部_计次, -72)
        {
            画板1.画直线 (0, 局部_计次, 画板1.宽度 - 40, 局部_计次)
            画板1.文本颜色 = 颜色类.红色
            如果真 (局部_临时变量 != 0)
            {
                画板1.定位写出 (画板1.宽度 - 40, 局部_计次 - 画板1.取高度 ("1") / 2, 数值到格式文本 (四舍五入 (成员_昨日收盘价 * (100 + 局部_临时变量) / 100, 2), 2, 假))

            }
            局部_临时变量 = 局部_临时变量 + 2

        }
        局部_临时变量 = 0
        循环 (0, (画板1.高度 - 40) / 2, 局部_计次, 72)
        {
            画板1.画直线 (0, 画板1.高度 / 2 + 局部_计次, 画板1.宽度 - 40, 画板1.高度 / 2 + 局部_计次)
            画板1.文本颜色 = 颜色类.绿色
            如果真 (局部_临时变量 != 0)
            {
                画板1.定位写出 (画板1.宽度 - 40, 画板1.高度 / 2 + 局部_计次 - 画板1.取高度 ("1") / 2, 数值到格式文本 (四舍五入 (成员_昨日收盘价 * (100 - 局部_临时变量) / 100, 2), 2, 假))
            }
            局部_临时变量 = 局部_临时变量 + 2

        }
        画板1.画笔粗细 = 2
        画板1.画笔类型 = 画笔类型.直线
        画板1.画直线 (0, 21, 画板1.宽度 - 40, 21)
        画板1.画直线 (画板1.宽度 - 45, 0, 画板1.宽度 - 45, 画板1.高度)
        画板1.画直线 (0, 画板1.高度 - 20 - 1, 画板1.宽度 - 40, 画板1.高度 - 20 - 1)
        画板1.画笔粗细 = 1
        画板1.画笔颜色 = 颜色类.银白色
        画板1.文本颜色 = 颜色类.白色
        画板1.画直线 (0, 画板1.高度 / 2, 画板1.宽度 - 40, 画板1.高度 / 2)
        画板1.定位写出 (画板1.宽度 - 40, 画板1.高度 / 2 - 画板1.取高度 ("1") / 2, 数值到格式文本 (四舍五入 (成员_昨日收盘价, 2), 2, 假))

    }

    方法 时钟_周期事件 <接收事件 类型 = 整数 注释 = "每当经过了指定的时钟周期,将发送本事件.">
    参数 来源对象 <类型 = 时钟 注释 = "提供事件产生的具体来源对象">
    参数 标记值 <类型 = 整数 注释 = "用户调用\"挂接事件\"命令时所提供的\"标记值\"参数值,非此方式挂接事件则本参数值固定为0.">
    {
        变量 局部_计次 <静态 类型 = 整数>
        变量 局部_开盘价 <类型 = 小数>
        变量 局部_收盘价 <类型 = 小数>
        变量 局部_最低价 <类型 = 小数>
        变量 局部_最高价 <类型 = 小数>
        变量 局部_开盘价位置 <类型 = 小数>
        变量 局部_收盘价位置 <类型 = 小数>
        变量 局部_最低价位置 <类型 = 小数>
        变量 局部_最高价位置 <类型 = 小数>
        变量 局部_JSON对象 <类型 = JSON对象类>
        如果真 (局部_计次 == 48)
        {
            局部_计次 = 0
            画板1.清除 ()
            方法_画框架 ()

        }
        局部_计次 = 局部_计次 + 1
        成员_JSON数组.取对象值 (局部_计次 - 1, 局部_JSON对象)
        局部_开盘价 = 文本到小数 (局部_JSON对象.取文本值 ("open"))
        局部_收盘价 = 文本到小数 (局部_JSON对象.取文本值 ("close"))
        局部_最低价 = 文本到小数 (局部_JSON对象.取文本值 ("low"))
        局部_最高价 = 文本到小数 (局部_JSON对象.取文本值 ("high"))
        画板1.画笔粗细 = 2
        如果 (局部_开盘价 < 局部_收盘价)
        {
            画板1.画笔颜色 = 颜色类.红色
            画板1.刷子颜色 = 颜色类.黑色
        }
        否则
        {
            画板1.画笔颜色 = 颜色类.艳青色
            画板1.刷子颜色 = 颜色类.艳青色
        }

        局部_开盘价位置 = (局部_开盘价 - 成员_昨日收盘价) * (画板1.高度 - 50) / 2.0 / (成员_昨日收盘价 * 0.1)
        局部_最低价位置 = (局部_最低价 - 成员_昨日收盘价) * (画板1.高度 - 50) / 2.0 / (成员_昨日收盘价 * 0.1)
        局部_最高价位置 = (局部_最高价 - 成员_昨日收盘价) * (画板1.高度 - 50) / 2.0 / (成员_昨日收盘价 * 0.1)
        局部_收盘价位置 = (局部_收盘价 - 成员_昨日收盘价) * (画板1.高度 - 50) / 2.0 / (成员_昨日收盘价 * 0.1)
        如果真 (局部_开盘价位置 == 局部_收盘价位置)
        {
            局部_收盘价位置 = 局部_开盘价位置 + 1
        }

        画板1.画直线 ((整数)((画板1.宽度 - 60) / 成员_JSON数组.成员数 * 局部_计次), (整数)(画板1.高度 / 2.0 - 局部_最低价位置), (整数)((画板1.宽度 - 60) / 成员_JSON数组.成员数 * 局部_计次), (整数)(画板1.高度 / 2.0 - 局部_最高价位置))
        画板1.画矩形 ((整数)((画板1.宽度 - 60) / 成员_JSON数组.成员数 * 局部_计次 - 3), (整数)(画板1.高度 / 2.0 - 局部_开盘价位置), (整数)((画板1.宽度 - 60) / 成员_JSON数组.成员数 * 局部_计次 + 4), (整数)(画板1.高度 / 2.0 - 局部_收盘价位置))
        返回 (0)
    }
}
