
###### 以下为一个火山软件开发平台系统类库程序的内容

<火山程序 类型 = "通常" 版本 = 1 />

包 火山.程序

类 启动类 <公开 基础类 = 窗口程序类>
{
    变量 主窗口对象 <类型 = 我的主窗口>

    方法 启动方法 <公开 类型 = 整数>
    {
        主窗口对象.创建主窗口 ()
        返回 (1)
    }
}

类 我的主窗口 <基础类 = 窗口 注释 = "样例主窗口" @视窗.布局 = "client_size = \"461, 325\"" 标题 = "线程池演示">
{
    变量 编辑框1 <类型 = 编辑框 折叠2 隐藏值属性 = "0" @视窗.布局 = "id = 101\r\npos = \"13, 14, 432, 256\"" 是否允许多行 = 真
            输入方式 = 只读方式 滚动条 = 纵向滚动条>
    变量 启动线程按钮 <类型 = 按钮 折叠2 隐藏值属性 = "0" @视窗.布局 = "id = 102\r\npos = \"22, 278, 88, 32\"" 标题 = "启动线程(&C)">
    变量 全部停止按钮 <类型 = 按钮 折叠2 隐藏值属性 = "0" @视窗.布局 = "id = 103\r\npos = \"242, 278, 88, 32\"" 标题 = "全部停止(&S)">
    变量 停止首线程按钮 <类型 = 按钮 折叠2 隐藏值属性 = "0" @视窗.布局 = "id = 104\r\npos = \"128, 278, 96, 32\"" 标题 = "停止首线程(&S)">
    变量 标签1 <类型 = 标签 隐藏值属性 = "0" @视窗.布局 = "id = 105\r\npos = \"5, 134, 32, 32\"" 可视 = 假>
    变量 添加一个线程 <类型 = 按钮 隐藏值属性 = "0" @视窗.布局 = "id = 106\r\npos = \"350, 278, 88, 32\"" 标题 = "添加一个线程(&D)">
    变量 ""
    变量 线程池1 <类型 = 线程池类 线程数量 = 3>

    方法 我的主窗口_创建完毕 <接收事件 类型 = 整数 注释 = "当本组件及其中所有子组件均被创建完毕后发送此事件.">
    参数 来源对象 <类型 = 我的主窗口 注释 = "提供事件产生的具体来源对象">
    参数 标记值 <类型 = 整数 注释 = "用户调用\"挂接事件\"命令时所提供的\"标记值\"参数值,非此方式挂接事件则本参数值固定为0.">
    {
        全局线程池.启用调试跟踪 ()
        全局线程池.置最多空闲线程数 (6)  // 减少默认允许空闲线程数,用来测试超出数目的空闲线程是否会被自动回收.
        显示信息行 ("提示: 本程序运行中请注意开发环境调试输出框中的内容输出.")
        返回 (0)
    }

    方法 按钮_被单击 <接收事件 类型 = 整数 注释 = "当按钮被单击后发送此事件">
    参数 来源对象 <类型 = 按钮 注释 = "提供事件产生的具体来源对象">
    参数 标记值 <类型 = 整数 注释 = "用户调用\"挂接事件\"命令时所提供的\"标记值\"参数值,非此方式挂接事件则本参数值固定为0.">
    {
        如果 (来源对象 == 添加一个线程)
        {
            线程池1.添加新线程 ()
        }
        如果 (来源对象 == 启动线程按钮)
        {
            线程池1.启动线程 (-1)  // 启动全部线程
        }
        否则 (来源对象 == 停止首线程按钮)
        {
            线程池1.停止线程 (0)
        }
        否则 (来源对象 == 全部停止按钮)
        {
            线程池1.停止线程 (-1)
        }
        返回 (0)
    }

    方法 显示信息行
    参数 所欲显示行 <类型 = 文本型>
    {
        如果 (文本是否为空 (所欲显示行) == 假)
        {
            编辑框1.加入文本行 (所欲显示行)
        }
    }

    方法 线程池类_线程运行 <接收事件 类型 = 整数>
    参数 来源对象 <类型 = 线程池类>
    参数 标记值 <类型 = 整数>
    参数 线程ID <类型 = 整数>
    参数 线程句柄 <类型 = 变整数>
    参数 用户参数 <类型 = 变整数>
    参数 退出事件 <类型 = 变整数>
    {
        如果 (来源对象 == 线程池1)
        {
            标签1.调用反馈事件 (0, 线程ID, 假)  // 线程中不能直接更新窗口界面,可能会导致死锁. 下同
            计次循环 (10)
            {
                如果 (缓存线程类.是否需要退出 (线程句柄, 1000))  // 等待1秒判断是否需要退出
                {
                    跳出循环
                }
                // 进行你自己的工作 ...
                标签1.调用反馈事件 (1, 线程ID * 100 + 取循环索引 () + 1, 假)
            }
            标签1.调用反馈事件 (2, 线程ID, 假)
        }
        返回 (0)
    }

    方法 标签_反馈事件 <接收事件 类型 = 整数 注释 = "当在程序中调用本对象的\"调用反馈事件\"方法基于Windows消息进行通讯时,本事件将被触发."
            返回值注释 = "所返回值将返回到\"调用反馈事件\"方法的调用方">
    参数 来源对象 <类型 = 标签 注释 = "提供事件产生的具体来源对象">
    参数 标记值 <类型 = 整数 注释 = "用户调用\"挂接事件\"命令时所提供的\"标记值\"参数值,非此方式挂接事件则本参数值固定为0.">
    参数 参数1 <类型 = 变整数 注释 = "\"调用反馈事件\"方法所提供的参数1值">
    参数 参数2 <类型 = 变整数 注释 = "\"调用反馈事件\"方法所提供的参数2值">
    {
        分支判断 ((整数)参数1)
        {
            分支 (0)
            {
                显示信息行 (取格式文本 ("线程 %d 开始运行", (整数)参数2))
            }
            分支 (1)
            {
                显示信息行 (取格式文本 ("线程 %d 正在运行第 %d 秒", (整数)参数2 / 100, (整数)参数2 % 100))
            }
            分支 (2)
            {
                显示信息行 (取格式文本 ("线程 %d 运行结束", (整数)参数2))
            }
        }
        返回 (0)
    }
}

类 线程池类 <公开 注释 = "提供线程池操作">
{
    变量 自定义线程序号 <类型 = 整数>
    变量 数组 <类型 = 对象数组类>

    方法 线程数量 <公开 属性写 注释 = "初始化线程数量">
    参数 线程数量 <类型 = 整数>
    {
        计次循环 (线程数量)
        {
            变量 对象索引 <类型 = 整数>
            对象索引 = 数组.加入新成员 (缓存线程类)
            自定义线程序号 = 自定义线程序号 + 1
            挂接事件 ((缓存线程类)数组.取成员 (对象索引), 自定义线程序号)
            // 取成员 (对象索引).启动 ()
        }
    }

    方法 添加新线程 <公开 类型 = 整数 注释 = "添加一个新的线程">
    参数 是否启动 <类型 = 逻辑型 @默认值 = 假>
    {
        变量 对象索引 <类型 = 整数>
        对象索引 = 数组.加入新成员 (缓存线程类)
        自定义线程序号 = 自定义线程序号 + 1
        挂接事件 ((缓存线程类)数组.取成员 (对象索引), 自定义线程序号)
        如果 (是否启动)
        {
            ((缓存线程类)数组.取成员 (对象索引)).启动 ()
        }
        返回 (对象索引)
    }

    方法 启动线程 <公开 注释 = "从0开始,提供-1则启动全部线程.">
    参数 线程索引 <类型 = 整数 注释 = "从0开始" @默认值 = -1>
    参数 用户参数 <类型 = 变整数 @默认值 = 0>
    {
        如果 (线程索引 < 0)
        {
            计次循环 (数组.取成员数 ())
            {
                ((缓存线程类)数组.取成员 (取循环索引 ())).启动 (用户参数)
            }
        }
        否则
        {
            如果 (线程索引 < 数组.取成员数 ())
            {
                ((缓存线程类)数组.取成员 (线程索引)).启动 (用户参数)
            }
        }
    }

    方法 停止线程 <公开 注释 = "从0开始,提供-1则停止全部线程.">
    参数 线程索引 <类型 = 整数 注释 = "从0开始" @默认值 = -1>
    {
        如果 (线程索引 < 0)
        {
            自定义线程序号 = 0
            数组.删除所有成员 ()
        }
        否则
        {
            如果 (线程索引 < 数组.取成员数 ())
            {
                // 自定义线程ID = 自定义线程ID - 1
                数组.删除成员 (线程索引)
            }
        }
    }

    方法 缓存线程类_线程运行 <接收事件 类型 = 整数 注释 = "  用户需要接收并处理本事件以执行所需要的功能,该事件处理方法将在一个单独的线程中运行."
            注释 = "  注意: 在本事件的接收处理方法中,必须尽可能快地频繁调用本对象的\"是否需要退出\"方法,"
            注释 = "一旦发现该方法返回真,则需要尽快结束执行并返回,否则将导致另一线程中的\"停止\"方法调用" 注释 = "进入死等状态.">
    参数 来源对象 <类型 = 缓存线程类 注释 = "提供事件产生的具体来源对象">
    参数 标记值 <类型 = 整数 注释 = "用户调用\"挂接事件\"命令时所提供的\"标记值\"参数值,非此方式挂接事件则本参数值固定为0.">
    参数 线程句柄 <类型 = 变整数 注释 = "提供所启动线程在线程池中的句柄值,必定不为0.">
    参数 用户参数 <类型 = 变整数 注释 = "提供启动时传递到线程函数的用户参数值">
    参数 退出事件 <类型 = 变整数 注释 = "本参数普通用户无需使用">
    {
        线程运行 (标记值, 线程句柄, 用户参数, 退出事件)
        返回 (0)
    }

    方法 线程运行 <公开 定义事件 类型 = 整数>
    参数 线程序号 <类型 = 整数 注释 = "返回线程序号">
    参数 线程句柄 <类型 = 变整数 注释 = "提供所启动线程在线程池中的句柄值,必定不为0.">
    参数 用户参数 <类型 = 变整数 注释 = "提供启动时传递到线程函数的用户参数值">
    参数 退出事件 <类型 = 变整数 注释 = "本参数普通用户无需使用">
}
