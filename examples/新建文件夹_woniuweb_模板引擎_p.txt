
###### 以下为一个火山软件开发平台系统类库程序的内容

<火山程序 类型 = "通常" 版本 = 1 />

包 蜗牛模块.服务器

类 HTML设置 <折叠>
{
    变量 压缩HTML <公开 类型 = 逻辑型 注释 = "0=默认,1=压缩HTML,2=格式化HTML。">
    变量 压缩JS <公开 类型 = 整数 注释 = "0=不压缩,1=完全压缩,2=压缩空格(保留换行符,删除注释),3=完全压缩+删除注释">
    变量 删除注释 <公开 类型 = 逻辑型>

    #
}

类 模板标签
{
    变量 标签类型 <公开 类型 = 整数 注释 = "标签类型_">
    变量 HTML <公开 类型 = 文本型>
    变量 标签名 <公开 类型 = 文本型>
    变量 标签参数 <公开 类型 = 文本型>
    变量 标签结束索引 <公开 类型 = 整数 值 = -1 注释 = "{{#循环}}  {{/循环}}">
    变量 运算符 <公开 类型 = 整数 注释 = "#类型_判断 ->  判断类型标签的运算符,#运算符_">
    变量 判断文本 <公开 类型 = 文本型 注释 = "#类型_判断 ->  用于判断的文本">
    变量 判断整数 <公开 类型 = 整数 注释 = "#类型_判断 ->  用于判断的整数">
    变量 判断逻辑 <公开 类型 = 逻辑型 注释 = "#类型_判断 ->  用于判断的逻辑">
    变量 数据类型 <公开 类型 = 整数 注释 = "数据类型_">
    变量 函数 <公开 类型 = 函数解析>

    #
}

类 模板数组 <基础类 = 对象数组模板类 折叠 @模板实现类 = "模板标签">
{

    #
}

类 标签类型 <@常量类 = 整数>
{
    常量 HTML <公开 值 = 0 注释 = "<html>.....</html>">
    常量 标签 <公开 值 = 1 注释 = "{{测试}}">
    常量 循环1 <公开 值 = 2 注释 = "{{#测试}}">
    常量 判断 <公开 值 = 3 注释 = "{?测试}}">
    常量 结束标签 <公开 值 = 4 注释 = "{{/测试}}">
    常量 内嵌模板 <公开 值 = 5 注释 = "{{>/模板.html}}">
    常量 函数 <公开 值 = 6 注释 = "{{  格式化时间(发布时间,\"{年}-{月}-{日}\",真)  }}">

    #
    #
}

类 运算符 <折叠 @常量类 = 整数>
{
    常量 空 <公开 值 = 0 注释 = "判断标签是否存在">
    常量 大于 <公开 值 = 1>
    常量 小于 <公开 值 = 2>
    常量 等于 <公开 值 = 3>
    常量 不等于 <公开 值 = 4>
    常量 小于等于 <公开 值 = 5>
    常量 大于等于 <公开 值 = 6>
    常量 近似等于 <公开 值 = 7>

    #
}

类 数据类型 <折叠 @常量类 = 整数>
{
    常量 文本 <公开 值 = 1>
    常量 整数型 <公开 值 = 2>
    常量 逻辑 <公开 值 = 3>

    #
}

类 单个模板 <公开>
{
    变量 m_读写锁 <类型 = w_读写锁>
    变量 m_模板标签 <类型 = 模板数组>
    变量 m_UTF8 <类型 = 逻辑型>
    变量 m_文件路径 <类型 = 文本型>
    变量 m_文件时间 <类型 = 小数>
    变量 m_HTML设置 <类型 = HTML设置>
    变量 m_标签集合 <类型 = 文本到整数哈希表>

    方法 取错误页面 <公开 静态 类型 = 文本型 折叠>
    参数 网站标题 <类型 = 文本型>
    参数 错误信息 <类型 = 文本型>
    {
        变量 错误模板 <类型 = 文本型
                值 = "<!doctype html>\r\n<html>\r\n<head>\r\n<meta charset=\"UTF-8\">\r\n<title>{{标题}}</title>\r\n<style>\r\nhtml,body,div,h1,*{margin:0;padding:0;}\r\nbody{\r\n\tbackground-color:#fefefe;\r\n\tcolor:#333\r\n}\r\n.box{\r\n\twidth:580px;\r\n\tmargin:0 auto;\r\n}\r\nh1{\r\n\tfont-size:20px;\r\n\ttext-align:center; \r\n\tbackground:url(data:image/png;base64,iVBORw0KGgoAAAANSUhEUgAAAIAAAACACAQAAABpN6lAAAAACXBIWXMAAAsTAAALEwEAmpwYAAAABGdBTUEAALGOfPtRkwAAACBjSFJNAAB6JQAAgIMAAPn/AACA6QAAdTAAAOpgAAA6mAAAF2+SX8VGAAAH+UlEQVR42uyde3AV1RnAfyFpTCBpFIVoaxUpSQSEAezwMDUJRK11DOTDAjJTtPRBBx1bK/VRO7T1VdFCRx6j6DgjltaamcIJWBpICRg0MFjGio6BRGKp4iOUMtBEkiBJ+ocxzQ17957de3bv3WY//mDYc75zzve73549z4+Ubga2DCIEEAIIAYQAQgAhgBBACCAEEAIIAYQAQgAhgBBACCAEEAIIAYQAQgADR9L8q0pyGUM+BRRwIVlkk00mp2ilhRaaaaCBRupVs78AUrzfF5ChlFBKKQVa2RuooYaX1fH/AwCSgfAdrnHxonWxneeoVO2BBSBj+BHzODeuQk5QwSpVHzgAMo6l3GSog+1iAw+ptwIDQPJZRjkpRgvtRnGfeifpAUgGP+NezvHkp+rgMR413ycYBCDX8hRf9bTHOsRt6q9JCUDSeJQlhh3f+mVYzv3qTJIBkIup4Crfxi6vcrP6IIkAyNVs5AJfh2/HmK1eSZK5gJRR7bP5cAHb5MakACC3sJGMBMxhMlGyIOEAZBHr/JxO9ZvGPS8/SGgfILPYQGpC57KdzFabEwRACtmeEOePlDZK1Z4EAJBR7GVoUqxoHGeKOuRzHyDpVCSJ+TCUFyXd707wN0wieeRKHvP1FZCZbCLZpEz92ScAkkMDuZqZD7CTowxnBpc7fK+3cJghTKBE00ebKVAn3X1NncrDmuYf4YfqL33Gi09zkZbeaR5kuero0cvjaaZraOXyAHf64AEygX1a3/5/UKzej9AcSS0Xx9Rrp1xti9BLZR3f1qjxDJPcrBs57QTXaJl/mvJI80G9yzy6Ymr+ONJ8UJ0s5G9avrzG86+AfJNCrYxPqDfPfqh281wMvT3qGQu9M+gNeYvkWq894OdaubpYFSVlZQzN31o/VvupMdg+twCkRPP3fyPacoV6iw9t3+KtUdNe0qq5WAq99ID7NfO9bpP2d5u0g6o1atpeoz7qBoCMRPcNO+YyrdllWl+5Xi7xygNu0c6Z6jJtkEu9iM86CzwBIE4KHm6TNsx2MOOuTLc/lCMPKGSkdpmTbTB+zUbvcsmJmjZVu/Z8meoFgHIHZY6WvKhmnG/bljIj9Zd7AaDEkV/dFeX5T2LoLRHL9sg0rnZQ+3TjAORcJjoCsEgstknkOubE0JvAEgu9DJ51tj4gXzTtAUUOR4yD+FP/10DG8gcNzV/Lt/rppVPBGEe1p1JkGoDj8RUXUSdzJeXzzk/ms0tr+ySNP8ojktkH28vMdFy7g/ZqTYdlk4tGfDYp3sG/GEYpIxzptVDFYQYziWmuNlwrlZhdEMnHnVzG91zpZTOXeCTfqAdIKqdIJ0jSwWDVZa4PuDRg5sM5XGqyExxO8GSYSQBZAQSQbRJAdgABZA10DzAKICOAADJNAmgLIIA2kwBaAwigdaADaAk9wCCAowEEcNQkgH9yOmDmd/CeQQCqk3cDBqBJdyqkuyDSGDAADtqrtx5wwOWCSKR8yiEOUE89H9HS8yeNLIYwhGxGkMco8hitP4iJKgdNA6iLqzndvMk2tlKnrPqSEz1/1/asPqQzjRmUMpkvuK7xVf2sektiORx3eZ6siSd5QX3sXFGGcSvf17xqFymdDFX/MQoAZB9XOm7IVlZTpeI6jy9F/NRmu8RaXlNTTL8CsNMhgD0sie8Ia88XaBe7ZDKro2+3WcgOJzXoOnalo2HobRSaML8HwmtM5Q4HUzJHpxi1T4lJk+b26H7meHHBTcayWasFjcpRv6F/TnA9v9TItYV56pOoRgxiFOP4Cl8il8FkkM6ntNPOMT7iQw7ytjoV1Q/elilU2e4ufya/cwZW3wNG0hTbW5mjOi21x3MD32Ayg231u2ikhmq2W4OQ86hlXIxP7gj1nicAQKpjHJLZw/TPT3j20cplIQsc7u59wibWU332gFZGsM92i71K3eCRB4CUsNMm+QTj+x+OlIncw02uBzSHWcva/ieAZS4VNjpfV3WeAQCps7kdeKda2a/TWkb8N7tOsorHI0+P2XhirSpxWoGz8d0jNvPvtX2aOESeYD8mLrblsJSmfvfDfuWifWY8wMYHHlf39ua5it9zmeGv4FYW/m9ALfWMtsjziipyXrDTEf7tWPby9J4NlbuoNW4+XM9+uab3X82WM4Db3RTsEIB6g6csE4oBJE2eYYVHNwmHUyWLACTFcvt7jbsgC25ujDRabpfOYgsvxLmvH1vuZgVLeeCs565vjJi7M9TN+1yC9/IBX7Z4OlO95K44F7N8tZnVVih9MR9L81e6Nd/ttbm7bU99+y2vc497Zbc3R/PYy3lJYX4ibo6Ceocy2pPA/DbK4jE/juvzqo75dCXY/E7mq93xFRFH/ABVyWIS+V+UdLNYxX2HNc4YInIrzyYohMIZvqvWx19M3EFUZCYVCThD0sY8958+owBAithou0hhXpIpigyoXUxgt4/m72aiKfMNhdRURyhmhU8d33KK1RFzBZqMJXYdT3ocS6yJxaZjiRkMqqqquYKHPTtM0cFDXGHafC/iCRawjFnG4wlWcp/y5JSCNxElx/MLZhuC0M0GHgxQRMleCGO5g5vJiauQk7wYyJiivRAyERYyw1VU2RrWsTHAUWX7YDif6ZQyQ/MiSyM17GCn+rc/g4oU/2YzciFjKOiNLJ1FNpm00UIrLXxMIw00UO/mNElAACSnhNHlQwAhgBBACCAEEAIIAYQAQgAhgBDAgJT/DgDyxCJjaj0UmAAAAABJRU5ErkJggg==) no-repeat top center; \r\n\tpadding-top:160px;\r\n\tmargin-top:30%;\r\n\tfont-weight:normal;\r\n}\r\n\r\n</style>\r\n</head>\r\n\r\n<body>\r\n<div class=\"box\">\r\n<h1>{{提示信息}}</h1><br><br>\r\n<a href=\"javascript:history.go(-1);\">返回上一页</a>\r\n</div>\r\n</body>\r\n</html>">
        替换所有子文本 (错误模板, "{{提示信息}}", 错误信息)
        替换所有子文本 (错误模板, "{{标题}}", 网站标题)
        返回 (错误模板)

    }

    方法 置模板设置 <公开 折叠>
    参数 HTML设置 <类型 = HTML设置>
    {
        m_HTML设置 = HTML设置

    }

    方法 检测更新 <公开 类型 = 逻辑型 折叠>
    {
        变量 文件 <类型 = 文本型>
        文件 = m_文件路径
        如果 (文件 == "")
        {
            返回 (假)
        }
        如果 (取文件时间 (文件) != m_文件时间)
        {
            返回 (加载模板 (文件))
        }
        返回 (假)
    }

    方法 加载模板 <公开 类型 = 逻辑型>
    参数 模板文件路径 <类型 = 文本型>
    {
        m_文件路径 = 模板文件路径
        m_文件时间 = 取文件时间 (m_文件路径)
        如果 (为最小时间 (m_文件时间))
        {
            返回 (假)
        }
        变量 模板 <类型 = 文本型>
        模板 = w_字节集_探测编码到文本 (读入文件 (m_文件路径))


        返回 (解析模板 (模板))
    }

    方法 取文件路径 <公开 类型 = 文本型 折叠>
    {
        返回 (m_文件路径)
    }

    方法 标签是否存在 <公开 类型 = 逻辑型 折叠>
    参数 标签名 <类型 = 文本型 注释 = "\"网站标题\"">
    {
        返回 (m_标签集合.是否存在 (标签名))
    }

    方法 _取字符 <静态 类型 = 字符 注释 = "防止越界...." 折叠>
    参数 文本 <类型 = 文本型>
    参数 索引 <类型 = 整数>
    {
        如果 (索引 >= 0 && 索引 < 取文本长度 (文本))
        {
            返回 (取字符 (文本, 索引))
        }
        返回 ('\0')

    }

    方法 解析模板 <公开 类型 = 逻辑型 折叠>
    参数 模板 <类型 = 文本型>
    {
        变量 索引 <类型 = 整数>
        变量 解析中 <类型 = 逻辑型 注释 = "解析">
        变量 标签名 <类型 = 文本型>
        变量 标签 <类型 = 模板标签>
        变量 HTML <类型 = 文本型>
        变量 ""
        变量 上一字符 <类型 = 字符>
        变量 当前字符 <类型 = 字符>
        变量 下一字符 <类型 = 字符>
        变量 解析器 <类型 = w_网页解析器>
        变量 HTML模板 <类型 = 文本型>
        m_标签集合.清空 ()
        解析器.解析 (模板)

        // HTML模板 = 解析器.取回网页 (取反 (m_HTML设置.压缩HTML), m_HTML设置.压缩JS, m_HTML设置.压缩HTML)

        HTML模板 = 解析器.取回网页 (取反 (m_HTML设置.压缩HTML), 0, 真)





        m_读写锁.写 ()
        m_模板标签.删除所有成员 ()
        判断循环 (索引 < 取文本长度 (HTML模板))
        {
            上一字符 = _取字符 (HTML模板, 索引 - 1)
            当前字符 = _取字符 (HTML模板, 索引)
            下一字符 = _取字符 (HTML模板, 索引 + 1)
            索引 = 索引 + 1
            如果 (解析中 == 假 && 当前字符 == '{' && 下一字符 == '{' && 上一字符 != '\\')  // 必须={{   不能=\{{
            {
                如果 (取文本长度 (HTML) > 0)  // 如果是空的HTML,那就没有必要加进去了
                {
                    标签.标签类型 = (整数)标签类型.HTML
                    标签.HTML = HTML
                    标签.标签名 = ""
                    m_模板标签.加入成员 (标签)
                    清空文本 (HTML)

                }
                解析中 = 真  // 遇到{{
                索引 = 索引 + 1

            }
            否则 (解析中 == 真 && 当前字符 == '}' && 下一字符 == '}')  // 必须=}}
            {
                标签.标签类型 = (整数)标签类型.标签
                标签.HTML = ""
                标签.标签名 = 标签名
                m_模板标签.加入成员 (标签)
                清空文本 (标签名)

                索引 = 索引 + 1
                解析中 = 假
            }
            否则 (解析中 == 真)
            {
                加入字符 (标签名, 当前字符)
            }
            否则
            {
                加入字符 (HTML, 当前字符)

            }


        }
        如果 (取文本长度 (HTML) > 0)
        {
            标签.标签类型 = (整数)标签类型.HTML
            标签.HTML = HTML
            标签.标签名 = ""
            m_模板标签.加入成员 (标签)
        }


        变量 i <类型 = 整数>
        循环 (0, m_模板标签.取成员数 (), i)
        {
            如果 (m_模板标签.取成员 (i).标签类型 != (整数)标签类型.HTML)
            {
                m_模板标签.取成员 (i).标签名 = 删首尾空 (m_模板标签.取成员 (i).标签名)
                如果 (是否以字符开头 (m_模板标签.取成员 (i).标签名, '/'))  // 结束标签
                {
                    m_模板标签.取成员 (i).标签类型 = (整数)标签类型.结束标签
                    m_模板标签.取成员 (i).标签名 = 删首尾空 (取文本右边 (m_模板标签.取成员 (i).标签名, 取文本长度 (m_模板标签.取成员 (i).标签名) - 1))
                }
                否则 (是否以字符开头 (m_模板标签.取成员 (i).标签名, '#'))  // 循环
                {
                    m_模板标签.取成员 (i).标签类型 = (整数)标签类型.循环1
                    m_模板标签.取成员 (i).标签名 = 删首尾空 (取文本右边 (m_模板标签.取成员 (i).标签名, 取文本长度 (m_模板标签.取成员 (i).标签名) - 1))
                }
                否则 (是否以字符开头 (m_模板标签.取成员 (i).标签名, '?'))  // 判断
                {
                    m_模板标签.取成员 (i).标签类型 = (整数)标签类型.判断
                    m_模板标签.取成员 (i).标签名 = 删首尾空 (取文本右边 (m_模板标签.取成员 (i).标签名, 取文本长度 (m_模板标签.取成员 (i).标签名) - 1))
                    _解析运算符 (m_模板标签.取成员 (i))
                }
                否则 (是否以字符开头 (m_模板标签.取成员 (i).标签名, '>'))  // 内嵌模板
                {
                    m_模板标签.取成员 (i).标签类型 = (整数)标签类型.内嵌模板
                    m_模板标签.取成员 (i).标签名 = 删首尾空 (取文本右边 (m_模板标签.取成员 (i).标签名, 取文本长度 (m_模板标签.取成员 (i).标签名) - 1))
                }
                否则
                {

                    如果 (m_模板标签.取成员 (i).函数.解析 (m_模板标签.取成员 (i).标签名))  // 是脚本函数!
                    {
                        m_模板标签.取成员 (i).标签类型 = (整数)标签类型.函数
                        到循环尾  // 脚本函数,没有标签参数..
                    }
                }


                如果 (寻找字符 (m_模板标签.取成员 (i).标签名, ':') != -1)  // 有标签参数...
                {
                    变量 _标签 <类型 = 文本型>
                    变量 _参数 <类型 = 文本型>
                    如果 (分割左右文本 (m_模板标签.取成员 (i).标签名, ':', _标签, _参数))
                    {
                        m_模板标签.取成员 (i).标签名 = _标签
                        m_模板标签.取成员 (i).标签参数 = _参数
                    }

                }


            }

        }

        循环 (0, m_模板标签.取成员数 (), i)
        {
            如果 (m_模板标签.取成员 (i).标签类型 == (整数)标签类型.循环1 || m_模板标签.取成员 (i).标签类型 == (整数)标签类型.判断)
            {
                _置标签结束索引 (i, m_模板标签, m_模板标签.取成员 (i).标签类型, m_模板标签.取成员 (i).标签名)
            }


            如果 (m_模板标签.取成员 (i).标签名 != "")
            {
                如果 (m_标签集合.是否存在 (m_模板标签.取成员 (i).标签名) == 假)  // 不存在,才插入
                {
                    m_标签集合.插入 (m_模板标签.取成员 (i).标签名, i)
                }



            }
            // 调试输出 (m_模板标签.取成员 (i).标签名, m_模板标签.取成员 (i).运算符, m_模板标签.取成员 (i).标签结束索引)
        }


        m_读写锁.结束写 ()


        //.如果 (解析.取网页编码 () = “UTF-8”)  ' 网页编码
        // UTF8 = 真
        //.否则
        // UTF8 = 假
        //.如果结束



        // 读写锁_写 (m_读写锁)  ' 给B上锁
        // m_UTF8 = UTF8
        // 指针_交换数组 (模板标签, m_模板标签)  ' 用交换的方式.让用户感觉不到任何热更新..虽然不这样做也感觉不到...但是,强迫症你懂的
        // 读写锁_结束写 (m_读写锁)



        返回 (真)
    }

    方法 取模板标签参数 <公开 类型 = 文本型>
    参数 标签名 <类型 = 文本型>
    {
        m_读写锁.读 ()
        如果 (m_标签集合.是否存在 (标签名) == 假)  // 不存在的标签!
        {
            m_读写锁.结束读 ()
            返回 ("")
        }
        变量 索引 <类型 = 整数>
        变量 返回值 <类型 = 文本型>
        索引 = m_标签集合.取值 (标签名)
        如果 (m_模板标签.索引是否有效 (索引))
        {
            返回值 = m_模板标签.取成员 (索引).标签参数
        }
        m_读写锁.结束读 ()
        返回 (返回值)





    }

    方法 _置标签结束索引 <静态 类型 = 逻辑型>
    参数 索引 <类型 = 整数>
    参数 标签数组 <类型 = 模板数组>
    参数 标签类型 <类型 = 整数 注释 = "循环 || 判断">
    参数 标签名 <类型 = 文本型>
    {
        如果 (标签数组.索引是否有效 (索引) == 假)
        {
            返回 (假)
        }
        如果 (标签类型 == (整数)标签类型.HTML)
        {
            返回 (假)
        }
        如果 (标签类型 == (整数)标签类型.结束标签)
        {
            返回 (假)
        }
        变量 i <类型 = 整数>
        变量 嵌套数量 <类型 = 整数>
        循环 (索引 + 1, 标签数组.取成员数 (), i)
        {
            如果 (标签数组.取成员 (i).标签类型 == 标签类型 && 标签数组.取成员 (i).标签名 == 标签名)
            {
                嵌套数量 = 嵌套数量 + 1
            }
            否则 (标签数组.取成员 (i).标签类型 == (整数)标签类型.结束标签 && 标签数组.取成员 (i).标签名 == 标签名)
            {
                嵌套数量 = 嵌套数量 - 1
                如果 (嵌套数量 <= 0)
                {
                    标签数组.取成员 (索引).标签结束索引 = i
                    返回 (真)
                }

            }

        }

        返回 (假)

    }

    方法 _解析运算符 <静态>
    参数 模板标签 <类型 = 模板标签>
    {
        模板标签.标签类型 = (整数)标签类型.判断
        模板标签.运算符 = (整数)运算符.空
        如果 (_________解析运算符 (模板标签, "?=", (整数)运算符.近似等于))
        {

        }
        否则 (_________解析运算符 (模板标签, "!=", (整数)运算符.不等于))
        {

        }
        否则 (_________解析运算符 (模板标签, ">=", (整数)运算符.大于等于))
        {

        }
        否则 (_________解析运算符 (模板标签, "<=", (整数)运算符.小于等于))
        {

        }
        否则 (_________解析运算符 (模板标签, ">", (整数)运算符.大于))
        {

        }
        否则 (_________解析运算符 (模板标签, "<", (整数)运算符.小于))
        {

        }
        否则 (_________解析运算符 (模板标签, "=", (整数)运算符.等于))
        {

        }
        否则
        {
            // w_信息框 ("出错???")


        }


    }

    方法 _________解析运算符 <静态 类型 = 逻辑型 折叠>
    参数 模板标签 <类型 = 模板标签>
    参数 运算符文本 <类型 = 文本型>
    参数 运算符 <类型 = 整数>
    {
        变量 位置1 <类型 = 整数>
        变量 标签名 <类型 = 文本型>
        位置1 = 寻找文本 (模板标签.标签名, 运算符文本, , 假)
        如果 (位置1 == -1)
        {
            返回 (假)
        }
        标签名 = 取文本左边 (模板标签.标签名, 位置1)
        模板标签.判断文本 = 取文本右边 (模板标签.标签名, 取文本长度 (模板标签.标签名) - 位置1 - 取文本长度 (运算符文本))
        模板标签.判断文本 = 删首尾空 (模板标签.判断文本)
        模板标签.判断整数 = 文本到整数 (模板标签.判断文本)
        模板标签.运算符 = 运算符
        模板标签.标签名 = 标签名

        如果 (模板标签.判断文本 != "")
        {
            如果 (是否以字符开头 (模板标签.判断文本, '"') || 是否以字符开头 (模板标签.判断文本, '\''))  // 字符串
            {
                模板标签.判断文本 = 取文本右边 (模板标签.判断文本, 取文本长度 (模板标签.判断文本) - 1)
                模板标签.判断文本 = 取文本左边 (模板标签.判断文本, 取文本长度 (模板标签.判断文本) - 1)
                模板标签.数据类型 = (整数)数据类型.文本

            }
            否则 (到文本 (文本到整数 (模板标签.判断文本)) == 模板标签.判断文本)  // 纯数字..
            {
                模板标签.数据类型 = (整数)数据类型.整数型
                模板标签.判断整数 = 文本到整数 (模板标签.判断文本)
            }
            否则 (模板标签.判断文本 == "真" || 模板标签.判断文本 == "假" || 文本比较 (模板标签.判断文本, "false", 假) == 0 || 文本比较 (模板标签.判断文本, "true", 假) == 0)
            {
                模板标签.判断逻辑 = 选择 (模板标签.判断文本 == "真" || 文本比较 (模板标签.判断文本, "true", 假) == 0, 真, 假)
                模板标签.数据类型 = (整数)数据类型.逻辑

            }
            否则
            {
                模板标签.数据类型 = (整数)数据类型.文本  // 不知道啥类型了..按文本处理吧
            }




        }
        返回 (真)


    }

    方法 渲染 <公开 类型 = 逻辑型>
    参数 父类 <类型 = 变整数>
    参数 返回模板 <类型 = 文本型>
    参数 文件路径 <类型 = 文本型>
    参数 渲染数据 <类型 = 哈希表>
    {
        返回 (渲染辅助.渲染 (父类, m_读写锁, m_模板标签, 文件路径, 渲染数据, 返回模板))




    }
}

类 渲染辅助
{
    方法 渲染 <公开 静态 类型 = 逻辑型>
    参数 父类 <类型 = 变整数>
    参数 读写锁 <类型 = w_读写锁>
    参数 模板标签 <类型 = 模板数组>
    参数 文件路径 <类型 = 文本型>
    参数 渲染数据 <类型 = 哈希表>
    参数 返回模板 <类型 = 文本型>
    {
        变量 i <类型 = 整数>
        读写锁.读 ()
        如果 (模板标签.取成员数 () == 0)
        {
            读写锁.结束读 ()
            返回 (假)
        }
        判断循环 (i < 模板标签.取成员数 ())
        {
            如果 (模板标签.取成员 (i).标签类型 == (整数)标签类型.标签)
            {
                添加渲染 (返回模板, 渲染数据, 模板标签.取成员 (i).标签名)
            }
            否则 (模板标签.取成员 (i).标签类型 == (整数)标签类型.函数)
            {

                处理模板函数 (返回模板, 模板标签.取成员 (i).函数, 读指针处对象 (父类, 模板引擎).m_注册函数, 渲染数据)
            }
            否则 (模板标签.取成员 (i).标签类型 == (整数)标签类型.HTML)
            {
                加入文本 (返回模板, 模板标签.取成员 (i).HTML)
            }
            否则 (模板标签.取成员 (i).标签类型 == (整数)标签类型.循环1)
            {
                变量 数组 <类型 = 数组类>
                数组 = 渲染数据.取数组 (模板标签.取成员 (i).标签名)
                渲染列表 (父类, 返回模板, 渲染数据, 数组, i, 模板标签)
                如果 (模板标签.取成员 (i).标签结束索引 == -1)  // 死循环
                {
                    置文本 (返回模板, 单个模板.取错误页面 ("标签错误", "没有找到<font size='5' face='arial' color='red'>{{" + 模板标签.取成员 (i).标签名 + "}}</font>的结束标签!<br>文件路径:" + 文件路径))
                    读写锁.结束读 ()
                    返回 (假)
                }
                i = 模板标签.取成员 (i).标签结束索引  // 循环完成.到循环尾
                到循环尾
            }
            否则 (模板标签.取成员 (i).标签类型 == (整数)标签类型.判断)  // {{? xxxx }}
            {
                如果 (判断标签 (返回模板, 渲染数据, i, 模板标签) == 假)
                {
                    如果 (模板标签.取成员 (i).标签结束索引 == -1)  // 死循环
                    {
                        置文本 (返回模板, 单个模板.取错误页面 ("标签错误", "没有找到<font size='5' face='arial' color='red'>{{" + 模板标签.取成员 (i).标签名 + "}}</font>的结束标签!<br>文件路径:" + 文件路径))
                        读写锁.结束读 ()
                        返回 (假)
                    }
                    i = 模板标签.取成员 (i).标签结束索引  // 条件没成立,跳过判断中间部分
                    到循环尾
                }

            }
            否则 (模板标签.取成员 (i).标签类型 == (整数)标签类型.内嵌模板)  // 这里,可能有点复杂了..
            {
                如果 (渲染子文件 (父类, 返回模板, 文件路径, 模板标签.取成员 (i).标签名, 渲染数据) == 假)
                {
                    置文本 (返回模板, 单个模板.取错误页面 ("渲染子模板失败", "没有找到<font size='5' face='arial' color='red'>{{" + 模板标签.取成员 (i).标签名 + "}}</font>子模板!<br>文件路径:" + 文件路径))
                    读写锁.结束读 ()
                    返回 (假)
                }


            }
            i = i + 1


        }

        读写锁.结束读 ()
        返回 (真)
    }

    方法 渲染子文件 <静态 类型 = 逻辑型 折叠>
    参数 父类 <类型 = 变整数>
    参数 返回模板 <类型 = 文本型>
    参数 父模板文件路径 <类型 = 文本型>
    参数 文件路径 <类型 = 文本型>
    参数 渲染数据 <类型 = 哈希表>
    {
        变量 执行状态 <类型 = 逻辑型>
        变量 渲染结果 <类型 = 文本型>
        变量 父目录 <类型 = 文本型>
        变量 文件 <类型 = 文本型>
        父目录 = w_文件_取目录 (父模板文件路径)
        文件 = 父目录 + 文件路径
        执行状态 = 读指针处对象 (父类, 模板引擎)._渲染子文件 (渲染结果, 文件, 渲染数据)
        如果 (执行状态)
        {
            加入文本 (返回模板, 渲染结果)
        }
        否则
        {
            置文本 (返回模板, 渲染结果)
        }
        返回 (执行状态)
    }

    方法 渲染列表 <静态 类型 = 逻辑型>
    参数 父类 <类型 = 变整数>
    参数 返回模板 <类型 = 文本型>
    参数 全局表 <类型 = 哈希表>
    参数 渲染列表 <类型 = 数组类>
    参数 标签索引 <类型 = 整数>
    参数 标签数组 <类型 = 模板数组>
    {
        变量 i <类型 = 整数>
        变量 n <类型 = 整数>
        变量 结束索引 <类型 = 整数>
        变量 局部表 <类型 = 哈希表>
        变量 列表 <类型 = 数组类>
        变量 跳到指定行 <类型 = 整数 值 = -1>
        结束索引 = 标签数组.取成员 (标签索引).标签结束索引
        如果 (结束索引 == -1)
        {
            返回 (假)
        }
        如果 (渲染列表.取数量 () <= 0)
        {
            返回 (假)
        }

        循环 (0, 渲染列表.取数量 (), i)
        {
            局部表 = 渲染列表.取哈希表 (i)
            循环 (标签索引 + 1, 结束索引, n, 1)  // 这里不知道要不要-1
            {
                如果 (跳到指定行 != -1)
                {
                    如果 (跳到指定行 == n)  // 到达了指定行
                    {
                        跳到指定行 = -1
                    }
                    // 调试输出 ("跳过")

                    到循环尾
                }
                // w_信息框 ("没有执行???")
                // 调试输出 ("没有执行吗", i, n)

                如果 (标签数组.取成员 (n).标签类型 == (整数)标签类型.标签)  // {{标签}}
                {
                    如果 (局部表.是否存在 (标签数组.取成员 (n).标签名))
                    {
                        添加渲染 (返回模板, 局部表, 标签数组.取成员 (n).标签名)
                    }
                    否则 (全局表.是否存在 (标签数组.取成员 (n).标签名))
                    {
                        添加渲染 (返回模板, 全局表, 标签数组.取成员 (n).标签名)
                    }


                }
                否则 (标签数组.取成员 (n).标签类型 == (整数)标签类型.函数)
                {
                    处理模板函数 (返回模板, 标签数组.取成员 (n).函数, 读指针处对象 (父类, 模板引擎).m_注册函数, 全局表, 局部表)

                }
                否则 (标签数组.取成员 (n).标签类型 == (整数)标签类型.HTML)
                {
                    加入文本 (返回模板, 标签数组.取成员 (n).HTML)
                }
                否则 (标签数组.取成员 (n).标签类型 == (整数)标签类型.循环1)  // {{#循环}}{{标签}}{{/循环}}
                {
                    如果 (局部表.是否存在 (标签数组.取成员 (n).标签名))
                    {
                        列表 = 局部表.取数组 (标签数组.取成员 (n).标签名)
                        渲染列表 (父类, 返回模板, 全局表, 列表, n, 标签数组)  // 递归
                    }
                    否则
                    {
                        渲染列表 (父类, 返回模板, 全局表, 渲染列表, n, 标签数组)  // 递归
                    }

                    跳到指定行 = 标签数组.取成员 (n).标签结束索引

                }
                否则 (标签数组.取成员 (n).标签类型 == (整数)标签类型.判断)
                {
                    如果 (局部表.是否存在 (标签数组.取成员 (n).标签名))
                    {
                        如果 (判断标签 (返回模板, 局部表, n, 标签数组) == 假)  // 查找局部表
                        {
                            跳到指定行 = 标签数组.取成员 (n).标签结束索引
                        }

                    }
                    否则
                    {
                        如果 (判断标签 (返回模板, 全局表, n, 标签数组) == 假)  // 查找全局表
                        {
                            跳到指定行 = 标签数组.取成员 (n).标签结束索引
                        }

                    }

                }
                否则
                {
                    // 跳过...

                }



            }


        }

        返回 (真)




    }

    方法 判断标签 <静态 类型 = 逻辑型 折叠>
    参数 返回模板 <类型 = 文本型>
    参数 渲染数据 <类型 = 哈希表>
    参数 标签索引 <类型 = 整数>
    参数 标签数组 <类型 = 模板数组>
    {
        变量 列表 <类型 = 数组类>
        变量 类型 <类型 = 整数>
        变量 结束索引 <类型 = 整数>
        变量 标签名 <类型 = 文本型>
        变量 运算符 <类型 = 整数>
        结束索引 = 标签数组.取成员 (标签索引).标签结束索引
        如果 (结束索引 < 0)
        {
            返回 (假)
        }
        标签名 = 标签数组.取成员 (标签索引).标签名
        类型 = 渲染数据.取类型 (标签名)
        运算符 = 标签数组.取成员 (标签索引).运算符
        如果 (运算符 == (整数)运算符.空)
        {
            返回 (渲染数据.是否存在 (标签名))
        }
        否则 (标签数组.取成员 (标签索引).运算符 == (整数)运算符.大于)
        {
            如果 (类型 == (整数)键值类型.数组类)
            {
                返回 (渲染数据.取数组 (标签名).取数量 () > 标签数组.取成员 (标签索引).判断整数)
            }
            否则
            {
                返回 (渲染数据.取整数 (标签名) > 标签数组.取成员 (标签索引).判断整数)
            }

        }
        否则 (运算符 == (整数)运算符.小于)
        {
            如果 (类型 == (整数)键值类型.数组类)
            {
                返回 (渲染数据.取数组 (标签名).取数量 () < 标签数组.取成员 (标签索引).判断整数)
            }
            否则
            {
                返回 (渲染数据.取整数 (标签名) < 标签数组.取成员 (标签索引).判断整数)
            }

        }
        否则 (运算符 == (整数)运算符.等于)
        {
            如果 (类型 == (整数)键值类型.数组类)
            {
                返回 (渲染数据.取数组 (标签名).取数量 () == 标签数组.取成员 (标签索引).判断整数)
            }
            否则 (类型 == (整数)数据类型.文本)
            {
                返回 (渲染数据.取文本 (标签名) == 标签数组.取成员 (标签索引).判断文本)
            }
            否则 (类型 == (整数)数据类型.整数型)
            {
                返回 (渲染数据.取整数 (标签名) == 标签数组.取成员 (标签索引).判断整数)
            }
            否则 (类型 == (整数)数据类型.逻辑)
            {
                返回 (渲染数据.取逻辑值 (标签名) == 标签数组.取成员 (标签索引).判断逻辑)
            }
            否则
            {
                返回 (渲染数据.取文本 (标签名) == 标签数组.取成员 (标签索引).判断文本)  // 不知道什么类型,按文本处理
            }


        }
        否则 (运算符 == (整数)运算符.不等于)
        {
            返回 (渲染数据.取文本 (标签名) != 标签数组.取成员 (标签索引).判断文本)
        }
        否则 (运算符 == (整数)运算符.小于等于)
        {
            如果 (类型 == (整数)键值类型.数组类)
            {
                返回 (渲染数据.取数组 (标签名).取数量 () <= 标签数组.取成员 (标签索引).判断整数)
            }
            否则
            {
                返回 (渲染数据.取整数 (标签名) <= 标签数组.取成员 (标签索引).判断整数)
            }



        }
        否则 (运算符 == (整数)运算符.大于等于)
        {
            如果 (类型 == (整数)键值类型.数组类)
            {
                返回 (渲染数据.取数组 (标签名).取数量 () >= 标签数组.取成员 (标签索引).判断整数)
            }
            否则
            {
                返回 (渲染数据.取整数 (标签名) >= 标签数组.取成员 (标签索引).判断整数)
            }


        }
        否则 (运算符 == (整数)运算符.近似等于)
        {
            返回 (近似等于 (渲染数据.取文本 (标签名), 标签数组.取成员 (标签索引).判断文本, 假))
        }

        返回 (假)
    }

    方法 添加渲染 <静态 折叠>
    参数 返回模板 <类型 = 文本型>
    参数 键值表 <类型 = 哈希表>
    参数 标签名 <类型 = 文本型>
    {
        加入文本 (返回模板, 键值表.取文本 (标签名))


    }

    方法 处理模板函数 <静态>
    参数 返回模板 <类型 = 文本型>
    参数 函数 <类型 = 函数解析>
    参数 注册函数 <类型 = 文本到变整数哈希表>
    参数 全局表 <类型 = 哈希表>
    参数 局部表 <类型 = 哈希表 @默认值 = 空对象>
    {
        变量 函数名称 <类型 = 文本型>
        变量 函数地址 <类型 = 变整数>
        变量 参数 <类型 = 文本型>
        变量 参数类型 <类型 = 整数>
        变量 参数数量 <类型 = 整数>
        变量 i <类型 = 整数>
        变量 参数值数组 <类型 = 文本数组类>
        变量 参数类型数组 <类型 = 整数数组类>
        函数名称 = 函数.取函数名称 ()
        如果 (注册函数.是否存在 (函数名称) == 假)  // 不存在的函数!
        {
            返回
        }
        函数地址 = 注册函数.取值 (函数名称)
        如果 (函数地址 == 0)
        {
            返回
        }

        参数数量 = 函数.取参数数量 ()
        循环 (0, 参数数量, i)
        {
            <> 参数 = 函数.取参数 (i)
            参数类型 = 函数.取参数类型 (i)  // -1=NULL,0=变量,1=整数,2=小数,3=逻辑,4=文本
            如果 (参数类型 == 0)  // 变量!
            {
                如果 (是否为空对象 (局部表) == 假 && 局部表.是否存在 (参数))
                {
                    <> 参数 = 局部表.取文本 (参数)
                }
                否则
                {
                    <> 参数 = 全局表.取文本 (参数)
                }
            }
            参数值数组.加入成员 (参数)
            参数类型数组.加入成员 (参数类型)

        }

        变量 数组 <类型 = "变整数 [3]">
        变量 文本指针 <类型 = 变整数>
        数组 [0] = 取变量地址 (参数值数组)
        数组 [1] = 取变量地址 (参数类型数组)
        文本指针 = __Call_1 (函数地址, 取数组变量地址 (数组))


        加入文本 (返回模板, 接收文本 (文本指针))


    }

    方法 __Call_1 <静态 类型 = 变整数 折叠 折叠2 "" @禁止流程检查 = 真>
    参数 函数地址 <类型 = 变整数>
    参数 参数1 <类型 = 变整数>
    {
        @ typedef  INT_PTR (WINAPI* hs) (INT_PTR);
        @ hs c=(hs)@<函数地址>;
        @ return c(@<参数1>);

    }

    #
}

类 模板函数 <公开>
{
    方法 格式化时间 <公开 静态 类型 = 变整数 折叠>
    参数 参数句柄 <类型 = 变整数>
    {
        变量 时间 <类型 = 小数>
        变量 时间模板 <类型 = 文本型>
        变量 补零 <类型 = 逻辑型>
        变量 返回值 <类型 = 文本型>
        时间 = 模板参数_取小数 (参数句柄, 0)  // 时间参数
        时间模板 = 模板参数_取文本 (参数句柄, 1)  // 时间模板
        补零 = 模板参数_取逻辑 (参数句柄, 2)  // 补零
        如果 (文本是否为空 (时间模板))
        {
            时间模板 = "{年}-{月}-{日} {时}:{分}:{秒}"
        }
        返回值 = w_时间_到文本 (时间, 时间模板, 补零)
        返回 (模板参数_返回文本 (返回值))
    }

    方法 取请求文件 <公开 静态 类型 = 变整数 折叠>
    参数 参数句柄 <类型 = 变整数>
    {
        变量 请求 <类型 = 请求类>
        返回 (模板参数_返回文本 (请求.取请求文件 (模板参数_取逻辑 (参数句柄, 0))))
    }

    方法 取请求域名 <公开 静态 类型 = 变整数 折叠>
    参数 参数句柄 <类型 = 变整数>
    {
        变量 请求 <类型 = 请求类>
        返回 (模板参数_返回文本 (请求.取请求域名 ()))
    }

    方法 取参数 <公开 静态 类型 = 变整数 折叠>
    参数 参数句柄 <类型 = 变整数>
    {
        变量 请求 <类型 = 请求类>
        变量 名称 <类型 = 文本型>
        名称 = 模板参数_取文本 (参数句柄, 0)
        返回 (模板参数_返回文本 (请求.取参数 (名称)))
    }

    方法 模板参数_取参数数量 <公开 静态 类型 = 整数 折叠>
    参数 参数句柄 <类型 = 变整数>
    {
        变量 参数指针 <类型 = 变整数>
        参数指针 = 读指针处值 (参数句柄, 变整数)
        返回 (读指针处对象 (参数指针, 文本数组类).取成员数 ())
    }

    方法 模板参数_取文本 <公开 静态 类型 = 文本型 折叠>
    参数 参数句柄 <类型 = 变整数>
    参数 索引 <类型 = 整数>
    {
        变量 参数指针 <类型 = 变整数>
        参数指针 = 读指针处值 (参数句柄, 变整数)
        如果 (读指针处对象 (参数指针, 文本数组类).索引是否有效 (索引))
        {
            返回 (读指针处对象 (参数指针, 文本数组类).取成员 (索引))
        }
        返回 ("")
    }

    方法 模板参数_取整数 <公开 静态 类型 = 整数 折叠>
    参数 参数句柄 <类型 = 变整数>
    参数 索引 <类型 = 整数>
    {
        变量 参数指针 <类型 = 变整数>
        参数指针 = 读指针处值 (参数句柄, 变整数)
        如果 (读指针处对象 (参数指针, 文本数组类).索引是否有效 (索引))
        {
            返回 (文本到整数 (读指针处对象 (参数指针, 文本数组类).取成员 (索引)))
        }
        返回 (0)
    }

    方法 模板参数_取小数 <公开 静态 类型 = 小数 折叠>
    参数 参数句柄 <类型 = 变整数>
    参数 索引 <类型 = 整数>
    {
        变量 参数指针 <类型 = 变整数>
        参数指针 = 读指针处值 (参数句柄, 变整数)
        如果 (读指针处对象 (参数指针, 文本数组类).索引是否有效 (索引))
        {
            返回 (文本到小数 (读指针处对象 (参数指针, 文本数组类).取成员 (索引)))
        }
        返回 (0)
    }

    方法 模板参数_取逻辑 <公开 静态 类型 = 逻辑型 折叠>
    参数 参数句柄 <类型 = 变整数>
    参数 索引 <类型 = 整数>
    {
        变量 参数指针 <类型 = 变整数>
        参数指针 = 读指针处值 (参数句柄, 变整数)
        如果 (读指针处对象 (参数指针, 文本数组类).索引是否有效 (索引))
        {
            返回 (文本到逻辑值 (读指针处对象 (参数指针, 文本数组类).取成员 (索引)))
        }
        返回 (假)
    }

    方法 模板参数_取参数类型 <公开 静态 类型 = 整数 折叠>
    参数 参数句柄 <类型 = 变整数>
    参数 索引 <类型 = 整数>
    {
        变量 参数指针 <类型 = 变整数>
        参数指针 = 读指针处值 (参数句柄 + 取变整数尺寸 (), 变整数)
        如果 (读指针处对象 (参数指针, 整数数组类).索引是否有效 (索引))
        {
            返回 (读指针处对象 (参数指针, 整数数组类).取成员 (索引))
        }
        返回 (-1)


    }

    方法 模板参数_返回文本 <公开 静态 类型 = 变整数 折叠>
    参数 要返回的文本 <类型 = 文本型>
    {
        返回 (返回文本 (要返回的文本))

    }
}

类 文本到变整数哈希表 <基础类 = 通用哈希表模板类 @模板实现类 = "文本型, 变整数">

类 模板引擎 <公开>
{
    变量 m_模板锁 <类型 = w_读写锁>
    变量 m_模板引擎 <类型 = 变整数数组类 注释 = " [单个模板]">
    变量 m_哈希锁 <类型 = w_读写锁>
    变量 m_模板索引 <类型 = 文本到整数哈希表>
    变量 m_HTML设置 <类型 = HTML设置>
    变量 m_模板目录 <类型 = 文本型>
    变量 m_注册函数 <公开 类型 = 文本到变整数哈希表 注释 = "m_脚本函数">

    方法 类_初始化
    {
        m_模板目录 = 取运行目录 ()
        本对象.注册模板函数 ("格式化时间", 取静态方法地址 (模板函数.格式化时间))  // 格式化时间(发布时间,"{年}-{月}-{日}",真)
        本对象.注册模板函数 ("请求.取请求域名", 取静态方法地址 (模板函数.取请求域名))  // 请求.取请求域名()
        本对象.注册模板函数 ("请求.取请求文件", 取静态方法地址 (模板函数.取请求文件))  // 请求.取请求文件()
        本对象.注册模板函数 ("请求.取参数", 取静态方法地址 (模板函数.取参数))  // 请求.取参数("id")


    }

    方法 类_清理 <折叠>
    {
        本对象.清理模板 ()
    }

    方法 清理模板 <公开 注释 = "清理已经加载的模板!" 折叠>
    {
        变量 i <类型 = 整数>
        m_哈希锁.写 ()  // 先让其它线程找不到...
        m_模板索引.清空 ()
        m_哈希锁.结束写 ()


        m_模板锁.写 ()  // 然后释放模板
        循环 (0, m_模板引擎.取成员数 (), i)
        {
            销毁对象指针 (m_模板引擎.取成员 (i))  // 销毁...
        }
        m_模板引擎.删除所有成员 ()

        m_模板锁.结束写 ()
    }

    方法 热更新 <公开 折叠>
    {
        变量 i <类型 = 整数 注释 = "检测模板是否发生了变化,如果变化了,会重新加载,请用一条时钟线程定时调用此方法">
        变量 成员数 <类型 = 整数>
        m_模板锁.读 ()
        成员数 = m_模板引擎.取成员数 ()
        m_模板锁.结束读 ()
        循环 (0, 成员数, i)
        {
            m_模板锁.读 ()
            读指针处对象 (m_模板引擎.取成员 (i), 单个模板).检测更新 ()
            m_模板锁.结束读 ()
            延时 (1)
        }
    }

    方法 设置 <公开 折叠>
    参数 模板目录 <类型 = 文本型 注释 = "\"D:\\模板目录\\\"">
    参数 压缩HTML <类型 = 逻辑型>
    参数 删除HTML注释 <类型 = 逻辑型>
    参数 压缩JS <类型 = 整数>
    {
        如果 (模板目录 != "")
        {
            m_模板目录 = 模板目录
            如果 (是否以字符结束 (m_模板目录, '\\') == 假)
            {
                加入字符 (m_模板目录, '\\')
            }
        }
        m_HTML设置.压缩HTML = 压缩HTML
        m_HTML设置.删除注释 = 删除HTML注释
        m_HTML设置.压缩JS = 压缩JS




    }

    方法 _查找模板 <类型 = 整数 返回值注释 = "如果不存在,则自动加载,如果加载失败,返回-1" 折叠>
    参数 文件 <类型 = 文本型>
    参数 返回模板路径 <类型 = 文本型>
    {
        变量 file <类型 = 文本型>
        变量 索引 <类型 = 整数>
        如果 (文本是否为空 (文件))
        {
            返回 (-1)
        }
        file = 到小写 (文件)
        如果 (寻找字符 (file, ':') == -1)  // 不是完整路径!
        {
            返回模板路径 = m_模板目录 + file
        }
        否则
        {
            返回模板路径 = file  // 完整路径..
        }

        m_哈希锁.读 ()
        如果 (m_模板索引.是否存在 (返回模板路径))
        {
            索引 = m_模板索引.取值 (返回模板路径)
        }
        否则
        {
            索引 = -1
        }
        m_哈希锁.结束读 ()
        如果 (索引 == -1)  // 这个模板文件.还没加载
        {
            索引 = _新建模板 (返回模板路径)
            如果 (索引 == -1)
            {
                返回 (-1)
            }
            m_哈希锁.写 ()
            m_模板索引.插入 (返回模板路径, 索引)
            m_哈希锁.结束写 ()
        }

        返回 (索引)


    }

    方法 标签是否存在 <公开 类型 = 逻辑型 折叠>
    参数 文件 <类型 = 文本型>
    参数 要判断的标签 <类型 = 文本型>
    {
        变量 索引 <类型 = 整数>
        变量 路径 <类型 = 文本型>
        索引 = _查找模板 (文件, 路径)
        如果 (索引 == -1)  // 模板不存在..
        {
            返回 (假)
        }
        返回 (读指针处对象 (m_模板引擎.取成员 (索引), 单个模板).标签是否存在 (要判断的标签))

    }

    方法 取模板标签参数 <公开 类型 = 文本型 折叠>
    参数 文件 <类型 = 文本型>
    参数 标签 <类型 = 文本型>
    {
        变量 索引 <类型 = 整数>
        变量 路径 <类型 = 文本型>
        索引 = _查找模板 (文件, 路径)
        如果 (索引 == -1)  // 模板不存在..
        {

            返回 ("")
        }
        返回 (读指针处对象 (m_模板引擎.取成员 (索引), 单个模板).取模板标签参数 (标签))


    }

    方法 渲染 <公开 类型 = 文本型 折叠>
    参数 文件 <类型 = 文本型>
    参数 渲染数据 <类型 = 哈希表>
    参数 返回渲染执行结果 <类型 = 变整数 注释 = "取变量地址(逻辑型变量)" @默认值 = 0>
    {
        变量 索引 <类型 = 整数>
        变量 路径 <类型 = 文本型>
        变量 渲染结果 <类型 = 文本型>
        变量 执行结果 <类型 = 逻辑型>
        变量 对象指针 <类型 = 变整数>
        索引 = _查找模板 (文件, 路径)
        如果 (索引 == -1)
        {
            如果 (返回渲染执行结果 != 0)
            {
                写指针处值 (返回渲染执行结果, 假)
            }
            返回 (单个模板.取错误页面 ("模板文件不存在", "模板文件不存在!!<br>文件路径:" + 路径))
        }

        m_模板锁.读 ()
        对象指针 = m_模板引擎.取成员 (索引)
        执行结果 = 读指针处对象 (对象指针, 单个模板).渲染 (本对象.取对象自身指针 (), 渲染结果, 路径, 渲染数据)
        m_模板锁.结束读 ()
        如果 (返回渲染执行结果 != 0)
        {
            写指针处值 (返回渲染执行结果, 执行结果)
        }

        返回 (渲染结果)






    }

    方法 _渲染子文件 <公开 类型 = 逻辑型 注释 = "!!!普通用户不要调用此方法!!!!切记,切记,切记.." 折叠>
    参数 返回模板 <类型 = 文本型>
    参数 模板路径 <类型 = 文本型>
    参数 渲染数据 <类型 = 哈希表>
    {
        变量 返回执行状态 <类型 = 逻辑型>
        变量 渲染结果 <类型 = 文本型>
        变量 文件 <类型 = 文本型>
        变量 文件名 <类型 = 文本型>
        文件名 = 模板路径
        判断循环 (是否以字符开头 (文件名, '/') || 是否以字符开头 (文件名, '\\'))
        {
            删除字符 (文件名, 0, 1)
        }

        如果 (寻找字符 (文件名, ':') == -1)
        {
            文件 = m_模板目录 + 文件名
        }
        否则
        {
            文件 = 文件名

        }



        m_模板锁.结束读 ()  // 先退出
        渲染结果 = 渲染 (文件, 渲染数据, 取变量地址 (返回执行状态))
        m_模板锁.读 ()
        如果 (返回执行状态)
        {
            加入文本 (返回模板, 渲染结果)
        }
        否则
        {
            置文本 (返回模板, 渲染结果)
        }
        返回 (返回执行状态)





    }

    方法 _新建模板 <类型 = 整数 注释 = "-1代表失败,模板文件不存在!" 折叠>
    参数 文件路径 <类型 = 文本型>
    {
        变量 对象指针 <类型 = 变整数>
        变量 索引 <类型 = 整数>
        对象指针 = 创建对象指针 (单个模板)
        如果 (对象指针 == 0)
        {
            返回 (-1)
        }
        读指针处对象 (对象指针, 单个模板).置模板设置 (m_HTML设置)
        如果 (读指针处对象 (对象指针, 单个模板).加载模板 (文件路径) == 假)
        {
            销毁对象指针 (对象指针)
            返回 (-1)
        }

        m_模板锁.写 ()
        索引 = m_模板引擎.加入成员 (对象指针)
        m_模板锁.结束写 ()
        返回 (索引)
    }

    方法 注册模板函数 <公开 类型 = 逻辑型>
    参数 函数名称 <类型 = 文本型>
    参数 函数地址 <类型 = 变整数>
    {
        如果 (函数解析.函数名是否合法 (函数名称) == 假)
        {
            返回 (假)
        }
        m_注册函数.插入 (函数名称, 函数地址)
        返回 (真)
    }

    方法 取消模板函数 <公开 类型 = 逻辑型>
    参数 函数名称 <类型 = 文本型>
    {
        如果 (函数解析.函数名是否合法 (函数名称) == 假)
        {
            返回 (假)
        }
        返回 (m_注册函数.删除 (函数名称))
    }

    方法 取注册函数 <公开 类型 = 变整数 折叠>
    参数 函数名称 <类型 = 文本型>
    {
        如果 (m_注册函数.是否存在 (函数名称))
        {
            返回 (m_注册函数.取值 (函数名称))
        }
        返回 (0)
    }
}

类 函数解析
{
    变量 m_函数名称 <类型 = 文本型>
    变量 m_参数 <类型 = 文本数组类>
    变量 m_参数类型 <类型 = 字节数组类 注释 = "-1=NULL,0=变量,1=整数,2=小数,3=逻辑,4=文本">
    变量 m_解析成功 <类型 = 逻辑型>

    方法 是否为函数 <公开 类型 = 逻辑型 折叠>
    {
        返回 (m_解析成功)
    }

    方法 取函数名称 <公开 类型 = 文本型 折叠>
    {
        返回 (m_函数名称)
    }

    方法 取参数数量 <公开 类型 = 整数 折叠>
    {
        返回 (m_参数.取成员数 ())
    }

    方法 取参数 <公开 类型 = 文本型>
    参数 参数索引 <类型 = 整数>
    {
        返回 (m_参数.取成员 (参数索引))
    }

    方法 取参数类型 <公开 类型 = 整数 返回值注释 = "-1=NULL,0=变量,1=整数,2=小数,3=逻辑,4=文本">
    参数 参数索引 <类型 = 整数>
    {
        返回 (m_参数类型.取成员 (参数索引))
    }

    方法 解析 <公开 类型 = 逻辑型 折叠>
    参数 脚本 <类型 = 文本型>
    {
        变量 索引 <类型 = 整数>
        变量 _脚本 <类型 = 文本型>
        清空文本 (m_函数名称)
        m_参数.删除所有成员 ()
        m_参数类型.删除所有成员 ()
        m_解析成功 = 假

        _脚本 = 脚本
        w_文本_删首尾空 (_脚本, 真)
        索引 = _置函数名称 (_脚本)
        如果 (索引 == -1)
        {
            返回 (假)
        }
        m_解析成功 = _解析参数 (索引, _脚本)
        返回 (m_解析成功)
    }

    方法 _解析参数 <类型 = 逻辑型 折叠>
    参数 索引 <类型 = 整数>
    参数 脚本 <类型 = 文本型>
    {
        变量 当前字符 <类型 = 字符>
        如果 (取字符 (脚本, 索引) != '(')
        {
            返回 (假)
        }
        索引 = 索引 + 1
        判断循环 (索引 < 取文本长度 (脚本))
        {
            索引 = __跳过空白 (索引, 脚本)
            如果 (索引 == -1)
            {
                返回 (假)
            }
            当前字符 = 取字符 (脚本, 索引)
            索引 = 索引 + 1

            如果 (当前字符 == ')')  // 结束啦...
            {
                返回 (真)
            }
            如果 (当前字符 == ',')
            {
                m_参数.加入成员 ("")
                m_参数类型.加入成员 (-1)  // 空对象
                到循环尾
            }
            如果 (当前字符 != '"' && 当前字符 != '\'')  // 变量 || 整数 || 逻辑型 || 函数
            {
                变量 参数 <类型 = 文本型>
                变量 解析完成 <类型 = 逻辑型>
                加入字符 (参数, 当前字符)
                判断循环 (索引 < 取文本长度 (脚本))
                {
                    当前字符 = 取字符 (脚本, 索引)
                    索引 = 索引 + 1
                    如果 (当前字符 == ',')
                    {
                        跳出循环
                    }
                    否则 (当前字符 == ')')
                    {
                        解析完成 = 真
                        跳出循环
                    }
                    加入字符 (参数, 当前字符)

                }
                w_文本_删首尾空 (参数, 真)
                m_参数.加入成员 (参数)
                如果 (w_文本_是否为整数 (参数))
                {
                    m_参数类型.加入成员 (1)
                }
                否则 (w_文本_是否为小数 (参数))
                {
                    m_参数类型.加入成员 (2)
                }
                否则 (w_文本_是否为逻辑 (参数))
                {
                    m_参数类型.加入成员 (3)
                }
                否则
                {
                    m_参数类型.加入成员 (0)  // 标签变量
                }

                如果 (解析完成)
                {
                    返回 (真)
                }
                到循环尾
            }
            否则 (当前字符 == '"' || 当前字符 == '\'')  // 引号
            {
                变量 参数 <类型 = 文本型>
                变量 引号 <类型 = 字符>
                变量 上个字符 <类型 = 字符>
                变量 字符串解析完成 <类型 = 逻辑型>
                引号 = 当前字符
                判断循环 (索引 < 取文本长度 (脚本))
                {
                    上个字符 = 当前字符
                    当前字符 = 取字符 (脚本, 索引)
                    索引 = 索引 + 1
                    如果 (上个字符 == '\\')
                    {
                        如果 (当前字符 == 引号 || 当前字符 == '\\')
                        {
                            // w_置字符 (参数, 取文本长度 (参数) - 1, 当前字符)  // 字符串还没有结束
                            删除字符 (参数, 取文本长度 (参数) - 1, 1)
                            加入字符 (参数, 当前字符)
                            上个字符 = 0
                            到循环尾
                        }
                    }
                    如果 (当前字符 == 引号)
                    {
                        字符串解析完成 = 真
                        跳出循环
                    }
                    加入字符 (参数, 当前字符)
                }
                如果 (字符串解析完成 == 假)  //    "我是字符串.....
                {
                    返回 (假)
                }
                w_文本_删首尾空 (参数, 真)
                m_参数.加入成员 (参数)
                m_参数类型.加入成员 (4)  // 文本型

                索引 = __跳过空白 (索引, 脚本)
                如果 (索引 == -1)
                {
                    返回 (假)
                }
                如果 (取字符 (脚本, 索引) == ',')  // 还有下一个参数
                {
                    索引 = 索引 + 1
                }


                到循环尾
            }


            返回 (假)
        }






        返回 (假)
    }

    方法 _置函数名称 <类型 = 整数 折叠>
    参数 脚本 <类型 = 文本型>
    {
        变量 索引 <类型 = 整数>
        变量 当前字符 <类型 = 字符>
        变量 函数名称 <类型 = 文本型>
        索引 = __跳过空白 (0, 脚本)
        如果 (索引 == -1)
        {
            返回 (-1)
        }
        判断循环 (索引 < 取文本长度 (脚本))
        {
            当前字符 = 取字符 (脚本, 索引)
            索引 = 索引 + 1
            如果 (当前字符 == '(')  // 函数开始啦!
            {
                w_文本_删首尾空 (函数名称, 真)
                如果 (函数名是否合法 (函数名称) == 假)
                {
                    返回 (-1)
                }
                否则
                {
                    m_函数名称 = 函数名称
                    返回 (索引 - 1)
                }
            }
            加入字符 (函数名称, 当前字符)
        }



        返回 (-1)
    }

    方法 函数名是否合法 <公开 静态 类型 = 逻辑型>
    参数 函数名称 <类型 = 文本型>
    {
        如果 (文本是否为空 (函数名称))
        {
            返回 (假)
        }
        变量 当前字符 <类型 = 字符>
        变量 i <类型 = 整数>
        当前字符 = 取字符 (函数名称, 0)
        如果 (w_字符_是否为汉字 (当前字符) == 假 && w_字符_是否为英文字母 (当前字符) == 假 && 当前字符 != '_')  // 不合法的开头!
        {
            返回 (假)
        }
        循环 (0, 取文本长度 (函数名称), i)
        {
            当前字符 = 取字符 (函数名称, i)
            如果 (w_字符_是否为汉字 (当前字符) == 假 && w_字符_是否为英文字母 (当前字符) == 假 && w_字符_是否为数字 (当前字符) == 假 && 当前字符 != '_' && 当前字符 != '.')
            {
                返回 (假)
            }
        }

        返回 (真)

    }

    方法 __跳过空白 <静态 类型 = 整数 折叠>
    参数 索引 <类型 = 整数>
    参数 脚本 <类型 = 文本型>
    {
        变量 当前字符 <类型 = 字符>
        判断循环 (索引 < 取文本长度 (脚本))
        {
            当前字符 = 取字符 (脚本, 索引)
            索引 = 索引 + 1
            如果 (当前字符 == '\r' || 当前字符 == '\n' || 当前字符 == '\t' || 当前字符 == ' ')  // 无字符..
            {
                到循环尾
            }
            索引 = 索引 - 1
            如果 (索引 < 取文本长度 (脚本))
            {
                返回 (索引)
            }
            返回 (-1)
        }
        返回 (-1)
    }
}
