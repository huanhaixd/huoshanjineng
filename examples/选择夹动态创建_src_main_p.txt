
###### 以下为一个火山软件开发平台系统类库程序的内容

<火山程序 类型 = "通常" 版本 = 1 />

包 火山.程序

类 启动类 <公开 基础类 = 窗口程序类>
{
    变量 主窗口对象 <类型 = 我的主窗口>

    方法 启动方法 <公开 类型 = 整数>
    {
        主窗口对象.创建主窗口 ()
        返回 (1)
    }
}

# ===

类 我的主窗口 <基础类 = 窗口 注释 = "样例主窗口" @视窗.布局 = "client_size = \"567, 389\"" 标题 = "我的主窗口">
{
    变量 选择夹1 <类型 = 选择夹 隐藏值属性 = "0" @视窗.布局 = "id = 101\r\npos = \"5, 46, 552, 328\"">
    变量 按钮1添加 <类型 = 按钮 折叠2 隐藏值属性 = "0" @视窗.布局 = "id = 102\r\npos = \"13, 6, 80, 32\"" 标题 = "添加">
    变量 按钮2取组件 <类型 = 按钮 折叠2 隐藏值属性 = "0" @视窗.布局 = "id = 103\r\npos = \"109, 6, 176, 32\"" 标题 = "取数组0中的组件">
    变量 布局数组 <类型 = 我的布局数组>

    方法 按钮_被单击 <接收事件 类型 = 整数>
    参数 来源对象 <类型 = 按钮>
    参数 标记值 <类型 = 整数>
    {
        如果 (来源对象 == 按钮2取组件)
        {
            如果 (选择夹1.取子夹数目 () > 0)
            {
                调试输出 (布局数组.取成员 (0).编辑框1.内容)
                挂接事件 (布局数组.取成员 (0).编辑框1, 0)

            }
        }
        如果 (来源对象 == 按钮1添加)
        {
            变量 临时布局器 <类型 = 组件布局器>
            变量 子夹索引 <类型 = 整数>
            子夹索引 = 选择夹1.添加子夹 ("子夹" + 到文本 (选择夹1.取子夹数目 () + 1), , 组件布局器)
            选择夹1.取子夹内容组件 (子夹索引, 临时布局器)

            变量 布局所加入位置 <类型 = 整数>
            布局所加入位置 = 布局数组.加入新成员 (我的布局1)
            布局数组.取成员 (布局所加入位置).创建容器组件 (临时布局器, 10, 10)  // 注意必须基于数组的返回对象创建容器组件,否则其内部子组件的自动事件挂接将失效.

            挂接事件 (布局数组.取成员 (布局所加入位置), 布局所加入位置)

        }
        返回 (0)
    }

    方法 编辑框_内容被改变 <接收事件 类型 = 整数>
    参数 来源对象 <类型 = 编辑框>
    参数 标记值 <类型 = 整数>
    {
        如果 (标记值 == 0)
        {
            调试输出 ("子夹1中的编辑框内容改变")
        }
        返回 (0)
    }

    方法 我的布局1_按钮点击 <接收事件 类型 = 整数>
    参数 来源对象 <类型 = 我的布局1>
    参数 标记值 <类型 = 整数>
    参数 编辑框内容 <类型 = 文本型>
    {
        信息框 (到文本 (标记值) + "被点击，" + 编辑框内容)
        返回 (0)
    }
}

类 我的布局1 <基础类 = 组件布局器 隐藏值属性 = "0" @视窗.布局 = "client_size = \"406, 159\"">
{
    变量 按钮1 <公开 类型 = 按钮 折叠2 隐藏值属性 = "0" @视窗.布局 = "id = 101\r\npos = \"304, 40, 80, 32\"" 标题 = "取内容">
    变量 编辑框1 <公开 类型 = 编辑框 隐藏值属性 = "0" @视窗.布局 = "id = 102\r\npos = \"16, 40, 280, 32\"">

    方法 按钮_被单击 <接收事件 类型 = 整数>
    参数 来源对象 <类型 = 按钮>
    参数 标记值 <类型 = 整数>
    {
        如果 (来源对象 == 按钮1)
        {
            按钮点击 (编辑框1.内容)
        }
        返回 (0)
    }

    方法 按钮点击 <公开 定义事件 类型 = 整数>
    参数 编辑框内容 <类型 = 文本型>
    {

    }
}

类 我的布局数组 <公开 基础类 = 对象数组模板类 @模板实现类 = "我的布局1">
{

    #
}
