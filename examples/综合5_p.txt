
###### 以下为一个火山软件开发平台系统类库程序的内容

<火山程序 类型 = "通常" 版本 = 1 />

包 火山.程序 <"">

类 启动类 <公开 基础类 = 窗口程序类>
{
    变量 主窗口对象 <类型 = 我的主窗口>

    方法 启动方法 <公开 类型 = 整数>
    {
        主窗口对象.创建主窗口 ()
        返回 (1)
    }
}

# ===

类 我的主窗口 <基础类 = 窗口 隐藏值属性 = "0" @视窗.布局 = "client_size = \"395, 388\"">
{
    变量 分组框1 <类型 = 分组框 折叠2 隐藏值属性 = "0" @视窗.布局 = "id = 101\r\npos = \"5, 10, 192, 368\"" 标题 = "服务器">
    变量 按钮_启动 <类型 = 按钮 折叠2 隐藏值属性 = "0" @视窗.布局 = "id = 102\r\nparent_id = 101\r\npos = \"16, 24, 80, 32\""
            标题 = "启动服务器">
    变量 编辑框_收到 <类型 = 编辑框 折叠2 隐藏值属性 = "0"
            @视窗.布局 = "id = 103\r\nparent_id = 101\r\npos = \"16, 64, 168, 288\"" 滚动条 = 纵向滚动条 是否允许多行 = 真>
    变量 分组框2 <类型 = 分组框 折叠2 隐藏值属性 = "0" @视窗.布局 = "id = 104\r\npos = \"205, 6, 184, 368\"" 标题 = "同步客户端">
    变量 按钮_连接 <类型 = 按钮 折叠2 隐藏值属性 = "0" @视窗.布局 = "id = 105\r\nparent_id = 104\r\npos = \"16, 24, 80, 32\""
            标题 = "连接">
    变量 编辑框_发送 <类型 = 编辑框 折叠2 隐藏值属性 = "0" @视窗.布局 = "id = 106\r\nparent_id = 104\r\npos = \"16, 65, 144, 33\""
            输入方式 = 通常方式 提示横幅 = "要发送的内容" 内容 = "要发送的内容">
    变量 编辑框_返回 <类型 = 编辑框 折叠2 隐藏值属性 = "0"
            @视窗.布局 = "id = 107\r\nparent_id = 104\r\npos = \"16, 113, 160, 240\"" 滚动条 = 纵向滚动条 是否允许多行 = 真>
    变量 按钮_发送 <类型 = 按钮 折叠2 隐藏值属性 = "0" @视窗.布局 = "id = 108\r\nparent_id = 104\r\npos = \"101, 23, 80, 32\""
            标题 = "发送">
    变量 客户端1 <类型 = HP_PACK同步客户端>
    变量 服务器1 <类型 = HP_PACK服务器>

    方法 按钮_被单击 <接收事件 类型 = 整数>
    参数 来源对象 <类型 = 按钮>
    参数 标记值 <类型 = 整数>
    {
        如果 (来源对象 == 按钮_启动)
        {
            如果 (服务器1.启动 ("0.0.0.0", 6666))
            {
                标题 = "启动成功"
            }
            否则
            {
                信息框 ("启动失败..")

            }
        }
        否则 (来源对象 == 按钮_连接)
        {
            如果 (客户端1.启动 ("127.0.0.1", 6666, 假))
            {
                分组框2.标题 = "同步客户端-连接成功"
            }
            否则
            {
                信息框 ("连接失败")
            }

        }
        否则 (来源对象 == 按钮_发送)
        {
            变量 返回字节 <类型 = 字节集类>
            返回字节 = 客户端1.发送同步数据 (文本到UTF8 (编辑框_发送.内容), , , 5)
            如果 (返回字节.字节集是否为空 ())
            {
                信息框 ("发送数据失败，可能没有连接服务器")
            }
            编辑框_返回.加入文本行 (UTF8到文本 (返回字节))
        }
        返回 (0)
    }

    变量 计数 <类型 = 整数>

    方法 HP_PACK服务器_数据进入 <接收事件 类型 = 整数>
    参数 来源对象 <类型 = HP_PACK服务器>
    参数 标记值 <类型 = 整数>
    参数 连接ID <类型 = 变整数>
    参数 数据 <类型 = 字节集类>
    参数 数据长度 <类型 = 整数>
    {
        如果 (来源对象 == 服务器1)
        {
            编辑框_收到.加入文本行 (UTF8到文本 (数据))
            计数 = 计数 + 1
            来源对象.发送数据 (连接ID, 文本到UTF8 ("服务器返回" + 到文本 (计数)))
        }
        返回 (0)
    }
}

类 HP_PACK同步客户端 <基础类 = HP_PACK客户端>
{
    变量 返回数据 <类型 = 字节集类>

    方法 发送同步数据 <公开 类型 = 字节集类 注释 = "向已连接的服务端发送数据." 返回值注释 = "返回服务器数据" 折叠 @禁止流程检查 = 真>
    参数 数据 <类型 = 字节集类 注释 = "所欲发送数据,本参数不可为空,否则将会发送失败.">
    参数 数据长度 <类型 = 整数 注释 = "提供所欲发送数据的数据长度,默认-1则使用所提供的数据长度." @默认值 = -1>
    参数 起始偏移量 <类型 = 整数 注释 = "发送数据缓冲区指针偏移量." @默认值 = 0>
    参数 超时返回 <类型 = 整数 注释 = "单位秒，默认为5秒" @默认值 = 5>
    {
        如果 (发送数据 (数据, 数据长度, 起始偏移量))
        {
            变量 计次 <类型 = 整数>
            判断循环 (真)
            {
                界面延时 (1000)
                计次 = 计次 + 1
                如果 (计次 == 超时返回)
                {
                    返回 (返回数据)
                }
                如果 (返回数据.字节集是否为空 () == 假)
                {
                    返回 (返回数据)
                }
                处理事件 ()
            }
        }
        返回 (返回数据)
    }

    方法 HP_PACK同步客户端_数据进入 <接收事件 类型 = 整数>
    参数 来源对象 <类型 = HP_PACK同步客户端>
    参数 标记值 <类型 = 整数>
    参数 连接ID <类型 = 变整数>
    参数 数据 <类型 = 字节集类>
    参数 数据长度 <类型 = 整数>
    {
        返回数据 = 数据
        返回 (0)
    }
}

【分隔符】


###### 以下为一个火山软件开发平台系统类库程序的内容

<火山程序 类型 = "通常" 版本 = 1 />

包 火山.程序 <"">

类 启动类 <公开 基础类 = 窗口程序类>
{
    变量 主窗口对象 <公开 类型 = 我的主窗口>

    方法 启动方法 <公开 类型 = 整数>
    {
        主窗口对象.创建主窗口 ()
        返回 (1)
    }
}

类 我的主窗口 <基础类 = 窗口 注释 = "样例主窗口" @视窗.布局 = "client_size = \"500, 403\"">
{
    变量 按钮1 <类型 = 按钮 隐藏值属性 = "0" @视窗.布局 = "id = 101\r\npos = \"29, 30, 80, 32\"">
    变量 编辑框1 <类型 = 编辑框 隐藏值属性 = "0" @视窗.布局 = "id = 102\r\npos = \"29, 70, 400, 288\"" 滚动条 = 纵向滚动条 是否允许多行 = 真>
    变量 Word <类型 = Word程序>

    方法 我的主窗口_创建完毕 <接收事件 类型 = 整数>
    参数 来源对象 <类型 = 我的主窗口>
    参数 标记值 <类型 = 整数>
    {

        如果 (Word.获取 ())  // 如果WORD程序已经存在的话,获取存在的程序.
        {
            Word.注册事件 ()  // 使用事件前需使用一次注册事件
            调试输出 ("获取Word对象成功。")
        }
        否则
        {
            如果 (Word.创建 ())  // 创建Word程序,成功将在任务管理器中将看到Word.exe进程。
            {
                Word.宏设置 ()  // 设置是否启用宏并信任VBA工程对象模型的访问,需要在文档未打开前使用.
                调试输出 ("创建Word对象成功，任务管理器中将看到Word.exe进程。")
                Word.注册事件 ()  // 使用事件前需使用一次注册事件
            }
            否则
            {
                调试输出 ("请检查本地计算机是否安装了office Word程序。")
            }
        }


        返回 (0)
    }

    方法 按钮_被单击 <接收事件 类型 = 整数>
    参数 来源对象 <类型 = 按钮>
    参数 标记值 <类型 = 整数>
    {
        如果 (来源对象 == 按钮1)
        {
            变量 文档集 <类型 = Word文档集>
            Word.可视 = 假
            文档集 = Word.文档集
            变量 文档 <类型 = Word文档>
            文档 = 文档集.打开 (取特定目录 (特定目录类型.系统桌面) + "测试.docx", , , , , , , , , , , 假)
            编辑框1.内容 = 文档.取文本区 ().内容
            文档.关闭 ()
            文档集.关闭 ()

        }
        返回 (0)
    }
}

【分隔符】


###### 以下为一个火山软件开发平台系统类库程序的内容

<火山程序 类型 = "通常" 版本 = 1 />

包 火山.程序 <注释 = "无名模块：http://www.vowm.cn">

类 启动类 <公开 基础类 = 窗口程序类>
{
    变量 主窗口对象 <类型 = 我的主窗口 标题 = "我的主窗口">

    方法 启动方法 <公开 类型 = 整数>
    {
        调试输出 (加法 (1, 1))
        主窗口对象.创建主窗口 ()
        返回 (1)
    }

    方法 加法 <公开 静态 类型 = 整数 @视窗.外部头文件 = "test.h" @视窗.外部源文件 = "test.cpp" @禁止流程检查 = 真>
    参数 A <类型 = 整数>
    参数 B <类型 = 整数>
    {
        @ return testAdd(@<A>, @<B>);
    }
}

# ===

类 我的主窗口 <基础类 = 窗口 注释 = "样例主窗口" @视窗.布局 = "client_size = \"500, 300\"">

【分隔符】


###### 以下为一个火山软件开发平台系统类库程序的内容

<火山程序 类型 = "通常" 版本 = 1 />

包 火山.视窗

类 启动类 <公开 基础类 = 窗口程序类>
{
    变量 主窗口 <类型 = 我的主窗口>

    方法 启动方法 <公开 类型 = 整数>
    {
        主窗口.创建主窗口 ()
        返回 (1)
    }
}

类 我的主窗口 <基础类 = 窗口 注释 = "" 注释 = "https://imgtu.com" 隐藏值属性 = "0" @视窗.布局 = "client_size = \"427, 231\""
        标题 = "POST图床上传">
{
    变量 按钮1 <类型 = 按钮 隐藏值属性 = "0" @视窗.布局 = "id = 101\r\npos = \"301, 182, 87, 40\"" 标题 = "浏览并上传">
    变量 编辑框1 <类型 = 编辑框 隐藏值属性 = "0" @视窗.布局 = "id = 102\r\npos = \"7, 9, 254, 181\"" 是否允许多行 = 真 滚动条 = 纵向滚动条>
    变量 标签1 <类型 = 标签 隐藏值属性 = "0" @视窗.布局 = "id = 103\r\npos = \"5, 198, 264, 32\"">
    变量 图片框1 <类型 = 图片框 隐藏值属性 = "0" @视窗.布局 = "id = 104\r\npos = \"285, 30, 128, 128\"" 显示方式 = 缩放图片>
    变量 文件上传 <类型 = 文件上传类 Cookie管理文件 = "">

    方法 按钮_被单击 <接收事件 类型 = 整数 注释 = "当按钮被单击后发送此事件">
    参数 来源对象 <类型 = 按钮 注释 = "提供事件产生的具体来源对象">
    参数 标记值 <类型 = 整数 注释 = "用户调用\"挂接事件\"命令时所提供的\"标记值\"参数值,非此方式挂接事件则本参数值固定为0.">
    {
        如果 (来源对象 == 按钮1)
        {
            变量 对话框 <类型 = 通用对话框 编辑时信息 = "179982, 0, 0, 0" "类型" = 打开文件
                    过滤器 = "所有图片文件|*.gif;*.bmp;*.emf;*.wmf;*.jpg;*.jpeg;*.png;*.tif;*.ico|GIF文件(*.gif)|*.gif|BMP文件(*.bmp)|*.bmp|EMF文件(*.emf)|*.emf|WMF文件(*.wmf)|*.wmf|JPG文件(*.jpg;*.jpeg)|*.jpg;*.jpeg|PNG文件(*.png)|*.png|TIFF文件(*.tif)|*.tif|ICO文件(*.ico)|*.ico|">
            如果 (对话框.打开 (本对象))
            {
                编辑框1.加入文本行 ("要上传的文件：" + 对话框.文件名)
                变量 哈希表 <类型 = 文本到文本哈希表>
                哈希表.插入 ("type", "file")
                哈希表.插入 ("action", "upload")
                哈希表.插入 ("timestamp", 到文本 (取时间戳 (取现行时间 ())))
                变量 auth_token <类型 = 文本型>
                auth_token = 文本_取中间内容 (文件上传.取源码 ("https://imgtu.com/", 文本编码类型.UTF8), "PF.obj.config.auth_token = \"", "\";")
                哈希表.插入 ("auth_token", auth_token)
                哈希表.插入 ("nsfw", "0")
                变量 访问响应 <类型 = 网页访问响应类>
                访问响应 = 文件上传.上传 ("https://imgtu.com/json", 对话框.文件名, "source", 哈希表, "image/jpeg")
                变量 临时对象 <类型 = JSON对象类>
                临时对象.创建自字节集 (访问响应.请求结果)
                如果 (临时对象.取文本值 ("status_txt") == "OK")
                {
                    变量 图片地址 <类型 = 文本型>
                    图片地址 = 临时对象.取文本值 ("//image.url")
                    编辑框1.加入文本行 ("图片地址：" + 图片地址)
                    变量 网络访问 <类型 = 网络访问类>
                    图片框1.置底图数据 (网络访问.网页访问 (图片地址).请求结果)

                }
                否则
                {
                    信息框 ("上传失败。。", 信息框按钮.错误图标)
                }

            }
        }
        返回 (0)
    }

    方法 文本_取中间内容 <类型 = 文本型 注释 = "取出两个文本的中间文本，返回取出的中间文本" 返回值注释 = "返回取出的中间文本,失败返回空文本" 折叠>
    参数 原文本 <类型 = 文本型 注释 = "提供要查找的原文本" 编辑时信息 = "E887A, 0, 0, 0">
    参数 前文本 <类型 = 文本型 注释 = "提供前文本" 编辑时信息 = "E887A, 0, 0, 0">
    参数 后文本 <类型 = 文本型 注释 = "提供后文本" 编辑时信息 = "E887A, 0, 0, 0">
    参数 是否不区分大小写 <类型 = 逻辑型 注释 = "指定比较时是否不区分英文字母的大小写,默认不区分" @默认值 = 假>
    {
        如果 (文本是否为空 (原文本) == 真 || 文本是否为空 (前文本) == 真 || 文本是否为空 (后文本) == 真 || 寻找文本 (原文本, 前文本, , 是否不区分大小写) == -1 || 寻找文本 (原文本, 后文本, , 是否不区分大小写) == -1)
        {
            返回 ("")
        }
        否则
        {
            变量 前文本位置 <类型 = 整数>
            变量 后文本位置 <类型 = 整数>
            变量 中间本本长度 <类型 = 整数>
            前文本位置 = 寻找文本 (原文本, 前文本, , 是否不区分大小写) + 取文本长度 (前文本)
            后文本位置 = 寻找文本 (原文本, 后文本, 前文本位置, 是否不区分大小写)
            中间本本长度 = 后文本位置 - 前文本位置
            返回 (取文本中间 (原文本, 前文本位置, 中间本本长度))
        }
    }

    方法 文件上传类_传输进度发生改变 <接收事件 类型 = 整数 注释 = "当传输进度发生改变时,本事件将被调用多次直到整个传输结束.">
    参数 来源对象 <类型 = 文件上传类 注释 = "提供事件产生的具体来源对象">
    参数 标记值 <类型 = 整数 注释 = "用户调用\"挂接事件\"命令时所提供的\"标记值\"参数值,非此方式挂接事件则本参数值固定为0.">
    参数 上传总量 <类型 = 长整数 注释 = "预计需要上传的字节数.">
    参数 当前上传 <类型 = 长整数 注释 = "当前已上传的字节数.">
    {
        标签1.标题 = 到文本 (当前上传) + "/" + 到文本 (上传总量)
        返回 (0)
    }
}

类 文件上传类 <公开>
{
    方法 Cookie管理文件 <公开 属性写 注释 = "提供Cookie管理文件,如果为空对象则不会管理Cookie内容(可使用\"提交协议头/提交Cookie\"参数自行提交)."
            注释 = "如提供空文本则表示在内存中管理Cookie(非持久化储存),如果指定了文件则表示持久化储存管理Cookie." 折叠2>
    参数 文件 <类型 = 文本型 @默认值 = 空对象>
    {
        如果 (文本是否为空对象 (文件) == 假)
        {
            如果 (文本是否为空 (文件))
            {
                网络传输.置读Cookie文件 ()
                网络传输.置写Cookie文件 ()
            }
            否则
            {
                网络传输.置读Cookie文件 (文件)
                网络传输.置写Cookie文件 (文件)
            }
        }
        否则
        {
            网络传输.置读Cookie文件 (文件)  // 空对象置入过去等于清除原设置
            网络传输.置写Cookie文件 (文件)
        }
    }

    方法 类_清理 <折叠>
    {
        网络传输.清理 ()
    }

    变量 网络传输 <类型 = 网络传输类 进度反馈 = 真>

    方法 取源码 <公开 类型 = 文本型>
    参数 网址 <类型 = 文本型 注释 = "提供所欲访问的网址内容." 编辑时信息 = "B3FF4, 0, 0, 0">
    参数 网站编码 <类型 = 文本编码类型 注释 = "多字节就是gbk" 编辑时信息 = "B3FF4, 0, 0, 0" @默认值 = 文本编码类型.UTF8>
    {
        网络传输.地址 = 网址
        网络传输.快速打开 = 真
        变量 返回结果 <类型 = 网页访问响应类>
        网络传输.发送请求 (返回结果.请求结果)
        如果 (网站编码 == 文本编码类型.UTF8)
        {
            返回 (UTF8到文本 (返回结果.请求结果))
        }
        否则 (网站编码 == 文本编码类型.多字节)
        {
            返回 (多字节到文本 (返回结果.请求结果))
        }
        否则 (网站编码 == 文本编码类型.UTF16)
        {
            返回 (字节集到文本 (返回结果.请求结果))
        }
        返回 (UTF8到文本 (返回结果.请求结果))
    }

    方法 上传 <公开 类型 = 网页访问响应类>
    参数 参_上传地址 <类型 = 文本型>
    参数 参_文件路径 <类型 = 文本型 注释 = "提供要上传的文件路径">
    参数 参_文件name值 <类型 = 文本型 注释 = "提供对应文件name值">
    参数 参_提交的其它字段 <类型 = 文本到文本哈希表 @默认值 = 空对象>
    参数 参_Content_Type头 <类型 = 文本型 注释 = "默认为：application/x-msdownload" @默认值 = "application/x-msdownload">
    参数 参_提交协议头 <类型 = 文本数组类 注释 = "提供本次请求所欲使用的附加协议头,每个成员表示一个协议." @默认值 = 空对象>
    参数 参_提交Cookie <类型 = 文本型 注释 = "提供本次请求所欲使用的Cookie内容,注意该参数提交的内容不会同步到\"Cookie管理文件\"(不会写出至管理文件)." @默认值 = "">
    {
        网络传输.地址 = 参_上传地址
        网络传输.置请求头 (参_提交协议头)
        网络传输.置Cookie内容 (参_提交Cookie)
        网络传输.置请求方式 ("POST")
        变量 表单 <类型 = 多用途扩展格式类>
        表单.创建 (网络传输)  // 必须先根据传输对象创建表单
        变量 子片段 <类型 = 多用途扩展格式片段类>
        子片段.创建 (表单)  // 基于此表单添加一个片段
        子片段.置文件数据 (参_文件路径)  // 直接添加欲上传的文件
        子片段.置片段名称 (参_文件name值)  // 对应name值
        子片段.置片段类型 (参_Content_Type头)  // 对应Content-Type头
        子片段.置远程文件字段名称 (取文件名无路径部分 (参_文件路径))  // 对应filename
        如果 (参_提交的其它字段 != 空对象)
        {
            参_提交的其它字段.枚举循环 ()
            {
                变量 子片段1 <类型 = 多用途扩展格式片段类>
                子片段1.创建 (表单)
                变量 关键字 <类型 = 文本型>
                关键字 = 参_提交的其它字段.取枚举关键字 ()
                子片段1.置片段名称 (关键字)
                子片段1.置数据 (文本到UTF8 (参_提交的其它字段.取枚举值 (), 假))

            }
        }
        网络传输.置表单数据 (表单)  // 将组合完毕的表单置入,直接发送请求即可
        变量 返回结果 <类型 = 网页访问响应类>
        网络传输.发送请求 (返回结果.请求结果, 返回结果.响应头)
        返回结果.响应头.枚举循环 ()
        {
            变量 当前值 <类型 = 文本型>
            当前值 = 返回结果.响应头.取枚举值 ()
            如果 (是否以文本开头 (当前值, "Set-Cookie", 假))
            {
                删除部分文本 (当前值, 0, 取文本长度 ("Set-Cookie"))
                常量 尾换行符 <类型 = 文本型 值 = "\r\n">
                如果 (是否以文本结束 (当前值, 尾换行符, 假))
                {
                    删除部分文本 (当前值, 取文本长度 (当前值) - 取文本长度 (尾换行符), 取文本长度 (尾换行符))
                }
                返回结果.Cookies.加入成员 (当前值)
            }
        }
        返回结果.重定向次数 = 网络传输.取重定向次数 ()
        返回结果.重定向地址 = 网络传输.取重定向地址 ()
        返回结果.响应码 = 网络传输.取上次请求响应码 ()
        返回结果.请求耗时 = 网络传输.取接收时长 ()

        返回 (返回结果)
    }

    方法 传输进度发生改变 <公开 定义事件 类型 = 整数 注释 = "当传输进度发生改变时,本事件将被调用多次直到整个传输结束.">
    参数 上传总量 <类型 = 长整数 注释 = "预计需要上传的字节数.">
    参数 当前上传 <类型 = 长整数 注释 = "当前已上传的字节数.">

    方法 网络传输类_传输进度发生改变 <接收事件 类型 = 整数 注释 = "当传输进度发生改变时,本事件将被调用多次直到整个传输结束.">
    参数 来源对象 <类型 = 网络传输类 注释 = "提供事件产生的具体来源对象">
    参数 标记值 <类型 = 整数 注释 = "用户调用\"挂接事件\"命令时所提供的\"标记值\"参数值,非此方式挂接事件则本参数值固定为0.">
    参数 下载总量 <类型 = 长整数 注释 = "预计需要下载的字节数.">
    参数 当前下载 <类型 = 长整数 注释 = "当前已下载的字节数.">
    参数 上传总量 <类型 = 长整数 注释 = "预计需要上传的字节数.">
    参数 当前上传 <类型 = 长整数 注释 = "当前已上传的字节数.">
    {
        传输进度发生改变 (上传总量, 当前上传)
        返回 (0)
    }
}

【分隔符】


###### 以下为一个火山软件开发平台系统类库程序的内容

<火山程序 类型 = "通常" 版本 = 1 />

包 火山.无名.菜鸟驿站 <注释 = "火山菜鸟驿站：http://www.vowm.cn">

类 启动类 <公开 基础类 = 窗口程序类>
{
    变量 主窗口对象 <公开 类型 = 我的主窗口 注释 = "需要公开才能跨窗口访问" 标题 = "我的主窗口">

    方法 启动方法 <公开 类型 = 整数 注释 = "火山菜鸟驿站：http://www.vowm.cn">
    {
        主窗口对象.创建主窗口 ()
        返回 (1)
    }
}

类 我的主窗口 <基础类 = 窗口 注释 = "样例主窗口" @视窗.布局 = "client_size = \"450, 237\"">
{
    变量 编辑框1 <公开 类型 = 编辑框 折叠2 隐藏值属性 = "0" @视窗.布局 = "id = 101\r\npos = \"37, 96, 256, 48\"" 标题 = "11111">
    变量 按钮1 <类型 = 按钮 折叠2 隐藏值属性 = "0" @视窗.布局 = "id = 102\r\npos = \"37, 32, 128, 40\"" 标题 = "载入窗口2">
    变量 窗口2 <类型 = 窗口二>

    方法 按钮_被单击 <接收事件 类型 = 整数 注释 = "火山菜鸟驿站：http://www.vowm.cn">
    参数 来源对象 <类型 = 按钮>
    参数 标记值 <类型 = 整数>
    {
        如果 (来源对象 == 按钮1)
        {
            窗口2.创建子窗口 (本对象)
        }
        返回 (0)
    }

    方法 主窗口方法 <公开>
    {
        信息框 ("主窗口方法")
    }
}

类 窗口二 <公开 基础类 = 窗口 注释 = "火山菜鸟驿站：http://www.vowm.cn" 隐藏值属性 = "0" @视窗.布局 = "client_size = \"346, 175\"">
{
    变量 按钮2 <类型 = 按钮 隐藏值属性 = "0" @视窗.布局 = "id = 101\r\npos = \"85, 104, 152, 48\"" 标题 = "调用主窗口方法">
    变量 按钮1 <类型 = 按钮 隐藏值属性 = "0" @视窗.布局 = "id = 102\r\npos = \"85, 32, 152, 48\"" 标题 = "取主窗口内容">

    方法 按钮_被单击 <接收事件 类型 = 整数 注释 = "火山菜鸟驿站：http://www.vowm.cn">
    参数 来源对象 <类型 = 按钮>
    参数 标记值 <类型 = 整数>
    {
        如果 (来源对象 == 按钮1)
        {
            信息框 (取程序 ().主窗口对象.编辑框1.内容, , "主窗口内容：")
        }
        否则 (来源对象 == 按钮2)
        {
            取程序 ().主窗口对象.主窗口方法 ()  // 调用主窗口方法
        }
        返回 (0)
    }
}
