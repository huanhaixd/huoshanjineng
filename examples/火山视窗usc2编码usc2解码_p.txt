
###### 以下为一个火山软件开发平台系统类库程序的内容

<火山程序 类型 = "通常" 版本 = 1 />

包 火山.无名模块 <注释 = "火山无名模块：http://m.vowm.cn">

类 启动类 <公开 基础类 = 窗口程序类>
{
    变量 主窗口对象 <类型 = 我的主窗口>

    方法 启动方法 <公开 类型 = 整数>
    {
        主窗口对象.创建主窗口 ()
        返回 (1)
    }
}

类 我的主窗口 <公开 基础类 = 窗口 注释 = "火山无名模块：http://m.vowm.cn" @视窗.布局 = "client_size = \"271, 199\""
        标题 = "usc2编码解码">
{
    变量 按钮1 <类型 = 按钮 隐藏值属性 = "0" @视窗.布局 = "id = 101\r\npos = \"10, 13, 115, 32\"" 标题 = "usc2编码">
    变量 按钮2 <类型 = 按钮 隐藏值属性 = "0" @视窗.布局 = "id = 102\r\npos = \"10, 96, 115, 32\"" 标题 = "usc2解码">
    变量 编辑框_原文本 <类型 = 编辑框 隐藏值属性 = "0" @视窗.布局 = "id = 103\r\npos = \"10, 51, 224, 32\"" 内容 = "火山软件">
    变量 编辑框_转换后 <类型 = 编辑框 隐藏值属性 = "0" @视窗.布局 = "id = 104\r\npos = \"17, 141, 224, 32\"">

    方法 按钮_被单击 <接收事件 类型 = 整数 折叠>
    参数 来源对象 <类型 = 按钮>
    参数 标记值 <类型 = 整数>
    {
        如果 (来源对象 == 按钮1)
        {
            编辑框_转换后.内容 = UCS2编码 (编辑框_原文本.内容)
        }
        否则 (来源对象 == 按钮2)
        {
            编辑框_原文本.内容 = UCS2解码 (编辑框_转换后.内容)
        }
        返回 (0)
    }

    方法 我的主窗口_创建完毕 <接收事件 类型 = 整数>
    参数 来源对象 <类型 = 我的主窗口>
    参数 标记值 <类型 = 整数>
    {
        调试输出 (UCS2解码 ("\u706B\u5C71\u8F6F\u4EF6"))
        调试输出 (UCS2解码 ("%u706B%u5C71%u8F6F%u4EF6"))
        调试输出 (UCS2编码 ("火山123"))
        返回 (0)
    }

    方法 UCS2编码 <类型 = 文本型 注释 = "将文本进行ucs2编码,返回编码后的文本" 返回值注释 = "返回例如：%u706B%u5C71%u8F6F%u4EF6">
    参数 要编码的文本 <类型 = 文本型>
    {
        变量 十六进制 <类型 = 文本型>
        变量 返回文本 <类型 = 文本型>
        十六进制 = 字节集到十六进制文本 (文本到字节集 (要编码的文本))
        计次循环 (取文本长度 (十六进制) / 4)
        {
            变量 进制 <类型 = 文本型>
            进制 = 取文本中间 (十六进制, 取循环索引 () * 4, 4)
            加入文本 (返回文本, "%u" + 取文本右边 (进制, 2) + 取文本左边 (进制, 2))
        }
        返回 (返回文本)
    }

    方法 UCS2解码 <类型 = 文本型 注释 = "将ucs2编码的文本还原成原文本">
    参数 usc2文本 <类型 = 文本型 注释 = "例如：\\u706B\\u5C71\\u8F6F\\u4EF6" 注释 = "或者：%u706B%u5C71%u8F6F%u4EF6">
    {
        变量 分割的文本 <类型 = 文本型 值 = "">
        如果 (是否以文本开头 (usc2文本, "%u"))
        {
            分割的文本="%u"
        }
        否则(是否以文本开头 (usc2文本, "\\u"))
        {
            分割的文本="\\u"
        }
        如果 (分割的文本 != "")
        {
            变量 数组 <类型 = 文本数组类>
            分割子文本 (usc2文本, 分割的文本, 数组)
            变量 返回文本 <类型 = 文本型>
            数组.枚举循环 ()
            {
                如果真 (取文本长度 (数组.取枚举值 ()) == 4)
                {
                    加入文本 (返回文本, 取文本右边 (数组.取枚举值 (), 2) + 取文本左边 (数组.取枚举值 (), 2))
                }
            }
            返回 (字节集到文本 (十六进制文本到字节集 (返回文本)))
        }
        返回 (usc2文本)

    }
}
