
###### 以下为一个火山软件开发平台系统类库程序的内容

<火山程序 类型 = "通常" 版本 = 1 />

包 火山.程序

类 启动类 <公开 基础类 = 窗口程序类>
{
    变量 主窗口对象 <类型 = 我的主窗口>

    方法 启动方法 <公开 类型 = 整数>
    {
        主窗口对象.创建主窗口 ()
        返回 (1)


    }
}

# ===

类 我的主窗口 <基础类 = 窗口 注释 = "样例主窗口" @视窗.布局 = "client_size = \"500, 300\"" 标题 = "我的主窗口">
{
    变量 按钮_删除文本文件 <类型 = 按钮 折叠2 隐藏值属性 = "0" @视窗.布局 = "id = 101\r\npos = \"133, 78, 80, 32\"" 标题 = "删除文本文件">
    变量 按钮_打开文本文件 <类型 = 按钮 折叠2 隐藏值属性 = "0" @视窗.布局 = "id = 102\r\npos = \"29, 78, 80, 32\"" 标题 = "打开文本文件">
    变量 按钮_追加写出文本数据 <类型 = 按钮 折叠2 隐藏值属性 = "0" @视窗.布局 = "id = 103\r\npos = \"285, 22, 120, 32\""
            标题 = "追加写出文本数据">
    变量 按钮_覆盖写出文本数据 <类型 = 按钮 折叠2 隐藏值属性 = "0" @视窗.布局 = "id = 104\r\npos = \"133, 22, 120, 32\""
            标题 = "覆盖写出文本数据">
    变量 按钮_读入文本数据 <类型 = 按钮 折叠2 隐藏值属性 = "0" @视窗.布局 = "id = 105\r\npos = \"29, 22, 80, 32\"" 标题 = "读入文本数据">

    方法 按钮_被单击 <接收事件 类型 = 整数 注释 = "当按钮被单击后发送此事件">
    参数 来源对象 <类型 = 按钮 注释 = "提供事件产生的具体来源对象">
    参数 标记值 <类型 = 整数 注释 = "用户调用\"挂接事件\"命令时所提供的\"标记值\"参数值,非此方式挂接事件则本参数值固定为0.">
    {
        如果 (来源对象 == 按钮_删除文本文件)
        {
            // 调试运行目录里边,有U编码文本.txt文件方可删除
            删除文件 (取运行目录 () + "U编码文本.txt")
        }
        如果 (来源对象 == 按钮_打开文本文件)
        {
            // 调试运行目录里边,有U编码文本.txt文件方可打开
            执行 (取运行目录 () + "U编码文本.txt")
        }
        如果 (来源对象 == 按钮_追加写出文本数据)
        {
            追加写出文本数据 ()
        }
        如果 (来源对象 == 按钮_覆盖写出文本数据)
        {
            覆盖写出文本数据 ()

        }
        如果 (来源对象 == 按钮_读入文本数据)
        {

            读入文本数据 ()


        }
        返回 (0)
    }

    方法 追加写出文本数据
    {
        变量 文件数据 <类型 = 字节集类>
        变量 文本内容 <类型 = 文本型>
        // ------------方法1:写到文件命令-------------
        // 追加写出UTF8文本
        文本内容 = "111111\r\n22222\r\n\naaaa\r\n啊啊啊啊啊啊\r\n噢噢噢噢啊啊111\r\n"

        // 在易语言中,字节集数据是可以通过+号来拼凑的,但是在火山中是不可以的,必须通过添加字节集来实现.
        文件数据 = 读入文件 (取运行目录 () + "U编码文本.txt", )  // 读入文件的返回值,是一个字节集类,所以要创建一个字节集类变量来接收
        文件数据.添加字节集 (文本到UTF8 (文本内容, ))  // 通过添加字节集方法,来进行字节集数据的拼接,注意编码类型

        写到文件 (取运行目录 () + "U编码文本.txt", 文件数据)
        调试输出 ("UTF8编码文本写出完毕")


        // 追加写出ANSI文本
        文本内容 = "111111\r\n22222\r\n\naaaa\r\n啊啊啊啊啊啊\r\n噢噢噢噢啊啊111\r\n"

        // 在易语言中,字节集数据是可以通过+号来拼凑的,但是在火山中是不可以的,必须通过添加字节集来实现.
        文件数据 = 读入文件 (取运行目录 () + "A编码文本.txt", )  // 读入文件的返回值,是一个字节集类,所以要创建一个字节集类变量来接收
        文件数据.添加字节集 (文本到多字节 (文本内容, ))  // 通过添加字节集方法,来进行字节集数据的拼接,注意编码类型

        写到文件 (取运行目录 () + "A编码文本.txt", 文件数据)
        调试输出 ("ANSI编码文本写出完毕")





        // ----------------方法2:写出文本文件命令:注意参数顺序-----------------
        // 追加写出UTF8文本
        文本内容 = "111111\r\n22222\r\naaaa\r\n啊啊啊啊啊啊\r\n噢噢噢噢\r\n"
        写出文本文件 (读入文本文件 (取运行目录 () + "U编码文本.txt", , 文本编码类型.UTF8) + 文本内容 + "\r\n", 取运行目录 () + "U编码文本.txt", , 文本编码类型.UTF8)
        调试输出 ("UTF8编码文本写出完毕")

        // 追加写出UTF8文本
        文本内容 = "111111\r\n22222\r\naaaa\r\n啊啊啊啊啊啊\r\n噢噢噢噢\r\n"
        写出文本文件 (读入文本文件 (取运行目录 () + "A编码文本.txt", , 文本编码类型.多字节) + 文本内容 + "\r\n", 取运行目录 () + "A编码文本.txt", , 文本编码类型.多字节)
        调试输出 ("ANSI编码文本写出完毕")

    }

    方法 覆盖写出文本数据
    {
        变量 文本内容 <类型 = 文本型>

        // ------------方法1:写到文件命令-------------
        // 知识点:下方的命令,不管是文本到UTF8,还是文本到多字节,它们的第二个参数:是否包括结束零字符,一定要为假,否则尾部会有个小空格.


        // 覆盖写出UTF8文本

        写到文件 (取运行目录 () + "U编码文本.txt", 文本到UTF8 ("111111\r\n22222\r\naaaa\r\n啊啊啊啊啊啊\r\n噢噢噢噢\r\n", 假))
        调试输出 ("UTF8编码文本写出完毕")

        // 覆盖写出ANSI文本
        写到文件 (取运行目录 () + "A编码文本.txt", 文本到多字节 ("111111\r\n22222\r\naaaa\r\n啊啊啊啊啊啊\r\n噢噢噢噢\r\n", 假))
        调试输出 ("ANSI编码文本写出完毕")


        // ----------------方法2:写出文本文件命令:注意参数顺序-----------------

        // 覆盖写出UTF8文本
        文本内容 = "111111\r\n22222\r\naaaa\r\n啊啊啊啊啊啊\r\n噢噢噢噢\r\n"
        写出文本文件 (文本内容, 取运行目录 () + "U编码文本.txt", , 文本编码类型.UTF8)
        调试输出 ("UTF8编码文本写出完毕")

        // 覆盖写出ANSI文本
        文本内容 = "111111\r\n22222\r\naaaa\r\n啊啊啊啊啊啊\r\n噢噢噢噢\r\n"
        写出文本文件 (文本内容, 取运行目录 () + "A编码文本.txt", , 文本编码类型.多字节)
        调试输出 ("ANSI编码文本写出完毕")

        // ---------------知识延伸:上方的覆盖写出命令,如果传入空文本,则可以实现文本文件内容清空操作------------------

    }

    方法 读入文本数据
    {
        变量 文本内容 <类型 = 文本型>
        // 如果首次使用,需要现在软件运行目录里面,创建两个TXT文本,分别是【U编码文本.txt】和【A编码文本.txt】
        // 【U编码文本.txt】采用UTF8编码格式,【A编码文本.txt】使用ANSI编码格式;两个文本都进行数据写入,尤其是中文字符.
        // 调试运行的目录  debug\x64\linker

        // ------------方法1:读入文件命令------------

        // 读入UTF8编码的TXT文本
        文本内容 = UTF8到文本 (读入文件 (取运行目录 () + "U编码文本.txt", ))
        调试输出 (文本内容)

        // 读入ANSI编码的TXT文本
        文本内容 = 多字节到文本 (读入文件 (取运行目录 () + "A编码文本.txt", ))
        调试输出 (文本内容)

        // ---------方法2:读入TXT文件命令-----------

        // 读入UTF8编码的TXT文本
        文本内容 = 读入文本文件 (取运行目录 () + "U编码文本.txt", , 文本编码类型.UTF8)
        调试输出 (文本内容)

        // 读入ANSI编码的TXT文本
        文本内容 = 读入文本文件 (取运行目录 () + "A编码文本.txt", , 文本编码类型.多字节)
        调试输出 (文本内容)

    }
}
