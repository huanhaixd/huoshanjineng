
###### 以下为一个火山软件开发平台系统类库程序的内容

<火山程序 类型 = "通常" 版本 = 1 />

包 火山.程序

类 启动类 <公开 基础类 = 窗口程序类>
{
    变量 主窗口对象 <类型 = 我的主窗口 标题 = "我的主窗口">

    方法 启动方法 <公开 类型 = 整数>
    {
        主窗口对象.创建主窗口 ()
        返回 (1)
    }
}

# ===

类 我的主窗口 <基础类 = 窗口 @视窗.布局 = "client_size = \"795, 568\"" 窗口边框 = 普通固定边框>
{
    变量 编辑框_整理后的内容 <类型 = 编辑框 隐藏值属性 = "0" @视窗.布局 = "id = 101\r\npos = \"4, 171, 786, 126\"" 是否允许多行 = 真
            滚动条 = 纵向滚动条>
    变量 按钮1 <类型 = 按钮 折叠2 隐藏值属性 = "0" @视窗.布局 = "id = 102\r\npos = \"4, 7, 88, 32\"" 标题 = "点击整理">
    变量 编辑框_Py中返回数据的变量 <类型 = 编辑框 折叠2 隐藏值属性 = "0" @视窗.布局 = "id = 103\r\npos = \"613, 7, 80, 32\""
            内容 = "str_text_all" 可视 = 假>
    变量 编辑框_原始内容 <类型 = 编辑框 折叠2 隐藏值属性 = "0" @视窗.布局 = "id = 104\r\npos = \"4, 41, 786, 126\"" 是否允许多行 = 真
            滚动条 = 纵向滚动条>
    变量 超级列表框1 <类型 = 超级列表框 折叠2 隐藏值属性 = "0" @视窗.布局 = "id = 105\r\npos = \"4, 301, 786, 126\"" "类型" = 报表列表框
            报表列 = "0\n-1\n0\n150\n收件人\n0\n-1\n0\n150\n联系电话\n0\n-1\n0\n400\n收货地址" 单一选择 = 真 整行选择 = 真 显示表格线 = 真>
    变量 表格1 <类型 = 表格 折叠2 隐藏值属性 = "0" @视窗.布局 = "id = 106\r\npos = \"4, 431, 786, 126\"" 列宽 = 150 表头行数 = 1
            表头列数 = 0 列数 = 3>
    变量 标签1 <类型 = 标签 折叠2 隐藏值属性 = "0" @视窗.布局 = "id = 107\r\npos = \"477, 7, 120, 32\"" 标题 = "Py中返回数据的变量"
            可视 = 假>
    变量 标签_数量 <类型 = 标签 折叠2 隐藏值属性 = "0" @视窗.布局 = "id = 108\r\npos = \"125, 7, 344, 32\"" 可视 = 真>
    变量 计次 <类型 = 整数>

    方法 我的主窗口_创建完毕 <接收事件 类型 = 整数>
    参数 来源对象 <类型 = 我的主窗口>
    参数 标记值 <类型 = 整数>
    {
        变量 局部_库路径文本 <类型 = 文本型>
        标题 = "【世恒带你学火山】- 借助Python整理快递信息"
        局部_库路径文本 = ""
        // 这里, 要根据X64/X86, 来使用不同的Dlls
        如果 (为64位程序 ())
        {
            加入格式文本 (局部_库路径文本, "%s\\Python3_9.zip;%s\\x64\\DLLs;%s", 取运行目录 (), 取运行目录 (), 取运行目录 ())
        }
        否则
        {
            加入格式文本 (局部_库路径文本, "%s\\Python3_9.zip;%s\\x86\\DLLs;%s", 取运行目录 (), 取运行目录 (), 取运行目录 ())
        }
        火山Python.置库路径 (局部_库路径文本)
        如果 (火山Python.初始化 () == 假)
        {
            信息框 ("Python脚本引擎初始化失败,请检查环境配置")
        }
        返回 (0)
    }

    方法 按钮_被单击 <接收事件 类型 = 整数>
    参数 来源对象 <类型 = 按钮>
    参数 标记值 <类型 = 整数>
    {

        如果 (来源对象 == 按钮1)
        {
            变量 局部_Py模块 <类型 = Py模块类>
            变量 局部_Py对象 <类型 = Py对象类>
            变量 局部_原始快递信息 <类型 = 文本型>
            变量 局部_原始快递信息分割 <类型 = 文本数组类>
            变量 局部_数量 <类型 = 整数>
            变量 局部_Py脚本 <类型 = 文本型
                    值 = "import re\r\nstr_text_all=\'\'\r\nstr_get = (&&[dz]&&)\r\nstr_split = str_get.split(\"\\n\")\r\nprint(len(str_split))\r\nfor text in str_split:\r\n\r\n    # 临时变量\r\n    str_name, str_phone, str_address = \'\', \'\', \'\'\r\n   \r\n    text1=\'\'\r\n    # 正则匹配规则\r\n    regex_phone = r\'1\\d{10}\'\r\n    text1= re.findall(regex_phone, text)\r\n    tmp=\'\'\r\n    for tmp in text1:\r\n      text=text.replace(tmp, \',\' + tmp+\',\' )\r\n\r\n    # 正则匹配规则\r\n    regex_phone2 = r\'(\\d{3})(-)?(\\d{4})(-)?(\\d{4})\'\r\n    regex_address = r\'([^(\\d|\\s)$省]+省|.+自治区)?([^市]+市)([^县]+县|.+区)?(.*)\'\r\n    regex_split = r\'(\\n)|(\\+)|(\\\\)|(\\.)|(\\~)|(，)|(。)|(,)|(;)|(？)|(：)|(:)|(、)|(\\?)\'\r\n    regex_delete = r\'(收件地址)|(收货地址)|(所在地区)|(详细地址)|(联系号码)|(手机号码)|(联系电话)|(手机号)|(收货人)|(收件人)|(（收）)|(号码)|(手机)|(名字)|(地址)|(电话)|(姓名)|(可能是电话号码，是否拨号?)|(在地图中查看)\'\r\n\r\n    # 分隔换行符\r\n    split_line = re.sub(regex_split, \' \', text.strip().lstrip().rstrip()).split()\r\n\r\n    # 提取信息\r\n    for s in split_line:\r\n        s = s.replace(\' \', \'\')\r\n        if s == \'\':\r\n            continue;\r\n        elif re.findall(regex_phone, s):\r\n            str_phone = str_phone + s;\r\n        elif re.findall(regex_phone2, s):\r\n            s = s.replace(\'-\', \'\'); str_phone = str_phone + s;\r\n        elif re.findall(regex_address, s):\r\n            str_address = str_address + s;\r\n        else:\r\n            str_name = str_name + s;\r\n\r\n    # 组合信息\r\n    str_text = \' \'.join([str_name+\"\\t\", str_phone+ \"\\t\", str_address])\r\n    str_text = re.sub(regex_delete, \'\', str_text)\r\n\r\n    # 输出结果\r\n    str_text_all=str_text_all+str_text+\"\\r\\n\"\r\n    print(str_text)\r\nprint(str_text_all)">
            变量 ""
            局部_原始快递信息 = 编辑框_原始内容.内容
            // 整理文本,便于分割
            子文本替换 (局部_原始快递信息, "\r\n", "\n")
            局部_数量 = 分割文本 (局部_原始快递信息, "\n", 局部_原始快递信息分割)

            标签_数量.标题 = "检索并处理返回了" + 到文本 (局部_数量) + "条信息"

            // 每行地址信息处理组合成Python需要的文本变量内容,替换到Py代码中
            局部_原始快递信息 = ""
            计次循环 (局部_数量)
            {
                局部_原始快递信息 = 局部_原始快递信息 + "\"" + 局部_原始快递信息分割.取成员 (取循环索引 ()) + "\\n\"" + "\n"

            }
            子文本替换 (局部_Py脚本, "&&[dz]&&", 局部_原始快递信息)

            // 借助Python代码整理地址返回
            局部_Py模块 = 火山Python.加载Py代码 (局部_Py脚本)
            局部_Py对象 = 局部_Py模块.取成员 (编辑框_Py中返回数据的变量.内容)

            // 返回数据显示到编辑框
            编辑框_整理后的内容.内容 = 局部_Py对象.到文本 ()


            // 返回数据置入剪辑版
            置剪辑板文本 (编辑框_整理后的内容.内容)

            // 对返回数据分割显示到超级列表框
            变量 局部_处理后信息分割 <类型 = 文本数组类>
            变量 局部_处理后信息二次分割 <类型 = 文本数组类>
            局部_数量 = 分割文本 (编辑框_整理后的内容.内容, "\r\n", 局部_处理后信息分割)
            // 每行地址信息处理组合成Python需要的文本变量内容,替换到Py代码中
            // 信息框 (到文本 (局部_数量))


            超级列表框1.全部删除 ()
            计次循环 (局部_数量)
            {
                如果真 (分割文本 (局部_处理后信息分割.取成员 (取循环索引 ()), "\t", 局部_处理后信息二次分割) >= 3)
                {
                    超级列表框1.插入表项 ()
                    超级列表框1.置标题 (超级列表框1.取表项数 () - 1, 0, 局部_处理后信息二次分割.取成员 (0))
                    超级列表框1.置标题 (超级列表框1.取表项数 () - 1, 1, 局部_处理后信息二次分割.取成员 (1))
                    超级列表框1.置标题 (超级列表框1.取表项数 () - 1, 2, 局部_处理后信息二次分割.取成员 (2))


                }

            }
            // 对返回数据分割显示到表格
            表格1.置列宽 (2, 400)
            表格1.行数 = 1
            表格1.置数据 (表格1.行数 - 1, 0, 表格常量.文本值型, "收件人")
            表格1.置数据 (表格1.行数 - 1, 1, 表格常量.文本值型, "联系电话")
            表格1.置数据 (表格1.行数 - 1, 2, 表格常量.文本值型, "收货地址")

            计次循环 (局部_数量)
            {
                如果真 (分割文本 (局部_处理后信息分割.取成员 (取循环索引 ()), "\t", 局部_处理后信息二次分割) >= 3)
                {
                    表格1.行数 = 表格1.行数 + 1
                    表格1.置数据 (表格1.行数 - 1, 0, 表格常量.文本值型, 局部_处理后信息二次分割.取成员 (0))
                    表格1.置数据 (表格1.行数 - 1, 1, 表格常量.文本值型, 局部_处理后信息二次分割.取成员 (1))
                    表格1.置数据 (表格1.行数 - 1, 2, 表格常量.文本值型, 局部_处理后信息二次分割.取成员 (2))


                }
            }


        }
        返回 (0)
    }
}

【分隔符】


###### 以下为一个火山软件开发平台系统类库程序的内容

<火山程序 类型 = "通常" 版本 = 1 />

包 火山.程序

类 启动类 <公开 基础类 = 窗口程序类>
{
    变量 主窗口对象 <类型 = 我的主窗口 标题 = "我的主窗口">

    方法 启动方法 <公开 类型 = 整数>
    {
        主窗口对象.创建主窗口 ()
        返回 (1)
    }
}

# ===

类 我的主窗口 <基础类 = 窗口 注释 = "样例主窗口" @视窗.布局 = "client_size = \"500, 300\"">
{
    变量 按钮1 <类型 = 按钮 隐藏值属性 = "0" @视窗.布局 = "id = 101\r\npos = \"93, 126, 144, 72\"" 标题 = "USC2到文本">

    方法 按钮_被单击 <接收事件 类型 = 整数 注释 = "当按钮被单击后发送此事件">
    参数 来源对象 <类型 = 按钮 注释 = "提供事件产生的具体来源对象">
    参数 标记值 <类型 = 整数 注释 = "用户调用\"挂接事件\"命令时所提供的\"标记值\"参数值,非此方式挂接事件则本参数值固定为0.">
    {
        调试输出 (USC2到文本 (文本到字节集 ("\u6559\u80b2\u7f51")))
        返回 (0)
    }

    # @begin
    # int ucs2_to_utf8( char *olData, int ilen,  char *buff, int bufLen) {
    #     int length = 0;
    #     if(!buff) return length;
    #     char *start = NULL;
    #     char *pout = buff;
    #     for(start = olData; start != NULL && start < olData+ilen-1; start +=2) {
    #         unsigned short ucs2_code = *(unsigned short *)start;
    #         if(0x0080 > ucs2_code) {
    #             if(length+1 > bufLen) return -1;
    #             *pout = *start;
    #             length++;
    #             pout ++;
    #         } else if(0x0800 > ucs2_code) {
    #             if(length+2 > bufLen) return -1;
    #             *pout = ((char)(ucs2_code >> 6)) | 0xc0;
    #             *(pout+1) = ((char)(ucs2_code & 0x003F)) | 0x80;
    #             length += 2;
    #             pout += 2;
    #         } else {
    #             if(length+3 > bufLen) return -1;
    #             *pout = ((char)(ucs2_code >> 12)) | 0xE0;
    #             *(pout+1) = ((char)((ucs2_code & 0x0FC0)>> 6)) | 0x80;
    #             *(pout+2) = ((char)(ucs2_code & 0x003F)) | 0x80;
    #             length += 3;
    #             pout += 3;
    #         }
    #     }
    #     return length;
    # }
    # @end

    方法 USC2到文本 <公开 静态 类型 = 文本型 @禁止流程检查 = 真>
    参数 USC2 <类型 = 字节集类>
    {
        @ int nSize = @<USC2>.GetSize() * 2;
        @ char * pBuff = (char *) malloc(nSize);
        @ int iSize = ucs2_to_utf8 ((char *)@<USC2>.GetPtr(), @<USC2>.GetSize(), pBuff, nSize);
        @ @an<CVolMem> memBuf (pBuff, iSize);
        @ free(pBuff);
        @ return @an<CVolString> ( (const CHAR*)memBuf.GetPtr () );
    }
}

【分隔符】


###### 以下为一个火山软件开发平台系统类库程序的内容

<火山程序 类型 = "通常" 版本 = 1 />

包 火山.DLL输出类

类 公开类_服务器 <公开 折叠>
{
    方法 服务器_创建 <公开 静态 类型 = 变整数 @输出名 = "fuwuqi_chuangjian" @视窗.输出 = 真>
    {
        变量 服务器对象 <类型 = 变整数>
        服务器对象 = 创建对象指针 (服务器)
        如果 (服务器对象 != 0)
        {
            全局类.互斥锁.加锁 ()
            全局类.服务器数组.加入成员 (服务器对象)
            全局类.互斥锁.解锁 ()
        }
        返回 (服务器对象)
    }

    方法 服务器_销毁 <公开 静态 折叠 @输出名 = "fuwuqi_xiaohui" @视窗.输出 = 真>
    参数 服务器指针 <类型 = 变整数>
    {
        如果 (服务器指针 != 0)
        {
            全局类.互斥锁.加锁 ()
            变量 i <类型 = 整数>
            循环 (0, 全局类.服务器数组.取成员数 (), i)
            {
                如果 (全局类.服务器数组.取成员 (i) == 服务器指针)
                {
                    全局类.服务器数组.删除成员 (i)
                    跳出循环
                }
            }
            全局类.互斥锁.解锁 ()
            销毁对象指针 (服务器指针)
        }

    }

    方法 服务器_停止 <公开 静态 类型 = 逻辑型 折叠 @输出名 = "fuwuqi_tingzhi" @视窗.输出 = 真>
    参数 服务器指针 <类型 = 变整数>
    {
        返回 (读指针处对象 (服务器指针, 服务器).服务器_停止 ())
    }

    方法 服务器_启动 <公开 静态 类型 = 逻辑型 注释 = "如果没有任何网站,或者端口被占用,将会返回假.." @输出名 = "fuwuqi_qidong" @视窗.输出 = 真>
    参数 服务器指针 <类型 = 变整数>
    参数 地址 <类型 = 文本型 注释 = "提供所欲监听的地址." "">
    参数 端口号 <类型 = 短整数 注释 = "提供所欲监听的端口号.">
    参数 工作线程数 <类型 = 整数 注释 = "默认不设置">
    参数 POST上传大小限制 <类型 = 整数 注释 = "单位:M">
    {
        返回 (读指针处对象 (服务器指针, 服务器).服务器_启动 (地址, 端口号, 工作线程数, POST上传大小限制))
    }

    方法 域名_创建 <公开 静态 类型 = 变整数 返回值注释 = "失败返回 0 成功返回 域名对象指针..." 折叠 @输出名 = "yuming_chuangjian" @视窗.输出 = 真>
    参数 服务器指针 <类型 = 变整数>
    参数 域名 <类型 = 文本型 注释 = "支持 * 比如:\"*.baidu.com\"" "">
    参数 网站编码 <类型 = 网站编码 "">
    参数 模板目录 <类型 = 文本型>
    参数 静态文件目录 <类型 = 文本型>
    参数 静态文件缓存时间 <类型 = 整数 注释 = "如果为0,则使用304缓存!,如果不为0,则缓存指定秒数">
    参数 压缩HTML <类型 = 逻辑型>
    参数 服务器名称 <类型 = 文本型 "">
    参数 GZIP压缩 <类型 = 逻辑型>
    参数 Session名称 <类型 = 文本型 "">
    参数 Session缓存时间 <类型 = 整数 注释 = "单位:秒">
    {
        返回 (读指针处对象 (服务器指针, 服务器).域名_创建 (域名, 网站编码, 模板目录, 静态文件目录, 静态文件缓存时间, 压缩HTML, 服务器名称, GZIP压缩, Session名称, Session缓存时间))

    }

    方法 域名_取对象指针 <公开 静态 类型 = 变整数 返回值注释 = "没有这个域名返回 0  成功返回对象指针" 折叠 @输出名 = "yuming_quduixiangzhizhen"
            @视窗.输出 = 真>
    参数 服务器指针 <类型 = 变整数>
    参数 域名 <类型 = 文本型 注释 = "不支持通配符" "">
    {
        返回 (读指针处对象 (服务器指针, 服务器).域名_取对象指针 (域名))
    }

    方法 域名_取数量 <公开 静态 类型 = 整数 @输出名 = "yuming_qushuliang" @视窗.输出 = 真>
    参数 服务器指针 <类型 = 变整数>
    {
        返回 (读指针处对象 (服务器指针, 服务器).域名_取数量 ())
    }

    方法 域名_取域名 <公开 静态 类型 = 变整数 返回值注释 = "返回数据指针,请注意回收哦..." @输出名 = "yuming_qusuoyouyuming" @视窗.输出 = 真>
    参数 服务器指针 <类型 = 变整数>
    参数 索引 <类型 = 整数>
    {
        变量 域名 <类型 = 文本型>
        域名 = 读指针处对象 (服务器指针, 服务器).域名_取域名 (索引)
        返回 (返回文本 (域名))
    }

    方法 域名_注册模板函数 <公开 静态 类型 = 逻辑型 @输出名 = "yuming_zhucemubanhanshu" @视窗.输出 = 真>
    参数 服务器对象 <类型 = 变整数>
    参数 网站对象 <类型 = 变整数 注释 = "创建域名() 的返回值!">
    参数 注册函数名 <类型 = 文本型>
    参数 函数地址 <类型 = 变整数>
    {
        返回 (读指针处对象 (服务器对象, 服务器).域名_注册模板函数 (网站对象, 注册函数名, 函数地址))
    }

    方法 域名_取消模板函数 <公开 静态 类型 = 逻辑型 @输出名 = "yuming_quxiamubanhanshu" @视窗.输出 = 真>
    参数 服务器对象 <类型 = 变整数>
    参数 网站对象 <类型 = 变整数 注释 = "创建域名() 的返回值!">
    参数 注册函数名 <类型 = 文本型>
    {
        返回 (读指针处对象 (服务器对象, 服务器).域名_取消模板函数 (网站对象, 注册函数名))

    }

    方法 域名_删除 <公开 静态 类型 = 逻辑型 @输出名 = "yuming_shanchu" @视窗.输出 = 真>
    参数 服务器指针 <类型 = 变整数>
    参数 域名对象 <类型 = 变整数 注释 = "创建域名 的返回值" "">
    {
        返回 (读指针处对象 (服务器指针, 服务器).域名_删除 (域名对象))
    }

    方法 视图_删除 <公开 静态 类型 = 逻辑型 注释 = "不可以多线程调用哦..." @输出名 = "shitu_shanchu" @视窗.输出 = 真>
    参数 服务器指针 <类型 = 变整数>
    参数 网站对象 <类型 = 变整数 注释 = "创建域名() 的返回值!">
    参数 匹配请求路径 <类型 = 文本型>
    {
        返回 (读指针处对象 (服务器指针, 服务器).视图_删除 (网站对象, 匹配请求路径))
    }

    方法 视图_订阅 <公开 静态 类型 = 逻辑型 @输出名 = "shitu_dingyue" @视窗.输出 = 真>
    参数 服务器指针 <类型 = 变整数>
    参数 网站对象 <类型 = 变整数 注释 = "创建域名() 的返回值!">
    参数 视图函数 <类型 = 变整数>
    参数 匹配请求路径 <类型 = 文本型 注释 = "/{栏目}/{编号}.html">
    参数 用户参数 <类型 = 变整数>
    参数 标签 <类型 = 文本型 注释 = "\"标签1/标签2/标签3\"">
    {
        返回 (读指针处对象 (服务器指针, 服务器).视图_订阅 (网站对象, 视图函数, 匹配请求路径, 用户参数, 标签))
    }

    方法 视图_订阅404 <公开 静态 类型 = 逻辑型 @输出名 = "shitu_dingyue404" @视窗.输出 = 真>
    参数 服务器指针 <类型 = 变整数>
    参数 网站对象 <类型 = 变整数 注释 = "创建域名() 的返回值!">
    参数 视图函数 <类型 = 变整数>
    参数 用户参数 <类型 = 变整数>
    参数 标签 <类型 = 文本型 注释 = "\"标签1/标签2/标签3\"">
    {
        返回 (读指针处对象 (服务器指针, 服务器).视图_订阅404 (网站对象, 视图函数, 用户参数, 标签))
    }

    #
}

类 公开类_请求类 <公开>
{
    方法 请求类_创建 <公开 静态 类型 = 变整数 @输出名 = "qingqiulei_chuangjian" @视窗.输出 = 真>
    {
        返回 (创建对象指针 (请求类))
    }

    方法 请求类_销毁 <公开 静态 @输出名 = "qingqiulei_xiaohui" @视窗.输出 = 真>
    参数 请求指针 <类型 = 变整数>
    {
        销毁对象指针 (请求指针)

    }

    方法 请求类_取模板目录 <公开 静态 类型 = 变整数 折叠 @输出名 = "qingqiulei_qumubanmulu" @视窗.输出 = 真>
    参数 请求指针 <类型 = 变整数>
    {
        变量 目录 <类型 = 文本型>
        目录 = 读指针处对象 (请求指针, 请求类).取模板目录 ()
        返回 (返回文本 (目录))
    }

    方法 请求类_取静态文件目录 <公开 静态 类型 = 变整数 折叠 折叠2 @输出名 = "qingqiulei_qujingtaiwenjianmulu" @视窗.输出 = 真>
    参数 请求指针 <类型 = 变整数>
    {
        变量 目录 <类型 = 文本型>
        目录 = 读指针处对象 (请求指针, 请求类).取静态文件目录 ()
        返回 (返回文本 (目录))




    }

    方法 请求类_取参数 <公开 静态 类型 = 变整数 折叠 @输出名 = "qingqiulei_qucanshu" @视窗.输出 = 真>
    参数 请求指针 <类型 = 变整数>
    参数 参数名 <类型 = 文本型>
    {
        变量 返回值 <类型 = 文本型>
        返回值 = 读指针处对象 (请求指针, 请求类).取参数 (参数名)
        返回 (返回文本 (返回值))




    }

    方法 请求类_取POST原始数据 <公开 静态 类型 = 变整数 @输出名 = "qingqiulei_quPOSTyuanshishujv" @视窗.输出 = 真>
    参数 请求指针 <类型 = 变整数>
    {
        变量 对象指针 <类型 = 变整数>
        对象指针 = 创建对象指针 (字节集类)
        读指针处对象 (对象指针, 字节集类).添加字节集 (读指针处对象 (请求指针, 请求类).取POST原始数据 ())
        返回 (对象指针)
    }

    方法 请求类_取所有参数 <公开 静态 类型 = 变整数 折叠 @输出名 = "qingqiulei_qusuoyoucanshu" @视窗.输出 = 真>
    参数 请求指针 <类型 = 变整数>
    参数 是否格式化 <类型 = 逻辑型>
    {
        变量 返回值 <类型 = 文本型>
        返回值 = 读指针处对象 (请求指针, 请求类).取所有参数 (是否格式化)
        返回 (返回文本 (返回值))




    }

    方法 请求类_取请求文件 <公开 静态 类型 = 变整数 折叠 @输出名 = "qingqiulei_quqingqiuwenjian" @视窗.输出 = 真>
    参数 请求指针 <类型 = 变整数>
    参数 是否包含参数 <类型 = 逻辑型>
    {
        变量 返回值 <类型 = 文本型>
        返回值 = 读指针处对象 (请求指针, 请求类).取请求文件 (是否包含参数)
        返回 (返回文本 (返回值))
    }

    方法 请求类_取请求域名 <公开 静态 类型 = 变整数 折叠 @输出名 = "qingqiulei_quqingqiuyuming" @视窗.输出 = 真>
    参数 请求指针 <类型 = 变整数>
    {
        变量 返回值 <类型 = 文本型>
        返回值 = 读指针处对象 (请求指针, 请求类).取请求域名 ()
        返回 (返回文本 (返回值))
    }

    方法 请求类_取页面标记 <公开 静态 类型 = 变整数 折叠 @输出名 = "qingqiulei_quyemianbiaoji" @视窗.输出 = 真>
    参数 请求指针 <类型 = 变整数>
    {
        变量 返回值 <类型 = 文本型>
        返回值 = 读指针处对象 (请求指针, 请求类).取页面标记 ()
        返回 (返回文本 (返回值))
    }

    方法 请求类_取请求头 <公开 静态 类型 = 变整数 折叠 @输出名 = "qingqiulei_quqingqiutou" @视窗.输出 = 真>
    参数 请求指针 <类型 = 变整数>
    参数 主键 <类型 = 文本型>
    {
        变量 返回值 <类型 = 文本型>
        返回值 = 读指针处对象 (请求指针, 请求类).取请求头 (主键)
        返回 (返回文本 (返回值))
    }

    方法 请求类_取用户IP <公开 静态 类型 = 变整数 折叠 @输出名 = "qingqiulei_quyonghuIP" @视窗.输出 = 真>
    参数 请求指针 <类型 = 变整数>
    {
        变量 返回值 <类型 = 文本型>
        返回值 = 读指针处对象 (请求指针, 请求类).取用户IP ()
        返回 (返回文本 (返回值))

    }

    方法 请求类_取绑定域名 <公开 静态 类型 = 变整数 折叠 @输出名 = "qingqiulei_qubangdingyuming" @视窗.输出 = 真>
    参数 请求指针 <类型 = 变整数>
    {
        变量 返回值 <类型 = 文本型>
        返回值 = 读指针处对象 (请求指针, 请求类).取绑定域名 ()
        返回 (返回文本 (返回值))
    }

    方法 请求类_取用户端口 <公开 静态 类型 = 整数 折叠 @输出名 = "qingqiulei_quyonghuduankou" @视窗.输出 = 真>
    参数 请求指针 <类型 = 变整数>
    {
        返回 (读指针处对象 (请求指针, 请求类).取用户端口 ())
    }

    方法 请求类_是否手机用户 <公开 静态 类型 = 逻辑型 折叠 @输出名 = "qingqiulei_shifoushoujiyonghu" @视窗.输出 = 真>
    参数 请求指针 <类型 = 变整数>
    {
        返回 (读指针处对象 (请求指针, 请求类).是否手机用户 ())


    }

    方法 请求类_取Cookies <公开 静态 类型 = 变整数 折叠 @输出名 = "qingqiulei_quCookies" @视窗.输出 = 真>
    参数 请求指针 <类型 = 变整数>
    参数 主键 <类型 = 文本型>
    {
        变量 返回值 <类型 = 文本型>
        返回值 = 读指针处对象 (请求指针, 请求类).取Cookies (主键)
        返回 (返回文本 (返回值))
    }

    方法 请求类_取请求方法 <公开 静态 类型 = 变整数 折叠 @输出名 = "qingqiulei_quqingqiufangfa" @视窗.输出 = 真>
    参数 请求指针 <类型 = 变整数>
    {
        变量 返回值 <类型 = 文本型>
        返回值 = 读指针处对象 (请求指针, 请求类).取请求方法 ()
        返回 (返回文本 (返回值))
    }

    方法 请求类_取订阅路径 <公开 静态 类型 = 变整数 折叠 @输出名 = "qingqiulei_qudingyuelujing" @视窗.输出 = 真>
    参数 请求指针 <类型 = 变整数>
    {
        变量 返回值 <类型 = 文本型>
        返回值 = 读指针处对象 (请求指针, 请求类).取订阅路径 ()
        返回 (返回文本 (返回值))
    }

    方法 请求类_取订阅文本参数 <公开 静态 类型 = 变整数 折叠 @输出名 = "qingqiulei_qudingyuewenbencanshu" @视窗.输出 = 真>
    参数 请求指针 <类型 = 变整数>
    {
        变量 返回值 <类型 = 文本型>
        返回值 = 读指针处对象 (请求指针, 请求类).取订阅文本参数 ()
        返回 (返回文本 (返回值))
    }

    方法 请求类_取订阅数值参数 <公开 静态 类型 = 变整数 折叠 @输出名 = "qingqiulei_qudingyueshuzhicanshu" @视窗.输出 = 真>
    参数 请求指针 <类型 = 变整数>
    {
        返回 (读指针处对象 (请求指针, 请求类).取订阅数值参数 ())
    }

    方法 请求类_Session_置值 <公开 静态 类型 = 逻辑型 折叠 @输出名 = "qingqiulei_Session_zhizhi" @视窗.输出 = 真>
    参数 请求指针 <类型 = 变整数>
    参数 主键 <类型 = 文本型>
    参数 Session值 <类型 = 文本型>
    {
        变量 返回值 <类型 = 逻辑型>
        返回值 = 读指针处对象 (请求指针, 请求类).Session_置值 (主键, Session值)
        返回 (返回值)
    }

    方法 请求类_Session_清空 <公开 静态 类型 = 逻辑型 折叠 @输出名 = "qingqiulei_Session_qingkong" @视窗.输出 = 真>
    参数 请求指针 <类型 = 变整数>
    {
        返回 (读指针处对象 (请求指针, 请求类).Session_清空 ())
    }

    方法 请求类_Session_取值 <公开 静态 类型 = 变整数 折叠 @输出名 = "qingqiulei_Session_quzhi" @视窗.输出 = 真>
    参数 请求指针 <类型 = 变整数>
    参数 主键 <类型 = 文本型>
    {
        变量 返回值 <类型 = 文本型>
        返回值 = 读指针处对象 (请求指针, 请求类).Session_取值 (主键)
        返回 (返回文本 (返回值))
    }

    方法 请求类_Session_取ID <公开 静态 类型 = 变整数 折叠 @输出名 = "qingqiulei_Session_quID" @视窗.输出 = 真>
    参数 请求指针 <类型 = 变整数>
    {
        变量 返回值 <类型 = 文本型>
        返回值 = 读指针处对象 (请求指针, 请求类).Session_取ID ()
        返回 (返回文本 (返回值))
    }
}

类 公开类_响应类 <公开>
{
    方法 响应类_创建 <公开 静态 类型 = 变整数 @输出名 = "xiangyinglei_chuangjian" @视窗.输出 = 真>
    {
        返回 (创建对象指针 (响应类))
    }

    方法 响应类_销毁 <公开 静态 折叠 @输出名 = "xiangyinglei_xiaohui" @视窗.输出 = 真>
    参数 对象指针 <类型 = 变整数>
    {
        销毁对象指针 (对象指针)
    }

    方法 响应类_写出数据 <公开 静态 类型 = 逻辑型 折叠 @输出名 = "xiangyinglei_xiechushujv" @视窗.输出 = 真>
    参数 对象指针 <类型 = 变整数>
    参数 字节集 <类型 = 字节集类>
    {
        返回 (读指针处对象 (对象指针, 响应类).写出数据 (字节集))

    }

    方法 响应类_写出错误提示 <公开 静态 类型 = 逻辑型 @输出名 = "xiangyinglei_xiechucuowutishi" @视窗.输出 = 真>
    参数 对象指针 <类型 = 变整数>
    参数 标题 <类型 = 文本型>
    参数 提示信息 <类型 = 文本型>
    {
        返回 (读指针处对象 (对象指针, 响应类).写出错误提示 (标题, 提示信息))
    }

    方法 响应类_写出文本 <公开 静态 类型 = 逻辑型 @输出名 = "xiangyinglei_xiechuwenben" @视窗.输出 = 真>
    参数 对象指针 <类型 = 变整数>
    参数 文本 <类型 = 文本型>
    {
        返回 (读指针处对象 (对象指针, 响应类).写出文本 (文本))

    }

    方法 响应类_置状态码 <公开 静态 类型 = 逻辑型 折叠 @输出名 = "xiangyinglei_zhizhuangtaima" @视窗.输出 = 真>
    参数 对象指针 <类型 = 变整数>
    参数 状态码 <类型 = 短整数>
    {
        返回 (读指针处对象 (对象指针, 响应类).置状态码 (状态码))
    }

    方法 响应类_置Cookies <公开 静态 类型 = 逻辑型 @输出名 = "xiangyinglei_zhiCookies" @视窗.输出 = 真>
    参数 对象指针 <类型 = 变整数>
    参数 Cookie名称 <类型 = 文本型>
    参数 Cookie值 <类型 = 文本型>
    参数 域名 <类型 = 文本型 注释 = "COOKIE对应的域名。不设置则为当前域名。">
    参数 目录 <类型 = 文本型 注释 = "对应网站中的路径。不设置则设置COOKIE到默认路径下。">
    参数 过期时间 <类型 = 整数 注释 = "单位:秒    (1800=30分钟)">
    {
        返回 (读指针处对象 (对象指针, 响应类).置Cookies (Cookie名称, Cookie值, 域名, 目录, 过期时间))

    }

    方法 响应类_置响应头 <公开 静态 类型 = 逻辑型 @输出名 = "xiangyinglei_zhixiangyingtou" @视窗.输出 = 真>
    参数 对象指针 <类型 = 变整数>
    参数 响应头名称 <类型 = 文本型>
    参数 响应头内容 <类型 = 文本型>
    {
        返回 (读指针处对象 (对象指针, 响应类).置响应头 (响应头名称, 响应头内容))
    }

    方法 响应类_置页面标记 <公开 静态 类型 = 逻辑型 @输出名 = "xiangyinglei_zhiyemianbiaoji" @视窗.输出 = 真>
    参数 对象指针 <类型 = 变整数>
    参数 页面标记 <类型 = 文本型>
    {
        返回 (读指针处对象 (对象指针, 响应类).置页面标记 (页面标记))
    }

    方法 响应类_重定向 <公开 静态 类型 = 逻辑型 @输出名 = "xiangyinglei_chongdingxiang" @视窗.输出 = 真>
    参数 对象指针 <类型 = 变整数>
    参数 重定向地址 <类型 = 文本型>
    参数 永久重定向 <类型 = 逻辑型 注释 = "真=301 / 假=302">
    {
        返回 (读指针处对象 (对象指针, 响应类).重定向 (重定向地址, 永久重定向))
    }

    方法 响应类_渲染JSON <公开 静态 类型 = 逻辑型 @输出名 = "xiangyinglei_xuanranJSON" @视窗.输出 = 真>
    参数 对象指针 <类型 = 变整数>
    参数 文件 <类型 = 文本型>
    参数 JSON <类型 = 文本型>
    {
        返回 (读指针处对象 (对象指针, 响应类).渲染JSON (文件, JSON))
    }

    方法 响应类_渲染JSON到文本 <公开 静态 类型 = 变整数 @输出名 = "xiangyinglei_xuanranJSONdaowenben" @视窗.输出 = 真>
    参数 对象指针 <类型 = 变整数>
    参数 文件 <类型 = 文本型>
    参数 JSON <类型 = 文本型>
    {
        变量 渲染结果 <类型 = 文本型>
        渲染结果 = 读指针处对象 (对象指针, 响应类).渲染JSON到文本 (文件, JSON)
        返回 (返回文本 (渲染结果))

    }

    方法 响应类_模板是否包含标签 <公开 静态 类型 = 逻辑型 折叠 @输出名 = "xiangyinglei_mubanshifoubaohanbiaoqian" @视窗.输出 = 真>
    参数 对象指针 <类型 = 变整数>
    参数 模板文件 <类型 = 文本型>
    参数 标签名 <类型 = 文本型>
    {
        返回 (读指针处对象 (对象指针, 响应类).模板是否包含标签 (模板文件, 标签名))
    }

    #

    方法 响应类_取模板标签参数 <公开 静态 类型 = 变整数 @输出名 = "xiangyinglei_qumubanbiaoqiancanshu" @视窗.输出 = 真>
    参数 对象指针 <类型 = 变整数>
    参数 模板文件 <类型 = 文本型>
    参数 标签名 <类型 = 文本型>
    {
        变量 返回值 <类型 = 文本型>
        返回值 = 读指针处对象 (对象指针, 响应类).取模板标签参数 (模板文件, 标签名)
        返回 (返回文本 (返回值))

    }

    方法 响应类_置响应类型 <公开 静态 类型 = 逻辑型 @输出名 = "xiangyinglei_zhixiangyingleixing" @视窗.输出 = 真>
    参数 对象指针 <类型 = 变整数>
    参数 后缀名 <类型 = 文本型 注释 = "\"js\"  \"html\"   \"css\"" "">
    {
        返回 (读指针处对象 (对象指针, 响应类).置响应类型 (后缀名))
    }

    方法 响应类_写出提示 <公开 静态 类型 = 逻辑型 @输出名 = "xiangyinglei_xiechutishi" @视窗.输出 = 真>
    参数 对象指针 <类型 = 变整数>
    参数 提示标题 <类型 = 文本型>
    参数 提示信息 <类型 = 文本型>
    参数 提示类型 <类型 = 整数 注释 = "0=开心,1=难过,2=生气">
    参数 链接1 <类型 = 文本型 注释 = "例如:\"javascript:history.go(-1);\"">
    参数 链接1标题 <类型 = 文本型 注释 = "例如:\"返回上一页\"">
    参数 链接2 <类型 = 文本型 注释 = "例如:tencent://Message/?uin=123456">
    参数 链接2标题 <类型 = 文本型 注释 = "例如:\"立即联系\"">
    {
        返回 (读指针处对象 (对象指针, 响应类).写出提示 (提示标题, 提示信息, 提示类型, 链接1, 链接1标题, 链接2, 链接2标题))
    }
}

类 公开类_其它 <公开 折叠>
{
    方法 其它类_取数据长度 <公开 静态 类型 = 整数 @输出名 = "qitalei_quhuishujv" @视窗.输出 = 真>
    参数 对象指针 <类型 = 变整数>
    {
        返回 (读指针处对象 (对象指针, 字节集类).取字节集长度 ())

    }

    方法 其它类_拷贝数据 <公开 静态 注释 = "拷贝并释放!所以,只能拷贝一次!指针就失效了!" @输出名 = "qitalei_kaobeishujv" @视窗.输出 = 真>
    参数 拷贝到数据地址 <类型 = 变整数>
    参数 对象指针 <类型 = 变整数>
    参数 拷贝长度 <类型 = 整数>
    {
        内存复制 (拷贝到数据地址, 读指针处对象 (对象指针, 字节集类).取字节集指针 (), 拷贝长度)
        销毁对象指针 (对象指针)
    }

    #
}

类 公开类_图片类 <公开 折叠>
{
    方法 图片类_创建验证码图片 <公开 静态 类型 = 变整数 返回值注释 = "返回字节集类" 折叠 @输出名 = "tupianlei_chuangjianyanzhengmatupian"
            @视窗.输出 = 真>
    参数 验证码文本 <类型 = 文本型>
    参数 图片宽度 <类型 = 整数>
    参数 图片高度 <类型 = 整数>
    参数 图片质量 <类型 = 整数 注释 = "1~100">
    参数 干扰字符数 <类型 = 整数>
    {
        变量 对象指针 <类型 = 变整数>
        对象指针 = 创建对象指针 (字节集类)
        读指针处对象 (对象指针, 字节集类).添加字节集 (图片类.创建图片验证码 (验证码文本, 图片宽度, 图片高度, 图片质量, 干扰字符数))
        返回 (对象指针)
    }

    #
}

类 公开类_模板参数类
{
    方法 参数类_取参数数量 <公开 静态 类型 = 整数 @输出名 = "canshulei_qucanshushuliang" @视窗.输出 = 真>
    参数 参数句柄 <类型 = 变整数>
    {
        返回 (模板函数.模板参数_取参数数量 (参数句柄))
    }

    方法 参数类_取文本 <公开 静态 类型 = 变整数 @输出名 = "canshulei_quwenbne" @视窗.输出 = 真>
    参数 参数句柄 <类型 = 变整数>
    参数 索引 <类型 = 整数>
    {
        返回 (返回文本 (模板函数.模板参数_取文本 (参数句柄, 索引)))
    }

    方法 参数类_取整数 <公开 静态 类型 = 整数 @输出名 = "canshulei_quzhengshu" @视窗.输出 = 真>
    参数 参数句柄 <类型 = 变整数>
    参数 索引 <类型 = 整数>
    {
        返回 (模板函数.模板参数_取整数 (参数句柄, 索引))
    }

    方法 参数类_取小数 <公开 静态 类型 = 小数 @输出名 = "canshulei_quxiaoshu" @视窗.输出 = 真>
    参数 参数句柄 <类型 = 变整数>
    参数 索引 <类型 = 整数>
    {
        返回 (模板函数.模板参数_取小数 (参数句柄, 索引))

    }

    方法 参数类_取逻辑 <公开 静态 类型 = 逻辑型 @输出名 = "canshulei_quluoji" @视窗.输出 = 真>
    参数 参数句柄 <类型 = 变整数>
    参数 索引 <类型 = 整数>
    {
        返回 (模板函数.模板参数_取逻辑 (参数句柄, 索引))
    }

    方法 参数类_取参数类型 <公开 静态 类型 = 整数 @输出名 = "canshulei_quucanshuleixing" @视窗.输出 = 真>
    参数 参数句柄 <类型 = 变整数>
    参数 索引 <类型 = 整数>
    {
        返回 (模板函数.模板参数_取参数类型 (参数句柄, 索引))

    }

    方法 参数类_返回文本 <公开 静态 类型 = 变整数 @输出名 = "canshulei_fanhuiwenben" @视窗.输出 = 真>
    参数 要返回的文本 <类型 = 文本型>
    {
        返回 (模板函数.模板参数_返回文本 (要返回的文本))
    }
}

类 公开类_验证类 <公开 折叠>
{
    方法 验证类_验证是否为邮箱 <公开 静态 类型 = 逻辑型 @输出名 = "yanzhenglei_yanzhengshifouweiyouxiang" @视窗.输出 = 真>
    参数 邮箱 <类型 = 文本型>
    {
        返回 (验证是否为邮箱 (邮箱))
    }

    方法 验证类_验证是否为手机号码 <公开 静态 类型 = 逻辑型 @输出名 = "yanzhenglei_yanzhengshifouweishoujihaoma" @视窗.输出 = 真>
    参数 手机号码 <类型 = 文本型>
    {
        返回 (验证是否为手机号码 (手机号码))
    }
}

【分隔符】


###### 以下为一个火山软件开发平台系统类库程序的内容

<火山程序 类型 = "通常" 版本 = 1 />

包 火山.程序

类 启动类 <公开 基础类 = 动态库程序类>
{
    变量 m_热更新线程 <类型 = w_线程类>

    方法 类_清理
    {
        m_热更新线程.终止线程 ()  // 直接终止线程!
    }

    方法 启动方法 <公开 类型 = 整数>
    {
        连接响应.请求 = w_线程储存器_创建 ()  // 初始化...
        连接响应.响应 = w_线程储存器_创建 ()  // 初始化...
        全局类_取后缀类型 ("js")  // 初始化
        m_热更新线程.创建线程 (取静态方法地址 (线程_热更新模板), 0, 假)  // 用于热更新模板...




        // 写到文件 ("D:\\我的坚果云\\3389x\\_int\\导航\\release\\x64\\linker\\1.jpg", 全局类_创建图片验证码 ("9565", 200, 80, 90, 20))



        // 取静态方法地址 (服务器.__发送静态文件)

        返回 (1)
    }

    方法 线程_热更新模板 <静态 类型 = 整数 注释 = "热更新模板!!!!!!!!!!!!" 折叠>
    参数 参数 <类型 = 变整数>
    {
        判断循环 (真)  // 死循环...
        {
            变量 i <类型 = 整数>
            变量 服务器对象 <类型 = 变整数>
            延时 (1000)  // 等...
            全局类.互斥锁.加锁 ()  // 开始检测更新...!
            循环 (0, 全局类.服务器数组.取成员数 (), i)
            {
                服务器对象 = 全局类.服务器数组.取成员 (i)
                读指针处对象 (服务器对象, 服务器).服务器_热更新模板 ()
            }
            全局类.互斥锁.解锁 ()
        }
        返回 (0)
    }

    方法 首页 <静态 类型 = 变整数 折叠>
    参数 用户参数 <类型 = 变整数>
    {
        变量 请求 <类型 = 请求类>
        变量 响应 <类型 = 响应类>
        变量 哈希表 <类型 = 哈希表>
        响应.写出文本 ("请求域名:" + 请求.取请求域名 () + "<br>")
        响应.写出文本 ("请求文件:" + 请求.取请求文件 () + "<br>")
        响应.写出文本 ("用户IP:" + 请求.取用户IP () + "<br>")
        响应.写出文本 ("用户端口:" + 到文本 (请求.取用户端口 ()) + "<br>")
        响应.写出文本 ("静态文件目录:" + 请求.取静态文件目录 () + "<br>")
        响应.写出文本 ("模板目录:" + 请求.取模板目录 () + "<br>")
        响应.写出文本 ("是否手机用户:" + 到文本 (请求.是否手机用户 ()) + "<br>")


        哈希表.置文本 ("网站标题", "试试会不会显示哈..")
        响应.渲染 ("D:\\我的坚果云\\火山模块\\蜗牛模块\\网页模板\\父模板.html", 哈希表)

        返回 (0)
    }
}

【分隔符】


###### 以下为一个火山软件开发平台系统类库程序的内容

<火山程序 类型 = "通常" 版本 = 1 />

包 蜗牛服务器.全局

类 全局类 <公开 @全局类 = 真>
{
    变量 服务器数组 <公开 静态 类型 = 变整数数组类 注释 = "记录所有服务器对象指针">
    变量 互斥锁 <公开 静态 类型 = 互斥锁类>

    方法 全局类_取描述文本 <公开 静态 类型 = 文本型 折叠>
    参数 状态码 <类型 = 短整数>
    {
        如果 (状态码 < 0 || 状态码 > 505)  // 防止越界..
        {
            返回 ("")
        }
        返回 (多项选择 (状态码, "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "Continue", "SwitchingProtocols", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "OK", "Created", "Accepted", "Non-AuthoritativeInformation", "No Content", "Reset Content", "Partial Content", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "Multiple Choices", "Moved Permanently", "Found", "See Other", "Not Modified", "Use Proxy", "", "TemporaryRedirect", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "Bad Request", "Unauthorized", "Payment Required", "Forbidden", "Not Found", "Method NotAllowed", "Not Acceptable", "Proxy AuthenticationRequired", "Request Time-out", "Conflict", "Gone", "Length Required", "PreconditionFailed", "Request Entity TooLarge", "Request-URI TooLarge", "Unsupported MediaType", "Requested range notsatisfiable", "ExpectationFailed", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "Internal ServerError", "Not Implemented", "Bad Gateway", "ServiceUnavailable", "Gateway Time-out", "HTTP Version notsupported"))



    }

    方法 全局类_创建GUID文本 <公开 静态 类型 = 文本型 折叠>
    {
        变量 GUID <类型 = GUID类>
        变量 GUID文本 <类型 = 文本型>
        GUID.创建 ()
        GUID文本 = GUID.取文本 ()
        如果 (取文本长度 (GUID文本) > 2)
        {
            删除字符 (GUID文本, 0, 1)  // 删除前面的"{"
            删除字符 (GUID文本, 取文本长度 (GUID文本) - 1, 1)  // 删除后面的"}"
        }
        返回 (GUID文本)

    }

    方法 全局类_取后缀类型 <公开 静态 类型 = 文本型 注释 = "需要初始化.." 折叠>
    参数 文件后缀 <类型 = 文本型 注释 = "\"js\"/\"css\"...">
    {
        变量 文件类型 <类型 = 文本型>
        变量 哈希表 <静态 类型 = 哈希表>
        如果 (哈希表.取数量 () == 0)
        {
            常量 后缀列表 <类型 = 视窗文件资源 值 = "Content-Type.txt">
            变量 字节集 <类型 = 字节集类>
            变量 JSON <类型 = 文本型>
            文件资源到字节集 (后缀列表, 字节集)
            JSON = 多字节到文本 (字节集)
            哈希表.从JSON载入 (JSON)
        }
        文件类型 = 到小写 (文件后缀)
        如果 (哈希表.是否存在 (文件类型) == 假)
        {
            返回 ("application/octet-stream")  // 没有的类型,按这个处理!
        }
        返回 (哈希表.取文本 (文件类型))
    }

    方法 全局类_删除尾部斜杠 <公开 静态 类型 = 文本型>
    参数 订阅路径或者请求文件 <类型 = 文本型>
    {
        如果 (取文本长度 (订阅路径或者请求文件) == 1)  //  "/"
        {
            返回 (订阅路径或者请求文件)
        }
        如果 (是否以字符结束 (订阅路径或者请求文件, '/') == 假)
        {
            返回 (订阅路径或者请求文件)
        }
        返回 (取文本左边 (订阅路径或者请求文件, 取文本长度 (订阅路径或者请求文件) - 1))
    }

    方法 全局类_取协议头时间文本 <公开 静态 类型 = 文本型 折叠>
    参数 时间 <类型 = 小数>
    {
        变量 临时文本 <类型 = 文本型>
        变量 索引 <类型 = 整数>
        索引 = 取星期几 (时间)
        临时文本 = 多项选择 (索引 - 1, "Sun, ", "Mon, ", "Tue, ", "Wed, ", "Thu, ", "Fri, ", "Sat, ")

        索引 = 取日 (时间)
        如果 (索引 < 10)
        {
            加入字符 (临时文本, '0')
        }
        加入文本 (临时文本, 到文本 (索引))
        加入字符 (临时文本, ' ')

        索引 = 取月份 (时间)
        加入文本 (临时文本, 多项选择 (索引 - 1, "Jan ", "Feb ", "Mar ", "Apr ", "May ", "Jun ", "Jul ", "Aug ", "Sep ", "Oct ", "Nov ", "Dec "))

        索引 = 取年份 (时间)
        加入文本 (临时文本, 到文本 (索引))
        加入字符 (临时文本, ' ')

        索引 = 取小时 (时间)
        如果 (索引 < 10)
        {
            加入字符 (临时文本, '0')
        }
        加入文本 (临时文本, 到文本 (索引))
        加入字符 (临时文本, ':')

        索引 = 取分钟 (时间)
        如果 (索引 < 10)
        {
            加入字符 (临时文本, '0')
        }
        加入文本 (临时文本, 到文本 (索引))
        加入字符 (临时文本, ':')

        索引 = 取秒 (时间)
        如果 (索引 < 10)
        {
            加入字符 (临时文本, '0')
        }
        加入文本 (临时文本, 到文本 (索引))
        加入文本 (临时文本, " GMT")
        返回 (临时文本)


    }
}

类 连接响应 <公开 折叠 @全局类 = 真>
{
    变量 请求 <公开 静态 类型 = 变整数 注释 = "线程局部储存器">
    变量 响应 <公开 静态 类型 = 变整数 注释 = "线程局部储存器">

    方法 响应信息_创建 <公开 静态 类型 = "变整数 ">
    参数 连接ID <类型 = 变整数>
    参数 网站指针 <类型 = 变整数>
    {
        变量 对象指针 <类型 = 变整数>
        对象指针 = 创建对象指针 (响应信息)
        w_线程储存器_置值 (响应, 对象指针)
        如果 (对象指针 != 0)
        {
            读指针处对象 (对象指针, 响应信息).连接ID = 连接ID
            读指针处对象 (对象指针, 响应信息).网站指针 = 网站指针
            如果 (网站指针 != 0)
            {
                读指针处对象 (对象指针, 响应信息).网站编码 = 读指针处对象 (网站指针, 网站).网站编码
            }
        }
        返回 (对象指针)

    }

    方法 响应信息_取指针 <公开 静态 类型 = 变整数>
    {
        返回 (w_线程储存器_取值 (响应))
    }

    方法 响应信息_销毁 <公开 静态 折叠>
    {
        变量 指针 <类型 = 变整数>
        指针 = 响应信息_取指针 ()
        w_线程储存器_置值 (响应, 0)
        如果 (指针 != 0)
        {
            销毁对象指针 (指针)
        }


    }

    方法 响应信息_取Cookie值 <公开 静态 类型 = 文本型 折叠>
    参数 响应指针 <类型 = 变整数>
    参数 Cookie名称 <类型 = 文本型>
    {
        如果 (响应指针 == 0)
        {
            返回 ("")
        }
        变量 数量 <类型 = 整数>
        变量 i <类型 = 整数>
        数量 = 读指针处对象 (响应指针, 响应信息).Cookie.取成员数 ()
        循环 (0, 数量, i)
        {
            如果 (读指针处对象 (响应指针, 响应信息).Cookie.取成员 (i).名称 == Cookie名称)
            {
                返回 (读指针处对象 (响应指针, 响应信息).Cookie.取成员 (i).值)
            }
        }
        返回 ("")
    }

    方法 响应信息_置Cookie值 <公开 静态 类型 = 逻辑型 折叠>
    参数 响应指针 <类型 = 变整数>
    参数 Cookie名称 <类型 = 文本型>
    参数 Cookie值 <类型 = 文本型>
    参数 域名 <类型 = 文本型 注释 = "COOKIE对应的域名。不设置则为当前域名。">
    参数 目录 <类型 = 文本型 注释 = "对应网站中的路径。不设置则设置COOKIE到默认路径下。">
    参数 过期时间 <类型 = 整数 注释 = "单位:秒">
    {
        如果 (响应指针 == 0)
        {
            返回 (假)
        }
        变量 数量 <类型 = 整数>
        变量 i <类型 = 整数>
        变量 set_Cookie <类型 = Cookie>
        set_Cookie.名称 = Cookie名称
        set_Cookie.值 = Cookie值
        set_Cookie.域名 = 域名
        set_Cookie.目录 = 目录
        set_Cookie.过期时间 = 过期时间
        数量 = 读指针处对象 (响应指针, 响应信息).Cookie.取成员数 ()
        循环 (0, 数量, i)
        {
            如果 (读指针处对象 (响应指针, 响应信息).Cookie.取成员 (i).名称 == Cookie名称)
            {
                读指针处对象 (响应指针, 响应信息).Cookie.置成员值 (i, set_Cookie)
                返回 (真)
            }
        }
        读指针处对象 (响应指针, 响应信息).Cookie.加入成员 (set_Cookie)
        返回 (真)
    }

    方法 响应信息_响应头是否存在 <公开 静态 类型 = 逻辑型 折叠>
    参数 响应指针 <类型 = 变整数>
    参数 响应头名称 <类型 = 文本型>
    {
        如果 (响应指针 != 0)
        {
            变量 数量 <类型 = 整数>
            变量 i <类型 = 整数>
            数量 = 读指针处对象 (响应指针, 响应信息).响应头.取成员数 ()
            循环 (0, 数量, i)
            {
                如果 (读指针处对象 (响应指针, 响应信息).响应头.取成员 (i).名称 == 响应头名称)
                {
                    返回 (真)
                }
            }

        }
        返回 (假)
    }

    方法 响应信息_置响应头 <公开 静态 类型 = 逻辑型 折叠>
    参数 响应指针 <类型 = 变整数>
    参数 响应头名称 <类型 = 文本型>
    参数 响应头内容 <类型 = 文本型>
    {
        如果 (响应指针 != 0)
        {
            变量 数量 <类型 = 整数>
            变量 i <类型 = 整数>
            数量 = 读指针处对象 (响应指针, 响应信息).响应头.取成员数 ()
            循环 (0, 数量, i)
            {
                如果 (读指针处对象 (响应指针, 响应信息).响应头.取成员 (i).名称 == 响应头名称)
                {
                    读指针处对象 (响应指针, 响应信息).响应头.取成员 (i).值 = 响应头内容
                    返回 (真)
                }

            }
            变量 回复头 <类型 = HP请求头类>
            回复头.名称 = 响应头名称
            回复头.值 = 响应头内容
            读指针处对象 (响应指针, 响应信息).响应头.加入成员 (回复头)
            返回 (真)
        }
        返回 (假)

    }

    方法 请求信息_创建 <公开 静态 类型 = 变整数>
    参数 连接ID <类型 = 变整数>
    参数 服务器指针 <类型 = 变整数 注释 = "\"服务器\"">
    参数 网站指针 <类型 = 变整数>
    参数 HP <类型 = HP_HTTP服务器>
    {
        变量 对象指针 <类型 = 变整数>
        对象指针 = 创建对象指针 (请求信息)
        w_线程储存器_置值 (请求, 对象指针)

        变量 组包指针 <类型 = 变整数>
        变量 POST数据 <类型 = 字节集类>
        HP.取连接附加数据 (连接ID, 取变量地址 (组包指针))
        如果 (组包指针 != 0)  //  有组包数据!!!
        {
            POST数据 = 读指针处对象 (组包指针, 字节集类)
            读指针处对象 (组包指针, 字节集类).清空字节集 ()
        }

        如果 (对象指针 != 0)
        {
            变量 查询参数 <类型 = 文本型 注释 = " 1=1&参数=1111&参数2=2222222">
            变量 提交类型 <类型 = 文本型>
            查询参数 = HP.取请求URL域值 (连接ID, HPURL字段.查询)
            读指针处对象 (对象指针, 请求信息).请求域名 = HP.取主机 (连接ID)
            读指针处对象 (对象指针, 请求信息).请求文件 = HP.取请求URL域值 (连接ID, HPURL字段.路径)
            读指针处对象 (对象指针, 请求信息).连接ID = 连接ID
            读指针处对象 (对象指针, 请求信息).查询参数 = 查询参数
            读指针处对象 (对象指针, 请求信息).服务器 = 服务器指针
            读指针处对象 (对象指针, 请求信息).请求方法 = HP.取请求方式 (连接ID)
            读指针处对象 (对象指针, 请求信息).请求头 = HP.取所有请求头 (连接ID)
            HP.取客户远程地址 (连接ID, 读指针处对象 (对象指针, 请求信息).用户IP)
            读指针处对象 (对象指针, 请求信息).用户端口 = HP.取客户远程端口 (连接ID)
            读指针处对象 (对象指针, 请求信息).网站指针 = 网站指针
            读指针处对象 (对象指针, 请求信息).请求参数.从表单载入 (查询参数, 真, 真)
            读指针处对象 (对象指针, 请求信息).POST数据 = POST数据

            提交类型 = HP.取请求头值 (连接ID, "Content-Type")
            如果 (近似等于 (提交类型, "application/x-www-form-urlencoded", 假))  // 提交表单!
            {
                变量 表单 <类型 = 文本型>
                表单 = __取POST文本 (提交类型, POST数据)
                读指针处对象 (对象指针, 请求信息).请求参数.从表单载入 (表单, 真, 真)
            }
            否则 (近似等于 (提交类型, "application/json", 假))
            {
                变量 JSON <类型 = 文本型>
                JSON = __取POST文本 (提交类型, POST数据)
                读指针处对象 (对象指针, 请求信息).请求参数.从JSON载入 (JSON, 假, 真)
            }

        }
        返回 (对象指针)
    }

    方法 __取POST文本 <静态 类型 = 文本型 折叠>
    参数 提交类型 <类型 = 文本型>
    参数 POST数据 <类型 = 字节集类>
    {
        变量 编码 <类型 = 文本型>
        编码 = w_文本_取右边 (提交类型, "charset=", , 真)
        如果 (编码 == "")
        {
            返回 (UTF8到文本 (POST数据))  // 默认按UTF-8处理
        }
        删自身首尾空 (编码)
        如果 (文本比较 (编码, "utf-8", 假) == 0)
        {
            返回 (UTF8到文本 (POST数据))
        }
        如果 (文本比较 (编码, "utf8", 假) == 0)
        {
            返回 (UTF8到文本 (POST数据))
        }
        如果 (文本比较 (编码, "gbk", 假) == 0)
        {
            返回 (多字节到文本 (POST数据))
        }
        如果 (文本比较 (编码, "gb2312", 假) == 0)
        {
            返回 (多字节到文本 (POST数据))
        }
        返回 (UTF8到文本 (POST数据))
    }

    方法 请求信息_取参数 <公开 静态 类型 = 文本型 折叠>
    参数 请求指针 <类型 = 变整数>
    参数 名称 <类型 = 文本型>
    {
        如果 (请求指针 == 0)
        {
            返回 ("")
        }
        返回 (读指针处对象 (请求指针, 请求信息).请求参数.取文本 (名称))
    }

    方法 请求信息_取请求头值 <公开 静态 类型 = 文本型 折叠>
    参数 请求指针 <类型 = 变整数>
    参数 名称 <类型 = 文本型>
    {
        变量 数量 <类型 = 整数>
        变量 i <类型 = 整数>
        如果 (请求指针 == 0)
        {
            返回 ("")
        }
        数量 = 读指针处对象 (请求指针, 请求信息).请求头.取成员数 ()
        循环 (0, 数量, i)
        {
            如果 (文本比较 (读指针处对象 (请求指针, 请求信息).请求头.取成员 (i).名称, 名称, 假) == 0)
            {
                变量 返回值 <类型 = 文本型>
                返回值 = 读指针处对象 (请求指针, 请求信息).请求头.取成员 (i).值
                返回 (返回值)
            }
        }
        返回 ("")
    }

    方法 请求信息_取指针 <公开 静态 类型 = 变整数 折叠>
    {
        返回 (w_线程储存器_取值 (请求))

    }

    方法 请求信息_销毁 <公开 静态 折叠>
    {
        变量 指针 <类型 = 变整数>
        指针 = 请求信息_取指针 ()
        w_线程储存器_置值 (请求, 0)
        如果 (指针 != 0)
        {
            销毁对象指针 (指针)
        }

    }

    #
}

类 返回转换类 <公开 @全局类 = 真>
{
    方法 返回文本 <公开 静态 类型 = 变整数 折叠>
    参数 要返回的文本 <类型 = 文本型>
    {
        变量 长度 <类型 = 整数>
        长度 = 取文本长度 (要返回的文本)
        如果 (长度 == 0)
        {
            返回 (0)
        }
        变量 指针 <类型 = 变整数>
        长度 = 长度 * 2 + 2
        指针 = 分配内存 (长度)
        如果 (指针 != 0)
        {
            内存复制 (指针, 取文本指针 (要返回的文本), 长度)
        }
        返回 (指针)



    }

    方法 接收文本 <公开 静态 类型 = 文本型 返回值注释 = "只能接收一次,会释放内存!" 折叠>
    参数 文本指针 <类型 = 变整数>
    {
        如果 (文本指针 == 0)
        {
            返回 ("")
        }
        变量 返回值 <类型 = 文本型>
        返回值 = 指针到文本 (文本指针)
        释放内存 (文本指针)
        返回 (返回值)





    }
}

【分隔符】


###### 以下为一个火山软件开发平台系统类库程序的内容

<火山程序 类型 = "通常" 版本 = 1 />

包 蜗牛服务器.图片类

类 图片类 <公开 @全局类 = 真>
{
    方法 创建图片验证码 <公开 静态 类型 = 字节集类>
    参数 验证码 <类型 = 文本型>
    参数 图片宽度 <类型 = 整数>
    参数 图片高度 <类型 = 整数>
    参数 图片质量 <类型 = 整数 注释 = "1~100">
    参数 干扰字符数 <类型 = 整数 注释 = "15次..比较合适">
    {
        变量 图片操作 <类型 = 图片操作类>
        变量 i <类型 = 整数>
        变量 x <类型 = 整数>
        变量 y <类型 = 整数>
        变量 粗细 <类型 = 整数>
        变量 字体缩放 <类型 = 整数>
        图片操作.创建 (图片宽度, 图片高度, 16)
        图片操作.填充 ()

        x = 图片宽度 / 2 - 取文本长度 (验证码) * 20
        y = 图片高度 / 2 + 20
        字体缩放 = 2
        粗细 = 2
        图片操作.画文本 (验证码, x, y, 0, 字体缩放, 粗细, 4, 假, , , , 150)

        x = 0
        y = 0

        循环 (0, 干扰字符数, i)
        {
            字体缩放 = 1
            粗细 = 1
            变量 干扰字符 <类型 = 文本型>


            干扰字符 = 到文本 ((字符)多项选择 (取随机数 (0, 2), 取随机数 ('0', '9'), 取随机数 ('a', 'z'), 取随机数 ('A', 'Z')))

            图片操作.画文本 (干扰字符, 取随机数 (0, 图片宽度), 取随机数 (0, 图片高度), 0, 字体缩放, 粗细, 4, 假, 取随机数 (0, 255), 取随机数 (0, 255), 取随机数 (0, 255), 150)

            // x = x + 矩形干扰间距
            // y = y + 矩形干扰间距
            // 宽度 = 图片宽度 - x * 2
            // 高度 = 图片高度 - y * 2
            // 图片操作.画矩形 (x, y, 宽度, 高度, 1, 取随机数 (0, 255), 取随机数 (0, 255), 取随机数 (0, 255))
        }


        返回 (图片操作.到JPG (图片质量))
    }
}

【分隔符】


###### 以下为一个火山软件开发平台系统类库程序的内容

<火山程序 类型 = "通常" 版本 = 1 />

包 蜗牛服务.属性表

类 属性表 <公开>
{
    方法 类_初始化
    {

    }
}

【分隔符】


###### 以下为一个火山软件开发平台系统类库程序的内容

<火山程序 类型 = "通常" 版本 = 1 />

包 蜗牛服务器.服务器

类 网站编码 <公开 折叠 @常量类 = 整数>
{
    常量 GBK <公开 值 = 0>
    常量 UTF8 <公开 值 = 1>

    #
}

类 视图
{
    变量 停用视图 <公开 类型 = 逻辑型 注释 = "停用当前视图...这个功能先不做....">
    变量 匹配类 <公开 类型 = w_文本匹配类 注释 = "用于匹配请求文件">
    变量 订阅路径 <公开 类型 = 文本型>
    变量 视图函数 <公开 类型 = 变整数>
    变量 用户参数 <公开 类型 = 变整数 注释 = "用户订阅时提供的参数">
    变量 用户文本参数 <公开 类型 = 文本型 注释 = "用户订阅时提供的参数">
}

类 错误视图
{
    变量 停用视图 <公开 类型 = 逻辑型 注释 = "停用当前视图...这个功能先不做....">
    变量 视图函数 <公开 类型 = 变整数>
    变量 用户参数 <公开 类型 = 变整数 注释 = "用户订阅时提供的参数">
    变量 用户文本参数 <公开 类型 = 文本型 注释 = "用户订阅时提供的参数">

    #
}

类 网站 <公开>
{
    变量 域名 <公开 类型 = 文本型 注释 = "127.0.0.1">
    变量 模糊匹配 <公开 类型 = 逻辑型>
    变量 视图数组 <公开 类型 = 变整数数组类 注释 = "指针 一个网站可以有很多个视图函数">
    变量 m_404视图 <公开 类型 = 变整数数组类 注释 = "指针 错误视图">
    变量 网站编码 <公开 类型 = 网站编码 注释 = "0=GBK        1=UTF-8">
    变量 模板 <公开 类型 = 模板引擎>
    变量 模板目录 <公开 类型 = 文本型>
    变量 静态文件目录 <公开 类型 = 文本型>
    变量 静态文件缓存时间 <公开 类型 = 整数 注释 = "单位:秒,如果为0,则使用304缓存">
    变量 服务器名称 <公开 类型 = 文本型>
    变量 GZIP压缩 <公开 类型 = 逻辑型>
    变量 Session名称 <公开 类型 = 文本型>
    变量 Session缓存 <公开 类型 = w_内存缓存对象>
    变量 Session缓存时间 <公开 类型 = 整数>

    方法 类_清理 <折叠>
    {
        变量 i <类型 = 整数>
        循环 (0, 视图数组.取成员数 (), i)
        {
            销毁对象指针 (视图数组.取成员 (i))
        }
    }

    方法 匹配域名 <公开 类型 = 逻辑型 折叠>
    参数 要匹配的域名 <类型 = 文本型>
    {
        如果 (模糊匹配)
        {
            返回 (w_文本_模糊匹配 (要匹配的域名, 域名))
        }
        返回 (要匹配的域名 == 域名)
    }

    方法 调用视图函数 <公开 类型 = 逻辑型>
    参数 请求文件 <类型 = 文本型>
    参数 请求信息指针 <类型 = 变整数>
    参数 请求参数 <类型 = 哈希表>
    {
        变量 i <类型 = 整数>
        变量 视图对象 <类型 = 变整数>
        循环 (0, 视图数组.取成员数 (), i)
        {
            视图对象 = 视图数组.取成员 (i)
            如果 (读指针处对象 (视图对象, 视图).匹配类.匹配 (请求文件, 请求参数, 真, 真))  // 视图匹配成功!
            {
                变量 回调函数 <类型 = 变整数>
                变量 返回值 <类型 = 变整数>
                变量 标签 <类型 = 文本数组类>
                回调函数 = 读指针处对象 (视图对象, 视图).视图函数
                读指针处对象 (请求信息指针, 请求信息).用户文本参数 = 读指针处对象 (视图对象, 视图).用户文本参数
                读指针处对象 (请求信息指针, 请求信息).用户数值参数 = 读指针处对象 (视图对象, 视图).用户参数
                读指针处对象 (请求信息指针, 请求信息).订阅路径 = 读指针处对象 (视图对象, 视图).订阅路径

                返回值 = w_call_0 (回调函数)
                如果 (返回值 == 1)  // 用户不要...
                {
                    到循环尾
                }
                返回 (真)
            }
        }
        返回 (假)
    }

    方法 调用404函数 <公开 类型 = 逻辑型>
    参数 请求文件 <类型 = 文本型>
    参数 请求信息指针 <类型 = 变整数>
    参数 请求参数 <类型 = 哈希表>
    {
        变量 i <类型 = 整数>
        变量 回调函数 <类型 = 变整数>
        变量 返回值 <类型 = 变整数>
        变量 对象 <类型 = 变整数>
        循环 (0, m_404视图.取成员数 (), i)
        {
            对象 = m_404视图.取成员 (i)
            回调函数 = 读指针处对象 (对象, 错误视图).视图函数
            读指针处对象 (请求信息指针, 请求信息).用户文本参数 = 读指针处对象 (对象, 错误视图).用户文本参数
            读指针处对象 (请求信息指针, 请求信息).订阅路径 = ""
            读指针处对象 (请求信息指针, 请求信息).用户数值参数 = 读指针处对象 (对象, 错误视图).用户参数

            返回值 = w_call_0 (回调函数)
            如果 (返回值 == 1)  // 用户不要...
            {
                到循环尾
            }
            返回 (真)
        }
        返回 (假)
    }
}

类 服务器 <公开>
{
    变量 m_服务器 <公开 类型 = HP_HTTP服务器>
    变量 m_网站列表 <类型 = 变整数数组类 注释 = "指针">
    变量 m_服务器锁 <类型 = w_读写锁 注释 = "保护 \"m_网站列表\"">
    变量 m_POST上传大小限制 <公开 类型 = 整数>

    方法 类_初始化

    方法 类_清理 <折叠>
    {
        变量 i <类型 = 整数>
        变量 对象指针 <类型 = 变整数>
        循环 (0, m_网站列表.取成员数 (), i)
        {
            对象指针 = m_网站列表.取成员 (i)
            销毁对象指针 (对象指针)
        }

    }

    方法 域名_创建 <公开 类型 = 变整数 返回值注释 = "失败返回 0 成功返回 域名对象指针...">
    参数 域名 <类型 = 文本型 注释 = "支持 * 比如:\"*.baidu.com\"" @默认值 = "*">
    参数 网站编码 <类型 = 网站编码 @默认值 = 网站编码.UTF8>
    参数 模板目录 <类型 = 文本型>
    参数 静态文件目录 <类型 = 文本型>
    参数 静态文件缓存时间 <类型 = 整数 注释 = "如果为0,则使用304缓存!,如果不为0,则缓存指定秒数" @默认值 = 0>
    参数 压缩HTML <类型 = 逻辑型>
    参数 服务器名称 <类型 = 文本型 @默认值 = "woniu">
    参数 GZIP压缩 <类型 = 逻辑型>
    参数 Session名称 <类型 = 文本型>
    参数 Session缓存时间 <类型 = 整数 注释 = "单位:秒">
    {
        变量 对象指针 <类型 = 变整数>
        变量 i <类型 = 整数>
        如果 (域名 == "")
        {
            返回 (0)
        }
        如果 (静态文件目录 == 取运行目录 () || 静态文件目录 + "\\" == 取运行目录 ())
        {
            w_信息框 ("静态文件目录,不能为程序的根目录,有可能会被别人偷走东西哦...", "创建域名>报错", 16)
            返回 (0)
        }
        m_服务器锁.写 ()
        循环 (0, m_网站列表.取成员数 (), i)
        {
            对象指针 = m_网站列表.取成员 (i)
            如果 (读指针处对象 (对象指针, 网站).域名 == 域名)
            {
                m_服务器锁.结束写 ()
                返回 (对象指针)  // 已经创建过了...
            }
        }
        对象指针 = 创建对象指针 (网站)
        如果 (对象指针 == -1)  // 创建失败...
        {
            m_服务器锁.结束写 ()
            返回 (0)
        }
        变量 缓存时间 <类型 = 整数>
        缓存时间 = 选择 (Session缓存时间 <= 0, 30 * 60, Session缓存时间)

        读指针处对象 (对象指针, 网站).域名 = 域名
        读指针处对象 (对象指针, 网站).模糊匹配 = 寻找字符 (域名, '*') != -1
        读指针处对象 (对象指针, 网站).网站编码 = 网站编码
        读指针处对象 (对象指针, 网站).模板.设置 (模板目录, 压缩HTML, 压缩HTML, 0)
        读指针处对象 (对象指针, 网站).模板目录 = 模板目录
        读指针处对象 (对象指针, 网站).静态文件目录 = 静态文件目录
        读指针处对象 (对象指针, 网站).静态文件缓存时间 = 静态文件缓存时间
        读指针处对象 (对象指针, 网站).服务器名称 = 服务器名称
        读指针处对象 (对象指针, 网站).GZIP压缩 = GZIP压缩
        读指针处对象 (对象指针, 网站).Session名称 = 选择 (文本是否为空 (Session名称), "woniuweb", Session名称)

        读指针处对象 (对象指针, 网站).Session缓存.初始化 (缓存时间)
        读指针处对象 (对象指针, 网站).Session缓存时间 = 缓存时间


        m_网站列表.加入成员 (对象指针)
        m_服务器锁.结束写 ()
        返回 (对象指针)

    }

    方法 域名_取对象指针 <公开 类型 = 变整数 返回值注释 = "没有这个域名返回 0  成功返回对象指针" 折叠>
    参数 域名 <类型 = 文本型 注释 = "不支持通配符" "">
    {
        变量 i <类型 = 整数>
        变量 对象指针 <类型 = 变整数>
        m_服务器锁.读 ()
        循环 (0, m_网站列表.取成员数 (), i)
        {
            对象指针 = m_网站列表.取成员 (i)
            如果 (读指针处对象 (对象指针, 网站).域名 == 域名)
            {
                m_服务器锁.结束读 ()
                返回 (对象指针)
            }
        }
        m_服务器锁.结束读 ()
        返回 (0)
    }

    方法 域名_取数量 <公开 类型 = 整数 折叠>
    {
        变量 返回值 <类型 = 整数>
        m_服务器锁.读 ()
        返回值 = m_网站列表.取成员数 ()
        m_服务器锁.结束读 ()
        返回 (返回值)
    }

    方法 域名_取域名 <公开 类型 = 文本型 折叠>
    参数 索引 <类型 = 整数>
    {
        变量 域名 <类型 = 文本型>
        m_服务器锁.读 ()
        如果 (m_网站列表.索引是否有效 (索引) == 假)
        {
            m_服务器锁.结束读 ()
            返回 ("")
        }
        域名 = 读指针处对象 (m_网站列表.取成员 (索引), 网站).域名
        m_服务器锁.结束读 ()
        返回 (域名)

    }

    方法 域名_删除 <公开 类型 = 逻辑型 折叠>
    参数 域名对象 <类型 = 变整数 注释 = "创建域名 的返回值" "">
    {
        变量 i <类型 = 整数>
        m_服务器锁.写 ()
        循环 (0, m_网站列表.取成员数 (), i)
        {
            如果 (m_网站列表.取成员 (i) == 域名对象)
            {
                m_网站列表.删除成员 (i)
                销毁对象指针 (域名对象)
                m_服务器锁.结束写 ()
                返回 (真)  // 删除成功
            }
        }
        m_服务器锁.结束写 ()
        返回 (假)  // 域名,不是我管理的...或者就没有这个域名..

    }

    方法 域名_注册模板函数 <公开 类型 = 逻辑型 折叠>
    参数 网站对象 <类型 = 变整数 注释 = "创建域名() 的返回值!">
    参数 注册函数名 <类型 = 文本型>
    参数 函数地址 <类型 = 变整数>
    {
        变量 返回值 <类型 = 逻辑型>
        如果 (网站对象 == 0)
        {
            返回 (假)
        }
        m_服务器锁.写 ()
        返回值 = 读指针处对象 (网站对象, 网站).模板.注册模板函数 (注册函数名, 函数地址)
        m_服务器锁.结束写 ()
        返回 (返回值)

    }

    方法 域名_取消模板函数 <公开 类型 = 逻辑型 折叠>
    参数 网站对象 <类型 = 变整数 注释 = "创建域名() 的返回值!">
    参数 注册函数名 <类型 = 文本型>
    {
        变量 返回值 <类型 = 逻辑型>
        如果 (网站对象 == 0)
        {
            返回 (假)
        }
        m_服务器锁.写 ()
        返回值 = 读指针处对象 (网站对象, 网站).模板.取消模板函数 (注册函数名)
        m_服务器锁.结束写 ()
        返回 (返回值)

    }

    方法 视图_删除 <公开 类型 = 逻辑型>
    参数 网站对象 <类型 = 变整数 注释 = "创建域名() 的返回值!">
    参数 订阅路径 <类型 = 文本型>
    {
        变量 i <类型 = 整数>
        变量 视图对象 <类型 = 变整数>
        如果 (网站对象 == 0)
        {
            返回 (假)
        }
        m_服务器锁.写 ()
        循环 (0, 读指针处对象 (网站对象, 网站).视图数组.取成员数 (), i)
        {
            视图对象 = 读指针处对象 (网站对象, 网站).视图数组.取成员 (i)
            如果 (读指针处对象 (视图对象, 视图).订阅路径 == 订阅路径)
            {
                销毁对象指针 (视图对象)
                读指针处对象 (网站对象, 网站).视图数组.删除成员 (i)
                m_服务器锁.结束写 ()
                返回 (真)
            }
        }
        m_服务器锁.结束写 ()
        返回 (假)
    }

    方法 视图_订阅404 <公开 类型 = 逻辑型 折叠>
    参数 网站对象 <类型 = 变整数 注释 = "创建域名() 的返回值!">
    参数 视图函数 <类型 = 变整数>
    参数 用户参数 <类型 = 变整数>
    参数 用户文本参数 <类型 = 文本型 注释 = "....">
    {
        如果 (网站对象 == 0 || 视图函数 == 0)
        {
            返回 (假)
        }
        变量 对象指针 <类型 = 变整数>
        对象指针 = 创建对象指针 (错误视图)
        如果 (对象指针 == 0)
        {
            返回 (假)
        }
        读指针处对象 (对象指针, 错误视图).用户文本参数 = 用户文本参数
        读指针处对象 (对象指针, 错误视图).视图函数 = 视图函数
        读指针处对象 (对象指针, 错误视图).停用视图 = 假
        读指针处对象 (对象指针, 错误视图).用户参数 = 用户参数
        m_服务器锁.写 ()
        读指针处对象 (网站对象, 网站).m_404视图.加入成员 (对象指针)
        m_服务器锁.结束写 ()
        返回 (真)
    }

    方法 视图_订阅 <公开 类型 = 逻辑型>
    参数 网站对象 <类型 = 变整数 注释 = "创建域名() 的返回值!">
    参数 视图函数 <类型 = 变整数>
    参数 订阅路径 <类型 = 文本型 注释 = "/{栏目}/{编号}.html">
    参数 用户参数 <类型 = 变整数>
    参数 用户文本参数 <类型 = 文本型 注释 = "....">
    {
        如果 (网站对象 == 0 || 视图函数 == 0 || 文本是否为空 (订阅路径))
        {
            返回 (假)
        }
        变量 对象指针 <类型 = 变整数>
        对象指针 = 创建对象指针 (视图)
        如果 (对象指针 == 0)
        {
            返回 (假)
        }
        如果 (读指针处对象 (对象指针, 视图).匹配类.创建 (订阅路径) == 假)
        {
            销毁对象指针 (对象指针)
            返回 (假)
        }
        读指针处对象 (对象指针, 视图).用户文本参数 = 用户文本参数
        读指针处对象 (对象指针, 视图).视图函数 = 视图函数
        读指针处对象 (对象指针, 视图).停用视图 = 假
        读指针处对象 (对象指针, 视图).订阅路径 = 订阅路径
        读指针处对象 (对象指针, 视图).用户参数 = 用户参数

        m_服务器锁.写 ()
        读指针处对象 (网站对象, 网站).视图数组.加入成员 (对象指针)
        m_服务器锁.结束写 ()

        返回 (真)



    }

    方法 服务器_停止 <公开 类型 = 逻辑型 折叠>
    {
        返回 (m_服务器.停止 ())
    }

    方法 服务器_热更新模板 <公开 折叠>
    {
        变量 i <类型 = 整数>
        变量 对象指针 <类型 = 变整数>
        m_服务器锁.读 ()
        循环 (0, m_网站列表.取成员数 (), i)
        {
            对象指针 = m_网站列表.取成员 (i)
            读指针处对象 (对象指针, 网站).模板.热更新 ()
        }
        m_服务器锁.结束读 ()

    }

    方法 服务器_取连接数 <公开 类型 = 整数 注释 = "还没封装..." 折叠>
    {
        返回 (m_服务器.客户端连接数)
    }

    方法 服务器_启动 <公开 类型 = 逻辑型 注释 = "如果没有任何网站,或者端口被占用,将会返回假..">
    参数 地址 <类型 = 文本型 注释 = "提供所欲监听的地址." @默认值 = "0.0.0.0">
    参数 端口号 <类型 = 短整数 注释 = "提供所欲监听的端口号.">
    参数 工作线程数 <类型 = 整数 注释 = "默认不设置">
    参数 POST上传大小限制 <类型 = 整数 注释 = "单位:M">
    {
        如果 (m_网站列表.取成员数 () == 0)
        {
            返回 (假)
        }
        如果 (工作线程数 > 0)
        {
            m_服务器.工作线程数 = 工作线程数
        }
        m_POST上传大小限制 = 选择 (POST上传大小限制 <= 0, 5 * 1024 * 1024, POST上传大小限制 * 1024 * 1024)
        m_服务器.发送事件同步策略 = HP发送事件同步策略.同步接收事件
        m_服务器.数据发送策略 = HP数据发送策略.打包模式

        返回 (m_服务器.启动 (地址, 端口号))
    }

    方法 _收到请求 <类型 = 整数>
    参数 来源对象 <类型 = HP_HTTP服务器>
    参数 连接ID <类型 = 变整数>
    {
        变量 Host <类型 = 文本型>
        变量 请求文件 <类型 = 文本型>
        变量 ""
        变量 网站对象 <类型 = 变整数 注释 = "这个不能销毁哦..">
        变量 指针_请求信息 <类型 = 变整数>
        变量 指针_响应信息 <类型 = 变整数>

        Host = 来源对象.取主机 (连接ID)
        如果 (寻找字符 (Host, ':') != -1)
        {
            Host = 取文本左边 (Host, 寻找字符 (Host, ':'))
        }

        请求文件 = 来源对象.取请求URL域值 (连接ID, HPURL字段.路径)

        网站对象 = __匹配域名 (Host, m_网站列表)  // 查找网站指针

        指针_响应信息 = 响应信息_创建 (连接ID, 网站对象)
        指针_请求信息 = 请求信息_创建 (连接ID, 本对象.取对象自身指针 (), 网站对象, 来源对象)
        如果 (指针_请求信息 == 0 || 指针_响应信息 == 0)
        {
            响应信息_销毁 ()
            请求信息_销毁 ()
            返回 (1)
        }

        如果 (网站对象 == 0)
        {
            读指针处对象 (指针_响应信息, 响应信息).响应数据 = 文本到UTF8 (单个模板.取错误页面 ("蜗牛服务器", "当前域名未创建哦..."))
            读指针处对象 (指针_响应信息, 响应信息).状态码 = 200
        }
        否则 (读指针处对象 (网站对象, 网站).调用视图函数 (请求文件, 指针_请求信息, 读指针处对象 (指针_请求信息, 请求信息).请求参数))
        {
            // 有视图处理了请求...
        }
        否则 (__发送静态文件 (网站对象, 指针_请求信息, 指针_响应信息, 请求文件) == 假)  // 不是静态文件...
        {
            读指针处对象 (指针_响应信息, 响应信息).状态码 = 404
            如果 (读指针处对象 (网站对象, 网站).调用404函数 (请求文件, 指针_请求信息, 读指针处对象 (指针_请求信息, 请求信息).请求参数))
            {
                // 用户自己处理了404页面
            }
            否则  // 系统内置404
            {
                读指针处对象 (指针_响应信息, 响应信息).响应数据 = 文本到UTF8 (单个模板.取错误页面 ("404", "法海不懂爱,页面出不来!"))
            }

        }

        __回复请求 (网站对象, 来源对象)

        响应信息_销毁 ()
        请求信息_销毁 ()
        如果 (来源对象.是否为保持连接 (连接ID) == 假)  // 不是长连接!!!....
        {
            来源对象.释放连接 (连接ID)  // 过三秒之后,断开连接..
        }
        返回 (0)
    }

    方法 __发送静态文件 <静态 类型 = 逻辑型>
    参数 网站指针 <类型 = 变整数>
    参数 请求指针 <类型 = 变整数>
    参数 响应指针 <类型 = 变整数>
    参数 请求文件 <类型 = 文本型 注释 = "不带查询参数的哦..">
    {
        变量 静态目录 <类型 = 文本型>
        静态目录 = 读指针处对象 (网站指针, 网站).静态文件目录
        如果 (文本是否为空 (静态目录))  // 没有设置静态文件目录!
        {
            返回 (假)
        }
        如果 (请求文件 == "/")  // 首页..不是请求文件!
        {
            返回 (假)
        }
        变量 文件 <类型 = 文本型>
        变量 后缀名 <类型 = 文本型>
        变量 文件时间 <类型 = 小数>
        文件 = 请求文件
        替换字符 (文件, '/', '\\', 0)
        后缀名 = w_文件_取后缀名 (文件, 真)
        文件 = 静态目录 + 文件
        如果 (文本是否为空 (后缀名))  // 没有文件后缀...
        {
            返回 (假)
        }
        文件时间 = 取文件时间 (文件)
        如果 (为最小时间 (文件时间))  // 静态文件不存在!
        {
            返回 (假)
        }
        如果 (读指针处对象 (网站指针, 网站).静态文件缓存时间 == 0)  // 使用304...
        {
            变量 文件标记 <类型 = 文本型>
            加入字符 (文件标记, '"')
            加入文本 (文件标记, 到文本 (文件时间))  // 等同于: 文件标记 = "+文件时间+"
            加入字符 (文件标记, '"')
            如果 (请求信息_取请求头值 (请求指针, "If-None-Match") == 文件标记)  // 这个文件,发过给用户,直接说,不需要更新!
            {
                读指针处对象 (响应指针, 响应信息).文档类型 = 后缀名
                读指针处对象 (响应指针, 响应信息).状态码 = 304
                返回 (真)
            }
            响应信息_置响应头 (响应指针, "ETag", 文件标记)  // 设置文件标记...
        }
        否则
        {
            响应信息_置响应头 (响应指针, "Cache-Control", "max-age=" + 到文本 (读指针处对象 (网站指针, 网站).静态文件缓存时间))

        }


        读指针处对象 (响应指针, 响应信息).文档类型 = 后缀名
        读指针处对象 (响应指针, 响应信息).响应数据 = 读入文件 (文件)
        读指针处对象 (响应指针, 响应信息).状态码 = 200
        返回 (真)
    }

    方法 __回复请求 <静态 折叠>
    参数 网站指针 <类型 = 变整数 注释 = "可以为0">
    参数 来源对象 <类型 = HP_HTTP服务器>
    {
        变量 响应头数组 <类型 = HP请求头数组类>
        变量 指针_响应信息 <类型 = 变整数>
        变量 状态码 <类型 = 短整数>
        变量 连接ID <类型 = 变整数>
        变量 响应数据 <类型 = 字节集类>
        变量 文档类型 <类型 = 文本型>
        变量 描述文本 <类型 = 文本型>
        指针_响应信息 = 响应信息_取指针 ()
        如果 (指针_响应信息 == 0)  // 不属于我管...
        {
            返回
        }
        状态码 = 读指针处对象 (指针_响应信息, 响应信息).状态码
        连接ID = 读指针处对象 (指针_响应信息, 响应信息).连接ID
        响应数据 = 读指针处对象 (指针_响应信息, 响应信息).响应数据
        文档类型 = 读指针处对象 (指针_响应信息, 响应信息).文档类型
        描述文本 = 全局类_取描述文本 (状态码)

        如果 (响应信息_响应头是否存在 (指针_响应信息, "Date") == 假)
        {
            响应信息_置响应头 (指针_响应信息, "Date", 全局类_取协议头时间文本 (取现行时间 ()))
        }

        如果 (响应信息_响应头是否存在 (指针_响应信息, "Server") == 假)
        {
            如果 (网站指针 != 0)
            {
                响应信息_置响应头 (指针_响应信息, "Server", 读指针处对象 (网站指针, 网站).服务器名称)
            }
            否则
            {
                响应信息_置响应头 (指针_响应信息, "Server", "woniu")  // 没有找到网站...
            }
        }

        如果 (响应信息_响应头是否存在 (指针_响应信息, "Connection") == 假)
        {
            响应信息_置响应头 (指针_响应信息, "Connection", "keep-alive")
        }

        如果 (响应信息_响应头是否存在 (指针_响应信息, "Content-Type") == 假)
        {
            响应信息_置响应头 (指针_响应信息, "Content-Type", 全局类_取后缀类型 (文档类型))
        }

        如果 (网站指针 != 0)
        {
            如果 (读指针处对象 (网站指针, 网站).GZIP压缩)
            {
                响应数据 = HPSocket类.压缩数据_GZip (响应数据)
                响应信息_置响应头 (指针_响应信息, "Content-Encoding", "gzip")
            }
        }

        响应头数组 = 读指针处对象 (指针_响应信息, 响应信息).响应头

        变量 i <类型 = 整数>
        变量 set_Cookie <类型 = HP请求头类>
        变量 Cookie <类型 = Cookie>
        变量 Cookie数量 <类型 = 整数>
        set_Cookie.名称 = "Set-Cookie"
        Cookie数量 = 读指针处对象 (指针_响应信息, 响应信息).Cookie.取成员数 ()


        循环 (0, Cookie数量, i)  // 组合COOKIE...
        {
            变量 过期时间 <类型 = 小数>
            Cookie = 读指针处对象 (指针_响应信息, 响应信息).Cookie.取成员 (i)
            过期时间 = 增减时间 (取现行时间 (), 时间字段类型.秒, Cookie.过期时间)

            清空文本 (set_Cookie.值)

            加入文本 (set_Cookie.值, Cookie.名称)
            加入字符 (set_Cookie.值, '=')
            加入文本 (set_Cookie.值, Cookie.值)
            加入文本 (set_Cookie.值, "; path=")
            加入文本 (set_Cookie.值, Cookie.目录)
            加入文本 (set_Cookie.值, "; expires=")
            加入文本 (set_Cookie.值, 全局类_取协议头时间文本 (过期时间))

            如果 (文本是否为空 (Cookie.域名) == 假)
            {
                加入文本 (set_Cookie.值, "; domain=")
                加入文本 (set_Cookie.值, Cookie.域名)
            }


            响应头数组.加入成员 (set_Cookie)
        }


        来源对象.回复请求 (连接ID, 状态码, 描述文本, 响应头数组, 响应数据)
    }

    方法 __匹配域名 <静态 类型 = 变整数 返回值注释 = "成功返回网站指针,失败返回0" 折叠>
    参数 Host <类型 = 文本型>
    参数 网站列表 <类型 = 变整数数组类>
    {
        变量 网站数量 <类型 = 整数>
        变量 i <类型 = 整数>
        变量 网站指针 <类型 = 变整数>
        网站数量 = 网站列表.取成员数 ()
        循环 (0, 网站数量, i)
        {
            网站指针 = 网站列表.取成员 (i)
            如果 (读指针处对象 (网站指针, 网站).匹配域名 (Host))
            {
                返回 (网站指针)
            }

        }
        返回 (0)
    }

    方法 HP_HTTP服务器_开始握手 <接收事件 类型 = 整数 注释 = "  当客户端通过事件\"客户进入\"后,将会立即触发本事件,只有通过本事件,才会进行握手动作."
            返回值注释 = "可选返回值:" 返回值注释 = "  0 = 本次操作成功, 客户端将与服务器进行握手." 返回值注释 = "  1 = 忽略本次操作, 客户端可与服务器建立正式连接."
            返回值注释 = "  2 = 本次操作错误, 握手动作失败, 主动断开与客户的连接." 折叠>
    参数 来源对象 <类型 = HP_HTTP服务器 注释 = "提供事件产生的具体来源对象">
    参数 标记值 <类型 = 整数 注释 = "用户调用\"挂接事件\"命令时所提供的\"标记值\"参数值,非此方式挂接事件则本参数值固定为0.">
    参数 连接ID <类型 = 变整数>
    {
        来源对象.置连接附加数据 (连接ID, 0)
        返回 (0)
    }

    方法 HP_HTTP服务器_请求头解析完毕 <接收事件 类型 = 整数 注释 = " 当服务器解析请求头完毕后,将通过本事件通知用户."
            注释 = "  请注意:本事件只是通知用户请求头解析完毕,不提供所解析的请" 注释 = "求头数据,如果想要获取请求头数据,请添加事件\"请求头进入\",此事"
            注释 = "件下将会提供请求头名称和请求头值." 返回值注释 = "返回-1则认定为解析错误并关闭该连接." 折叠 折叠2>
    参数 来源对象 <类型 = HP_HTTP服务器 注释 = "提供事件产生的具体来源对象">
    参数 标记值 <类型 = 整数 注释 = "用户调用\"挂接事件\"命令时所提供的\"标记值\"参数值,非此方式挂接事件则本参数值固定为0.">
    参数 连接ID <类型 = 变整数>
    {
        返回 (0)
    }

    方法 HP_HTTP服务器_请求数据进入 <接收事件 类型 = 整数 注释 = "  当接收到HTTP BODY报文时本事件将被触发." 注释 = "  本事件返回值拥有具体意义,请您参考如下返回值:"
            注释 = "    0  - 本次操作成功,默认返回值." 注释 = "    -1 - 本次操作失败,主动与客户断开连接."
            注释 = "  请注意:如果本事件返回-1,将会触发事件\"解析错误\",您可以忽略本次\"解析" 注释 = "错误\"事件." 返回值注释 = "返回-1则认定为解析错误并关闭该连接." 折叠 折叠2>
    参数 来源对象 <类型 = HP_HTTP服务器 注释 = "提供事件产生的具体来源对象">
    参数 标记值 <类型 = 整数 注释 = "用户调用\"挂接事件\"命令时所提供的\"标记值\"参数值,非此方式挂接事件则本参数值固定为0.">
    参数 连接ID <类型 = 变整数>
    参数 数据 <类型 = 字节集类 注释 = "所收到的请求体数据.">
    参数 数据长度 <类型 = 整数 注释 = "所收到的请求体数据长度.">
    {
        变量 数据指针 <类型 = 变整数>
        来源对象.取连接附加数据 (连接ID, 取变量地址 (数据指针))
        如果 (数据指针 == 0)
        {
            数据指针 = 创建对象指针 (字节集类)  // 创建组包对象
            如果 (数据指针 == 0)  // 创建指针失败!
            {
                返回 (-1)
            }
            如果 (来源对象.置连接附加数据 (连接ID, 数据指针) == 假)  // 附加失败!
            {
                销毁对象指针 (数据指针)
                返回 (-1)
            }
        }

        如果 (读指针处对象 (数据指针, 字节集类).取字节集长度 () > m_POST上传大小限制)  // 你上传太大了..
        {
            销毁对象指针 (数据指针)
            来源对象.置连接附加数据 (连接ID, 0)
            返回 (-1)
        }
        读指针处对象 (数据指针, 字节集类).添加字节集 (数据)
        返回 (0)
    }

    方法 HP_HTTP服务器_消息解析完毕 <接收事件 类型 = 整数 注释 = "  客户请求解析完毕." 注释 = "  本事件返回值拥有具体意义,请您参考如下返回值:"
            注释 = "    0  - 本次操作成功,默认返回值." 注释 = "    -1 - 本次操作失败,主动与客户断开连接."
            注释 = "  请注意:如果本事件返回-1,将会触发事件\"解析错误\",您可以忽略本次\"解析" 注释 = "错误\"事件." 返回值注释 = "返回-1则认定为解析错误并关闭该连接." 折叠2>
    参数 来源对象 <类型 = HP_HTTP服务器 注释 = "提供事件产生的具体来源对象">
    参数 标记值 <类型 = 整数 注释 = "用户调用\"挂接事件\"命令时所提供的\"标记值\"参数值,非此方式挂接事件则本参数值固定为0.">
    参数 连接ID <类型 = 变整数>
    {
        变量 返回值 <类型 = 整数>
        如果 (来源对象.是否升级协议 (连接ID))
        {
            返回 (0)
        }
        m_服务器锁.读 ()
        返回值 = _收到请求 (来源对象, 连接ID)
        m_服务器锁.结束读 ()

        变量 数据指针 <类型 = 变整数>
        来源对象.取连接附加数据 (连接ID, 取变量地址 (数据指针))
        如果 (数据指针 != 0)  // 有POST包...
        {
            销毁对象指针 (数据指针)  // 直接释放
            来源对象.置连接附加数据 (连接ID, 0)  // 设置没有POST包...以免断开事件的时候重复回收,导致崩溃!
        }
        返回 (返回值)
    }

    方法 HP_HTTP服务器_客户离开 <接收事件 类型 = 整数 注释 = "当客户端主动或被动与服务器断开后将触发本事件." 折叠>
    参数 来源对象 <类型 = HP_HTTP服务器 注释 = "提供事件产生的具体来源对象">
    参数 标记值 <类型 = 整数 注释 = "用户调用\"挂接事件\"命令时所提供的\"标记值\"参数值,非此方式挂接事件则本参数值固定为0.">
    参数 连接ID <类型 = 变整数 注释 = "触发该事件的连接ID.">
    参数 关闭原因 <类型 = HP操作类型 注释 = "返回导致连接被关闭的操作.">
    参数 错误码 <类型 = HP错误码 注释 = "如果客户端为非正常退出,此参数为错误代码.">
    {
        变量 数据指针 <类型 = 变整数>
        来源对象.取连接附加数据 (连接ID, 取变量地址 (数据指针))
        如果 (数据指针 != 0)
        {
            销毁对象指针 (数据指针)
        }
        返回 (0)
    }
}

【分隔符】


###### 以下为一个火山软件开发平台系统类库程序的内容

<火山程序 类型 = "通常" 版本 = 1 />

包 蜗牛模块.服务器

类 HTML设置 <折叠>
{
    变量 压缩HTML <公开 类型 = 逻辑型 注释 = "0=默认,1=压缩HTML,2=格式化HTML。">
    变量 压缩JS <公开 类型 = 整数 注释 = "0=不压缩,1=完全压缩,2=压缩空格(保留换行符,删除注释),3=完全压缩+删除注释">
    变量 删除注释 <公开 类型 = 逻辑型>

    #
}

类 模板标签
{
    变量 标签类型 <公开 类型 = 整数 注释 = "标签类型_">
    变量 HTML <公开 类型 = 文本型>
    变量 标签名 <公开 类型 = 文本型>
    变量 标签参数 <公开 类型 = 文本型>
    变量 标签结束索引 <公开 类型 = 整数 值 = -1 注释 = "{{#循环}}  {{/循环}}">
    变量 运算符 <公开 类型 = 整数 注释 = "#类型_判断 ->  判断类型标签的运算符,#运算符_">
    变量 判断文本 <公开 类型 = 文本型 注释 = "#类型_判断 ->  用于判断的文本">
    变量 判断整数 <公开 类型 = 整数 注释 = "#类型_判断 ->  用于判断的整数">
    变量 判断逻辑 <公开 类型 = 逻辑型 注释 = "#类型_判断 ->  用于判断的逻辑">
    变量 数据类型 <公开 类型 = 整数 注释 = "数据类型_">
    变量 函数 <公开 类型 = 函数解析>

    #
}

类 模板数组 <基础类 = 对象数组模板类 折叠 @模板实现类 = "模板标签">
{

    #
}

类 标签类型 <@常量类 = 整数>
{
    常量 HTML <公开 值 = 0 注释 = "<html>.....</html>">
    常量 标签 <公开 值 = 1 注释 = "{{测试}}">
    常量 循环1 <公开 值 = 2 注释 = "{{#测试}}">
    常量 判断 <公开 值 = 3 注释 = "{?测试}}">
    常量 结束标签 <公开 值 = 4 注释 = "{{/测试}}">
    常量 内嵌模板 <公开 值 = 5 注释 = "{{>/模板.html}}">
    常量 函数 <公开 值 = 6 注释 = "{{  格式化时间(发布时间,\"{年}-{月}-{日}\",真)  }}">

    #
    #
}

类 运算符 <折叠 @常量类 = 整数>
{
    常量 空 <公开 值 = 0 注释 = "判断标签是否存在">
    常量 大于 <公开 值 = 1>
    常量 小于 <公开 值 = 2>
    常量 等于 <公开 值 = 3>
    常量 不等于 <公开 值 = 4>
    常量 小于等于 <公开 值 = 5>
    常量 大于等于 <公开 值 = 6>
    常量 近似等于 <公开 值 = 7>

    #
}

类 数据类型 <折叠 @常量类 = 整数>
{
    常量 文本 <公开 值 = 1>
    常量 整数型 <公开 值 = 2>
    常量 逻辑 <公开 值 = 3>

    #
}

类 单个模板 <公开>
{
    变量 m_读写锁 <类型 = w_读写锁>
    变量 m_模板标签 <类型 = 模板数组>
    变量 m_UTF8 <类型 = 逻辑型>
    变量 m_文件路径 <类型 = 文本型>
    变量 m_文件时间 <类型 = 小数>
    变量 m_HTML设置 <类型 = HTML设置>
    变量 m_标签集合 <类型 = 文本到整数哈希表>

    方法 取错误页面 <公开 静态 类型 = 文本型 折叠>
    参数 网站标题 <类型 = 文本型>
    参数 错误信息 <类型 = 文本型>
    {
        变量 错误模板 <类型 = 文本型
                值 = "<!doctype html>\r\n<html>\r\n<head>\r\n<meta charset=\"UTF-8\">\r\n<title>{{标题}}</title>\r\n<style>\r\nhtml,body,div,h1,*{margin:0;padding:0;}\r\nbody{\r\n\tbackground-color:#fefefe;\r\n\tcolor:#333\r\n}\r\n.box{\r\n\twidth:580px;\r\n\tmargin:0 auto;\r\n}\r\nh1{\r\n\tfont-size:20px;\r\n\ttext-align:center; \r\n\tbackground:url(data:image/png;base64,iVBORw0KGgoAAAANSUhEUgAAAIAAAACACAQAAABpN6lAAAAACXBIWXMAAAsTAAALEwEAmpwYAAAABGdBTUEAALGOfPtRkwAAACBjSFJNAAB6JQAAgIMAAPn/AACA6QAAdTAAAOpgAAA6mAAAF2+SX8VGAAAH+UlEQVR42uyde3AV1RnAfyFpTCBpFIVoaxUpSQSEAezwMDUJRK11DOTDAjJTtPRBBx1bK/VRO7T1VdFCRx6j6DgjltaamcIJWBpICRg0MFjGio6BRGKp4iOUMtBEkiBJ+ocxzQ17957de3bv3WY//mDYc75zzve73549z4+Ubga2DCIEEAIIAYQAQgAhgBBACCAEEAIIAYQAQgAhgBBACCAEEAIIAYQAQgADR9L8q0pyGUM+BRRwIVlkk00mp2ilhRaaaaCBRupVs78AUrzfF5ChlFBKKQVa2RuooYaX1fH/AwCSgfAdrnHxonWxneeoVO2BBSBj+BHzODeuQk5QwSpVHzgAMo6l3GSog+1iAw+ptwIDQPJZRjkpRgvtRnGfeifpAUgGP+NezvHkp+rgMR413ycYBCDX8hRf9bTHOsRt6q9JCUDSeJQlhh3f+mVYzv3qTJIBkIup4Crfxi6vcrP6IIkAyNVs5AJfh2/HmK1eSZK5gJRR7bP5cAHb5MakACC3sJGMBMxhMlGyIOEAZBHr/JxO9ZvGPS8/SGgfILPYQGpC57KdzFabEwRACtmeEOePlDZK1Z4EAJBR7GVoUqxoHGeKOuRzHyDpVCSJ+TCUFyXd707wN0wieeRKHvP1FZCZbCLZpEz92ScAkkMDuZqZD7CTowxnBpc7fK+3cJghTKBE00ebKVAn3X1NncrDmuYf4YfqL33Gi09zkZbeaR5kuero0cvjaaZraOXyAHf64AEygX1a3/5/UKzej9AcSS0Xx9Rrp1xti9BLZR3f1qjxDJPcrBs57QTXaJl/mvJI80G9yzy6Ymr+ONJ8UJ0s5G9avrzG86+AfJNCrYxPqDfPfqh281wMvT3qGQu9M+gNeYvkWq894OdaubpYFSVlZQzN31o/VvupMdg+twCkRPP3fyPacoV6iw9t3+KtUdNe0qq5WAq99ID7NfO9bpP2d5u0g6o1atpeoz7qBoCMRPcNO+YyrdllWl+5Xi7xygNu0c6Z6jJtkEu9iM86CzwBIE4KHm6TNsx2MOOuTLc/lCMPKGSkdpmTbTB+zUbvcsmJmjZVu/Z8meoFgHIHZY6WvKhmnG/bljIj9Zd7AaDEkV/dFeX5T2LoLRHL9sg0rnZQ+3TjAORcJjoCsEgstknkOubE0JvAEgu9DJ51tj4gXzTtAUUOR4yD+FP/10DG8gcNzV/Lt/rppVPBGEe1p1JkGoDj8RUXUSdzJeXzzk/ms0tr+ySNP8ojktkH28vMdFy7g/ZqTYdlk4tGfDYp3sG/GEYpIxzptVDFYQYziWmuNlwrlZhdEMnHnVzG91zpZTOXeCTfqAdIKqdIJ0jSwWDVZa4PuDRg5sM5XGqyExxO8GSYSQBZAQSQbRJAdgABZA10DzAKICOAADJNAmgLIIA2kwBaAwigdaADaAk9wCCAowEEcNQkgH9yOmDmd/CeQQCqk3cDBqBJdyqkuyDSGDAADtqrtx5wwOWCSKR8yiEOUE89H9HS8yeNLIYwhGxGkMco8hitP4iJKgdNA6iLqzndvMk2tlKnrPqSEz1/1/asPqQzjRmUMpkvuK7xVf2sektiORx3eZ6siSd5QX3sXFGGcSvf17xqFymdDFX/MQoAZB9XOm7IVlZTpeI6jy9F/NRmu8RaXlNTTL8CsNMhgD0sie8Ia88XaBe7ZDKro2+3WcgOJzXoOnalo2HobRSaML8HwmtM5Q4HUzJHpxi1T4lJk+b26H7meHHBTcayWasFjcpRv6F/TnA9v9TItYV56pOoRgxiFOP4Cl8il8FkkM6ntNPOMT7iQw7ytjoV1Q/elilU2e4ufya/cwZW3wNG0hTbW5mjOi21x3MD32Ayg231u2ikhmq2W4OQ86hlXIxP7gj1nicAQKpjHJLZw/TPT3j20cplIQsc7u59wibWU332gFZGsM92i71K3eCRB4CUsNMm+QTj+x+OlIncw02uBzSHWcva/ieAZS4VNjpfV3WeAQCps7kdeKda2a/TWkb8N7tOsorHI0+P2XhirSpxWoGz8d0jNvPvtX2aOESeYD8mLrblsJSmfvfDfuWifWY8wMYHHlf39ua5it9zmeGv4FYW/m9ALfWMtsjziipyXrDTEf7tWPby9J4NlbuoNW4+XM9+uab3X82WM4Db3RTsEIB6g6csE4oBJE2eYYVHNwmHUyWLACTFcvt7jbsgC25ujDRabpfOYgsvxLmvH1vuZgVLeeCs565vjJi7M9TN+1yC9/IBX7Z4OlO95K44F7N8tZnVVih9MR9L81e6Nd/ttbm7bU99+y2vc497Zbc3R/PYy3lJYX4ibo6Ceocy2pPA/DbK4jE/juvzqo75dCXY/E7mq93xFRFH/ABVyWIS+V+UdLNYxX2HNc4YInIrzyYohMIZvqvWx19M3EFUZCYVCThD0sY8958+owBAithou0hhXpIpigyoXUxgt4/m72aiKfMNhdRURyhmhU8d33KK1RFzBZqMJXYdT3ocS6yJxaZjiRkMqqqquYKHPTtM0cFDXGHafC/iCRawjFnG4wlWcp/y5JSCNxElx/MLZhuC0M0GHgxQRMleCGO5g5vJiauQk7wYyJiivRAyERYyw1VU2RrWsTHAUWX7YDif6ZQyQ/MiSyM17GCn+rc/g4oU/2YzciFjKOiNLJ1FNpm00UIrLXxMIw00UO/mNElAACSnhNHlQwAhgBBACCAEEAIIAYQAQgAhgBDAgJT/DgDyxCJjaj0UmAAAAABJRU5ErkJggg==) no-repeat top center; \r\n\tpadding-top:160px;\r\n\tmargin-top:30%;\r\n\tfont-weight:normal;\r\n}\r\n\r\n</style>\r\n</head>\r\n\r\n<body>\r\n<div class=\"box\">\r\n<h1>{{提示信息}}</h1><br><br>\r\n<a href=\"javascript:history.go(-1);\">返回上一页</a>\r\n</div>\r\n</body>\r\n</html>">
        替换所有子文本 (错误模板, "{{提示信息}}", 错误信息)
        替换所有子文本 (错误模板, "{{标题}}", 网站标题)
        返回 (错误模板)

    }

    方法 置模板设置 <公开 折叠>
    参数 HTML设置 <类型 = HTML设置>
    {
        m_HTML设置 = HTML设置

    }

    方法 检测更新 <公开 类型 = 逻辑型 折叠>
    {
        变量 文件 <类型 = 文本型>
        文件 = m_文件路径
        如果 (文件 == "")
        {
            返回 (假)
        }
        如果 (取文件时间 (文件) != m_文件时间)
        {
            返回 (加载模板 (文件))
        }
        返回 (假)
    }

    方法 加载模板 <公开 类型 = 逻辑型>
    参数 模板文件路径 <类型 = 文本型>
    {
        m_文件路径 = 模板文件路径
        m_文件时间 = 取文件时间 (m_文件路径)
        如果 (为最小时间 (m_文件时间))
        {
            返回 (假)
        }
        变量 模板 <类型 = 文本型>
        模板 = w_字节集_探测编码到文本 (读入文件 (m_文件路径))


        返回 (解析模板 (模板))
    }

    方法 取文件路径 <公开 类型 = 文本型 折叠>
    {
        返回 (m_文件路径)
    }

    方法 标签是否存在 <公开 类型 = 逻辑型 折叠>
    参数 标签名 <类型 = 文本型 注释 = "\"网站标题\"">
    {
        返回 (m_标签集合.是否存在 (标签名))
    }

    方法 _取字符 <静态 类型 = 字符 注释 = "防止越界...." 折叠>
    参数 文本 <类型 = 文本型>
    参数 索引 <类型 = 整数>
    {
        如果 (索引 >= 0 && 索引 < 取文本长度 (文本))
        {
            返回 (取字符 (文本, 索引))
        }
        返回 ('\0')

    }

    方法 解析模板 <公开 类型 = 逻辑型 折叠>
    参数 模板 <类型 = 文本型>
    {
        变量 索引 <类型 = 整数>
        变量 解析中 <类型 = 逻辑型 注释 = "解析">
        变量 标签名 <类型 = 文本型>
        变量 标签 <类型 = 模板标签>
        变量 HTML <类型 = 文本型>
        变量 ""
        变量 上一字符 <类型 = 字符>
        变量 当前字符 <类型 = 字符>
        变量 下一字符 <类型 = 字符>
        变量 解析器 <类型 = w_网页解析器>
        变量 HTML模板 <类型 = 文本型>
        m_标签集合.清空 ()
        解析器.解析 (模板)

        // HTML模板 = 解析器.取回网页 (取反 (m_HTML设置.压缩HTML), m_HTML设置.压缩JS, m_HTML设置.压缩HTML)

        HTML模板 = 解析器.取回网页 (取反 (m_HTML设置.压缩HTML), 0, 真)





        m_读写锁.写 ()
        m_模板标签.删除所有成员 ()
        判断循环 (索引 < 取文本长度 (HTML模板))
        {
            上一字符 = _取字符 (HTML模板, 索引 - 1)
            当前字符 = _取字符 (HTML模板, 索引)
            下一字符 = _取字符 (HTML模板, 索引 + 1)
            索引 = 索引 + 1
            如果 (解析中 == 假 && 当前字符 == '{' && 下一字符 == '{' && 上一字符 != '\\')  // 必须={{   不能=\{{
            {
                如果 (取文本长度 (HTML) > 0)  // 如果是空的HTML,那就没有必要加进去了
                {
                    标签.标签类型 = (整数)标签类型.HTML
                    标签.HTML = HTML
                    标签.标签名 = ""
                    m_模板标签.加入成员 (标签)
                    清空文本 (HTML)

                }
                解析中 = 真  // 遇到{{
                索引 = 索引 + 1

            }
            否则 (解析中 == 真 && 当前字符 == '}' && 下一字符 == '}')  // 必须=}}
            {
                标签.标签类型 = (整数)标签类型.标签
                标签.HTML = ""
                标签.标签名 = 标签名
                m_模板标签.加入成员 (标签)
                清空文本 (标签名)

                索引 = 索引 + 1
                解析中 = 假
            }
            否则 (解析中 == 真)
            {
                加入字符 (标签名, 当前字符)
            }
            否则
            {
                加入字符 (HTML, 当前字符)

            }


        }
        如果 (取文本长度 (HTML) > 0)
        {
            标签.标签类型 = (整数)标签类型.HTML
            标签.HTML = HTML
            标签.标签名 = ""
            m_模板标签.加入成员 (标签)
        }


        变量 i <类型 = 整数>
        循环 (0, m_模板标签.取成员数 (), i)
        {
            如果 (m_模板标签.取成员 (i).标签类型 != (整数)标签类型.HTML)
            {
                m_模板标签.取成员 (i).标签名 = 删首尾空 (m_模板标签.取成员 (i).标签名)
                如果 (是否以字符开头 (m_模板标签.取成员 (i).标签名, '/'))  // 结束标签
                {
                    m_模板标签.取成员 (i).标签类型 = (整数)标签类型.结束标签
                    m_模板标签.取成员 (i).标签名 = 删首尾空 (取文本右边 (m_模板标签.取成员 (i).标签名, 取文本长度 (m_模板标签.取成员 (i).标签名) - 1))
                }
                否则 (是否以字符开头 (m_模板标签.取成员 (i).标签名, '#'))  // 循环
                {
                    m_模板标签.取成员 (i).标签类型 = (整数)标签类型.循环1
                    m_模板标签.取成员 (i).标签名 = 删首尾空 (取文本右边 (m_模板标签.取成员 (i).标签名, 取文本长度 (m_模板标签.取成员 (i).标签名) - 1))
                }
                否则 (是否以字符开头 (m_模板标签.取成员 (i).标签名, '?'))  // 判断
                {
                    m_模板标签.取成员 (i).标签类型 = (整数)标签类型.判断
                    m_模板标签.取成员 (i).标签名 = 删首尾空 (取文本右边 (m_模板标签.取成员 (i).标签名, 取文本长度 (m_模板标签.取成员 (i).标签名) - 1))
                    _解析运算符 (m_模板标签.取成员 (i))
                }
                否则 (是否以字符开头 (m_模板标签.取成员 (i).标签名, '>'))  // 内嵌模板
                {
                    m_模板标签.取成员 (i).标签类型 = (整数)标签类型.内嵌模板
                    m_模板标签.取成员 (i).标签名 = 删首尾空 (取文本右边 (m_模板标签.取成员 (i).标签名, 取文本长度 (m_模板标签.取成员 (i).标签名) - 1))
                }
                否则
                {

                    如果 (m_模板标签.取成员 (i).函数.解析 (m_模板标签.取成员 (i).标签名))  // 是脚本函数!
                    {
                        m_模板标签.取成员 (i).标签类型 = (整数)标签类型.函数
                        到循环尾  // 脚本函数,没有标签参数..
                    }
                }


                如果 (寻找字符 (m_模板标签.取成员 (i).标签名, ':') != -1)  // 有标签参数...
                {
                    变量 _标签 <类型 = 文本型>
                    变量 _参数 <类型 = 文本型>
                    如果 (分割左右文本 (m_模板标签.取成员 (i).标签名, ':', _标签, _参数))
                    {
                        m_模板标签.取成员 (i).标签名 = _标签
                        m_模板标签.取成员 (i).标签参数 = _参数
                    }

                }


            }

        }

        循环 (0, m_模板标签.取成员数 (), i)
        {
            如果 (m_模板标签.取成员 (i).标签类型 == (整数)标签类型.循环1 || m_模板标签.取成员 (i).标签类型 == (整数)标签类型.判断)
            {
                _置标签结束索引 (i, m_模板标签, m_模板标签.取成员 (i).标签类型, m_模板标签.取成员 (i).标签名)
            }


            如果 (m_模板标签.取成员 (i).标签名 != "")
            {
                如果 (m_标签集合.是否存在 (m_模板标签.取成员 (i).标签名) == 假)  // 不存在,才插入
                {
                    m_标签集合.插入 (m_模板标签.取成员 (i).标签名, i)
                }



            }
            // 调试输出 (m_模板标签.取成员 (i).标签名, m_模板标签.取成员 (i).运算符, m_模板标签.取成员 (i).标签结束索引)
        }


        m_读写锁.结束写 ()


        //.如果 (解析.取网页编码 () = “UTF-8”)  ' 网页编码
        // UTF8 = 真
        //.否则
        // UTF8 = 假
        //.如果结束



        // 读写锁_写 (m_读写锁)  ' 给B上锁
        // m_UTF8 = UTF8
        // 指针_交换数组 (模板标签, m_模板标签)  ' 用交换的方式.让用户感觉不到任何热更新..虽然不这样做也感觉不到...但是,强迫症你懂的
        // 读写锁_结束写 (m_读写锁)



        返回 (真)
    }

    方法 取模板标签参数 <公开 类型 = 文本型>
    参数 标签名 <类型 = 文本型>
    {
        m_读写锁.读 ()
        如果 (m_标签集合.是否存在 (标签名) == 假)  // 不存在的标签!
        {
            m_读写锁.结束读 ()
            返回 ("")
        }
        变量 索引 <类型 = 整数>
        变量 返回值 <类型 = 文本型>
        索引 = m_标签集合.取值 (标签名)
        如果 (m_模板标签.索引是否有效 (索引))
        {
            返回值 = m_模板标签.取成员 (索引).标签参数
        }
        m_读写锁.结束读 ()
        返回 (返回值)





    }

    方法 _置标签结束索引 <静态 类型 = 逻辑型>
    参数 索引 <类型 = 整数>
    参数 标签数组 <类型 = 模板数组>
    参数 标签类型 <类型 = 整数 注释 = "循环 || 判断">
    参数 标签名 <类型 = 文本型>
    {
        如果 (标签数组.索引是否有效 (索引) == 假)
        {
            返回 (假)
        }
        如果 (标签类型 == (整数)标签类型.HTML)
        {
            返回 (假)
        }
        如果 (标签类型 == (整数)标签类型.结束标签)
        {
            返回 (假)
        }
        变量 i <类型 = 整数>
        变量 嵌套数量 <类型 = 整数>
        循环 (索引 + 1, 标签数组.取成员数 (), i)
        {
            如果 (标签数组.取成员 (i).标签类型 == 标签类型 && 标签数组.取成员 (i).标签名 == 标签名)
            {
                嵌套数量 = 嵌套数量 + 1
            }
            否则 (标签数组.取成员 (i).标签类型 == (整数)标签类型.结束标签 && 标签数组.取成员 (i).标签名 == 标签名)
            {
                嵌套数量 = 嵌套数量 - 1
                如果 (嵌套数量 <= 0)
                {
                    标签数组.取成员 (索引).标签结束索引 = i
                    返回 (真)
                }

            }

        }

        返回 (假)

    }

    方法 _解析运算符 <静态>
    参数 模板标签 <类型 = 模板标签>
    {
        模板标签.标签类型 = (整数)标签类型.判断
        模板标签.运算符 = (整数)运算符.空
        如果 (_________解析运算符 (模板标签, "?=", (整数)运算符.近似等于))
        {

        }
        否则 (_________解析运算符 (模板标签, "!=", (整数)运算符.不等于))
        {

        }
        否则 (_________解析运算符 (模板标签, ">=", (整数)运算符.大于等于))
        {

        }
        否则 (_________解析运算符 (模板标签, "<=", (整数)运算符.小于等于))
        {

        }
        否则 (_________解析运算符 (模板标签, ">", (整数)运算符.大于))
        {

        }
        否则 (_________解析运算符 (模板标签, "<", (整数)运算符.小于))
        {

        }
        否则 (_________解析运算符 (模板标签, "=", (整数)运算符.等于))
        {

        }
        否则
        {
            // w_信息框 ("出错???")


        }


    }

    方法 _________解析运算符 <静态 类型 = 逻辑型 折叠>
    参数 模板标签 <类型 = 模板标签>
    参数 运算符文本 <类型 = 文本型>
    参数 运算符 <类型 = 整数>
    {
        变量 位置1 <类型 = 整数>
        变量 标签名 <类型 = 文本型>
        位置1 = 寻找文本 (模板标签.标签名, 运算符文本, , 假)
        如果 (位置1 == -1)
        {
            返回 (假)
        }
        标签名 = 取文本左边 (模板标签.标签名, 位置1)
        模板标签.判断文本 = 取文本右边 (模板标签.标签名, 取文本长度 (模板标签.标签名) - 位置1 - 取文本长度 (运算符文本))
        模板标签.判断文本 = 删首尾空 (模板标签.判断文本)
        模板标签.判断整数 = 文本到整数 (模板标签.判断文本)
        模板标签.运算符 = 运算符
        模板标签.标签名 = 标签名

        如果 (模板标签.判断文本 != "")
        {
            如果 (是否以字符开头 (模板标签.判断文本, '"') || 是否以字符开头 (模板标签.判断文本, '\''))  // 字符串
            {
                模板标签.判断文本 = 取文本右边 (模板标签.判断文本, 取文本长度 (模板标签.判断文本) - 1)
                模板标签.判断文本 = 取文本左边 (模板标签.判断文本, 取文本长度 (模板标签.判断文本) - 1)
                模板标签.数据类型 = (整数)数据类型.文本

            }
            否则 (到文本 (文本到整数 (模板标签.判断文本)) == 模板标签.判断文本)  // 纯数字..
            {
                模板标签.数据类型 = (整数)数据类型.整数型
                模板标签.判断整数 = 文本到整数 (模板标签.判断文本)
            }
            否则 (模板标签.判断文本 == "真" || 模板标签.判断文本 == "假" || 文本比较 (模板标签.判断文本, "false", 假) == 0 || 文本比较 (模板标签.判断文本, "true", 假) == 0)
            {
                模板标签.判断逻辑 = 选择 (模板标签.判断文本 == "真" || 文本比较 (模板标签.判断文本, "true", 假) == 0, 真, 假)
                模板标签.数据类型 = (整数)数据类型.逻辑

            }
            否则
            {
                模板标签.数据类型 = (整数)数据类型.文本  // 不知道啥类型了..按文本处理吧
            }




        }
        返回 (真)


    }

    方法 渲染 <公开 类型 = 逻辑型>
    参数 父类 <类型 = 变整数>
    参数 返回模板 <类型 = 文本型>
    参数 文件路径 <类型 = 文本型>
    参数 渲染数据 <类型 = 哈希表>
    {
        返回 (渲染辅助.渲染 (父类, m_读写锁, m_模板标签, 文件路径, 渲染数据, 返回模板))




    }
}

类 渲染辅助
{
    方法 渲染 <公开 静态 类型 = 逻辑型>
    参数 父类 <类型 = 变整数>
    参数 读写锁 <类型 = w_读写锁>
    参数 模板标签 <类型 = 模板数组>
    参数 文件路径 <类型 = 文本型>
    参数 渲染数据 <类型 = 哈希表>
    参数 返回模板 <类型 = 文本型>
    {
        变量 i <类型 = 整数>
        读写锁.读 ()
        如果 (模板标签.取成员数 () == 0)
        {
            读写锁.结束读 ()
            返回 (假)
        }
        判断循环 (i < 模板标签.取成员数 ())
        {
            如果 (模板标签.取成员 (i).标签类型 == (整数)标签类型.标签)
            {
                添加渲染 (返回模板, 渲染数据, 模板标签.取成员 (i).标签名)
            }
            否则 (模板标签.取成员 (i).标签类型 == (整数)标签类型.函数)
            {

                处理模板函数 (返回模板, 模板标签.取成员 (i).函数, 读指针处对象 (父类, 模板引擎).m_注册函数, 渲染数据)
            }
            否则 (模板标签.取成员 (i).标签类型 == (整数)标签类型.HTML)
            {
                加入文本 (返回模板, 模板标签.取成员 (i).HTML)
            }
            否则 (模板标签.取成员 (i).标签类型 == (整数)标签类型.循环1)
            {
                变量 数组 <类型 = 数组类>
                数组 = 渲染数据.取数组 (模板标签.取成员 (i).标签名)
                渲染列表 (父类, 返回模板, 渲染数据, 数组, i, 模板标签)
                如果 (模板标签.取成员 (i).标签结束索引 == -1)  // 死循环
                {
                    置文本 (返回模板, 单个模板.取错误页面 ("标签错误", "没有找到<font size='5' face='arial' color='red'>{{" + 模板标签.取成员 (i).标签名 + "}}</font>的结束标签!<br>文件路径:" + 文件路径))
                    读写锁.结束读 ()
                    返回 (假)
                }
                i = 模板标签.取成员 (i).标签结束索引  // 循环完成.到循环尾
                到循环尾
            }
            否则 (模板标签.取成员 (i).标签类型 == (整数)标签类型.判断)  // {{? xxxx }}
            {
                如果 (判断标签 (返回模板, 渲染数据, i, 模板标签) == 假)
                {
                    如果 (模板标签.取成员 (i).标签结束索引 == -1)  // 死循环
                    {
                        置文本 (返回模板, 单个模板.取错误页面 ("标签错误", "没有找到<font size='5' face='arial' color='red'>{{" + 模板标签.取成员 (i).标签名 + "}}</font>的结束标签!<br>文件路径:" + 文件路径))
                        读写锁.结束读 ()
                        返回 (假)
                    }
                    i = 模板标签.取成员 (i).标签结束索引  // 条件没成立,跳过判断中间部分
                    到循环尾
                }

            }
            否则 (模板标签.取成员 (i).标签类型 == (整数)标签类型.内嵌模板)  // 这里,可能有点复杂了..
            {
                如果 (渲染子文件 (父类, 返回模板, 文件路径, 模板标签.取成员 (i).标签名, 渲染数据) == 假)
                {
                    置文本 (返回模板, 单个模板.取错误页面 ("渲染子模板失败", "没有找到<font size='5' face='arial' color='red'>{{" + 模板标签.取成员 (i).标签名 + "}}</font>子模板!<br>文件路径:" + 文件路径))
                    读写锁.结束读 ()
                    返回 (假)
                }


            }
            i = i + 1


        }

        读写锁.结束读 ()
        返回 (真)
    }

    方法 渲染子文件 <静态 类型 = 逻辑型 折叠>
    参数 父类 <类型 = 变整数>
    参数 返回模板 <类型 = 文本型>
    参数 父模板文件路径 <类型 = 文本型>
    参数 文件路径 <类型 = 文本型>
    参数 渲染数据 <类型 = 哈希表>
    {
        变量 执行状态 <类型 = 逻辑型>
        变量 渲染结果 <类型 = 文本型>
        变量 父目录 <类型 = 文本型>
        变量 文件 <类型 = 文本型>
        父目录 = w_文件_取目录 (父模板文件路径)
        文件 = 父目录 + 文件路径
        执行状态 = 读指针处对象 (父类, 模板引擎)._渲染子文件 (渲染结果, 文件, 渲染数据)
        如果 (执行状态)
        {
            加入文本 (返回模板, 渲染结果)
        }
        否则
        {
            置文本 (返回模板, 渲染结果)
        }
        返回 (执行状态)
    }

    方法 渲染列表 <静态 类型 = 逻辑型>
    参数 父类 <类型 = 变整数>
    参数 返回模板 <类型 = 文本型>
    参数 全局表 <类型 = 哈希表>
    参数 渲染列表 <类型 = 数组类>
    参数 标签索引 <类型 = 整数>
    参数 标签数组 <类型 = 模板数组>
    {
        变量 i <类型 = 整数>
        变量 n <类型 = 整数>
        变量 结束索引 <类型 = 整数>
        变量 局部表 <类型 = 哈希表>
        变量 列表 <类型 = 数组类>
        变量 跳到指定行 <类型 = 整数 值 = -1>
        结束索引 = 标签数组.取成员 (标签索引).标签结束索引
        如果 (结束索引 == -1)
        {
            返回 (假)
        }
        如果 (渲染列表.取数量 () <= 0)
        {
            返回 (假)
        }

        循环 (0, 渲染列表.取数量 (), i)
        {
            局部表 = 渲染列表.取哈希表 (i)
            循环 (标签索引 + 1, 结束索引, n, 1)  // 这里不知道要不要-1
            {
                如果 (跳到指定行 != -1)
                {
                    如果 (跳到指定行 == n)  // 到达了指定行
                    {
                        跳到指定行 = -1
                    }
                    // 调试输出 ("跳过")

                    到循环尾
                }
                // w_信息框 ("没有执行???")
                // 调试输出 ("没有执行吗", i, n)

                如果 (标签数组.取成员 (n).标签类型 == (整数)标签类型.标签)  // {{标签}}
                {
                    如果 (局部表.是否存在 (标签数组.取成员 (n).标签名))
                    {
                        添加渲染 (返回模板, 局部表, 标签数组.取成员 (n).标签名)
                    }
                    否则 (全局表.是否存在 (标签数组.取成员 (n).标签名))
                    {
                        添加渲染 (返回模板, 全局表, 标签数组.取成员 (n).标签名)
                    }


                }
                否则 (标签数组.取成员 (n).标签类型 == (整数)标签类型.函数)
                {
                    处理模板函数 (返回模板, 标签数组.取成员 (n).函数, 读指针处对象 (父类, 模板引擎).m_注册函数, 全局表, 局部表)

                }
                否则 (标签数组.取成员 (n).标签类型 == (整数)标签类型.HTML)
                {
                    加入文本 (返回模板, 标签数组.取成员 (n).HTML)
                }
                否则 (标签数组.取成员 (n).标签类型 == (整数)标签类型.循环1)  // {{#循环}}{{标签}}{{/循环}}
                {
                    如果 (局部表.是否存在 (标签数组.取成员 (n).标签名))
                    {
                        列表 = 局部表.取数组 (标签数组.取成员 (n).标签名)
                        渲染列表 (父类, 返回模板, 全局表, 列表, n, 标签数组)  // 递归
                    }
                    否则
                    {
                        渲染列表 (父类, 返回模板, 全局表, 渲染列表, n, 标签数组)  // 递归
                    }

                    跳到指定行 = 标签数组.取成员 (n).标签结束索引

                }
                否则 (标签数组.取成员 (n).标签类型 == (整数)标签类型.判断)
                {
                    如果 (局部表.是否存在 (标签数组.取成员 (n).标签名))
                    {
                        如果 (判断标签 (返回模板, 局部表, n, 标签数组) == 假)  // 查找局部表
                        {
                            跳到指定行 = 标签数组.取成员 (n).标签结束索引
                        }

                    }
                    否则
                    {
                        如果 (判断标签 (返回模板, 全局表, n, 标签数组) == 假)  // 查找全局表
                        {
                            跳到指定行 = 标签数组.取成员 (n).标签结束索引
                        }

                    }

                }
                否则
                {
                    // 跳过...

                }



            }


        }

        返回 (真)




    }

    方法 判断标签 <静态 类型 = 逻辑型 折叠>
    参数 返回模板 <类型 = 文本型>
    参数 渲染数据 <类型 = 哈希表>
    参数 标签索引 <类型 = 整数>
    参数 标签数组 <类型 = 模板数组>
    {
        变量 列表 <类型 = 数组类>
        变量 类型 <类型 = 整数>
        变量 结束索引 <类型 = 整数>
        变量 标签名 <类型 = 文本型>
        变量 运算符 <类型 = 整数>
        结束索引 = 标签数组.取成员 (标签索引).标签结束索引
        如果 (结束索引 < 0)
        {
            返回 (假)
        }
        标签名 = 标签数组.取成员 (标签索引).标签名
        类型 = 渲染数据.取类型 (标签名)
        运算符 = 标签数组.取成员 (标签索引).运算符
        如果 (运算符 == (整数)运算符.空)
        {
            返回 (渲染数据.是否存在 (标签名))
        }
        否则 (标签数组.取成员 (标签索引).运算符 == (整数)运算符.大于)
        {
            如果 (类型 == (整数)键值类型.数组类)
            {
                返回 (渲染数据.取数组 (标签名).取数量 () > 标签数组.取成员 (标签索引).判断整数)
            }
            否则
            {
                返回 (渲染数据.取整数 (标签名) > 标签数组.取成员 (标签索引).判断整数)
            }

        }
        否则 (运算符 == (整数)运算符.小于)
        {
            如果 (类型 == (整数)键值类型.数组类)
            {
                返回 (渲染数据.取数组 (标签名).取数量 () < 标签数组.取成员 (标签索引).判断整数)
            }
            否则
            {
                返回 (渲染数据.取整数 (标签名) < 标签数组.取成员 (标签索引).判断整数)
            }

        }
        否则 (运算符 == (整数)运算符.等于)
        {
            如果 (类型 == (整数)键值类型.数组类)
            {
                返回 (渲染数据.取数组 (标签名).取数量 () == 标签数组.取成员 (标签索引).判断整数)
            }
            否则 (类型 == (整数)数据类型.文本)
            {
                返回 (渲染数据.取文本 (标签名) == 标签数组.取成员 (标签索引).判断文本)
            }
            否则 (类型 == (整数)数据类型.整数型)
            {
                返回 (渲染数据.取整数 (标签名) == 标签数组.取成员 (标签索引).判断整数)
            }
            否则 (类型 == (整数)数据类型.逻辑)
            {
                返回 (渲染数据.取逻辑值 (标签名) == 标签数组.取成员 (标签索引).判断逻辑)
            }
            否则
            {
                返回 (渲染数据.取文本 (标签名) == 标签数组.取成员 (标签索引).判断文本)  // 不知道什么类型,按文本处理
            }


        }
        否则 (运算符 == (整数)运算符.不等于)
        {
            返回 (渲染数据.取文本 (标签名) != 标签数组.取成员 (标签索引).判断文本)
        }
        否则 (运算符 == (整数)运算符.小于等于)
        {
            如果 (类型 == (整数)键值类型.数组类)
            {
                返回 (渲染数据.取数组 (标签名).取数量 () <= 标签数组.取成员 (标签索引).判断整数)
            }
            否则
            {
                返回 (渲染数据.取整数 (标签名) <= 标签数组.取成员 (标签索引).判断整数)
            }



        }
        否则 (运算符 == (整数)运算符.大于等于)
        {
            如果 (类型 == (整数)键值类型.数组类)
            {
                返回 (渲染数据.取数组 (标签名).取数量 () >= 标签数组.取成员 (标签索引).判断整数)
            }
            否则
            {
                返回 (渲染数据.取整数 (标签名) >= 标签数组.取成员 (标签索引).判断整数)
            }


        }
        否则 (运算符 == (整数)运算符.近似等于)
        {
            返回 (近似等于 (渲染数据.取文本 (标签名), 标签数组.取成员 (标签索引).判断文本, 假))
        }

        返回 (假)
    }

    方法 添加渲染 <静态 折叠>
    参数 返回模板 <类型 = 文本型>
    参数 键值表 <类型 = 哈希表>
    参数 标签名 <类型 = 文本型>
    {
        加入文本 (返回模板, 键值表.取文本 (标签名))


    }

    方法 处理模板函数 <静态>
    参数 返回模板 <类型 = 文本型>
    参数 函数 <类型 = 函数解析>
    参数 注册函数 <类型 = 文本到变整数哈希表>
    参数 全局表 <类型 = 哈希表>
    参数 局部表 <类型 = 哈希表 @默认值 = 空对象>
    {
        变量 函数名称 <类型 = 文本型>
        变量 函数地址 <类型 = 变整数>
        变量 参数 <类型 = 文本型>
        变量 参数类型 <类型 = 整数>
        变量 参数数量 <类型 = 整数>
        变量 i <类型 = 整数>
        变量 参数值数组 <类型 = 文本数组类>
        变量 参数类型数组 <类型 = 整数数组类>
        函数名称 = 函数.取函数名称 ()
        如果 (注册函数.是否存在 (函数名称) == 假)  // 不存在的函数!
        {
            返回
        }
        函数地址 = 注册函数.取值 (函数名称)
        如果 (函数地址 == 0)
        {
            返回
        }

        参数数量 = 函数.取参数数量 ()
        循环 (0, 参数数量, i)
        {
            <> 参数 = 函数.取参数 (i)
            参数类型 = 函数.取参数类型 (i)  // -1=NULL,0=变量,1=整数,2=小数,3=逻辑,4=文本
            如果 (参数类型 == 0)  // 变量!
            {
                如果 (是否为空对象 (局部表) == 假 && 局部表.是否存在 (参数))
                {
                    <> 参数 = 局部表.取文本 (参数)
                }
                否则
                {
                    <> 参数 = 全局表.取文本 (参数)
                }
            }
            参数值数组.加入成员 (参数)
            参数类型数组.加入成员 (参数类型)

        }

        变量 数组 <类型 = "变整数 [3]">
        变量 文本指针 <类型 = 变整数>
        数组 [0] = 取变量地址 (参数值数组)
        数组 [1] = 取变量地址 (参数类型数组)
        文本指针 = __Call_1 (函数地址, 取数组变量地址 (数组))


        加入文本 (返回模板, 接收文本 (文本指针))


    }

    方法 __Call_1 <静态 类型 = 变整数 折叠 折叠2 "" @禁止流程检查 = 真>
    参数 函数地址 <类型 = 变整数>
    参数 参数1 <类型 = 变整数>
    {
        @ typedef  INT_PTR (WINAPI* hs) (INT_PTR);
        @ hs c=(hs)@<函数地址>;
        @ return c(@<参数1>);

    }

    #
}

类 模板函数 <公开>
{
    方法 格式化时间 <公开 静态 类型 = 变整数 折叠>
    参数 参数句柄 <类型 = 变整数>
    {
        变量 时间 <类型 = 小数>
        变量 时间模板 <类型 = 文本型>
        变量 补零 <类型 = 逻辑型>
        变量 返回值 <类型 = 文本型>
        时间 = 模板参数_取小数 (参数句柄, 0)  // 时间参数
        时间模板 = 模板参数_取文本 (参数句柄, 1)  // 时间模板
        补零 = 模板参数_取逻辑 (参数句柄, 2)  // 补零
        如果 (文本是否为空 (时间模板))
        {
            时间模板 = "{年}-{月}-{日} {时}:{分}:{秒}"
        }
        返回值 = w_时间_到文本 (时间, 时间模板, 补零)
        返回 (模板参数_返回文本 (返回值))
    }

    方法 取请求文件 <公开 静态 类型 = 变整数 折叠>
    参数 参数句柄 <类型 = 变整数>
    {
        变量 请求 <类型 = 请求类>
        返回 (模板参数_返回文本 (请求.取请求文件 (模板参数_取逻辑 (参数句柄, 0))))
    }

    方法 取请求域名 <公开 静态 类型 = 变整数 折叠>
    参数 参数句柄 <类型 = 变整数>
    {
        变量 请求 <类型 = 请求类>
        返回 (模板参数_返回文本 (请求.取请求域名 ()))
    }

    方法 取参数 <公开 静态 类型 = 变整数 折叠>
    参数 参数句柄 <类型 = 变整数>
    {
        变量 请求 <类型 = 请求类>
        变量 名称 <类型 = 文本型>
        名称 = 模板参数_取文本 (参数句柄, 0)
        返回 (模板参数_返回文本 (请求.取参数 (名称)))
    }

    方法 模板参数_取参数数量 <公开 静态 类型 = 整数 折叠>
    参数 参数句柄 <类型 = 变整数>
    {
        变量 参数指针 <类型 = 变整数>
        参数指针 = 读指针处值 (参数句柄, 变整数)
        返回 (读指针处对象 (参数指针, 文本数组类).取成员数 ())
    }

    方法 模板参数_取文本 <公开 静态 类型 = 文本型 折叠>
    参数 参数句柄 <类型 = 变整数>
    参数 索引 <类型 = 整数>
    {
        变量 参数指针 <类型 = 变整数>
        参数指针 = 读指针处值 (参数句柄, 变整数)
        如果 (读指针处对象 (参数指针, 文本数组类).索引是否有效 (索引))
        {
            返回 (读指针处对象 (参数指针, 文本数组类).取成员 (索引))
        }
        返回 ("")
    }

    方法 模板参数_取整数 <公开 静态 类型 = 整数 折叠>
    参数 参数句柄 <类型 = 变整数>
    参数 索引 <类型 = 整数>
    {
        变量 参数指针 <类型 = 变整数>
        参数指针 = 读指针处值 (参数句柄, 变整数)
        如果 (读指针处对象 (参数指针, 文本数组类).索引是否有效 (索引))
        {
            返回 (文本到整数 (读指针处对象 (参数指针, 文本数组类).取成员 (索引)))
        }
        返回 (0)
    }

    方法 模板参数_取小数 <公开 静态 类型 = 小数 折叠>
    参数 参数句柄 <类型 = 变整数>
    参数 索引 <类型 = 整数>
    {
        变量 参数指针 <类型 = 变整数>
        参数指针 = 读指针处值 (参数句柄, 变整数)
        如果 (读指针处对象 (参数指针, 文本数组类).索引是否有效 (索引))
        {
            返回 (文本到小数 (读指针处对象 (参数指针, 文本数组类).取成员 (索引)))
        }
        返回 (0)
    }

    方法 模板参数_取逻辑 <公开 静态 类型 = 逻辑型 折叠>
    参数 参数句柄 <类型 = 变整数>
    参数 索引 <类型 = 整数>
    {
        变量 参数指针 <类型 = 变整数>
        参数指针 = 读指针处值 (参数句柄, 变整数)
        如果 (读指针处对象 (参数指针, 文本数组类).索引是否有效 (索引))
        {
            返回 (文本到逻辑值 (读指针处对象 (参数指针, 文本数组类).取成员 (索引)))
        }
        返回 (假)
    }

    方法 模板参数_取参数类型 <公开 静态 类型 = 整数 折叠>
    参数 参数句柄 <类型 = 变整数>
    参数 索引 <类型 = 整数>
    {
        变量 参数指针 <类型 = 变整数>
        参数指针 = 读指针处值 (参数句柄 + 取变整数尺寸 (), 变整数)
        如果 (读指针处对象 (参数指针, 整数数组类).索引是否有效 (索引))
        {
            返回 (读指针处对象 (参数指针, 整数数组类).取成员 (索引))
        }
        返回 (-1)


    }

    方法 模板参数_返回文本 <公开 静态 类型 = 变整数 折叠>
    参数 要返回的文本 <类型 = 文本型>
    {
        返回 (返回文本 (要返回的文本))

    }
}

类 文本到变整数哈希表 <基础类 = 通用哈希表模板类 @模板实现类 = "文本型, 变整数">

类 模板引擎 <公开>
{
    变量 m_模板锁 <类型 = w_读写锁>
    变量 m_模板引擎 <类型 = 变整数数组类 注释 = " [单个模板]">
    变量 m_哈希锁 <类型 = w_读写锁>
    变量 m_模板索引 <类型 = 文本到整数哈希表>
    变量 m_HTML设置 <类型 = HTML设置>
    变量 m_模板目录 <类型 = 文本型>
    变量 m_注册函数 <公开 类型 = 文本到变整数哈希表 注释 = "m_脚本函数">

    方法 类_初始化
    {
        m_模板目录 = 取运行目录 ()
        本对象.注册模板函数 ("格式化时间", 取静态方法地址 (模板函数.格式化时间))  // 格式化时间(发布时间,"{年}-{月}-{日}",真)
        本对象.注册模板函数 ("请求.取请求域名", 取静态方法地址 (模板函数.取请求域名))  // 请求.取请求域名()
        本对象.注册模板函数 ("请求.取请求文件", 取静态方法地址 (模板函数.取请求文件))  // 请求.取请求文件()
        本对象.注册模板函数 ("请求.取参数", 取静态方法地址 (模板函数.取参数))  // 请求.取参数("id")


    }

    方法 类_清理 <折叠>
    {
        本对象.清理模板 ()
    }

    方法 清理模板 <公开 注释 = "清理已经加载的模板!" 折叠>
    {
        变量 i <类型 = 整数>
        m_哈希锁.写 ()  // 先让其它线程找不到...
        m_模板索引.清空 ()
        m_哈希锁.结束写 ()


        m_模板锁.写 ()  // 然后释放模板
        循环 (0, m_模板引擎.取成员数 (), i)
        {
            销毁对象指针 (m_模板引擎.取成员 (i))  // 销毁...
        }
        m_模板引擎.删除所有成员 ()

        m_模板锁.结束写 ()
    }

    方法 热更新 <公开 折叠>
    {
        变量 i <类型 = 整数 注释 = "检测模板是否发生了变化,如果变化了,会重新加载,请用一条时钟线程定时调用此方法">
        变量 成员数 <类型 = 整数>
        m_模板锁.读 ()
        成员数 = m_模板引擎.取成员数 ()
        m_模板锁.结束读 ()
        循环 (0, 成员数, i)
        {
            m_模板锁.读 ()
            读指针处对象 (m_模板引擎.取成员 (i), 单个模板).检测更新 ()
            m_模板锁.结束读 ()
            延时 (1)
        }
    }

    方法 设置 <公开 折叠>
    参数 模板目录 <类型 = 文本型 注释 = "\"D:\\模板目录\\\"">
    参数 压缩HTML <类型 = 逻辑型>
    参数 删除HTML注释 <类型 = 逻辑型>
    参数 压缩JS <类型 = 整数>
    {
        如果 (模板目录 != "")
        {
            m_模板目录 = 模板目录
            如果 (是否以字符结束 (m_模板目录, '\\') == 假)
            {
                加入字符 (m_模板目录, '\\')
            }
        }
        m_HTML设置.压缩HTML = 压缩HTML
        m_HTML设置.删除注释 = 删除HTML注释
        m_HTML设置.压缩JS = 压缩JS




    }

    方法 _查找模板 <类型 = 整数 返回值注释 = "如果不存在,则自动加载,如果加载失败,返回-1" 折叠>
    参数 文件 <类型 = 文本型>
    参数 返回模板路径 <类型 = 文本型>
    {
        变量 file <类型 = 文本型>
        变量 索引 <类型 = 整数>
        如果 (文本是否为空 (文件))
        {
            返回 (-1)
        }
        file = 到小写 (文件)
        如果 (寻找字符 (file, ':') == -1)  // 不是完整路径!
        {
            返回模板路径 = m_模板目录 + file
        }
        否则
        {
            返回模板路径 = file  // 完整路径..
        }

        m_哈希锁.读 ()
        如果 (m_模板索引.是否存在 (返回模板路径))
        {
            索引 = m_模板索引.取值 (返回模板路径)
        }
        否则
        {
            索引 = -1
        }
        m_哈希锁.结束读 ()
        如果 (索引 == -1)  // 这个模板文件.还没加载
        {
            索引 = _新建模板 (返回模板路径)
            如果 (索引 == -1)
            {
                返回 (-1)
            }
            m_哈希锁.写 ()
            m_模板索引.插入 (返回模板路径, 索引)
            m_哈希锁.结束写 ()
        }

        返回 (索引)


    }

    方法 标签是否存在 <公开 类型 = 逻辑型 折叠>
    参数 文件 <类型 = 文本型>
    参数 要判断的标签 <类型 = 文本型>
    {
        变量 索引 <类型 = 整数>
        变量 路径 <类型 = 文本型>
        索引 = _查找模板 (文件, 路径)
        如果 (索引 == -1)  // 模板不存在..
        {
            返回 (假)
        }
        返回 (读指针处对象 (m_模板引擎.取成员 (索引), 单个模板).标签是否存在 (要判断的标签))

    }

    方法 取模板标签参数 <公开 类型 = 文本型 折叠>
    参数 文件 <类型 = 文本型>
    参数 标签 <类型 = 文本型>
    {
        变量 索引 <类型 = 整数>
        变量 路径 <类型 = 文本型>
        索引 = _查找模板 (文件, 路径)
        如果 (索引 == -1)  // 模板不存在..
        {

            返回 ("")
        }
        返回 (读指针处对象 (m_模板引擎.取成员 (索引), 单个模板).取模板标签参数 (标签))


    }

    方法 渲染 <公开 类型 = 文本型 折叠>
    参数 文件 <类型 = 文本型>
    参数 渲染数据 <类型 = 哈希表>
    参数 返回渲染执行结果 <类型 = 变整数 注释 = "取变量地址(逻辑型变量)" @默认值 = 0>
    {
        变量 索引 <类型 = 整数>
        变量 路径 <类型 = 文本型>
        变量 渲染结果 <类型 = 文本型>
        变量 执行结果 <类型 = 逻辑型>
        变量 对象指针 <类型 = 变整数>
        索引 = _查找模板 (文件, 路径)
        如果 (索引 == -1)
        {
            如果 (返回渲染执行结果 != 0)
            {
                写指针处值 (返回渲染执行结果, 假)
            }
            返回 (单个模板.取错误页面 ("模板文件不存在", "模板文件不存在!!<br>文件路径:" + 路径))
        }

        m_模板锁.读 ()
        对象指针 = m_模板引擎.取成员 (索引)
        执行结果 = 读指针处对象 (对象指针, 单个模板).渲染 (本对象.取对象自身指针 (), 渲染结果, 路径, 渲染数据)
        m_模板锁.结束读 ()
        如果 (返回渲染执行结果 != 0)
        {
            写指针处值 (返回渲染执行结果, 执行结果)
        }

        返回 (渲染结果)






    }

    方法 _渲染子文件 <公开 类型 = 逻辑型 注释 = "!!!普通用户不要调用此方法!!!!切记,切记,切记.." 折叠>
    参数 返回模板 <类型 = 文本型>
    参数 模板路径 <类型 = 文本型>
    参数 渲染数据 <类型 = 哈希表>
    {
        变量 返回执行状态 <类型 = 逻辑型>
        变量 渲染结果 <类型 = 文本型>
        变量 文件 <类型 = 文本型>
        变量 文件名 <类型 = 文本型>
        文件名 = 模板路径
        判断循环 (是否以字符开头 (文件名, '/') || 是否以字符开头 (文件名, '\\'))
        {
            删除字符 (文件名, 0, 1)
        }

        如果 (寻找字符 (文件名, ':') == -1)
        {
            文件 = m_模板目录 + 文件名
        }
        否则
        {
            文件 = 文件名

        }



        m_模板锁.结束读 ()  // 先退出
        渲染结果 = 渲染 (文件, 渲染数据, 取变量地址 (返回执行状态))
        m_模板锁.读 ()
        如果 (返回执行状态)
        {
            加入文本 (返回模板, 渲染结果)
        }
        否则
        {
            置文本 (返回模板, 渲染结果)
        }
        返回 (返回执行状态)





    }

    方法 _新建模板 <类型 = 整数 注释 = "-1代表失败,模板文件不存在!" 折叠>
    参数 文件路径 <类型 = 文本型>
    {
        变量 对象指针 <类型 = 变整数>
        变量 索引 <类型 = 整数>
        对象指针 = 创建对象指针 (单个模板)
        如果 (对象指针 == 0)
        {
            返回 (-1)
        }
        读指针处对象 (对象指针, 单个模板).置模板设置 (m_HTML设置)
        如果 (读指针处对象 (对象指针, 单个模板).加载模板 (文件路径) == 假)
        {
            销毁对象指针 (对象指针)
            返回 (-1)
        }

        m_模板锁.写 ()
        索引 = m_模板引擎.加入成员 (对象指针)
        m_模板锁.结束写 ()
        返回 (索引)
    }

    方法 注册模板函数 <公开 类型 = 逻辑型>
    参数 函数名称 <类型 = 文本型>
    参数 函数地址 <类型 = 变整数>
    {
        如果 (函数解析.函数名是否合法 (函数名称) == 假)
        {
            返回 (假)
        }
        m_注册函数.插入 (函数名称, 函数地址)
        返回 (真)
    }

    方法 取消模板函数 <公开 类型 = 逻辑型>
    参数 函数名称 <类型 = 文本型>
    {
        如果 (函数解析.函数名是否合法 (函数名称) == 假)
        {
            返回 (假)
        }
        返回 (m_注册函数.删除 (函数名称))
    }

    方法 取注册函数 <公开 类型 = 变整数 折叠>
    参数 函数名称 <类型 = 文本型>
    {
        如果 (m_注册函数.是否存在 (函数名称))
        {
            返回 (m_注册函数.取值 (函数名称))
        }
        返回 (0)
    }
}

类 函数解析
{
    变量 m_函数名称 <类型 = 文本型>
    变量 m_参数 <类型 = 文本数组类>
    变量 m_参数类型 <类型 = 字节数组类 注释 = "-1=NULL,0=变量,1=整数,2=小数,3=逻辑,4=文本">
    变量 m_解析成功 <类型 = 逻辑型>

    方法 是否为函数 <公开 类型 = 逻辑型 折叠>
    {
        返回 (m_解析成功)
    }

    方法 取函数名称 <公开 类型 = 文本型 折叠>
    {
        返回 (m_函数名称)
    }

    方法 取参数数量 <公开 类型 = 整数 折叠>
    {
        返回 (m_参数.取成员数 ())
    }

    方法 取参数 <公开 类型 = 文本型>
    参数 参数索引 <类型 = 整数>
    {
        返回 (m_参数.取成员 (参数索引))
    }

    方法 取参数类型 <公开 类型 = 整数 返回值注释 = "-1=NULL,0=变量,1=整数,2=小数,3=逻辑,4=文本">
    参数 参数索引 <类型 = 整数>
    {
        返回 (m_参数类型.取成员 (参数索引))
    }

    方法 解析 <公开 类型 = 逻辑型 折叠>
    参数 脚本 <类型 = 文本型>
    {
        变量 索引 <类型 = 整数>
        变量 _脚本 <类型 = 文本型>
        清空文本 (m_函数名称)
        m_参数.删除所有成员 ()
        m_参数类型.删除所有成员 ()
        m_解析成功 = 假

        _脚本 = 脚本
        w_文本_删首尾空 (_脚本, 真)
        索引 = _置函数名称 (_脚本)
        如果 (索引 == -1)
        {
            返回 (假)
        }
        m_解析成功 = _解析参数 (索引, _脚本)
        返回 (m_解析成功)
    }

    方法 _解析参数 <类型 = 逻辑型 折叠>
    参数 索引 <类型 = 整数>
    参数 脚本 <类型 = 文本型>
    {
        变量 当前字符 <类型 = 字符>
        如果 (取字符 (脚本, 索引) != '(')
        {
            返回 (假)
        }
        索引 = 索引 + 1
        判断循环 (索引 < 取文本长度 (脚本))
        {
            索引 = __跳过空白 (索引, 脚本)
            如果 (索引 == -1)
            {
                返回 (假)
            }
            当前字符 = 取字符 (脚本, 索引)
            索引 = 索引 + 1

            如果 (当前字符 == ')')  // 结束啦...
            {
                返回 (真)
            }
            如果 (当前字符 == ',')
            {
                m_参数.加入成员 ("")
                m_参数类型.加入成员 (-1)  // 空对象
                到循环尾
            }
            如果 (当前字符 != '"' && 当前字符 != '\'')  // 变量 || 整数 || 逻辑型 || 函数
            {
                变量 参数 <类型 = 文本型>
                变量 解析完成 <类型 = 逻辑型>
                加入字符 (参数, 当前字符)
                判断循环 (索引 < 取文本长度 (脚本))
                {
                    当前字符 = 取字符 (脚本, 索引)
                    索引 = 索引 + 1
                    如果 (当前字符 == ',')
                    {
                        跳出循环
                    }
                    否则 (当前字符 == ')')
                    {
                        解析完成 = 真
                        跳出循环
                    }
                    加入字符 (参数, 当前字符)

                }
                w_文本_删首尾空 (参数, 真)
                m_参数.加入成员 (参数)
                如果 (w_文本_是否为整数 (参数))
                {
                    m_参数类型.加入成员 (1)
                }
                否则 (w_文本_是否为小数 (参数))
                {
                    m_参数类型.加入成员 (2)
                }
                否则 (w_文本_是否为逻辑 (参数))
                {
                    m_参数类型.加入成员 (3)
                }
                否则
                {
                    m_参数类型.加入成员 (0)  // 标签变量
                }

                如果 (解析完成)
                {
                    返回 (真)
                }
                到循环尾
            }
            否则 (当前字符 == '"' || 当前字符 == '\'')  // 引号
            {
                变量 参数 <类型 = 文本型>
                变量 引号 <类型 = 字符>
                变量 上个字符 <类型 = 字符>
                变量 字符串解析完成 <类型 = 逻辑型>
                引号 = 当前字符
                判断循环 (索引 < 取文本长度 (脚本))
                {
                    上个字符 = 当前字符
                    当前字符 = 取字符 (脚本, 索引)
                    索引 = 索引 + 1
                    如果 (上个字符 == '\\')
                    {
                        如果 (当前字符 == 引号 || 当前字符 == '\\')
                        {
                            // w_置字符 (参数, 取文本长度 (参数) - 1, 当前字符)  // 字符串还没有结束
                            删除字符 (参数, 取文本长度 (参数) - 1, 1)
                            加入字符 (参数, 当前字符)
                            上个字符 = 0
                            到循环尾
                        }
                    }
                    如果 (当前字符 == 引号)
                    {
                        字符串解析完成 = 真
                        跳出循环
                    }
                    加入字符 (参数, 当前字符)
                }
                如果 (字符串解析完成 == 假)  //    "我是字符串.....
                {
                    返回 (假)
                }
                w_文本_删首尾空 (参数, 真)
                m_参数.加入成员 (参数)
                m_参数类型.加入成员 (4)  // 文本型

                索引 = __跳过空白 (索引, 脚本)
                如果 (索引 == -1)
                {
                    返回 (假)
                }
                如果 (取字符 (脚本, 索引) == ',')  // 还有下一个参数
                {
                    索引 = 索引 + 1
                }


                到循环尾
            }


            返回 (假)
        }






        返回 (假)
    }

    方法 _置函数名称 <类型 = 整数 折叠>
    参数 脚本 <类型 = 文本型>
    {
        变量 索引 <类型 = 整数>
        变量 当前字符 <类型 = 字符>
        变量 函数名称 <类型 = 文本型>
        索引 = __跳过空白 (0, 脚本)
        如果 (索引 == -1)
        {
            返回 (-1)
        }
        判断循环 (索引 < 取文本长度 (脚本))
        {
            当前字符 = 取字符 (脚本, 索引)
            索引 = 索引 + 1
            如果 (当前字符 == '(')  // 函数开始啦!
            {
                w_文本_删首尾空 (函数名称, 真)
                如果 (函数名是否合法 (函数名称) == 假)
                {
                    返回 (-1)
                }
                否则
                {
                    m_函数名称 = 函数名称
                    返回 (索引 - 1)
                }
            }
            加入字符 (函数名称, 当前字符)
        }



        返回 (-1)
    }

    方法 函数名是否合法 <公开 静态 类型 = 逻辑型>
    参数 函数名称 <类型 = 文本型>
    {
        如果 (文本是否为空 (函数名称))
        {
            返回 (假)
        }
        变量 当前字符 <类型 = 字符>
        变量 i <类型 = 整数>
        当前字符 = 取字符 (函数名称, 0)
        如果 (w_字符_是否为汉字 (当前字符) == 假 && w_字符_是否为英文字母 (当前字符) == 假 && 当前字符 != '_')  // 不合法的开头!
        {
            返回 (假)
        }
        循环 (0, 取文本长度 (函数名称), i)
        {
            当前字符 = 取字符 (函数名称, i)
            如果 (w_字符_是否为汉字 (当前字符) == 假 && w_字符_是否为英文字母 (当前字符) == 假 && w_字符_是否为数字 (当前字符) == 假 && 当前字符 != '_' && 当前字符 != '.')
            {
                返回 (假)
            }
        }

        返回 (真)

    }

    方法 __跳过空白 <静态 类型 = 整数 折叠>
    参数 索引 <类型 = 整数>
    参数 脚本 <类型 = 文本型>
    {
        变量 当前字符 <类型 = 字符>
        判断循环 (索引 < 取文本长度 (脚本))
        {
            当前字符 = 取字符 (脚本, 索引)
            索引 = 索引 + 1
            如果 (当前字符 == '\r' || 当前字符 == '\n' || 当前字符 == '\t' || 当前字符 == ' ')  // 无字符..
            {
                到循环尾
            }
            索引 = 索引 - 1
            如果 (索引 < 取文本长度 (脚本))
            {
                返回 (索引)
            }
            返回 (-1)
        }
        返回 (-1)
    }
}

【分隔符】


###### 以下为一个火山软件开发平台系统类库程序的内容

<火山程序 类型 = "通常" 版本 = 1 />

包 蜗牛服务器.缓存

类 缓存数据
{
    变量 m_互斥锁 <公开 类型 = 互斥锁类>
    变量 m_哈希数组 <类型 = 整数数组类>
    变量 m_主键数组 <公开 类型 = 文本数组类>
    变量 m_内容数组 <类型 = 文本数组类>
    变量 m_使用时间数组 <类型 = 整数数组类>
    变量 m_过期时间数组 <类型 = 整数数组类>

    方法 清理事件 <公开 定义事件 类型 = 整数>
    参数 清理主键 <类型 = 文本型>
    参数 清理内容 <类型 = 文本型>
    {

    }

    方法 清理超时缓存 <公开>
    {
        __清理超时缓存 (本对象, m_互斥锁, m_哈希数组, m_主键数组, m_内容数组, m_使用时间数组, m_过期时间数组)
    }

    方法 __清理超时缓存 <静态>
    参数 缓存数据对象 <类型 = 缓存数据>
    参数 互斥锁 <类型 = 互斥锁类>
    参数 哈希数组 <类型 = 整数数组类>
    参数 主键数组 <类型 = 文本数组类>
    参数 内容数组 <类型 = 文本数组类>
    参数 使用时间数组 <类型 = 整数数组类>
    参数 过期时间数组 <类型 = 整数数组类>
    {
        变量 成员数 <类型 = 整数>
        变量 i <类型 = 整数>
        变量 经历秒数 <类型 = 整数>
        变量 清理主键数组 <类型 = 文本数组类>
        变量 清理内容数组 <类型 = 文本数组类>
        经历秒数 = 取经历秒数 ()
        互斥锁.加锁 ()
        成员数 = 使用时间数组.取成员数 ()
        循环 (成员数 - 1, -1, i, -1)
        {
            如果 (经历秒数 - 使用时间数组.取成员 (i) > 过期时间数组.取成员 (i))  // 超时啦...
            {
                变量 被清理的主键 <类型 = 文本型>
                变量 被清理的内容 <类型 = 文本型>
                被清理的主键 = 主键数组.取成员 (i)
                被清理的内容 = 内容数组.取成员 (i)
                清理主键数组.加入成员 (被清理的主键)
                清理内容数组.加入成员 (被清理的内容)
                哈希数组.删除成员 (i)
                主键数组.删除成员 (i)
                内容数组.删除成员 (i)
                使用时间数组.删除成员 (i)
            }
        }

        互斥锁.解锁 ()
        循环 (0, 清理主键数组.取成员数 (), i)
        {
            缓存数据对象.清理事件 (清理主键数组.取成员 (i), 清理内容数组.取成员 (i))
        }


    }

    方法 是否存在 <公开 类型 = 逻辑型>
    参数 主键 <类型 = 文本型>
    {
        变量 索引 <类型 = 整数>
        变量 哈希值 <类型 = 整数>
        哈希值 = 取文本哈希值 (主键)
        m_互斥锁.加锁 ()
        索引 = __取索引 (哈希值, 主键, m_哈希数组, m_主键数组, m_使用时间数组, 假)

        m_互斥锁.解锁 ()
        返回 (索引 != -1)
    }

    方法 计次加 <公开 类型 = 整数>
    参数 主键 <类型 = 文本型>
    参数 过期时间 <类型 = 整数>
    {
        变量 索引 <类型 = 整数>
        变量 哈希值 <类型 = 整数>
        变量 返回值 <类型 = 整数>
        哈希值 = 取文本哈希值 (主键)
        m_互斥锁.加锁 ()
        索引 = __取索引 (哈希值, 主键, m_哈希数组, m_主键数组, m_使用时间数组, 假)
        如果 (索引 == -1)  // 不存在的主键!
        {
            m_哈希数组.加入成员 (哈希值)
            m_主键数组.加入成员 (主键)
            m_内容数组.加入成员 ("1")
            m_使用时间数组.加入成员 (取经历秒数 ())
            m_过期时间数组.加入成员 (过期时间)
            返回 (1)
        }
        返回值 = 文本到整数 (m_内容数组.取成员 (索引)) + 1
        m_内容数组.置成员值 (索引, 到文本 (返回值))
        m_互斥锁.解锁 ()
        返回 (返回值)

    }

    方法 置文本 <公开 类型 = 逻辑型>
    参数 主键 <类型 = 文本型>
    参数 文本 <类型 = 文本型>
    参数 过期时间 <类型 = 整数 注释 = "单位:秒..">
    {
        变量 哈希值 <类型 = 整数>
        变量 索引 <类型 = 整数>
        变量 秒数 <类型 = 整数>
        哈希值 = 取文本哈希值 (主键)
        秒数 = 取经历秒数 ()

        m_互斥锁.加锁 ()
        索引 = __取索引 (哈希值, 主键, m_哈希数组, m_主键数组, m_使用时间数组, 真)
        如果 (索引 == -1)  // 不存在,直接加入...
        {
            m_哈希数组.加入成员 (哈希值)
            m_主键数组.加入成员 (主键)
            m_内容数组.加入成员 (文本)
            m_使用时间数组.加入成员 (秒数)
            m_过期时间数组.加入成员 (过期时间)
            m_互斥锁.解锁 ()
            返回 (真)
        }
        m_内容数组.置成员值 (索引, 文本)
        m_使用时间数组.置成员值 (索引, 秒数)
        m_过期时间数组.置成员值 (索引, 过期时间)
        m_互斥锁.解锁 ()
        返回 (真)

    }

    方法 取文本 <公开 类型 = 文本型>
    参数 主键 <类型 = 文本型>
    参数 是否续期 <类型 = 逻辑型>
    {
        变量 哈希值 <类型 = 整数>
        变量 返回值 <类型 = 文本型>
        变量 索引 <类型 = 整数>
        哈希值 = 取文本哈希值 (主键)
        m_互斥锁.加锁 ()
        索引 = __取索引 (哈希值, 主键, m_哈希数组, m_主键数组, m_使用时间数组, 是否续期)
        如果 (索引 == -1)
        {
            m_互斥锁.解锁 ()
            返回 ("")
        }
        返回值 = m_内容数组.取成员 (索引)
        m_互斥锁.解锁 ()
        返回 (返回值)
    }

    方法 __取索引 <静态 类型 = 整数 注释 = "-1 代表不存在!">
    参数 哈希值 <类型 = 整数>
    参数 主键 <类型 = 文本型>
    参数 哈希数组 <类型 = 整数数组类>
    参数 主键数组 <类型 = 文本数组类>
    参数 使用时间数组 <类型 = 整数数组类>
    参数 是否续期 <类型 = 逻辑型>
    {
        变量 成员数 <类型 = 整数>
        变量 i <类型 = 整数>
        成员数 = 哈希数组.取成员数 ()
        循环 (0, 成员数, i)
        {
            如果 (哈希数组.取成员 (i) == 哈希值 && 主键数组.取成员 (i) == 主键)
            {
                如果 (是否续期)
                {
                    使用时间数组.置成员值 (i, 取经历秒数 ())
                }
                返回 (i)
            }

        }
        返回 (-1)
    }

    #
}

类 w_缓存对象 <公开 注释 = "多线程安全对象">
{
    变量 m_缓存数据 <类型 = "缓存数据 [2000]">
    变量 m_上次清理时间 <类型 = "原子整数类 [2000]" 注释 = "记录 秒">

    方法 _清理超时缓存
    参数 索引 <类型 = 整数>
    {
        变量 经历秒数 <类型 = 整数>
        经历秒数 = 取经历秒数 ()
        如果 (m_上次清理时间 [索引].取值 () == 经历秒数)  // 这一秒,已经清理了...下次再来了...
        {
            返回
        }
        m_上次清理时间 [索引].置值 (经历秒数)
        m_缓存数据 [索引].清理超时缓存 ()

    }

    方法 主动清理超时缓存 <公开 注释 = "一般情况下不需要调用此方法...">
    {
        变量 i <类型 = 整数>
        循环 (0, 取数组成员数 (m_缓存数据), i)
        {
            _清理超时缓存 (i)
        }
    }

    方法 取所有主键 <公开 类型 = 整数 返回值注释 = "返回成员数">
    参数 返回主键数组 <类型 = 文本数组类>
    {
        变量 i <类型 = 整数>
        返回主键数组.删除所有成员 ()
        循环 (0, 取数组成员数 (m_缓存数据), i)
        {
            _清理超时缓存 (i)  // 先清理超时的缓存!
            m_缓存数据 [i].m_互斥锁.加锁 ()
            返回主键数组.添加数组 (m_缓存数据 [i].m_主键数组)
            m_缓存数据 [i].m_互斥锁.解锁 ()
        }
        返回 (返回主键数组.取成员数 ())
    }

    方法 是否存在 <公开 类型 = 逻辑型>
    参数 主键 <类型 = 文本型>
    {
        变量 索引 <类型 = 整数>
        索引 = w_文本_取索引 (主键, 取数组成员数 (m_上次清理时间))
        _清理超时缓存 (索引)
        返回 (m_缓存数据 [索引].是否存在 (主键))
    }

    方法 置文本 <公开 类型 = 逻辑型 返回值注释 = "直接返回真...">
    参数 主键 <类型 = 文本型>
    参数 文本内容 <类型 = 文本型>
    参数 过期时间 <类型 = 整数 注释 = "单位:秒">
    {
        变量 索引 <类型 = 整数>
        索引 = w_文本_取索引 (主键, 取数组成员数 (m_上次清理时间))
        _清理超时缓存 (索引)
        返回 (m_缓存数据 [索引].置文本 (主键, 文本内容, 过期时间))
    }

    方法 取文本 <公开 类型 = 文本型>
    参数 主键 <类型 = 文本型>
    参数 当前主键缓存是否续期 <类型 = 逻辑型>
    {
        变量 索引 <类型 = 整数>
        索引 = w_文本_取索引 (主键, 取数组成员数 (m_上次清理时间))
        _清理超时缓存 (索引)
        返回 (m_缓存数据 [索引].取文本 (主键, 当前主键缓存是否续期))
    }

    方法 清理缓存事件 <公开 定义事件 类型 = 整数 注释 = "清理后触发,所以,您如果判断\"被清理的主键\"是否存在,会返回假!">
    参数 被清理的主键 <类型 = 文本型>
    参数 被清理的内容 <类型 = 文本型>
    {


    }

    方法 缓存数据_清理事件 <接收事件 类型 = 整数>
    参数 来源对象 <类型 = 缓存数据 注释 = "提供事件产生的具体来源对象">
    参数 标记值 <类型 = 整数 注释 = "用户调用\"挂接事件\"命令时所提供的\"标记值\"参数值,非此方式挂接事件则本参数值固定为0.">
    参数 清理主键 <类型 = 文本型>
    参数 清理内容 <类型 = 文本型>
    {
        本对象.清理缓存事件 (清理主键, 清理内容)
        返回 (0)
    }
}

【分隔符】


###### 以下为一个火山软件开发平台系统类库程序的内容

<火山程序 类型 = "通常" 版本 = 1 />

包 蜗牛服务器.请求响应

类 Cookie <公开>
{
    变量 名称 <公开 类型 = 文本型>
    变量 值 <公开 类型 = 文本型>
    变量 域名 <公开 类型 = 文本型 注释 = "COOKIE对应的域名。不设置则为当前域名。">
    变量 目录 <公开 类型 = 文本型 注释 = "对应网站中的路径。不设置则设置COOKIE到默认路径下。">
    变量 过期时间 <公开 类型 = 整数 注释 = "举例:\"增减时间(取现行时间(),#日,30)\" //30天后删除">
}

类 Cookie数组类 <公开 基础类 = 对象数组模板类 @模板实现类 = "Cookie">
{

    #
}

类 请求信息 <公开>
{
    变量 用户IP <公开 类型 = 文本型>
    变量 用户端口 <公开 类型 = 整数>
    变量 请求参数 <公开 类型 = 哈希表 注释 = "/{栏目}/{帖子}.html">
    变量 请求文件 <公开 类型 = 文本型>
    变量 查询参数 <公开 类型 = 文本型>
    变量 请求域名 <公开 类型 = 文本型>
    变量 请求头 <公开 类型 = HP请求头数组类>
    变量 请求方法 <公开 类型 = 文本型 注释 = "\"GET\"/\"POST\"/\"HEAD\" 等等...">
    变量 POST数据 <公开 类型 = 字节集类 注释 = "未完成">
    变量 网站指针 <公开 类型 = 变整数 注释 = "\"网站\"   指针">
    变量 服务器 <公开 类型 = 变整数 注释 = "\"服务器\" 指针">
    变量 连接ID <公开 类型 = 变整数>
    变量 订阅路径 <公开 类型 = 文本型>
    变量 用户文本参数 <公开 类型 = 文本型>
    变量 用户数值参数 <公开 类型 = 变整数>
}

类 响应信息 <公开 折叠>
{
    变量 连接ID <公开 类型 = 变整数>
    变量 响应数据 <公开 类型 = 字节集类>
    变量 状态码 <公开 类型 = 短整数 值 = 200>
    变量 网站编码 <公开 类型 = 网站编码>
    变量 响应头 <公开 类型 = HP请求头数组类>
    变量 Cookie <公开 类型 = Cookie数组类>
    变量 文档类型 <公开 类型 = 文本型 值 = "html" 注释 = "html/js">
    变量 网站指针 <公开 类型 = 变整数 注释 = "读指针对对象(网站指针,网站)">

    #
}

类 请求类 <公开>
{
    变量 m_请求指针 <类型 = 变整数 注释 = "[请求信息]">

    方法 类_初始化 <折叠>
    {
        m_请求指针 = 请求信息_取指针 ()
    }

    方法 取模板目录 <公开 类型 = 文本型 返回值注释 = "返回数据地址" 折叠>
    {
        如果 (m_请求指针 != 0)
        {
            变量 网站指针 <类型 = 变整数>
            网站指针 = 读指针处对象 (m_请求指针, 请求信息).网站指针
            如果 (网站指针 != 0)
            {
                返回 (读指针处对象 (网站指针, 网站).模板目录)
            }
        }
        返回 ("")
    }

    方法 取静态文件目录 <公开 类型 = 文本型 折叠>
    {
        如果 (m_请求指针 != 0)
        {
            变量 网站指针 <类型 = 变整数>
            网站指针 = 读指针处对象 (m_请求指针, 请求信息).网站指针
            如果 (网站指针 != 0)
            {
                返回 (读指针处对象 (网站指针, 网站).静态文件目录)
            }
        }
        返回 ("")
    }

    方法 取参数 <公开 类型 = 文本型 折叠>
    参数 名称 <类型 = 文本型>
    {
        如果 (m_请求指针 != 0)
        {
            如果 (读指针处对象 (m_请求指针, 请求信息).请求参数.是否存在 (名称))
            {
                返回 (读指针处对象 (m_请求指针, 请求信息).请求参数.取文本 (名称))
            }
        }
        返回 ("")

    }

    方法 取请求文件 <公开 类型 = 文本型 折叠>
    参数 是否包含参数 <类型 = 逻辑型 @默认值 = 假>
    {
        如果 (m_请求指针 != 0)
        {
            如果 (是否包含参数 && 文本是否为空 (读指针处对象 (m_请求指针, 请求信息).查询参数) == 假)
            {
                返回 (读指针处对象 (m_请求指针, 请求信息).请求文件 + "?" + 读指针处对象 (m_请求指针, 请求信息).查询参数)
            }
            返回 (读指针处对象 (m_请求指针, 请求信息).请求文件)
        }
        返回 ("")
    }

    方法 取请求域名 <公开 类型 = 文本型 折叠>
    {
        如果 (m_请求指针 != 0)
        {
            返回 (读指针处对象 (m_请求指针, 请求信息).请求域名)
        }
        返回 ("")
    }

    方法 取请求头 <公开 类型 = 文本型 折叠>
    参数 名称 <类型 = 文本型>
    {
        返回 (请求信息_取请求头值 (m_请求指针, 名称))
    }

    方法 取页面标记 <公开 类型 = 文本型 折叠>
    {
        变量 页面标记 <类型 = 文本型>
        页面标记 = 请求信息_取请求头值 (m_请求指针, "If-None-Match")
        如果 (取文本长度 (页面标记) > 0)
        {
            如果 (取字符 (页面标记, 取文本长度 (页面标记) - 1) == '"')
            {
                删除字符 (页面标记, 取文本长度 (页面标记) - 1, 1)  // 删除尾部的引号...
            }
            如果 (取文本长度 (页面标记) > 0)
            {
                如果 (取字符 (页面标记, 0) == '"')
                {
                    删除字符 (页面标记, 0, 1)  // 删除前面的引号...
                }
            }
        }


        返回 (页面标记)

    }

    方法 取用户IP <公开 类型 = 文本型 折叠>
    {
        如果 (m_请求指针 != 0)
        {
            返回 (读指针处对象 (m_请求指针, 请求信息).用户IP)
        }
        返回 ("")

    }

    方法 取绑定域名 <公开 类型 = 文本型 折叠>
    {
        如果 (m_请求指针 != 0)
        {
            变量 网站指针 <类型 = 变整数>
            网站指针 = 读指针处对象 (m_请求指针, 请求信息).网站指针
            如果 (网站指针 != 0)
            {
                返回 (读指针处对象 (网站指针, 网站).域名)
            }
        }
        返回 ("")

    }

    方法 取请求方法 <公开 类型 = 文本型 折叠>
    {
        如果 (m_请求指针 != 0)
        {
            返回 (读指针处对象 (m_请求指针, 请求信息).请求方法)
        }
        返回 ("")

    }

    方法 取用户端口 <公开 类型 = 整数 折叠>
    {
        如果 (m_请求指针 != 0)
        {
            返回 (读指针处对象 (m_请求指针, 请求信息).用户端口)
        }
        返回 (0)
    }

    方法 取POST原始数据 <公开 类型 = 字节集类 折叠>
    {
        返回 (读指针处对象 (m_请求指针, 请求信息).POST数据)

    }

    方法 取所有参数 <公开 类型 = 文本型 折叠>
    参数 是否格式化 <类型 = 逻辑型>
    {
        返回 (读指针处对象 (m_请求指针, 请求信息).请求参数.到JSON (是否格式化))
    }

    方法 取订阅文本参数 <公开 类型 = 文本型 注释 = "订阅视图的时候,提供的是啥文本,返回的就是啥文本!">
    {
        返回 (读指针处对象 (m_请求指针, 请求信息).用户文本参数)
    }

    方法 取订阅数值参数 <公开 类型 = 变整数 注释 = "订阅视图的时候,提供的是啥数值,返回的就是啥数值!">
    {
        返回 (读指针处对象 (m_请求指针, 请求信息).用户数值参数)
    }

    方法 取订阅路径 <公开 类型 = 文本型 注释 = "获取匹配到的订阅路径,比如:\"/{栏目}/{帖子}.html\"" 返回值注释 = "返回订阅视图()中的订阅路径" 折叠>
    {
        返回 (读指针处对象 (m_请求指针, 请求信息).订阅路径)
    }

    方法 是否手机用户 <公开 类型 = 逻辑型 折叠>
    {
        变量 UA <类型 = 文本型>
        变量 手机UA识别大全 <类型 = "文本型 [4]" 值 = {"Mobile","Android","iPhone","UCBrowser"}>
        变量 i <类型 = 整数>
        UA = 取请求头 ("User-Agent")
        循环 (0, 取数组成员数 (手机UA识别大全), i)
        {
            如果 (寻找文本 (UA, 手机UA识别大全 [i], , 真) != -1)
            {
                返回 (真)
            }
        }
        返回 (假)
    }

    方法 取Cookies <公开 类型 = 文本型 折叠>
    参数 主键 <类型 = 文本型>
    {
        // 如果 (m_请求指针 != 0)
        {
            变量 "// 服务器对象" <类型 = 变整数>
            变量 "// 连接ID" <类型 = 变整数>
            // 服务器对象 = 读指针处对象 (m_请求指针, 请求信息).服务器
            // 连接ID = 读指针处对象 (m_请求指针, 请求信息).连接ID
            // 返回 (读指针处对象 (服务器对象, 服务器).m_服务器.取Cookie (连接ID, 主键))
        }

        变量 Cookie <类型 = 文本型>
        变量 Cookie数组 <类型 = 文本数组类>
        变量 i <类型 = 整数>
        变量 当前主键 <类型 = 文本型>
        变量 当前内容 <类型 = 文本型>
        Cookie = 本对象.取请求头 ("Cookie")
        分割文本 (Cookie, ";", Cookie数组)
        循环 (0, Cookie数组.取成员数 (), i)
        {
            如果 (分割左右文本 (Cookie数组.取成员 (i), '=', 当前主键, 当前内容, 真) == 假)
            {
                到循环尾
            }
            如果 (文本比较 (主键, 当前主键, 假) == 0)
            {
                返回 (当前内容)
            }
        }
        返回 ("")
        // Cookie: lO16_2132_saltkey=A2fmKx9S; lO16_2132_lastvisit=1632762194; lO16_2132_client_token=74B82459C7FCB1D9EA9B3072D34F373D; lO16_2132_connect_is_bind=1; lO16_2132_connect_uin=74B82459C7FCB1D9EA9B3072D34F373D; lO16_2132_nofavfid=1; lO16_2132_smile=1D1; lO16_2132_con_request_uri=http%3A%2F%2Fbbs.voldp.com%2Fconnect.php%3Fmod%3Dlogin%26op%3Dcallback%26referer%3Dforum.php%253Fmod%253Dforumdisplay%2526fid%253D2%2526page%253D1; lO16_2132_client_created=1634114600; lO16_2132_auth=678bcs253D62hRBSM%2BznX06ufV0pqBzOKQjcCzW2KymgRlKAfCz8Mj5xPhHvHKCPzMy%2FGz5U7thQmKvMPdy%2BVMk; lO16_2132_connect_login=1; lO16_2132_home_diymode=1; lO16_2132_sid=L1qZR0; lO16_2132_lip=59.55.86.118%2C1634212963; lO16_2132_ulastactivity=a816P3C8m5A7uSFCV6Zm2tSL1g%2F8saCcezXZEGw9ESFnkeTy0Blp; lO16_2132_visitedfid=38D2D43D54; lO16_2132_st_t=439%7C1634216334%7C95171219a1df449f01f2a0c7535aae31; lO16_2132_forum_lastvisit=D_54_1634142408D_43_1634149960D_2_1634216298D_38_1634216334; lO16_2132_st_p=439%7C1634216338%7C07e634e5175a477fbb666d8e7c1aec14; lO16_2132_viewid=tid_10723; lO16_2132_lastact=1634216338%09misc.php%09patch


        返回 ("")

    }

    方法 Session_置值 <公开 类型 = 逻辑型 折叠>
    参数 主键 <类型 = 文本型>
    参数 Session值 <类型 = 文本型>
    {
        变量 GUID <类型 = 文本型>
        变量 Cookie名称 <类型 = 文本型>
        变量 网站指针 <类型 = 变整数>
        网站指针 = 读指针处对象 (m_请求指针, 请求信息).网站指针
        如果 (网站指针 == 0)
        {
            返回 (假)
        }
        Cookie名称 = 读指针处对象 (网站指针, 网站).Session名称
        GUID = 本对象.取Cookies (Cookie名称)
        如果 (文本是否为空 (GUID))  // 没有GUID..
        {
            GUID = 全局类_创建GUID文本 ()
            如果 (文本是否为空 (GUID))
            {
                返回 (假)
            }

        }

        响应信息_置Cookie值 (响应信息_取指针 (), Cookie名称, GUID, "", "/", 读指针处对象 (网站指针, 网站).Session缓存时间)
        变量 JSON <类型 = 文本型>
        变量 哈希表 <类型 = 哈希表>
        JSON = 读指针处对象 (网站指针, 网站).Session缓存.取文本 (主键, 真)
        哈希表.从JSON载入 (JSON)
        哈希表.置文本 (主键, Session值)
        JSON = 哈希表.到JSON (假)

        读指针处对象 (网站指针, 网站).Session缓存.置文本 (GUID, JSON)
        返回 (假)
    }

    方法 Session_取ID <公开 类型 = 文本型 折叠>
    {
        变量 GUID <类型 = 文本型>
        变量 Cookie名称 <类型 = 文本型>
        变量 Session缓存时间 <类型 = 整数>
        变量 网站指针 <类型 = 变整数>
        网站指针 = 读指针处对象 (m_请求指针, 请求信息).网站指针
        如果 (网站指针 == 0)
        {
            返回 ("")
        }
        Cookie名称 = 读指针处对象 (网站指针, 网站).Session名称
        Session缓存时间 = 读指针处对象 (网站指针, 网站).Session缓存时间

        GUID = 本对象.取Cookies (Cookie名称)
        如果 (文本是否为空 (GUID))  // 没有GUID..
        {
            GUID = 全局类_创建GUID文本 ()
            如果 (文本是否为空 (GUID) == 假)
            {
                返回 ("")
            }
            响应信息_置Cookie值 (响应信息_取指针 (), Cookie名称, GUID, "", "/", Session缓存时间)
        }
        返回 (GUID)
    }

    方法 Session_清空 <公开 类型 = 逻辑型 折叠>
    {
        变量 GUID <类型 = 文本型>
        变量 Cookie名称 <类型 = 文本型>
        变量 网站指针 <类型 = 变整数>
        网站指针 = 读指针处对象 (m_请求指针, 请求信息).网站指针
        如果 (网站指针 == 0)
        {
            返回 (假)
        }
        Cookie名称 = 读指针处对象 (网站指针, 网站).Session名称

        GUID = 本对象.取Cookies (Cookie名称)

        响应信息_置Cookie值 (响应信息_取指针 (), Cookie名称, GUID, "", "/", -1)  // 通知浏览器,删除Cookie


        返回 (读指针处对象 (网站指针, 网站).Session缓存.删除 (GUID))
    }

    方法 Session_取值 <公开 类型 = 文本型 折叠>
    参数 主键 <类型 = 文本型>
    {
        变量 GUID <类型 = 文本型>
        变量 Cookie名称 <类型 = 文本型>
        变量 Session缓存时间 <类型 = 整数>
        变量 网站指针 <类型 = 变整数>
        变量 响应指针 <类型 = 变整数>
        网站指针 = 读指针处对象 (m_请求指针, 请求信息).网站指针
        响应指针 = 响应信息_取指针 ()


        如果 (网站指针 == 0)
        {
            返回 ("")
        }
        Cookie名称 = 读指针处对象 (网站指针, 网站).Session名称
        Session缓存时间 = 读指针处对象 (网站指针, 网站).Session缓存时间

        GUID = 响应信息_取Cookie值 (响应指针, Cookie名称)



        如果 (文本是否为空 (GUID))  // 系统里没有cookie...
        {
            GUID = 本对象.取Cookies (Cookie名称)
            如果 (文本是否为空 (GUID))  // 用户都没有提供Cookie..
            {
                返回 ("")
            }
        }




        响应信息_置Cookie值 (响应指针, Cookie名称, GUID, "", "/", Session缓存时间)  // 缓存续期...
        变量 JSON <类型 = 文本型>
        变量 哈希表 <类型 = 哈希表>

        JSON = 读指针处对象 (网站指针, 网站).Session缓存.取文本 (GUID, 真)
        哈希表.从JSON载入 (JSON)
        返回 (哈希表.取文本 (主键))
    }

    方法 参数注入到属性表 <公开 注释 = "后面有空再写..">
    {

    }
}

类 响应类 <公开>
{
    变量 m_响应指针 <类型 = 变整数 注释 = "[响应信息]">

    方法 类_初始化 <折叠>
    {
        m_响应指针 = 响应信息_取指针 ()
    }

    方法 写出数据 <公开 类型 = 逻辑型 折叠>
    参数 字节集 <类型 = 字节集类>
    {
        如果 (m_响应指针 != 0)
        {
            读指针处对象 (m_响应指针, 响应信息).响应数据.添加字节集 (字节集)
            返回 (真)
        }
        返回 (假)
    }

    方法 写出提示 <公开 类型 = 逻辑型 折叠>
    参数 提示标题 <类型 = 文本型>
    参数 提示信息 <类型 = 文本型>
    参数 提示类型 <类型 = 整数 注释 = "0=开心,1=难过,2=生气,3=问号">
    参数 链接1 <类型 = 文本型 注释 = "例如:\"javascript:history.go(-1);\"">
    参数 链接1标题 <类型 = 文本型 注释 = "例如:\"返回上一页\"">
    参数 链接2 <类型 = 文本型 注释 = "例如:tencent://Message/?uin=123456">
    参数 链接2标题 <类型 = 文本型 注释 = "例如:\"立即联系\"">
    {
        变量 开心 <类型 = 视窗文件资源 值 = "开心.png">
        变量 难过 <类型 = 视窗文件资源 值 = "难过.png">
        变量 生气 <类型 = 视窗文件资源 值 = "生气.png">
        变量 问号 <类型 = 视窗文件资源 值 = "问号.png">
        变量 图标 <类型 = 字节集类>
        如果 (提示类型 == 0)
        {
            文件资源到字节集 (开心, 图标)
        }
        否则 (提示类型 == 1)
        {
            文件资源到字节集 (难过, 图标)
        }
        否则 (提示类型 == 2)
        {
            文件资源到字节集 (生气, 图标)
        }
        否则 (提示类型 == 3)
        {
            文件资源到字节集 (问号, 图标)
        }
        变量 模板 <类型 = 文本型
                值 = "<!DOCTYPE html>\r\n<html lang=\'zh-cn\'>\r\n<head>\r\n<meta charset=\'UTF-8\'>\r\n<meta name=\"viewport\" content=\"width=device-width, initial-scale=1.0, user-scalable=0, minimum-scale=1.0, maximum-scale=1.0\">\r\n<title>{{_提示标题_}}</title>\r\n<style>\r\nhtml,body {height: 100%;margin: 0;padding: 0;}body {font-family: \'lucida grande\', \'lucida sans unicode\', lucida, helvetica, \'Hiragino Sans GB\', \'Microsoft YaHei\', \'WenQuanYi Micro Hei\', sans-serif;align-items: center;display: flex;}a{text-decoration:none;}#container {max-width: 400px;flex-basis: 100%;margin: 0 auto;background: #FFF;border-radius: 10px;box-shadow: 0 0 30px rgba(0, 0, 0, 0.3);-webkit-box-shadow: 0 0 30px rgba(0, 0, 0, 0.3);overflow: hidden;}#container #hero-img {width: 100%;height: 200px;background: #007bff;}#container #profile-img {width: 160px;height: 160px;margin: -80px auto 0 auto;border: 6px solid #FFF;border-radius: 50%;box-shadow: 0 0 5px rgba(90, 90, 90, 0.3);}#container #profile-img img {width: 100%;background: #FFF;border-radius: 50%;}#container #content {text-align: center;width: 320px;margin: 0 auto;padding: 0 0 50px 0;}#container #content h1 {font-size: 29px;font-weight: 500;margin: 50px 0 0 0;}#container #content p {font-size: 18px;font-weight: 400;line-height: 1.4;color: #666;margin: 15px 0 40px 0;}#container #content a {color: #CCC;font-size: 14px;margin: 0 10px;transition: color .3s ease-in-out;-webkit-transition: color .3s ease-in-out;}#container #content a:hover {color: #007bff;}.btn{background: none repeat scroll 0 0 #1BA1E2; border: 0 none; border-radius: 2px; color: #FFFFFF !important; cursor: pointer; font-family: \"Open Sans\",\"Hiragino Sans GB\",\"Microsoft YaHei\",\"WenQuanYi Micro Hei\",Arial,Verdana,Tahoma,sans-serif; font-size: 14px;  padding: 6px 10%;}.btn:hover,.yanshibtn:hover{background: none repeat scroll 0 0 #9B59B6; border: 0 none; border-radius: 2px; color: #FFFFFF!important; cursor: pointer; font-family: \"Open Sans\",\"Hiragino Sans GB\",\"Microsoft YaHei\",\"WenQuanYi Micro Hei\",Arial,Verdana,Tahoma,sans-serif; font-size: 14px; padding: 8px 10%;}\r\n</style>\r\n</head>\r\n<body>\r\n<div id=\'container\'>\r\n\t<div id=\'hero-img\'>\r\n\t</div>\r\n\t<div id=\'profile-img\'>\r\n\t\t<img height=\"178\" width=\"178\" src=\'data:image/png;base64,{{_提示图标_}}\'/>\r\n\t</div>\r\n\t<div id=\'content\'>\r\n\t\t<h1>{{_提示标题_}}</h1>\r\n\t\t<p>\r\n\t\t\t{{_提示信息_}}\r\n\t\t</p>\r\n\t\t{{_下方链接_}}\r\n\t</div>\r\n</div>\r\n</body>\r\n</html>">
        变量 下方链接 <类型 = 文本型>
        如果 (文本是否为空 (链接1) == 假)
        {
            加入文本 (下方链接, "<a href='" + 链接1 + "' class='btn btn-default' rel='nofollow'>" + 链接1标题 + "</a>")
        }
        如果 (文本是否为空 (链接2) == 假)
        {
            加入文本 (下方链接, "<a href='" + 链接2 + "' class='btn btn-default' rel='nofollow'>" + 链接2标题 + "</a>")
        }

        子文本替换 (模板, "{{_下方链接_}}", 下方链接, , 1)
        子文本替换 (模板, "{{_提示信息_}}", 提示信息, , 1)
        子文本替换 (模板, "{{_提示标题_}}", 提示标题, , 2)
        子文本替换 (模板, "{{_提示图标_}}", 图标.字节集到BASE64文本 (), , 1)

        返回 (本对象.写出数据 (文本到UTF8 (模板, 假)))

    }

    方法 写出错误提示 <公开 类型 = 逻辑型 折叠>
    参数 标题 <类型 = 文本型 注释 = "title">
    参数 错误提示 <类型 = 文本型>
    {
        返回 (本对象.写出文本 (单个模板.取错误页面 (标题, 错误提示)))
    }

    方法 写出文本 <公开 类型 = 逻辑型 折叠>
    参数 要写出的文本 <类型 = 文本型>
    {
        如果 (m_响应指针 != 0)
        {
            变量 字节集 <类型 = 字节集类>
            如果 (读指针处对象 (m_响应指针, 响应信息).网站编码 == 网站编码.GBK)
            {
                字节集 = 文本到多字节 (要写出的文本, 假)
            }
            否则
            {
                字节集 = 文本到UTF8 (要写出的文本, 假)

            }
            读指针处对象 (m_响应指针, 响应信息).响应数据.添加字节集 (字节集)
            返回 (真)
        }
        返回 (假)
    }

    方法 置状态码 <公开 类型 = 逻辑型 折叠>
    参数 状态码 <类型 = 短整数>
    {
        如果 (m_响应指针 == 0)
        {
            返回 (假)
        }
        如果 (状态码 > 99 && 状态码 < 505)
        {
            读指针处对象 (m_响应指针, 响应信息).状态码 = 状态码
            返回 (真)
        }
        返回 (假)
    }

    方法 置响应头 <公开 类型 = 逻辑型 折叠>
    参数 响应头名称 <类型 = 文本型>
    参数 响应头内容 <类型 = 文本型>
    {
        返回 (响应信息_置响应头 (m_响应指针, 响应头名称, 响应头内容))
    }

    方法 重定向 <公开 类型 = 逻辑型 折叠>
    参数 重定向地址 <类型 = 文本型>
    参数 永久重定向 <类型 = 逻辑型 注释 = "真=301 / 假=302">
    {
        置状态码 ((短整数)选择 (永久重定向, 301, 302))
        返回 (置响应头 ("Location", 重定向地址))
    }

    方法 渲染JSON到文本 <公开 类型 = 文本型 折叠>
    参数 文件 <类型 = 文本型>
    参数 JSON <类型 = 文本型>
    {
        变量 渲染结果 <类型 = 文本型>
        变量 哈希表 <类型 = 哈希表>
        如果 (哈希表.从JSON载入 (JSON) == 假)
        {
            渲染结果 = 单个模板.取错误页面 ("渲染的JSON格式错误!", "当前JSON格式错误!解析失败!")
            返回 (渲染结果)
        }
        如果 (m_响应指针 != 0)
        {
            变量 网站指针 <类型 = 变整数>
            变量 渲染状态 <类型 = 逻辑型>
            网站指针 = 读指针处对象 (m_响应指针, 响应信息).网站指针
            如果 (网站指针 == 0)
            {
                返回 ("网站指针为NULL")
            }
            渲染结果 = 读指针处对象 (网站指针, 网站).模板.渲染 (文件, 哈希表, 取变量地址 (渲染状态))
        }
        返回 (渲染结果)
    }

    方法 渲染JSON <公开 类型 = 逻辑型 折叠>
    参数 文件 <类型 = 文本型>
    参数 JSON <类型 = 文本型>
    {
        变量 哈希表 <类型 = 哈希表>
        如果 (哈希表.从JSON载入 (JSON) == 假)
        {
            变量 模板 <类型 = 文本型>
            模板 = 单个模板.取错误页面 ("渲染的JSON格式错误!", "当前JSON格式错误!解析失败!")
            本对象.写出文本 (模板)
            返回 (假)
        }
        返回 (本对象.渲染 (文件, 哈希表))
    }

    方法 渲染 <公开 类型 = 逻辑型 注释 = "没有公开" 折叠>
    参数 文件 <类型 = 文本型 "">
    参数 渲染数据 <类型 = 哈希表>
    {
        如果 (m_响应指针 != 0)
        {
            变量 网站指针 <类型 = 变整数>
            变量 模板 <类型 = 文本型>
            变量 渲染状态 <类型 = 逻辑型>
            网站指针 = 读指针处对象 (m_响应指针, 响应信息).网站指针
            如果 (网站指针 == 0)
            {
                返回 (假)
            }
            模板 = 读指针处对象 (网站指针, 网站).模板.渲染 (文件, 渲染数据, 取变量地址 (渲染状态))
            本对象.写出文本 (模板)
            返回 (渲染状态)
        }
        返回 (假)

    }

    方法 置页面标记 <公开 类型 = 逻辑型 折叠>
    参数 页面标记 <类型 = 文本型>
    {
        变量 标记 <类型 = 文本型>
        加入字符 (标记, '"')
        加入文本 (标记, 页面标记)
        加入字符 (标记, '"')
        返回 (本对象.置响应头 ("ETag", 标记))
    }

    方法 模板是否包含标签 <公开 类型 = 逻辑型 折叠>
    参数 文件 <类型 = 文本型>
    参数 标签 <类型 = 文本型 注释 = "不需要输入\"{{}}\"">
    {
        如果 (m_响应指针 != 0)
        {
            变量 网站指针 <类型 = 变整数>
            网站指针 = 读指针处对象 (m_响应指针, 响应信息).网站指针
            如果 (网站指针 == 0)
            {
                返回 (假)
            }
            返回 (读指针处对象 (网站指针, 网站).模板.标签是否存在 (文件, 标签))
        }
        返回 (假)
    }

    方法 置Cookies <公开 类型 = 逻辑型>
    参数 Cookie名称 <类型 = 文本型>
    参数 Cookie值 <类型 = 文本型>
    参数 域名 <类型 = 文本型 注释 = "COOKIE对应的域名。不设置则为当前域名。" @默认值 = "">
    参数 目录 <类型 = 文本型 注释 = "对应网站中的路径。不设置则设置COOKIE到默认路径下。" @默认值 = "/">
    参数 过期时间 <类型 = 整数 注释 = "单位:秒    (1800=30分钟)" @默认值 = 1800>
    {
        如果 (m_响应指针 != 0)
        {
            返回 (响应信息_置Cookie值 (m_响应指针, Cookie名称, Cookie值, 域名, 目录, 过期时间))
        }
        返回 (假)
    }

    方法 取模板标签参数 <公开 类型 = 文本型 折叠>
    参数 文件 <类型 = 文本型>
    参数 标签 <类型 = 文本型 注释 = "不需要输入\"{{}}\"">
    {
        如果 (m_响应指针 != 0)
        {
            变量 网站指针 <类型 = 变整数>
            网站指针 = 读指针处对象 (m_响应指针, 响应信息).网站指针
            如果 (网站指针 == 0)
            {
                返回 ("")
            }
            返回 (读指针处对象 (网站指针, 网站).模板.取模板标签参数 (文件, 标签))
        }
        返回 ("")
    }

    方法 置响应类型 <公开 类型 = 逻辑型 折叠>
    参数 后缀名 <类型 = 文本型 注释 = "\"js\"  \"html\"   \"css\"" "">
    {
        如果 (m_响应指针 != 0)
        {
            读指针处对象 (m_响应指针, 响应信息).文档类型 = 后缀名
            返回 (真)
        }
        返回 (假)

    }

    #
}

【分隔符】


###### 以下为一个火山软件开发平台系统类库程序的内容

<火山程序 类型 = "通常" 版本 = 1 />

包 蜗牛服务器.验证

类 验证类 <公开 @全局类 = 真>
{
    方法 __检测邮箱字符是否合法 <静态 类型 = 逻辑型 折叠>
    参数 文本 <类型 = 文本型>
    {
        如果 (文本是否为空 (文本))
        {
            返回 (假)
        }
        变量 当前字符 <类型 = 字符>
        变量 i <类型 = 整数>
        循环 (0, 取文本长度 (文本), i)
        {
            当前字符 = 取字符 (文本, i)
            如果 (当前字符 >= 'a' && 当前字符 <= 'z' || 当前字符 >= 'A' && 当前字符 <= 'Z' || 当前字符 >= '0' && 当前字符 <= '9' || 当前字符 == '-')
            {
                到循环尾
            }
            返回 (假)
        }
        返回 (真)
    }

    方法 验证是否为手机号码 <公开 静态 类型 = 逻辑型>
    参数 手机号码 <类型 = 文本型>
    {
        如果 (取文本长度 (手机号码) != 11)
        {
            返回 (假)
        }

        变量 i <类型 = 整数>
        循环 (0, 取文本长度 (手机号码), i)
        {
            如果 (取反 (取字符 (手机号码, i) >= '0' && 取字符 (手机号码, i) <= '9'))  // 不是数字..
            {
                返回 (假)
            }
        }
        变量 手机开头数组 <类型 = "文本型 [51]"
                值 = {"133","149","153","173","177","180","181","189","190","191","193","199","130","131","132","145","146","155","156","166","171","175","176","185","186","196","134","135","136","137","138","139","147","148","150","151","152","157","158","159","172","178","182","183","184","187","188","195","197","198","192"}>
        循环 (0, 取数组成员数 (手机开头数组), i)
        {
            如果 (近似等于 (手机号码, 手机开头数组 [i], 假))  // 是手机号码..
            {
                返回 (真)
            }
        }
        返回 (假)
    }

    方法 验证是否为邮箱 <公开 静态 类型 = 逻辑型 折叠>
    参数 邮箱地址 <类型 = 文本型>
    {
        变量 模糊匹配 <类型 = w_文本匹配类>
        变量 哈希表 <类型 = 哈希表>
        模糊匹配.创建 ("{前面}@{中间}.{后缀}", 假)
        如果 (模糊匹配.匹配 (邮箱地址, 哈希表) == 假)
        {
            返回 (假)
        }
        变量 文本 <类型 = 文本型>
        变量 当前字符 <类型 = 字符>
        变量 i <类型 = 整数>
        文本 = 哈希表.取文本 ("前面")
        如果 (__检测邮箱字符是否合法 (文本) == 假)
        {
            返回 (假)
        }
        文本 = 哈希表.取文本 ("中间")
        如果 (__检测邮箱字符是否合法 (文本) == 假)
        {
            返回 (假)
        }
        文本 = 哈希表.取文本 ("后缀")
        如果 (文本是否为空 (文本))
        {
            返回 (假)
        }
        循环 (0, 取文本长度 (文本), i)
        {
            当前字符 = 取字符 (文本, i)
            如果 (当前字符 >= 'a' && 当前字符 <= 'z' || 当前字符 >= 'A' && 当前字符 <= 'Z')  // 后缀只能是英文字符..
            {
                到循环尾
            }
            返回 (假)
        }
        返回 (真)
    }
}

【分隔符】


###### 以下为一个火山软件开发平台系统类库程序的内容

<火山程序 类型 = "通常" 版本 = 1 />

包 火山.世恒带你学火山

类 启动类 <公开 基础类 = 窗口程序类>
{
    变量 主窗口对象 <类型 = 我的主窗口 标题 = "我的主窗口">

    方法 启动方法 <公开 类型 = 整数>
    {
        主窗口对象.创建主窗口 ()
        返回 (1)
    }
}

# ===

类 我的主窗口 <基础类 = 窗口 注释 = "样例主窗口" @视窗.布局 = "client_size = \"937, 756\"" 标题 = "【世恒带你学火山】-代码生成锐浪自由表格推模式套打">
{
    变量 查询显示器_显示 <类型 = 查询显示器 隐藏值属性 = "0" @视窗.布局 = "id = 101\r\npos = \"13, 423, 912, 320\"">
    变量 报表_工资表 <类型 = 报表>
    变量 明细网格_工资表 <类型 = 明细网格>
    变量 部件框_临时存放 <类型 = 部件框>
    变量 内容格_工资表 <类型 = 内容格>
    变量 内容行_工资表 <类型 = 内容行>
    变量 自由表格_工资表 <类型 = 自由表格>
    变量 报表设计器_显示 <类型 = 报表设计器>
    变量 图片框_显示报表设计器 <类型 = 图片框 隐藏值属性 = "0" @视窗.布局 = "id = 103\r\npos = \"13, 7, 912, 352\"">
    变量 ""
    变量 按钮_生成报表 <类型 = 按钮 隐藏值属性 = "0" @视窗.布局 = "id = 104\r\npos = \"125, 367, 232, 40\"" 标题 = "生成报表"
            字体 = ", 14, 0, 0, 0, 0, 0">
    变量 按钮_打印预览 <类型 = 按钮 折叠2 隐藏值属性 = "0" @视窗.布局 = "id = 105\r\npos = \"541, 367, 232, 40\"" 标题 = "打印预览"
            字体 = ", 14, 0, 0, 0, 0, 0">
    变量 ""
    变量 ""

    方法 按钮_被单击 <接收事件 类型 = 整数 注释 = "当按钮被单击后发送此事件">
    参数 来源对象 <类型 = 按钮 注释 = "提供事件产生的具体来源对象">
    参数 标记值 <类型 = 整数 注释 = "用户调用\"挂接事件\"命令时所提供的\"标记值\"参数值,非此方式挂接事件则本参数值固定为0.">
    {
        如果 (来源对象 == 按钮_生成报表)
        {
            // ★★★在进行以下用代码生成报表之前,最好是学会手工设计,熟悉了报表的各组成部分以及相关名称概念之后再进行★★★
            报表_工资表.初始化 ()  // 这个“初始化”必须写并且写在最前面
            明细网格_工资表 = 报表_工资表.插入明细网格 ()  // 插入“明细网格”并且返回到“明细网格_工资表”变量
            明细网格_工资表.获取边框.获取画笔.宽度 = 0  // 设置边框宽度为0,也就是不显示"明细网格_工资表"的边框
            明细网格_工资表.显示列线 = 假  // 设置不显示"明细网格_工资表"里的列线
            明细网格_工资表.显示行线 = 假  // 设置不显示"明细网格_工资表"里的行线
            明细网格_工资表.增加列 ("", "", "", 16)  // 给"明细网格_工资表"里增加一列,这样才有了放置其他元素的区域
            明细网格_工资表.获取标题行.高度 = 0  // 给"明细网格_工资表"的“标题行”的高度设置为0,也就是为了不显示
            明细网格_工资表.获取标题行.可见性 = 假  // 给"明细网格_工资表"的“标题行”的可见性设置为假,也是保证不显示
            内容行_工资表 = 明细网格_工资表.获取内容行  // 获取"明细网格_工资表"里的“内容行”
            内容行_工资表.高度 = 4  // 设置"明细网格_工资表"里的“内容行”高度为4厘米
            内容格_工资表 = 内容行_工资表.获取内容格集合.按序号获取项目 (1)  // 获取“内容行”里的第1格,序号从1开始
            内容格_工资表.自由格 = 真  // 设置“内容行”里的第1格为“自由格”,这样才可以给里面任意放其他元素
            部件框_临时存放 = 内容格_工资表.获取部件框集合.增加 (部件框类型枚举.自由表格)  // 给这个内容格里面插入“自由表格”
            自由表格_工资表 = 部件框_临时存放.获取为自由表格  // 取出插入的“自由表格”并且关联到“自由表格_工资表”变量
            自由表格_工资表.上 = 0.5  // 设置"自由表格_工资表"的上边距为0.5厘米
            自由表格_工资表.居中方式 = 居中方式枚举.水平  // 设置"自由表格_工资表"的“居中方式”为水平居中
            // 下面这行设置"自由表格_工资表"的“边框样式”,上下左右可以自由组合,显示什么或者不显示什么
            自由表格_工资表.获取边框.边框模式 = 边框模式枚举.画上 + 边框模式枚举.画下 + 边框模式枚举.画左 + 边框模式枚举.画右
            自由表格_工资表.列数 = 6  // 设置"自由表格_工资表"的“列数”为6列
            自由表格_工资表.行数 = 4  // 设置"自由表格_工资表"的“行数”为4行
            计次循环 (6)  // 循环给6列都设置宽度为2厘米
            {
                自由表格_工资表.获取指定列 (取循环索引 () + 1).宽度 = 2  // 列号从1开始

            }
            自由表格_工资表.获取指定行 (1).高度 = 1  // 指定第1行的高度为1厘米

            变量 局部_行 <类型 = 整数>
            变量 局部_列 <类型 = 整数>
            // 以下为循环对4行6列自由表格里的所有单元格设置文字对齐方式为水平和垂直都是居中
            计次循环 (4)
            {
                局部_行 = 取循环索引 () + 1  // 行号从1开始
                计次循环 (6)
                {
                    局部_列 = 取循环索引 () + 1  // 列号从1开始
                    自由表格_工资表.获取指定单元格 (局部_行, 局部_列).获取文本格式.文字对齐方式 = 文字对齐方式枚举.中中

                }
            }
            自由表格_工资表.获取指定单元格 (1, 1).合并列数 = 6  // 对第1行的6个列进行合并
            自由表格_工资表.获取指定单元格 (1, 1).文本 = "工资条"  // 给第1行合并后的单元格显示文字“工资条”
            自由表格_工资表.获取指定单元格 (1, 1).获取字体.字号 = 18  // 给第1行合并后的单元格设置字体大小18
            自由表格_工资表.获取指定单元格 (1, 1).获取字体.名称 = "华文行楷"  // 给第1行合并后的单元格设置字体名称
            自由表格_工资表.获取指定单元格 (2, 1).合并行数 = 2  // 把第2行和第3行的第1列进行合并
            自由表格_工资表.获取指定单元格 (2, 1).文本 = "姓名"  // 给合并后的单元格显示文字“姓名”
            自由表格_工资表.获取指定单元格 (2, 2).合并列数 = 5  // 把第2行的第2列到第6列进行合并
            自由表格_工资表.获取指定单元格 (2, 2).文本 = "工资项目"  // 给合并后的单元格显示文字“工资项目”
            变量 局部_字段名 <类型 = "文本型 []" 值 = {"姓名","基本工资","全勤奖金","交通补助","伙食补贴","其他"}>
            计次循环 (6)  // 下面是循环增加数据字段名,这个很重要,后续填充数据要按照这个名字找对应,用了数组+循环方式
            {
                明细网格_工资表.获取数据集.增加字段 (局部_字段名 [取循环索引 ()], 字段类型枚举.字符型)

            }

            计次循环 (6)  // 下面是循环给第3行各列设置固定文本和给第4行各列设置后期填数据用的标识文本,格式如:[#姓名#]
            {
                自由表格_工资表.获取指定单元格 (3, 取循环索引 () + 1).文本 = 局部_字段名 [取循环索引 ()]
                自由表格_工资表.获取指定单元格 (4, 取循环索引 () + 1).文本 = "[#" + 局部_字段名 [取循环索引 ()] + "#]"

            }

            如果 (报表设计器_显示.是否为空 () == 真)  // 下面用来在窗口中某个组件里创建一个显示报表设计器的区域,我们用个图片框
            {
                报表设计器_显示.创建组件 (图片框_显示报表设计器, 0, 0, 图片框_显示报表设计器.宽度, 图片框_显示报表设计器.高度, 12345)

            }
            报表_工资表.保存到文件 (取运行目录 () + "工资条.grf")  // 可以把设计好的报表保存到文件中,类似手工设计时报表保存到文件
            报表设计器_显示.关联报表 = 报表_工资表  // 关联到报表设计器,就可以看到用代码创建的报表是什么样子了
            变量 局部_员工数组 <类型 = "文本型 []"
                    值 = {"狐突","狐偃","狐毛","介子推","里克","邳郑","宋襄公","秦穆公","楚庄王","石碏","石厚","州吁","郑突","郑忽","老子","孔子","孙武","左丘明","伍子胥","范蠡","西施","勾践","阖闾","夫差","文种","专诸","要离","庆忌","吴王僚","伊尹","烛之武","公子光","魏舒","晏婴","庆父","乐毅","吴起","孙膑","庞涓","廉颇","赵牧","赵奢","赵括","项燕","田单","韩非","荀子","庄子","墨子","惠子","孟子","燕丹","荆轲","高渐离","樊於期","孟尝君","春申君","信陵君","平原君","邹忌","白起","商鞅","李悝","蔺相如","屈原","魏斯","乐羊","西门豹","孔伋","杨朱","聂政","申不害","尸佼","赵武灵王","匡章","淳于髡","张仪","苏秦","田辟疆","田忌","鬼谷子","甘德","石申","李冰","扁鹊","范雎","蔡泽","郭隗","唐蔑","宋玉","触龙","毛遂","鲁仲连","公孙龙","嬴政","吕不韦","李斯","尉缭","王翦","王贲","李信","蒙骜","蒙武","蒙恬","嬴扶苏","嬴胡亥","赵高","章邯","司马欣","董翳","李由","甘罗","嫪毐","阳泉君","嬴子婴","嬴成矫","徐福","卢生","陈胜","吴广","项梁","项羽","张良","韩信","萧何","刘邦","陈平","周勃","夏侯婴","曹参","范增","项伯","项庄","樊哙","英布","彭越","熊心","龙且","郦食其","钟离昧","周殷","灌婴","周章","魏咎","赵歇","田儋","田臧","李归","邓说","伍徐","张贺","庄贾","朱鸡石","余樊君","王离","陈余","张耳","宋义","涉间","苏角","李左车","田荣","殷通","刘邦","萧何","曹参","韩信","张良","英布","周勃","周亚夫","吕雉","吕禄","吕产","陈平","灌婴","灌夫","郅都","宁成","张汤","东方朔","刘彻","刘启","刘安","卫青","卫子夫","霍去病","霍光","刘贺","田蚡","窦婴","苏建","苏武","司马相如","卓文君","司马谈","司马迁","扬雄","李广","李陵","王莽","赵飞燕","赵合德","刘秀","马援","邓禹","吴汉","盖延","赵破奴","张骞","刘细君","王昭君","梁冀","陈蕃","窦武","何进","卢植","皇甫嵩","朱隽","班超","班固","班昭","董卓","蔡邕","袁绍","袁术","郑玄","张衡","贾谊","晁错","郭解","剧孟","曹操","刘备","孙权","关羽","张飞","诸葛亮","赵云","黄忠","马超","魏延","法正","庞统","周瑜","司马懿","徐庶","曹丕","曹植","曹彰","郭嘉","荀彧","荀攸","贾诩","程昱","满宠","蒋济","韩浩","陈群","张辽","徐晃","许褚","典韦","张郃","于禁","乐进","李典","曹仁","文聘","张燕","曹洪","曹休","曹真","夏侯敦","夏侯渊","夏侯霸","夏侯威","姜维","鲁肃","吕蒙","陆逊","张昭","张纮","虞翻","丁奉","徐盛","陈武","潘璋","甘宁","太史慈","蒋钦","周泰","董袭","骆统","廖化","马谡","王平","蒋琬","刘禅","孙亮","凌统","费祎","诸葛瑾","公孙渊","邓艾","钟会","诸葛恪","司马师","司马昭","孙皓","曹髦","司马炎","司马衷","贾充","贾南风","王祥","羊祜","王浑","王浚","陆机","陆云","周处","刘琨","谢逖","陶侃","孙秀","刘渊","石勒","石虎","王弥","苟晞","潘岳","石崇","左思","王览","王导","嵇康","阮籍","阮咸","刘伶","王戎","杜预","荀勖","何曾","张华","司马攸","司马玮","司马亮","司马伦","司马冏","司马颖","司马颙","司马乂","司马越","段匹磾","张寔","刘曜","刘聪","司马睿","王羲之","王献之","桓温","谢安","谢玄","谢道韫","#123","321","666","233"}>
            报表_工资表.加载数据准备 ()  // 这就是锐浪报表的“推模式”,用代码给表格中设置数据
            置随机数种子 ()
            计次循环 (取数组成员数 (局部_员工数组))  // 构造很多条数据到报表中,也就是这个模板复制(追加)了很多次
            {
                报表_工资表.获取明细网格.获取数据集.追加 ()  // 相当于复制了一份设计好的报表模板出来
                // 以下就是给之前报表模板中设置的类似“[#姓名#]”、“[#基本工资#]”的文本标记位置设置具体数据
                报表_工资表.获取明细网格.获取数据集.按名称取字段 ("姓名").字符值 = 局部_员工数组 [取循环索引 ()]
                报表_工资表.获取明细网格.获取数据集.按名称取字段 ("基本工资").字符值 = 到文本 (多项选择 (取随机数 (0, 4), 2000, 3000, 4000, 5000, 6000))
                报表_工资表.获取明细网格.获取数据集.按名称取字段 ("全勤奖金").字符值 = 到文本 (多项选择 (取随机数 (0, 2), 200, 0, 400))
                报表_工资表.获取明细网格.获取数据集.按名称取字段 ("交通补助").字符值 = 到文本 (多项选择 (取随机数 (0, 1), 200, 300))
                报表_工资表.获取明细网格.获取数据集.按名称取字段 ("伙食补贴").字符值 = 到文本 (多项选择 (取随机数 (0, 3), 200, 300, 400, 500))
                报表_工资表.获取明细网格.获取数据集.按名称取字段 ("其他").字符值 = 到文本 (取随机数 (100, 800))
                报表_工资表.获取明细网格.获取数据集.提交 ()  // 完成了模板的复制和数据的设置并准备显示


            }
            查询显示器_显示.初始化 ()  // 这个“初始化”必须先写上,然后再“关联报表”
            查询显示器_显示.关联报表 = 报表_工资表  // 这个“查询显示器_显示”和报表建立关联
            查询显示器_显示.启动 ()  // 启动查询显示器,也就是让显示报表,就可以在界面上看到报表样子

        }
        否则 (来源对象 == 按钮_打印预览)
        {
            报表_工资表.打印预览 (真)  // 这样还可以直接显示出这个报表的打印预览


        }
        返回 (0)

    }

    方法 我的主窗口_创建完毕 <接收事件 类型 = 整数 注释 = "当本组件及其中所有子组件均被创建完毕后发送此事件.">
    参数 来源对象 <类型 = 我的主窗口 注释 = "提供事件产生的具体来源对象">
    参数 标记值 <类型 = 整数 注释 = "用户调用\"挂接事件\"命令时所提供的\"标记值\"参数值,非此方式挂接事件则本参数值固定为0.">
    {
        标题 = "【世恒带你学火山】- 代码生成锐浪自由表格推模式套打"
        返回 (0)
    }
}

【分隔符】


###### 以下为一个火山软件开发平台系统类库程序的内容

<火山程序 类型 = "通常" 版本 = 1 />

包 火山.程序 <@视窗.附属文件 = "DLL.dll">

类 启动类 <公开 基础类 = 窗口程序类 折叠>
{
    变量 主窗口对象 <公开 类型 = 我的主窗口>

    方法 启动方法 <公开 类型 = 整数>
    {
        主窗口对象.创建主窗口 ()
        返回 (1)
    }
}

# ===

类 我的主窗口 <基础类 = 窗口 注释 = "样例主窗口" 注释 = "" @视窗.布局 = "client_size = \"504, 349\"" 标题 = "动态调用DLL">
{
    变量 编辑框1 <公开 类型 = 编辑框 隐藏值属性 = "0" @视窗.布局 = "id = 101\r\npos = \"13, 15, 472, 216\"" 是否允许多行 = 真
            滚动条 = 纵向滚动条>
    变量 按钮1 <类型 = 按钮 隐藏值属性 = "0" @视窗.布局 = "id = 102\r\npos = \"13, 253, 80, 32\"" 标题 = "加载窗口">
    变量 按钮2 <类型 = 按钮 隐藏值属性 = "0" @视窗.布局 = "id = 103\r\npos = \"101, 253, 80, 32\"" 标题 = "乘法运算">
    变量 按钮3 <类型 = 按钮 隐藏值属性 = "0" @视窗.布局 = "id = 104\r\npos = \"189, 253, 80, 32\"" 标题 = "传递文本">
    变量 按钮4 <类型 = 按钮 隐藏值属性 = "0" @视窗.布局 = "id = 105\r\npos = \"277, 253, 80, 32\"" 标题 = "传递编辑框">
    变量 按钮5 <类型 = 按钮 隐藏值属性 = "0" @视窗.布局 = "id = 106\r\npos = \"365, 253, 80, 32\"" 标题 = "释放DLL">
    变量 按钮6 <类型 = 按钮 隐藏值属性 = "0" @视窗.布局 = "id = 107\r\npos = \"13, 295, 80, 32\"" 标题 = "传递事件">
    变量 按钮7 <类型 = 按钮 隐藏值属性 = "0" @视窗.布局 = "id = 108\r\npos = \"101, 295, 80, 32\"" 标题 = "弹出信息">
    变量 ""
    变量 DLL <类型 = 动态链接库类_>
    变量 DLL静态方法指针 <类型 = 变整数>
    变量 DLL句柄 <类型 = 变整数>

    方法 按钮_被单击 <公开 接收事件 类型 = 整数>
    参数 来源对象 <类型 = 按钮>
    参数 标记值 <类型 = 整数>
    {
        如果 (来源对象 == 按钮1)
        {
            变量 局部DLL句柄 <类型 = 文本型>
            变量 DLL窗口句柄 <类型 = 文本型>
            DLL.动态加载窗口_ ("DLL.dll", "cjck", 1, 本对象, 局部DLL句柄, DLL窗口句柄)
            DLL句柄 = (变整数)文本到长整数 (局部DLL句柄)
            编辑框1.加入文本行 ("DLL句柄" + 局部DLL句柄 + "\r\n")
            编辑框1.加入文本行 ("DLL窗口句柄" + DLL窗口句柄 + "\r\n")

            // 方法2
            变量 "// 地址句柄" <类型 = 变整数>
            // DLL句柄 = DLL.动态加载DLL_ ("DLL.dll")
            // 地址句柄 = DLL.取函数地址_ (DLL句柄, "cjck")
            // DLL.执行函数_ ( 地址句柄,1, 取变量地址 (取程序 ().主窗口对象))

        }
        否则 (来源对象 == 按钮2)
        {
            变量 提示文本 <类型 = 文本型>
            变量 返回结果 <类型 = 变整数>
            提示文本 = "EXE窗口说要执行乘法运算"
            DLL.执行函数2_ ("DLL.dll", "cfys", 3, 取变量地址 (返回结果), 2, 3, 取文本指针 (提示文本))
            编辑框1.加入文本行 ("运算结果：" + 到文本 (返回结果) + "\r\n")

        }
        否则 (来源对象 == 按钮3)
        {
            变量 返回结果 <类型 = 变整数>
            DLL.执行函数2_ ("DLL.dll", "cdwb", 0, 取变量地址 (返回结果))
            编辑框1.加入文本行 ("返回文本信息：" + 指针到文本 (返回结果) + "\r\n")

        }
        否则 (来源对象 == 按钮4)
        {
            变量 地址句柄 <类型 = 变整数>
            变量 文本数据 <类型 = 文本型>
            变量 局部DLL句柄 <类型 = 文本型>
            DLL.动态加载窗口_ ("DLL.dll", "cjck", 1, 本对象, 局部DLL句柄)
            DLL句柄 = (变整数)文本到长整数 (局部DLL句柄)

            DLL静态方法指针 = 取静态方法地址 (传递对象数据)
            地址句柄 = DLL.取函数地址_ (DLL句柄, "cddx")
            DLL.执行函数_ (地址句柄, 1, 取变量地址 (DLL静态方法指针))
            文本数据 = "传递编辑框数据"
            DLL.执行函数_ (DLL静态方法指针, 2, 0, 取变量地址 (文本数据))
            编辑框1.加入文本行 ("DLL返回的数据：" + 文本数据 + "\r\n")

        }
        否则 (来源对象 == 按钮5)
        {
            DLL.动态释放所有DLL_ (DLL句柄)
            // DLL.动态释放DLL_ (DLL句柄)

        }
        否则 (来源对象 == 按钮6)
        {
            变量 地址句柄 <类型 = 变整数>
            变量 局部DLL句柄 <类型 = 文本型>
            DLL.动态加载窗口_ ("DLL.dll", "cjck", 1, 本对象, 局部DLL句柄)
            DLL句柄 = (变整数)文本到长整数 (局部DLL句柄)

            DLL静态方法指针 = 取静态方法地址 (执行按钮事件)
            地址句柄 = DLL.取函数地址_ (DLL句柄, "cddx")
            DLL.执行函数_ (地址句柄, 1, 取变量地址 (DLL静态方法指针))

        }
        否则 (来源对象 == 按钮7 || 标记值 == 1)
        {
            信息框 ("我是EXE窗口中的信息框，被触发了。")

        }
        返回 (0)
    }

    方法 执行按钮事件 <静态>
    参数 指针 <类型 = 变整数>
    {
        如果 (指针 == 7)
        {
            取程序 ().主窗口对象.按钮_被单击 (取程序 ().主窗口对象.按钮7, 1)
        }

    }

    方法 传递对象数据 <静态>
    参数 指针 <类型 = 变整数>
    {
        变量 临时编辑框 <类型 = 编辑框>
        临时编辑框 = 读指针处对象 (指针, 编辑框)  // 把临时编辑框变为DLL的编辑框
        临时编辑框.文本颜色 = 取颜色值 (取随机数 (0, 255), 取随机数 (0, 255), 取随机数 (0, 255))  // 操作DLL中的编辑框属性
        取程序 ().主窗口对象.编辑框1.内容 = 临时编辑框.内容  // 取出DLL中编辑框内容到EXE编辑框中

    }

    方法 编辑框_内容被改变 <接收事件 类型 = 整数>
    参数 来源对象 <类型 = 编辑框>
    参数 标记值 <类型 = 整数>
    {
        变量 文本数据 <类型 = 文本型>
        如果真 (DLL.指针是否有效_ (DLL静态方法指针) == 真)
        {
            DLL.执行函数_ (DLL静态方法指针, 3, 取变量地址 (编辑框1), 取变量地址 (文本数据), 1)  // 调用DLL中的静态方法

        }
        返回 (0)
    }
}

【分隔符】


###### 以下为一个火山软件开发平台系统类库程序的内容

<火山程序 类型 = "通常" 版本 = 1 />

包 火山.程序

类 启动类 <公开 基础类 = 动态库程序类>
{
    变量 DLL窗口 <类型 = DLL窗口>
    变量 方法指针 <公开 类型 = 变整数>

    方法 启动方法 <公开 类型 = 整数 折叠>
    {
        返回 (1)
    }

    方法 载入DLL窗口 <公开 静态 类型 = 变整数 @输出名 = "cjck" @视窗.输出 = 真>
    参数 父窗口指针 <类型 = 变整数>
    {
        取程序 ().DLL窗口.创建弹出子窗口 (读指针处对象 (父窗口指针, 窗口))  // 创建DLL窗口
        返回 (取程序 ().DLL窗口.取窗口句柄 ())  // 返回该窗口句柄

    }

    方法 乘法运算 <公开 静态 类型 = 变整数 @输出名 = "cfys" @视窗.输出 = 真>
    参数 被乘数 <类型 = 变整数>
    参数 乘数 <类型 = 变整数>
    参数 传递参数 <类型 = 文本型>
    {
        取程序 ().DLL窗口.信息框 ("DLL窗口提醒你，已成功取得EXE传递的参数：" + 传递参数)  // 取得exe窗口传递的数据
        返回 (被乘数 * 乘数)  // 返回乘法运算结果

    }

    方法 传递文本数据 <公开 静态 类型 = 变整数 @输出名 = "cdwb" @视窗.输出 = 真>
    {
        变量 传递参数 <类型 = 变整数>
        传递参数 = 取文本指针 ("我是来自DLL内部的文本数据")
        返回 (传递参数)  // 返回文本数据

    }

    方法 取方法指针 <公开 静态 @输出名 = "cddx" @视窗.输出 = 真>
    参数 指针 <类型 = 变整数>
    {
        取程序 ().方法指针 = 读指针处值 (指针, 变整数)  // 取得EXE中的静态方法指针
        取程序 ().DLL窗口.标题 = "获得EXE里方法指针：" + 到文本 (取程序 ().方法指针)  // 修改DLL窗口标题
        写指针处值 (指针, 取静态方法地址 (传递对象数据))  // 返回DLL中的静态方法指针到EXE中

    }

    方法 传递对象数据 <静态>
    参数 指针 <类型 = 变整数>
    参数 读写数据 <类型 = 文本型>
    参数 类型 <类型 = 整数>
    {
        变量 临时编辑框 <类型 = 编辑框>
        如果 (类型 == 0)
        {
            取程序 ().DLL窗口.按钮1.禁止 = 真
            取程序 ().DLL窗口.信息框 ("DLL端已接收到EXE端数据：" + 读写数据)
            读写数据 = "我是DLL里的信息"

        }
        否则 (类型 == 1)
        {
            取程序 ().DLL窗口.按钮1.禁止 = 真
            临时编辑框 = 读指针处对象 (指针, 编辑框)  // 将临时编辑框转变成EXE中的编辑框,可以直接操作该编辑框的相关属性
            取程序 ().DLL窗口.编辑框1.内容 = 临时编辑框.内容
        }

    }
}

类 DLL窗口 <公开 基础类 = 窗口 隐藏值属性 = "0" @视窗.布局 = "client_size = \"467, 305\"" 标题 = "DLL窗口">
{
    变量 编辑框1 <公开 类型 = 编辑框 隐藏值属性 = "0" @视窗.布局 = "id = 101\r\npos = \"21, 15, 424, 240\"" 是否允许多行 = 真
            滚动条 = 纵向滚动条>
    变量 按钮1 <公开 类型 = 按钮 隐藏值属性 = "0" @视窗.布局 = "id = 102\r\npos = \"165, 266, 128, 32\"" 标题 = "EXE窗口弹出信息">
    变量 DLL <类型 = 动态链接库类_>

    方法 编辑框_内容被改变 <接收事件 类型 = 整数>
    参数 来源对象 <类型 = 编辑框>
    参数 标记值 <类型 = 整数>
    {
        如果 (来源对象 == 编辑框1)
        {
            DLL.执行函数_ (取程序 ().方法指针, 1, 取变量地址 (编辑框1))  // 将DLL中的编辑框内容传递到EXE中
        }

        返回 (0)
    }

    方法 按钮_被单击 <接收事件 类型 = 整数>
    参数 来源对象 <类型 = 按钮>
    参数 标记值 <类型 = 整数>
    {
        DLL.执行函数_ (取程序 ().方法指针, 1, 7)  // 告诉EXE要执行按钮7

        返回 (0)
    }
}
