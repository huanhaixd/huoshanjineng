
###### 以下为一个火山软件开发平台系统类库程序的内容

<火山程序 类型 = "通常" 版本 = 1 />

包 火山.测试

类 启动类 <公开 基础类 = 窗口程序类>
{
    变量 主窗口对象 <类型 = 我的主窗口>

    方法 启动方法 <公开 类型 = 整数>
    {
        主窗口对象.创建主窗口 ()
        返回 (1)
    }
}

类 我的主窗口 <公开 基础类 = 窗口 注释 = "样例主窗口" @视窗.布局 = "client_size = \"324, 299\"">
{
    方法 我的主窗口_创建完毕 <接收事件 类型 = 整数 注释 = "当本组件及其中所有子组件均被创建完毕后发送此事件.">
    参数 来源对象 <类型 = 我的主窗口 注释 = "提供事件产生的具体来源对象">
    参数 标记值 <类型 = 整数 注释 = "用户调用\"挂接事件\"命令时所提供的\"标记值\"参数值,非此方式挂接事件则本参数值固定为0.">
    {
        变量 哈希表 <类型 = 可序列化哈希表>
        哈希表.插入 ("火山", 1)
        调试输出 (哈希表.取对象数据 ())

        变量 哈希表2 <类型 = 可序列化哈希表>
        哈希表2.置对象数据 (哈希表.取对象数据 ())
        调试输出 (哈希表2)

        返回 (0)
    }
}

类 可序列化哈希表 <基础类 = 文本到整数哈希表>
{
    方法 流写出 <公开 @虚拟方法 = 可覆盖>
    参数 写出流 <类型 = 输出流类>
    {
        写出流.写 (1024)
        枚举循环 ()
        {
            变量 哈希表 <类型 = 哈希表值>
            哈希表.关键字 = 取枚举关键字 ()
            哈希表.键值 = 取枚举值 ()
            写出流.写对象 (哈希表)
        }
    }

    方法 流读入 <公开 @虚拟方法 = 可覆盖>
    参数 流对象 <类型 = 输入流类 注释 = "提供欲从其中读入本对象数据内容的输入流">
    {
        变量 标志 <类型 = 整数>
        如果 (流对象.读 (标志).是否出错 ())
        {
            返回
        }
        如果 (标志 != 1024)
        {
            流对象.错误码 = 流错误码.无效数据
        }
        判断循环 (真)
        {
            变量 对象1 <类型 = 哈希表值>
            流对象.读对象 (对象1)
            如果 (流对象.是否出错 ())
            {
                返回
            }
            插入 (对象1.关键字, 对象1.键值)
        }
    }
}

类 哈希表值
{
    变量 关键字 <公开 类型 = 文本型>
    变量 键值 <公开 类型 = 整数>

    #
}

【分隔符】


###### 以下为一个火山软件开发平台系统类库程序的内容

<火山程序 类型 = "通常" 版本 = 1 />

包 火山.测试 <注释 = "火山无名模块：http://m.vowm.cn">

类 启动类 <公开 基础类 = 窗口程序类>
{
    变量 主窗口 <公开 类型 = 我的主窗口>

    方法 启动方法 <公开 类型 = 整数>
    {
        主窗口.创建主窗口 ()
        返回 (1)
    }
}

类 我的主窗口 <公开 基础类 = 窗口 隐藏值属性 = "0" @视窗.布局 = "client_size = \"350, 272\"">
{
    方法 我的主窗口_创建完毕 <接收事件 类型 = 整数>
    参数 来源对象 <类型 = 我的主窗口>
    参数 标记值 <类型 = 整数>
    {
        变量 数组内容 <类型 = "整数 []" 值 = {1,2,3,3,5,4,6,5,4,8,7,3,2}>
        变量 数组 <类型 = 整数数组类>
        数组.添加基本数组 (数组内容, 0, 取数组成员数 (数组内容))
        变量 重复数组 <类型 = 重复数组类>
        重复数组 = 整数数组取重复数据 (数组)
        重复数组.枚举循环 ()
        {
            变量 数据 <类型 = 重复数据类>
            数据 = 重复数组.取枚举值 ()
            调试输出 ("内容：" + 到文本 (数据.重复内容) + "次数：" + 到文本 (数据.重复次数))
        }
        返回 (0)
    }

    方法 整数数组取重复数据 <类型 = 重复数组类 注释 = "整数数组重复内容统计">
    参数 参_整数数组 <类型 = 整数数组类>
    {
        变量 局_重复数组 <类型 = 重复数组类>
        变量 局_临时 <类型 = 整数数组类>
        参_整数数组.枚举循环 ()
        {
            变量 局_成员内容 <类型 = 整数>
            局_成员内容 = 参_整数数组.取枚举值 ()
            变量 局_数据 <类型 = 重复数据类>
            局_数据.重复内容 = 局_成员内容
            局_数据.重复次数 = 1
            如果 (局_临时.查找首成员 (局_成员内容) == -1)
            {
                局_临时.压入成员 (局_成员内容)
                局_重复数组.加入成员 (局_数据)
            }
            否则
            {
                如果 (局_重复数组.成员是否存在 (局_成员内容) == -1)
                {
                    局_重复数组.加入成员 (局_数据)
                }

            }
        }
        返回 (局_重复数组)
    }
}

类 重复数组类 <基础类 = 对象数组模板类 @模板实现类 = "重复数据类">
{
    方法 成员是否存在 <公开 静态 类型 = 整数>
    参数 当前对象 <类型 = 重复数组类>
    参数 成员值 <类型 = 整数>
    {
        当前对象.枚举循环 ()
        {
            变量 局_数据 <类型 = 重复数据类>
            局_数据 = 当前对象.取枚举值 ()
            如果 (成员值 == 局_数据.重复内容)
            {
                局_数据.重复次数 = 局_数据.重复次数 + 1
                当前对象.置成员值 (取枚举索引 (), 局_数据)
                返回 (取枚举索引 ())
            }
        }
        返回 (-1)
    }
}

类 重复数据类
{
    变量 重复内容 <公开 类型 = 整数>
    变量 重复次数 <公开 类型 = 整数>

    #
}

【分隔符】


###### 以下为一个火山软件开发平台系统类库程序的内容

<火山程序 类型 = "通常" 版本 = 1 />

包 火山.测试

类 启动类 <公开 基础类 = 窗口程序类>
{
    变量 主窗口对象 <类型 = 我的主窗口>

    方法 启动方法 <公开 类型 = 整数>
    {
        // 窗口组件.使用MFC动态链接库 ()  // 如果想要所编译程序动态链接到MFC,请启用本语句.
        主窗口对象.创建主窗口 ()
        返回 (1)
    }
}

# ===

类 我的主窗口 <基础类 = 窗口 标题 = "挂接事件测试" Esc键关闭 = 真 @视窗.布局 = "client_size = \"282, 239\"">
{
    变量 按钮1 <类型 = 按钮 隐藏值属性 = "0" @视窗.布局 = "id = 101\r\npos = \"69, 27, 120, 40\"" 标题 = "启动时钟">
    变量 标签1 <类型 = 标签 隐藏值属性 = "0" @视窗.布局 = "id = 102\r\npos = \"13, 110, 260, 40\"">
    变量 标签2 <类型 = 标签 隐藏值属性 = "0" @视窗.布局 = "id = 103\r\npos = \"13, 174, 260, 40\"">
    变量 时钟数组 <类型 = 对象数组类>

    方法 按钮_被单击 <接收事件 类型 = 整数>
    参数 来源对象 <类型 = 按钮>
    参数 标记值 <类型 = 整数>
    {
        如果 (来源对象 == 按钮1)
        {
            // 在使用pc的时候事件不触发,通常是因为没有挂接事件,挂接后即可解决。
            // 还有一个原因是因为,变量被释放,导致事件无效,所以需要使用对象数组类,将要触发事件的组件或者变量搞进去。。
            变量 时钟1 <类型 = 时钟>
            时钟1.创建组件 (本对象, 0, 0, 20, 20)
            时钟数组.加入成员 (时钟1)  // 必须放到对象数组中
            挂接事件 ((时钟)时钟数组.取成员 (0), 时钟1常量)  // 这样取出来使用,0是索引代表第一个成员
            时钟1.时钟周期 = 500  // 创建后才能设置属性

            变量 时钟2 <类型 = 时钟>
            时钟2.创建组件 (本对象, 20, 20, 20, 20)
            时钟数组.加入成员 (时钟2)  // 必须放到对象数组中
            挂接事件 ((时钟)时钟数组.取成员 (1), 时钟2常量)  // 这样取出来使用,1是索引代表第二个成员
            时钟2.时钟周期 = 300  // 创建后才能设置属性

        }
        返回 (0)
    }

    常量 时钟1常量 <类型 = 整数 值 = 1 注释 = "为时钟1服务">
    常量 时钟2常量 <类型 = 整数 值 = 2 注释 = "为时钟2服务">

    变量 计次时钟1 <类型 = 整数>
    变量 计次时钟2 <类型 = 整数>

    方法 时钟_周期事件 <接收事件 类型 = 整数>
    参数 来源对象 <类型 = 时钟>
    参数 标记值 <类型 = 整数>
    {
        如果 (标记值 == 时钟1常量)  // 通过标记值判断来源
        {
            计次时钟1 = 计次时钟1 + 1
            标签1.标题 = "时钟1运行次数：" + 到文本 (计次时钟1)
            如果 (计次时钟1 == 10)
            {
                来源对象.时钟周期 = 0  // 停止时钟1

            }

        }
        否则 (标记值 == 时钟2常量)
        {
            计次时钟2 = 计次时钟2 + 1
            标签2.标题 = "时钟2运行次数：" + 到文本 (计次时钟2)
            如果 (计次时钟2 == 10)
            {
                来源对象.时钟周期 = 0  // 停止时钟2
            }

        }
        返回 (0)
    }
}

【分隔符】


###### 以下为一个火山软件开发平台系统类库程序的内容

<火山程序 类型 = "通常" 版本 = 1 />

包 火山.程序

类 启动类 <公开 基础类 = 窗口程序类>
{
    变量 主窗口对象 <类型 = 我的主窗口>

    方法 启动方法 <公开 类型 = 整数>
    {
        主窗口对象.创建主窗口 ()
        返回 (1)
    }
}

# ===

类 我的主窗口 <基础类 = 窗口 注释 = "样例主窗口" @视窗.布局 = "client_size = \"500, 300\"" 标题 = "我的主窗口">
{
    变量 超级列表框1 <类型 = 超级列表框 折叠2 隐藏值属性 = "0" @视窗.布局 = "id = 101\r\npos = \"13, 22, 472, 160\"" "类型" = 报表列表框
            报表列 = "0\n-1\n0\n50\nID\n0\n-1\n0\n100\n姓名\n0\n-1\n0\n100\n年龄" 整行选择 = 真 显示表格线 = 真
            表项 = "0\n-1\n-1\n0\n0\n1\n\n1\n-1\n-1\n0\n0\n张三1\n\n1\n-1\n-1\n0\n0\n11\n\n0\n-1\n-1\n0\n0\n2\n\n1\n-1\n-1\n0\n0\n张三2\n\n1\n-1\n-1\n0\n0\n12\n\n0\n-1\n-1\n0\n0\n3\n\n1\n-1\n-1\n0\n0\n张三3\n\n1\n-1\n-1\n0\n0\n13\n\n0\n-1\n-1\n0\n0\n4\n\n1\n-1\n-1\n0\n0\n张三4\n\n1\n-1\n-1\n0\n0\n14\n\n0\n-1\n-1\n0\n0\n5\n\n1\n-1\n-1\n0\n0\n张三5\n\n1\n-1\n-1\n0\n0\n15\n">

    方法 超级列表框_即将绘制 <接收事件 类型 = 整数 注释 = "  在超级列表框即将绘制某项目之前被发送,用户可以修改所提供参数中的"
            注释 = "\"文本颜色\"和\"背景颜色\"成员值以修改在绘制时所实际使用的颜色值.">
    参数 来源对象 <类型 = 超级列表框 注释 = "提供事件产生的具体来源对象">
    参数 标记值 <类型 = 整数 注释 = "用户调用\"挂接事件\"命令时所提供的\"标记值\"参数值,非此方式挂接事件则本参数值固定为0.">
    参数 绘制参数 <类型 = 超级列表框绘制参数 注释 = "  用户可以修改本参数中的\"文本颜色\"和\"背景颜色\"成员值以修改在绘制" 注释 = "时所实际使用的颜色值."
            注释 = "  注意: 如果修改了本参数中的\"文本颜色\"和\"背景颜色\"成员值,将会对" 注释 = "当前表项的当前列及其后所有列均造成影响.">
    {
        如果 (来源对象 == 超级列表框1)
        {
            如果 (绘制参数.表项索引 % 2 == 1)
            {
                绘制参数.文本颜色 = 0xFFFFFF
                绘制参数.背景颜色 = 0xFF8800

            }
            否则
            {
                绘制参数.文本颜色 = 0xFFFFFF
                绘制参数.背景颜色 = 0x0000FF



            }
        }
        返回 (0)
    }
}
